(subclass 120ACPowerSource ACPowerSource)
(documentation 120ACPowerSource EnglishLanguage "&%120ACPowerSource refers to an 
&%ACPowerSource that generates 120 &%Volt of &%Electricity")
(termFormat EnglishLanguage 120ACPowerSource "120 ac")

(=>
  (instance ?AC 120ACPowerSource)
  (hasPurpose ?AC
    (voltageMeasure ?AC (MeasureFn 120 Volt))))
    
(subclass 220ACPowerSource ACPowerSource)
(documentation 220ACPowerSource EnglishLanguage "&%220ACPowerSource refers to an 
&%ACPowerSource that generates 220 &%Volt of &%Electricity")
(termFormat EnglishLanguage 220ACPowerSource "220 ac")

(=>
  (instance ?AC 220ACPowerSource)
  (hasPurpose ?AC
    (voltageMeasure ?AC (MeasureFn 220 Volt))))

(subclass 120DCPowerSource DCPowerSource)
(documentation 120DCPowerSource EnglishLanguage "&%120DCPowerSource refers to an 
&%DCPowerSource that generates 120 &%Volt of &%Electricity")
(termFormat EnglishLanguage 120DCPowerSource "120 ac")

(=>
  (instance ?DC 120DCPowerSource)
  (hasPurpose ?DC
    (voltageMeasure ?DC (MeasureFn 120 Volt))))
    
(subclass 220DCPowerSource DCPowerSource)
(documentation 220DCPowerSource EnglishLanguage "&%220DCPowerSource refers to an 
&%DCPowerSource that generates 220 &%Volt of &%Electricity")
(termFormat EnglishLanguage 220ACPowerSource "220 ac")

(=>
  (instance ?DC 220DCPowerSource)
  (hasPurpose ?DC
    (voltageMeasure ?DC (MeasureFn 220 Volt))))

(subclass BrailleElevator Elevator)
(documentation BrailleElevator EnglishLanguage "&%BrailleElevator is a type of
&%Elevator that a &%Blind person is able to use as it has &%Braille controls")
(termFormat EnglishLanguage BrailleElevator "braille elevator")

(=>
  (instance ?X BrailleElevator)
  (exists (?CONTROL)
    (and
      (instance ?CONTROL Controller)
      (part ?CONTROL ?X)
      (attribute ?CONTROL Braille))))

(subclass HistoricalCity City)
(documentation HistoricalCity EnglishLanguage "&%HistoricalCity is a type of 
&%City that has figured a prominent role in &%History")
(termFormat EnglishLanguage HistoricalCity "historical city")

(=>
  (instance ?X HistoricalCity)
  (attribute ?X Historical))
      
(subclass DutyFreeStore RetailStore)
(documentation DutyFreeStore EnglishLanguage "&%DutyFreeStore is a type of &%RetailStore
that sells products that are &%DutyFree")
(termFormat EnglishLanguage DutyFreeStore "duty free store")

(=>
  (instance ?X DutyFreeStore)
  (attribute ?X DutyFree))
      
(subclass IndoorSwimmingPool SwimmingPool)
(documentation IndoorSwimmingPool EnglishLanguage "&%IndoorSwimmingPool is a type of
&%SwimmingPool that is &%Indoors")
(termFormat EnglishLanguage IndoorSwimmingPool "indoor swimming pool")

(=>
  (instance ?X IndoorSwimmingPool)
  (exists (?INDOOR)
    (and
      (instance ?INDOOR Indoors)
      (located ?X ?INDOOR))))

(subclass OutdoorSwimmingPool SwimmingPool)
(documentation OutdoorSwimmingPool EnglishLanguage "&%OutdoorSwimmingPool is a type of
&%SwimmingPool that is &%Outdoors")
(termFormat EnglishLanguage OutdoorSwimmingPool "outdoor swimming pool")

(=>
  (instance ?X OutdoorSwimmingPool)
  (exists (?OUTDOOR)
    (and
      (instance ?OUTDOOR Outdoors)
      (located ?X ?OUTDOOR))))
      
(subclass PoolsideSnackAndNonalcoholicBeverageBar SnackAndNonalcoholicBeverageBars)
(documentation PoolsideSnackAndNonalcoholicBeverageBar EnglishLanguage
"&%PoolsideSnackAndNonalcoholicBeverageBar is a type of &%SnackAndNonalcoholicBeverageBars
that is &%Adjacent to a &%SwimmingPool")
(termFormat EnglishLanguage PoolsideSnackAndNonalcoholicBeverageBar "poolside snack bar")

(=>
  (instance ?X PoolsideSnackAndNonalcoholicBeverageBar)
  (exists (?REGION ?POOL)
    (and
      (instance ?POOL SwimmingPool)
      (instance ?REGION Region)
      (orientation ?REGION ?POOL Adjacent)
      (located ?X ?REGION))))

(subclass IndoorTennisCourt TennisCourt)
(documentation IndoorTennisCourt EnglishLanguage "&%IndoorTennisCourt is a type of
&%TennisCourt that can be found &%Indoors")
(termFormat EnglishLanguage IndoorTennisCourt "indoor tennis court")

(=>
  (instance ?X IndoorTennisCourt)
  (exists (?INDOOR)
    (and
      (instance ?INDOOR Indoors)
      (located ?X ?INDOOR))))
      
(subclass OutdoorTennisCourt TennisCourt)
(documentation OutdoorTennisCourt EnglishLanguage "&%OutdoorTennisCourt is a type of
&%TennisCourt that can be found &%Outdoors")
(termFormat EnglishLanguage OutdoorTennisCourt "outdoor tennis court")

(=>
  (instance ?X OutdoorTennisCourt)
  (exists (?OUTDOOR)
    (and
      (instance ?OUTDOOR Outdoors)
      (located ?X ?OUTDOOR))))
      
(subclass DoubleWhirlpoolTub WhirlpoolTub)
(documentation DoubleWhirlpoolTub EnglishLanguage "&%DoubleWhirlpoolTub is a 
&%WhirlpoolTub that can fit two &%Human at the same time")
(termFormat EnglishLanguage DoubleWhirlpoolTub "double whirlpool tub")

(=>
  (instance ?X DoubleWhirlpoolTub)
  (modalAttribute
    (exists (?GRP)
      (and
        (instance ?GRP GroupOfPeople)
        (memberCount ?GRP 2)
        (located ?GRP ?X))) Possibility))

(subclass CableTelevisionSet TelevisionSet)
(documentation CableTelevisionSet EnglishLanguage "&%CableTelevisionSet is a 
&%TelevisionSet with a &%TelevisionReceiver that is a &%engineeringSubcomponent of
a &%CableTelevisionSystem")
(termFormat EnglishLanguage CableTelevisionSet "cable tv")

(=>
  (instance ?X CableTelevisionSet)
  (modalAttribute
    (exists (?CABLE)
      (and
        (instance ?CABLE CableTelevisionSystem)
        (member ?X ?CABLE))) Possibility))

(subclass SatelliteTVSet TelevisionSet)
(documentation SatelliteTVSet EnglishLanguage "&%SatelliteTVSet is a &%TelevisionSet
with a &%TelevisionReceiver that is an &%engineeringSubcomponent of a 
&%SatelliteTelevisionSystem")
(termFormat EnglishLanguage SatelliteTVSet "satellite tv")

(=>
  (instance ?TV SatelliteTVSet)
  (modalAttribute
    (exists (?SAT)
      (and
        (instance ?SAT SatelliteTelevisionSystem)
        (member ?TV ?SAT))) Possibility))


(subclass ChildFriendlyActivity RecreationOrExercise)
(documentation ChildFriendlyActivity EnglishLanguage "&%ChildFriendlyActivity is a type
of &%RecreationOrExercise that is &%ChildFriendlyAttribute")
(termFormat EnglishLanguage ChildFriendlyActivity "child-friendly activity")

(=>
  (instance ?X ChildFriendlyActivity)
  (attribute ?X ChildFriendlyAttribute))

(subclass BreakfastBuffet Buffet)
(documentation BreakfastBuffet EnglishLanguage "&%BreakfastBuffet is a type of
&%Buffet served at &%BreakfastTime made with food for &%Breakfast")
(termFormat EnglishLanguage BreakfastBuffet "breakfast buffet")

(=>
  (instance ?X BreakfastBuffet)
  (attribute ?X Breakfast))
  
(=>
  (instance ?X BreakfastBuffet)
  (modalAttribute
    (exists (?EAT)
      (and
        (instance ?EAT Eating)
        (patient ?EAT ?X)
        (during (WhenFn ?EAT) BreakfastTime))) Likely))

(subclass LocalTelephoneCommunication TelephoneCommunication)
(documentation LocalTelephoneCommunication EnglishLanguage
"&%LocalTelephoneCommunication is a type of &%TelephoneCommunication to 
&%TelephonyDevice in the same area or vicinity")
(termFormat EnglishLanguage LocalTelephoneCommunication "local phone call")

(=>
  (instance ?CALL LocalTelephoneCommunication)
  (attribute ?CALL Local))
  
(subclass InternationalDirectDialTelephoneCommunication DirectDialTelephoneCommunication)
(documentation InternationalDirectDialTelephoneCommunication EnglishLanguage
"&%InternationalDirectDialTelephoneCommunication is a type of &%DirectDialTelephoneCommunication
where the destination of the call is in a different &%Nation")
(termFormat EnglishLanguage InternationalDirectDialTelephoneCommunication
"international direct dial")

(=>
  (instance ?X InternationalDirectDialTelephoneCommunication)
  (attribute ?X International))

(subclass CommunalLounge SittingArea)
(documentation CommunalLounge EnglishLanguage "&%CommunalLounge is a type of 
&%SittingArea that is shared by many groups of people, or is &%CommunalAttribute")
(termFormat EnglishLanguage CommunalLounge "communal lounge")

(=>
  (instance ?X CommunalLounge)
  (attribute ?X CommunalAttribute))
  
(subclass CommunalKitchenArea KitchenArea)
(documentation CommunalKitchenArea EnglishLanguage "&%CommunalKitchenArea is a type of
&%KitchenArea that is shared by many groups of people, or is &%CommunalAttribute")
(termFormat EnglishLanguage CommunalKitchenArea "communal kitchen area")

(=>
  (instance ?X CommunalKitchenArea)
  (attribute ?X CommunalAttribute))
  
(subclass CommunalInRoomBathroom Bathroom)
(documentation CommunalInRoomBathroom EnglishLanguage "&%CommunalInRoomBathroom is a 
type of &%Bathroom that is shared by people staying in multiple rooms")
(termFormat EnglishLanguage CommunalInRoomBathroom "communal in-room bathroom")

(=>
  (physicalAmenity ?ROOM CommunalInRoomBathroom)
  (exists (?ROOM1 ?ROOM2 ?BATH)
    (and
      (instance ?BATH CommunalInRoomBathroom)
      (instance ?ROOM1 ?ROOM)
      (instance ?ROOM2 ?ROOM)
      (not (equal ?ROOM1 ?ROOM2))
      (orientation ?ROOM1 ?BATH Adjacent)
      (orientation ?ROOM2 ?BATH Adjacent)
      (=>
        (and
          (stays ?GUEST1 ?ROOM1)
          (stays ?GUEST2 ?ROOM2))
        (modalAttribute
          (and
            (uses ?BATH ?GUEST1)
            (uses ?BATH ?GUEST2)) Possibility)))))
                
                
(subclass NewspaperDeliveryService DeliveryService)
(documentation NewspaperDeliveryService EnglishLanguage "&%NewspaperDeliveryService
is a type of &%DeliveryService where the &%patient is a &%Newspaper")
(termFormat EnglishLanguage NewspaperDeliveryService "newspaper delivery")

(=>
  (instance ?NEWS NewspaperDeliveryService)
  (exists (?PAPER)
    (and
      (instance ?PAPER Newspaper)
      (patient ?NEWS ?PAPER))))
      

(subclass SpeakerPhone Telephone)
(documentation SpeakerPhone EnglishLanguage "&%SpeakerPhone is a type of &%Telephone
with a &%SpeakerDevice instead of an &%Earphone")
(termFormat EnglishLanguage SpeakerPhone "speaker phone")

(=>
  (instance ?PHONE SpeakerPhone)
  (exists (?SPEAKER)
    (and
      (instance ?SPEAKER SpeakerDevice)
      (part ?SPEAKER ?PHONE))))
    
(subclass OutdoorDiningArea DiningArea)
(documentation OutdoorDiningArea EnglishLanguage "&%OutdoorDiningArea refers to a 
&%DiningArea that is located &%Outdoors")
(termFormat EnglishLanguage OutdoorDiningArea "outdoor dining area")

(=>
  (instance ?DA OutdoorDiningArea)
  (exists (?OUTDOORS)
    (and
      (instance ?OUTDOORS Outdoors)
      (located ?DA ?OUTDOORS))))
      
(subclass AllDayRoomService RoomService)
(documentation AllDayRoomService EnglishLanguage "&%AllDayRoomService refers to the
capability of a &%TravelerAccommodation to offer &%RoomService for all of 24 hours")
(termFormat EnglishLanguage AllDayRoomService "24-hr room service")

(=>
  (and
    (serviceAmenity ?ROOM AllDayRoomService)
    (instance ?X ?ROOM)
    (element ?X (PropertyFn ?HOTEL)))
  (capabilityDuring RoomService agent ?HOTEL (RecurrentTimeIntervalFn (HourFn 0 Day) (HourFn 24 Day))))

(subclass AllDayFrontDesk HotelFrontDesk)
(documentation AllDayFrontDesk EnglishLanguage "&%AllDayFrontDesk means that the
&%HotelFrontDesk organization is available the whole day")
(termFormat EnglishLanguage AllDayFrontDesk "24-hr front desk")

(=>
  (instance ?X AllDayFrontDesk)
  (standardRetailHours (RecurrentTimeIntervalFn (HourFn 0 Day) (HourFn 24 Day)) ?X))
     
      
(subclass UniformedSecurityUnit SecurityUnit)
(documentation UniformedSecurityUnit EnglishLanguage "&%UniformedSecurityUnit refers
to a &%SecurityUnit whose members wear &%UniformClothing.")
(termFormat EnglishLanguage UniformedSecurityUnit "uniformed security unit")

(=>
  (instance ?X UniformedSecurityUnit)
  (exists (?UNIFORM)
    (and
      (subclass ?UNIFORM UniformClothing)
      (hasUniform ?X ?UNIFORM))))


(subclass TouristArea GeographicArea)
(documentation TouristArea EnglishLanguage "&%TouristArea refers to the vicinity where
&%TouristSite can be found")
(termFormat EnglishLanguage TouristArea "tourist area")

(=>
  (instance ?X TouristArea)
  (exists (?SITE)
    (and
      (instance ?SITE TouristSite)
      (located ?SITE ?X))))
      
(subclass RuralArea GeographicArea)
(documentation RuralArea EnglishLanguage "&%RuralArea refers to a &%GeographicArea
that is &%Rural")
(termFormat EnglishLanguage RuralArea "rural area")

(=>
  (instance ?X RuralArea)
  (attribute ?X Rural))
            
(subclass SuburbanArea GeographicArea)
(documentation SuburbanArea EnglishLanguage "&%SuburbanArea refers to a &%GeographicArea
that is &%Suburban")
(termFormat EnglishLanguage SuburbanArea "suburban area")

(=>
  (instance ?X SuburbanArea)
  (attribute ?X Suburban))
     

;; NOTE: tried searching for what this means in the internet, but could not find
;; any reliable source      
(subclass MealPlanCaribbean MealPlan)
(termFormat EnglishLanguage MealPlanCaribbean "caribbean meal plan")
 
