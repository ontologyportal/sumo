;; ============================================================================
;;                  	REARDEN COMMERCE, INC.
;;                  	Ratings Ontology
;; ============================================================================

;; Created by: Karen Joy Nomorosa (karen.nomorosa [at] reardencommerce [dot] com)
;; Created on: March 17, 2011
;; Version: 1.1
;; Last modified: March 20, 2011
;; Modified by: Karen Joy Nomorosa

;; Description:  This ontology describes different concepts related to Ratings Systems

;; Ontology dependencies for Dining.kif: Merge.kif, Mid-level-ontology.kif

;; Access to and use of these products is governed by the GNU General Public 
;; License <http://www.gnu.org/copyleft/gpl.html>. 
;; By using these products, you agree to be bound by the terms 
;; of the GPL.

;; We ask the people using or referencing this work cite our primary paper:

;; Niles, I., and Pease, A.  2001.  Towards a Standard Upper Ontology.  In 
;; Proceedings of the 2nd International Conference on Formal Ontology in 
;; Information Systems (FOIS-2001), Chris Welty and Barry Smith, eds, 
;; Ogunquit, Maine, October 17-19, 2001.  See also http://www.ontologyportal.org


(subclass RatingAttribute SubjectiveAssessmentAttribute)
(documentation RatingAttribute "&%RatingAttribute attempts to quantify the subjective assessment of an &%Object using some scoring system")

;; if ?RATING is an instance of RatingAttribute, then there exists some agent that underwent some decision-making to come up with that rating
(=>
  (instance ?RATING RatingAttribute)
  (exists (?AGENT)
    (ratingsAgent ?RATING ?AGENT)))

;;(ratingsAgent ?RATING ?AGENT)
(instance ratingsAgent BinaryPredicate)      
(documentation ratingsAgent "(&%ratingsAgent ?RATING ?AGENT) means that &%RatingAttribute ?RATING was created by ?AGENT")
(domain ratingsAgent 1 RatingAttribute)
(domain ratingsAgent 2 Agent)

(=>
  (ratingsAgent ?RATING ?AGENT)
  (exists (?PROCESS)
    (and
      (instance ?PROCESS Deciding)
      (agent ?PROCESS ?AGENT)
      (result ?PROCESS ?RATING))))

 
(subclass RestaurantRating RatingAttribute)
(documentation RestaurantRating "&%RestaurantRating is a &%SubjectiveAssessmentAttribute specifically targeted at the overall quality of a &%Restaurant")

(=>
  (and
    (objectRating ?ENTITY ?RATING ?AGENT ?OBJ)
    (instance ?RATING RestaurantRating))
  (and
    (subclass ?OBJ CommercialAgent)
    (instance ?ENTITY Restaurant)))

(subclass FoodRating RatingAttribute)
(documentation FoodRating "&%FoodRating is a &%SubjectiveAssessmentAttribute specifically targeted the overall quality of &%Food")

;; if a rating is a Food Rating, then the object of the rating is food, and that food was cooked by the agent. 
(=>
  (and
    (objectRating ?ENTITY ?RATING ?AGENT ?OBJ)
    (instance ?RATING FoodRating))
  (subclass ?OBJ PreparedFood)

(subclass ServiceRating RatingAttribute)
(documentation ServiceRating "&%ServiceRating is a &%SubjectiveAssessmentAttribute specifically targeted at the overall quality of &%service given by an agent")

;; if a rating is a Service Rating, then the object of that rating is a ServiceProcess, and that service process was executed by the agent
(=>
  (and
    (serviceRating ?ENTITY ?RATING ?AGENT ?SVC)
    (instance ?RATING ServiceRating))
  (subclass ?SVC ServiceProcess))

(subclass AmbienceRating RatingAttribute)
(documentation AmbienceRating "&%AmbienceRating is a &%SubjectiveAssessmentAttribute specifically targeted at the overall quality of an environment's atmosphere") 

;; if a rating is an AmbienceRating, then the object of that rating is an instance of SubjectiveAssessmentAttribute
(=>
  (and
    (attributeRating ?ENTITY ?RATING ?AGENT ?ATTR)
    (instance ?RATING AmbienceRating))
  (subclass ?ATTR SubjectiveAssessmentAttribute))

(subclass PriceRating RatingAttribute)
(documentation PriceRating "&%PriceRating is a &%SubjectiveAssessmentAttribute specifically targeted at the price (or average price) of an object is")

;; if a rating is a PriceRating, then the object of the rating is the CostAttribute
(=>
  (and
    (objectRating ?ENTITY ?RATING ?AGENT ?OBJ)
    (instance ?RATING PriceRating))
  (subclass ?OBJ SubjectiveAssessmentAttribute))

