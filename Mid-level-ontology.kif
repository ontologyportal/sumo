;; ==============================
;;      MILO (MId-Level Ontology)
;; ==============================

;; This is the source file for the MILO (MId-Level Ontology), an ontology that 
;; is being developed as a bridge between the abstract content of the SUMO and 
;; the rich detail of the various domain ontologies.    
;; Those who are interested in making use of this ontology are urged 
;; to contact Adam Pease (apease [at] articulatesoftware [dot] com).

;; The MILO (MId-Level Ontology) is copyrighted by Teknowledge (c) 
;; 2003 and Articulate Software 2004-2007.  It is released under the GNU Public License 
;; <http://www.gnu.org/copyleft/gpl.html>.  Users of this code also consent, by 
;; use of this material, to credit Teknowledge and Articulate Software
;; in any writings, briefings, 
;; publications, presentations, or other representations of any code or other 
;; product which incorporates, builds on, or uses this material. 

;; Version date: April, 2008
;; We ask that people using or referencing this work cite our primary paper:

;; Niles, I., and Pease, A.  2001.  Towards a Standard Upper Ontology.  In 
;; Proceedings of the 2nd International Conference on Formal Ontology in 
;; Information Systems (FOIS-2001), Chris Welty and Barry Smith, eds, 
;; Ogunquit, Maine, October 17-19, 2001.  See also http://www.ontologyportal.org

(instance DeadFn UnaryFunction)
(domainSubclass DeadFn 1 CorpuscularObject)
(rangeSubclass DeadFn OrganicObject)
(documentation DeadFn EnglishLanguage "The &%Dead counterpart to the particular live
organism.  For example &%HumanCorpse is (DeadFn Human).  Note that the dead
counterpart may not have all the original parts of the live object.  For
example, a &%CutFlower will lack the roots of a &%FloweringPlant.")
(=>
  (instance ?DEAD (DeadFn ?LIVE))
  (exists (?I)
    (and
      (instance ?I ?LIVE)
      (attribute ?I Living)
      (attribute ?DEAD Dead))))

(=>
  (instance ?DEAD (DeadFn ?LIVE))
  (exists (?I)
    (and
      (instance ?I ?LIVE)
      (before (WhenFn ?I) (WhenFn ?DEAD)))))

(subclass HumanCorpse OrganicObject)
(documentation HumanCorpse EnglishLanguage "A &%Dead thing which was formerly a &%Human.")
(equal HumanCorpse (DeadFn Human))

(=>
  (instance ?CORPSE HumanCorpse)
  (exists (?HUMAN)
    (and
      (instance ?HUMAN Human)
      (before (WhenFn ?HUMAN) (WhenFn ?CORPSE))
      (not
        (exists (?OTHERPART)
          (and
            (holdsDuring
              (WhenFn ?CORPSE)
              (part ?OTHERPART ?CORPSE))
            (not
              (holdsDuring 
                (WhenFn ?HUMAN) 
                (part ?OTHERPART ?HUMAN)))))))))
            
(=>
  (instance ?CORPSE HumanCorpse)
  (attribute ?CORPSE Dead))

(instance HumanSlave SocialRole)
(documentation HumanSlave EnglishLanguage "A &%Human who is owned by someone else.")

(=>
   (attribute ?SLAVE HumanSlave)
   (exists (?PERSON)
      (and
         (instance ?PERSON Human)
         (not (equal ?PERSON ?SLAVE))
         (possesses ?PERSON ?SLAVE))))

(subclass HumanAdult Human)
(documentation HumanAdult EnglishLanguage "The class of &%Humans that are 18 years of 
age or older.")

(=>
   (and
      (instance ?ADULT HumanAdult)
      (age ?ADULT (MeasureFn ?NUMBER YearDuration)))
   (greaterThanOrEqualTo ?NUMBER 18))

(subclass HumanYouth Human)
(documentation HumanYouth EnglishLanguage "The class of &%Humans that are younger than 
18 years of age.")

(=>
   (and
      (instance ?YOUTH HumanYouth)
      (age ?YOUTH (MeasureFn ?NUMBER YearDuration)))
   (lessThan ?NUMBER 18))

(subclass HumanChild HumanYouth)
(documentation HumanChild EnglishLanguage "A &%HumanYouth between birth and puberty, i.e a 
&%Human who is &%NonFullyFormed.")

(=>
   (instance ?CHILD HumanChild)
   (attribute ?CHILD NonFullyFormed))

(=>
   (and
      (instance ?CHILD HumanChild)
      (age ?CHILD (MeasureFn ?NUMBER YearDuration)))
   (lessThanOrEqualTo ?NUMBER 14)) 

(subclass Teenager HumanYouth)
(documentation Teenager EnglishLanguage "A &%HumanYouth between puberty and the age of 20.")

(=>
   (and
      (instance ?TEEN Teenager)
      (age ?TEEN (MeasureFn ?NUMBER YearDuration)))
   (and
      (greaterThan ?NUMBER 12)
      (lessThan ?NUMBER 20))) 

(subclass Boy HumanChild)
(subclass Boy Man)
(documentation Boy EnglishLanguage "A &%HumanChild who is &%Male.")

(subclass Girl HumanChild)
(subclass Girl Woman)
(documentation Girl EnglishLanguage "A &%HumanChild who is &%Female.")

(subclass HumanBaby HumanChild)
(documentation HumanBaby EnglishLanguage "A &%Human between birth and the first year of age.")

(=>
   (and
      (instance ?BABY HumanBaby)
      (age ?BABY (MeasureFn ?NUMBER YearDuration)))
   (lessThanOrEqualTo ?NUMBER 1)) 

(documentation older EnglishLanguage "(older ?OBJ1 ?OBJ2) means that ?OBJ1 is older than 
?OBJ2, i.e. the age of ?OBJ1 is &%greaterThan the age of ?OBJ2.")
(instance older BinaryPredicate)
(domain older 1 Object)
(domain older 2 Object)

(=>
  (older ?O1 ?O2)
  (exists (?N1 ?N2 ?U)
    (and
      (age ?O1 (MeasureFn ?N1 ?U))
      (age ?O2 (MeasureFn ?N2 ?U))
      (greaterThan ?N1 ?N2))))

(documentation StartFn EnglishLanguage "(StartFn ?PROCESS) denotes &%IntentionalProcesses 
of bringing it about that &%Processes of type ?PROCESS begin, e.g. start 
working, begin running, etc.")
(instance StartFn UnaryFunction)
(domain StartFn 1 Process)
(rangeSubclass StartFn IntentionalProcess)

(=>
  (instance ?S (StartFn ?P))
  (exists (?I)
    (and
      (instance ?I ?P)
      (starts (WhenFn ?S) (WhenFn ?I)))))

(documentation StopFn EnglishLanguage "(StopFn ?PROCESS) denotes &%IntentionalProcesses 
of bringing it about that &%Processes of type ?PROCESS end, e.g. stop 
walking, quit working, etc.")
(instance StopFn UnaryFunction)
(domain StopFn 1 Process)
(rangeSubclass StopFn IntentionalProcess)

(=>
  (instance ?S (StopFn ?P))
  (exists (?I)
    (and
      (instance ?I ?P)
      (finishes (WhenFn ?S) (WhenFn ?I)))))

(subclass Dodging BodyMotion)
(subclass Dodging IntentionalProcess)
(documentation Dodging EnglishLanguage "Purposely moving one's body in such a way as 
to avoid being hit by something.")
(=>
  (and
    (instance ?D Dodging)
    (agent ?D ?A))
  (hasPurpose ?D 
    (not 
      (exists (?I)
        (and
          (instance ?I Impacting)
          (patient ?I ?A))))))

(subclass Mounting BodyMotion)
(documentation Mounting EnglishLanguage "Any &%BodyMotion which results in being &%On 
something else.")

(=>
  (and
    (instance ?M Mounting)
    (agent ?M ?A)
    (patient ?M ?O))
  (and
    (holdsDuring 
      (BeginFn (WhenFn ?M)) 
      (not (orientation ?A ?O On)))
    (holdsDuring 
      (EndFn (WhenFn ?M)) 
      (orientation ?A ?O On))))

(subclass Stepping BodyMotion)
(documentation Stepping EnglishLanguage "The &%BodyMotion of extending one foot forward 
and then bringing the other foot to the same lateral position as the 
first leg.")

(=>
   (instance ?AMBULATE Ambulating)
   (exists (?SUB)
      (and
         (subclass ?SUB Stepping)
         (forall (?INST)
            (<=>
               (instance ?INST ?SUB)
               (subProcess ?INST ?AMBULATE))))))

(subclass Wading Walking)
(documentation Wading EnglishLanguage "Any instance of &%Walking which occurs through a 
&%BodyOfWater.")

(=>
  (instance ?P Wading)
  (exists (?W)
    (and
      (instance ?W BodyOfWater)
      (located ?P ?W))))

(subclass Jumping BodyMotion)
(subclass Jumping MotionUpward)
(documentation Jumping EnglishLanguage "Any &%MotionUpward which is done by one's body 
and which results in a situation where one's feet are unsupported.")

(=>
  (and
    (instance ?J Jumping)
    (agent ?J ?A))
  (holdsDuring 
    (EndFn (WhenFn ?J))
    (not
      (attribute ?A Standing))))

(subclass Somersaulting BodyMotion)
(documentation Somersaulting EnglishLanguage "Any &%BodyMotion which begins and ends in 
a &%Sitting position and where the feet roll over the head and return to 
their original position.")

(=>
  (and
    (instance ?S Somersaulting)
    (agent ?S ?A))
  (and
    (holdsDuring 
      (BeginFn (WhenFn ?S)) 
      (attribute ?A Sitting))
    (holdsDuring 
      (EndFn (WhenFn ?S)) 
      (attribute ?A Sitting))))  

(subclass Reflecting Radiating)
(documentation Reflecting EnglishLanguage "Any instance of &%Radiating where the radiated 
waves rebound from a surface, e.g. an echo of sound or a reflection of 
light.")

(subclass ReflectingLight Reflecting)
(subclass ReflectingLight RadiatingLight)
(documentation ReflectingLight EnglishLanguage "Those instances of &%RadiatingLight where the 
&%instrument is not a light source, but is simply a surface which bends light 
waves that come in contact with it.")

(documentation wavelength EnglishLanguage "(wavelength ?RADIATION ?MEASURE) means that the 
instance of radiation, ?RADIATION, has an average wavelength of ?MEASURE.")
(instance wavelength SingleValuedRelation)
(domain wavelength 1 Radiating)
(domain wavelength 2 LengthMeasure)
(subrelation wavelength measure)

(subclass RadiatingInfrared Radiating)
(disjoint RadiatingInfrared RadiatingLight)
(disjoint RadiatingInfrared RadioEmission)
(documentation RadiatingInfrared EnglishLanguage "Any instance of &%Radiating where the 
&%wavelengths are longer than those of visible light and shorter than those 
of radio emissions.")

(subclass RadiatingUltraviolet RadiatingElectromagnetic)
(disjoint RadiatingUltraviolet RadiatingLight)
(disjoint RadiatingUltraviolet XRayRadiation)
(documentation RadiatingUltraviolet EnglishLanguage "Any case of &%RadiatingElectromagnetic where the 
&%wavelengths are shorter than those of visible light and longer than those 
of X-Rays.")
(abbreviation "UV" RadiatingUltraviolet)

(subclass XRayRadiation Radiating)
(documentation XRayRadiation EnglishLanguage "Electro-magnetic radiation of short wavelength, 
often made use of by devices that scan the inside of objects.")

(subclass RadiatingVisibleLight RadiatingLight)
(disjoint RadiatingVisibleLight RadiatingUltraviolet)
(documentation RadiatingVisibleLight EnglishLanguage "Any instance of &%RadiatingLight that can 
be detected by normal human visual perception.")

(=>
  (and
    (instance ?R RadiatingLight)
    (instance ?H Human)
    (instance ?S Seeing)
    (agent ?S ?H)
    (patient ?S ?R))
  (instance ?R RadiatingVisibleLight))

(subclass Sunlight RadiatingLight)
(documentation Sunlight EnglishLanguage "Any instance of &%RadiatingLight where the 
Sun (&%Sol) is the origin.")

(=>
  (instance ?L Sunlight)
  (origin ?L Sol))

(subclass RadioReceiver ReceiverDevice)
(documentation RadioReceiver EnglishLanguage "A &%RadioReceiver is a &%Device for receiving 
radio broadcast signals from a &%RadioStation.")

(subclass TelevisionReceiver ReceiverDevice)
(subclass TelevisionReceiver EngineeringComponent)
(synonymousExternalConcept "television" TelevisionReceiver EnglishLanguage)
(synonymousExternalConcept "TV" TelevisionReceiver EnglishLanguage)
(documentation TelevisionReceiver EnglishLanguage "A &%TelevisionReceiver is a &%Device for receiving 
television broadcast signals from a &%TelevisionStation or signals
transmitted through a cable from a &%CableTelevisionSystem.")
(=>
  (instance ?SYSTEM TelevisionSystem)
  (exists (?DEVICE)
    (and
      (instance ?DEVICE TelevisionReceiver)
      (engineeringSubcomponent ?DEVICE ?SYSTEM))))

(=>
  (instance ?SYSTEM CableTelevisionSystem)
  (exists (?DEVICE)
    (and
      (instance ?DEVICE TelevisionReceiver)
      (engineeringSubcomponent ?DEVICE ?SYSTEM))))

(=>
  (instance ?T TelevisionReceiver)
  (capability TelevisionBroadcasting patient ?T))

(subclass Script Collection)
(documentation Script EnglishLanguage "The collection of &%Characters in a particular 
written language.  Every &%WrittenCommunication consists of &%Characters 
written in a particular script.  Scripts include different typefaces, as 
well as entirely different characters.  'Times Roman' is a very specific 
script.  While the 'latin' character set is a general one, that has 
specific subclasses like Times Roman.  Other scripts include Devanagri 
(which may be expressed in many different more specific typefaces), or 
Simplified Chinese.") 

(=>
  (instance ?W WrittenCommunication)
  (exists (?T ?C ?S)
    (and
      (result ?W ?T)
      (instance ?T Text)
      (part ?C ?T)
      (instance ?C Character)
      (instance ?S Script)
      (member ?C ?S))))

(subclass Orthography Procedure)
(documentation Orthography EnglishLanguage "A method for representing the sounds of a &%Language 
with written &%Characters.")

(subclass Lyrics Text)
(documentation Lyrics EnglishLanguage "Any &%Text which is intended to be sung.")

(=>
   (instance ?LYRIC Lyrics)
   (hasPurpose ?LYRIC (exists (?SING)
                         (and
                            (instance ?SING Singing)
                            (patient ?SING ?LYRIC)))))

(subclass Paragraph Text)
(documentation Paragraph EnglishLanguage "A &%Text which consists of one or more sentences, 
begins with an indented line, and expresses a single topic.")

(=>
  (instance ?T Paragraph)
  (exists (?S)
    (and
      (instance ?S Sentence)
      (part ?S ?T))))

(documentation sententialObject EnglishLanguage "(sententialObject ?OBJECT ?SENTENCE) 
means that the &%NounPhrase ?OBJECT is the object of the &%Sentence 
?SENTENCE.")
(domain sententialObject 1 NounPhrase)
(domain sententialObject 2 Sentence)

(subrelation sententialObject grammaticalRelation)
(documentation sententialSubject EnglishLanguage "(sententialSubject ?SUBJECT ?SENTENCE) 
means that the &%NounPhrase ?SUBJECT is the subject of the &%Sentence 
?SENTENCE.")
(domain sententialSubject 1 NounPhrase)
(domain sententialSubject 2 Sentence)
(subrelation sententialSubject grammaticalRelation)

(documentation speaksLanguage EnglishLanguage "(speaksLanguage ?AGENT ?LANGUAGE) means that the 
&%SentientAgent ?AGENT is capable of understanding and/or generating the &%Language 
?LANGUAGE.")
(instance speaksLanguage BinaryPredicate)
(domain speaksLanguage 1 SentientAgent)
(domain speaksLanguage 2 Language)

(subclass InstrumentalMusic Music)
(documentation InstrumentalMusic EnglishLanguage "&%Music which is produced (at least in part) 
by a &%MusicalInstrument.")

(=>
   (instance ?MUSIC InstrumentalMusic)
   (exists (?INSTRUMENT)
      (and
         (instance ?INSTRUMENT MusicalInstrument)
         (instrument ?MUSIC ?INSTRUMENT))))

(partition Music MonophonicMusic PolyphonicMusic)

(subclass MonophonicMusic Music)
(documentation MonophonicMusic EnglishLanguage "&%Music which has a single part, i.e. &%Music 
which cannot be divided into two or more contemporaneous &%subProcesses which 
are also instances of &%Music.")

(subclass PolyphonicMusic Music)
(documentation PolyphonicMusic EnglishLanguage "&%Music which has two or more parts, i.e. &%Music 
which can be divided into two or more contemporaneous &%subProcesses which are 
also instances of &%Music.")

(<=>
   (instance ?MUSIC PolyphonicMusic)
   (exists (?PART1 ?PART2)
      (and
         (instance ?MUSIC Music)
         (instance ?PART1 Music)
         (instance ?PART2 Music)
         (subProcess ?PART1 ?MUSIC)
         (subProcess ?PART2 ?MUSIC)
         (not (equal ?PART1 ?PART2))
         (cooccur ?PART1 ?MUSIC)
         (cooccur ?PART2 ?MUSIC))))  

(subclass VocalMusic Music)
(documentation VocalMusic EnglishLanguage "&%Music which is produced (at least in part)
by &%Singing.")

(=>
  (instance ?M VocalMusic)
  (exists (?S)
    (and
      (instance ?S Singing)
      (subProcess ?S ?M))))

(subclass HornInstrument MusicalInstrument)
(documentation HornInstrument EnglishLanguage "A &%MusicalInstrument which is played by 
blowing it.  It uses lip vibration to generates sound and control part 
of the pitch generation process as well.")

(subclass WindInstrument MusicalInstrument)
(documentation WindInstrument EnglishLanguage "A &%MusicalInstrument which is played by 
blowing it and which uses a reed, or resonator hole as in the case of a flute.")

(=>
  (and
    (instance ?WI WindInstrument)
    (instance ?M Music)
    (agent ?M ?A)
    (instrument ?M ?WI))
  (exists (?B)
    (and
      (instance ?B Exhaling)
      (agent ?B ?A)
      (subProcess ?B ?M))))

(subclass PercussionInstrument MusicalInstrument)
(documentation PercussionInstrument EnglishLanguage "A &%MusicalInstrument which does not 
have strings and which is played by striking it.")

(subclass StringInstrument MusicalInstrument)
(documentation StringInstrument EnglishLanguage "A &%MusicalInstrument which is played by 
striking strings, either directly as with a guitar or indirectly via keys 
as with &%Pianos.")

(subclass Violin StringInstrument)
(documentation Violin EnglishLanguage "A &%StringInstrument that has four strings, a hollow 
body, and is played on the shoulder with a bow.")

(subclass Piano StringInstrument)
(documentation Piano EnglishLanguage "A &%StringInstrument with keys that, when pressed down, 
activate hammers that, in turn, strike strings.")

(subclass MusicalTone RadiatingSound)
(documentation MusicalTone EnglishLanguage "A &%Process of &%RadiatingSound where the
sound has a fundamental frequency and 0 or more partial frequencies
or unrelated transients or noise components that have a lesser
amplitude than the fundamental.")

(subclass Ringing RadiatingSound)
(documentation Ringing EnglishLanguage "Any instance of &%RadiatingSound which is produced by 
a &%Bell.")

(=>
  (instance ?R Ringing)
  (exists (?B)
    (and
      (instance ?B Bell)
      (instrument ?R ?B))))

(subclass RadiatingSoundUltrasonic RadiatingSound)
(documentation RadiatingSoundUltrasonic EnglishLanguage "All sound waves that have frequencies 
above those that normal &%Human ears can detect.")

(subclass soundFrequency BinaryRelation)
(documentation soundFrequency EnglishLanguage "A relation between a &%Process of 
&%MusicalTone and the fundamental frequency of that tone.")
(domain soundFrequency 1 MusicalTone)
(domain soundFrequency 2 FunctionQuantity)

(subclass Bell PercussionInstrument)
(documentation Bell EnglishLanguage "A &%PercussionInstrument that produces a single tone 
when it is struck.")
(=>
  (and
    (instance ?B Bell)
    (instance ?I Impacting)
    (destination ?I ?B))
  (exists (?MT)
    (and
      (instance ?MT MusicalTone)
      (causes ?I ?MT))))

(subclass Bugle HornInstrument)
(documentation Bugle EnglishLanguage "A &%HornInstrument that has no valves.")
(=>
  (instance ?B Bugle)
  (not
    (exists (?V)
      (and
        (instance ?V Valve)
        (part ?V ?B)))))

(documentation Drumming EnglishLanguage "Playing a Drum.  Note that this includes both
musical performance, as well as signalling and cerimonial applications.")
(subclass Drumming RadiatingSound)
(subclass Drumming Demonstrating)

(=>
  (instance ?D Drumming)
  (exists (?A ?DRUM ?I)
    (and
      (agent ?D ?A)
      (instrument ?D ?DRUM)
      (subProcess ?I ?D)
      (agent ?I ?A)
      (patient ?I ?DRUM)
      (instance ?I Impacting)
      (instance ?DRUM Drum))))

(subclass Drum PercussionInstrument)
(documentation Drum EnglishLanguage "An atonal &%PercussionInstrument which consists of a 
hollow cylinder and a fabric stretched across at least one end of the 
cylinder.")

(=>
  (instance ?D Drum)
  (hasPurpose ?D 
    (exists (?P)
      (and
        (instance ?P Drumming)
        (instrument ?P ?D)))))

(subclass Guitar StringInstrument)
(documentation Guitar EnglishLanguage "A &%StringInstrument that has six to twelve strings and 
is played by strumming with one hand while grasping frets with the other hand.")

(subclass WrittenCommunication LinguisticCommunication)
(documentation WrittenCommunication EnglishLanguage "Any &%LinguisticCommunication where the 
&%instrument is a &%Text, e.g. a letter, an email, a memo, etc.")

(=>
   (instance ?COMMUNICATE WrittenCommunication)
   (exists (?WRITE ?READ ?TEXT)
      (and
         (instance ?WRITE Writing)
         (instance ?READ Reading)
         (instance ?TEXT Text)
         (instrument ?COMMUNICATE ?TEXT)
         (result ?WRITE ?TEXT)
         (patient ?READ ?TEXT)
         (subProcess ?WRITE ?COMMUNICATE)
         (subProcess ?READ ?COMMUNICATE))))

(subclass Corresponding LinguisticCommunication)
(documentation Corresponding EnglishLanguage "Instances of &%LinguisticCommunication which 
are achieved by means of &%Texts that are mailed between the persons 
communicating with one another.")
(=>
  (instance ?C Corresponding)
  (exists (?M ?T)
    (and
      (instance ?M Mailing)
      (subProcess ?M ?C)
      (instance ?T Text)
      (patient ?M ?T)
      (patient ?C ?T))))

(subclass Reminding Requesting)
(documentation Reminding EnglishLanguage "Any &%Requesting that is intended to cause a 
&%Remembering of something.")

(=>
   (instance ?REMIND Reminding)
   (exists (?REMEMBER)
      (and
         (instance ?REMEMBER Remembering)
         (causes ?REMIND ?REMEMBER))))

(subclass Threatening Committing)
(documentation Threatening EnglishLanguage "Any &%Committing where the thing promised 
is something that is deemed undesirable by the &%destination of the 
&%Committing.")

(=>
   (and
      (instance ?THREATEN Threatening)
      (agent ?THREATEN ?AGENT)
      (patient ?THREATEN ?PROP)
      (destination ?THREATEN ?DEST))
  (not (desires ?DEST ?PROP)))

(subclass Punishing RegulatoryProcess)
(documentation Punishing EnglishLanguage "A &%RegulatoryProcess where the agent does 
something to the &%destination that the &%agent knows is undesirable 
for the &%destination.")

(=>
  (and
    (instance ?P Punishing)
    (agent ?P ?A)
    (patient ?P ?F)
    (destination ?P ?D))
  (knows ?A 
    (not 
      (desires ?D ?F))))

(subclass Registering Stating)
(subclass Registering PoliticalProcess)
(documentation Registering EnglishLanguage "Submitting official paperwork in a government 
agency, e.g. filing for divorce, making a legal claim against someone.")

(=>
   (instance ?ACTION LegalAction)
   (exists (?REGISTER)
      (and
         (instance ?REGISTER Registering)
         (subProcess ?REGISTER ?ACTION))))

(subclass Answering Stating)
(documentation Answering EnglishLanguage "Responding to a &%Questioning, i.e. trying to answer 
someone's question.")

(=>
   (instance ?ANSWER Answering)
   (exists (?QUESTION)
      (and
         (instance ?QUESTION Questioning)
         (refers ?ANSWER ?QUESTION)
         (earlier (WhenFn ?QUESTION) (WhenFn ?ANSWER)))))

(subclass Arguing Stating)
(documentation Arguing EnglishLanguage "Any &%Stating which has the form of an &%Argument.")

(=>
   (instance ?ARGUE Arguing)
   (exists (?STATEMENT ?ARGUMENT)
      (and
         (patient ?ARGUE ?STATEMENT)
         (instance ?STATEMENT Statement)
         (containsInformation ?STATEMENT ?ARGUMENT)
         (instance ?ARGUMENT Argument))))

(subclass TellingALie Stating)
(documentation TellingALie EnglishLanguage "Any &%Stating which is both &%False and believed 
to be &%False by the &%agent of the &%Stating.")

(=>
   (and
      (instance ?STATE TellingALie)
      (agent ?STATE ?AGENT)
      (patient ?STATE ?STATEMENT))
   (holdsDuring
      (WhenFn ?STATE)
      (believes ?AGENT (truth ?STATEMENT False))))

(subclass Founding Declaring)
(subclass Founding OrganizationalProcess)
(documentation Founding EnglishLanguage "Setting up an &%Organization.")

(=>
   (instance ?FOUND Founding)
   (exists (?ORG)
      (and
         (instance ?ORG Organization)
         (result ?FOUND ?ORG)))) 

(documentation yearOfFounding EnglishLanguage "(yearOfFounding ?ORG ?NUMBER) means that the &%Organization 
?ORG was founded in the year expressed in ?NUMBER.")
(instance yearOfFounding BinaryPredicate)
(instance yearOfFounding AsymmetricRelation)
(domain yearOfFounding 1 Organization)
(domain yearOfFounding 2 Integer)

(=>
  (yearOfFounding ?O ?Y)
  (dateEstablished ?O 
    (YearFn ?Y)))

(<=>
  (yearOfFounding ?O ?Y)
  (exists (?F)
    (and
      (overlapsTemporally ?F (YearFn ?Y))
      (instance ?F Founding)
      (result ?F ?O))))

(subclass Accrediting Declaring)
(documentation Accrediting EnglishLanguage "Any process of certifying an &%EducationalOrganization.")
(=>
   (and
      (instance ?A Accrediting)
      (patient ?A ?EO))
   (instance ?EO EducationalOrganization))

(subclass Indicating Communication)
(subclass Indicating BodyMotion)
(documentation Indicating EnglishLanguage "Pointing out a person, place or thing with 
one's hand or with an &%Artifact.")

(subclass Divorcing Declaring)
(documentation Divorcing EnglishLanguage "A legal act whereby a marriage is dissolved.  This 
includes annulments.")
(=>
  (instance ?D Divorcing)
  (exists (?P1 ?P2 ?T1 ?T2)
    (and
      (holdsDuring ?T1 
        (spouse ?P1 ?P2))
      (holdsDuring ?T2 
        (not (spouse ?P1 ?P2)))
      (finishes ?T1 ?D)
      (starts ?T2 ?D))))

(subclass Appointing Declaring)
(documentation Appointing EnglishLanguage "Any instance of &%Declaring by which the 
patient is assigned to a &%Position within an &%Organization where 
the patient previously had no position.  For example, the appointments 
of people to non-elective offices in a government.")
(=>
  (and
    (instance ?A Appointing)
    (patient ?A ?H))
  (exists (?P ?ORG)
    (and
      (holdsDuring 
        (BeginFn (WhenFn ?A)) 
        (not 
          (occupiesPosition ?H ?P ?ORG)))
      (holdsDuring 
        (EndFn (WhenFn ?A)) 
        (occupiesPosition ?H ?P ?ORG)))))

(=>
  (instance ?A Appointing)
  (not
    (exists (?H)
      (and
        (agent ?A ?H) 
        (instance ?H Public)))))

(subclass TurningOffDevice InternalChange)
(documentation TurningOffDevice EnglishLanguage "Any &%Process whose result is that the patient 
of the process is in the state of &%DeviceOff.  In other words, this class covers 
any process of turning off a device, e.g. turning off the lights, switching off a 
television set, etc.")

(=>
  (and
    (instance ?P TurningOffDevice)
    (patient ?P ?D))
  (and
    (holdsDuring 
      (BeginFn (WhenFn ?P)) 
      (attribute ?D DeviceOn))
    (holdsDuring 
      (EndFn (WhenFn ?P)) 
      (attribute ?D DeviceOff))))

(=>
  (and
    (instance ?P TurningOffDevice)
    (patient ?P ?D))
  (instance ?D Device))

(subclass TurningOnDevice InternalChange)
(documentation TurningOnDevice EnglishLanguage "Any &%Process whose result is that the patient 
of the process is in the state of &%DeviceOn.  In other words, this class covers 
any process of turning on a device, e.g. turning on the lights, switching on a 
television set, etc.")

(=>
  (and
    (instance ?P TurningOnDevice)
    (patient ?P ?D))
  (and
    (holdsDuring 
      (BeginFn (WhenFn ?P)) 
      (attribute ?D DeviceOff))
    (holdsDuring 
      (EndFn (WhenFn ?P)) 
      (attribute ?D DeviceOn))))

(=>
  (and
    (instance ?P TurningOnDevice)
    (patient ?P ?D))
  (instance ?D Device))

(subclass Projectile Weapon)
(documentation Projectile EnglishLanguage "A missile, bullet, etc. that is fired from 
a &%Weapon.")

(=>
   (instance ?PROJECTILE Projectile)
   (capability Shooting patient ?PROJECTILE))

(subclass ProjectileShell Container)
(documentation ProjectileShell EnglishLanguage "The outer casing of a &%Projectile.")

(=>
   (instance ?PROJECTILE Projectile)
   (exists (?SHELL)
      (and
         (instance ?SHELL ProjectileShell)
         (part ?SHELL ?PROJECTILE))))

(subclass ProjectileLauncher Weapon)
(documentation ProjectileLauncher EnglishLanguage "A &%Weapon that shoots a &%Projectile.")

(=>
  (instance ?L ProjectileLauncher)
  (capability Shooting instrument ?L))

(=>
  (instance ?S Shooting)
  (exists (?P ?L)
    (and
      (instance ?P Projectile)
      (patient ?S ?P)
      (instance ?L ProjectileLauncher)
      (instrument ?S ?L))))

(subclass Spear Weapon)
(documentation Spear EnglishLanguage "A &%Weapon with a long handle and a short 
blade.")

(subclass Sword Weapon)
(documentation Sword EnglishLanguage "A &%Weapon with a long blade and covered 
handle.")

(subclass Whip Weapon)
(documentation Whip EnglishLanguage "A &%Weapon that consists of a thin strand of 
&%Fabric, usually &%Leather, and a handle by which the strand is 
impelled at a high rate of speed.")

(subclass Gun Weapon)
(subclass Gun ProjectileLauncher)
(partition Gun ArtilleryGun Firearm)
(documentation Gun EnglishLanguage "A &%Weapon that shoots a &%Projectile.")

(=>
   (instance ?GUN Gun)
   (capability Shooting instrument ?GUN))

(=>
   (instance ?SHOOT Shooting)
   (exists (?PROJECTILE ?GUN)
      (and
         (instance ?PROJECTILE Projectile)
         (patient ?SHOOT ?PROJECTILE)
         (instance ?GUN Gun)
         (instrument ?SHOOT ?GUN))))

(subclass MortarGun ArtilleryGun)
(documentation MortarGun EnglishLanguage "A short-range &%Gun that is positioned on the ground 
at a high angle and fires explosive shells.")

(subclass Bullet Projectile)
(documentation Bullet EnglishLanguage "A &%Projectile that is designed to be fired by a &%Gun.")
(=>
  (instance ?B Bullet)
  (hasPurpose ?B 
    (exists (?S ?GUN)
      (and
        (instance ?S Shooting)
        (instrument ?S ?GUN)
        (instance ?GUN Gun)
        (patient ?S ?B)))))

(subclass GunBarrel EngineeringComponent)
(documentation GunBarrel EnglishLanguage "The part of a &%Gun through which a &%Projectile travels 
when it is fired.")
(=>
  (instance ?B GunBarrel)
  (exists (?G)
    (and
      (instance ?G Gun)
      (part ?B ?G))))

(=>
  (and
    (instance ?G Gun)
    (instance ?B GunBarrel)
    (part ?B ?G)
    (instance ?S Shooting)
    (instrument ?S ?GUN)
    (patient ?S ?P)
    (instance ?P Projectile))
  (exists (?SUB)
    (and
      (subProcess ?SUB ?S)
      (path ?SUB ?B))))

(subclass GunPowder Mixture)
(documentation GunPowder EnglishLanguage "A &%Mixture of potassium nitrate, sulfur, and 
charcoal that is used in &%ExplosiveDevices and to propell &%Projectiles 
in &%ProjectileWeapons.")

(=>
  (instance ?P GunPowder)
  (capability Explosion instrument ?P))

(subclass GunTrigger EngineeringComponent)
(documentation GunTrigger EnglishLanguage "The part of the &%Gun which is pulled in &%Shooting 
the &%Gun.")

(subclass LoadingWeapon Inserting)
(documentation LoadingWeapon EnglishLanguage "Inserting ammunition into a &%ProjectileWeapon 
in such a way that the ammunition can be fired by the weapon.")

(=>
  (and
    (instance ?L LoadingWeapon)
    (patient ?L ?P))
  (instance ?P Projectile))

(=>
  (and
    (instance ?L LoadingWeapon)
    (destination ?L ?W))
  (instance ?W ProjectileLauncher))

(instance effectiveRange BinaryPredicate)
(instance effectiveRange SingleValuedRelation)
(subrelation effectiveRange measure)
(domain effectiveRange 1 Device)
(domain effectiveRange 2 LengthMeasure)
(documentation effectiveRange EnglishLanguage "Models the effective range of some
device that is able to move by itself (like vehicles, rockets and so
on) or move other things (like weapons).")

;; effectiveRange of weapons means the ability to damage objects

(=>
  (and
    (instance ?GUN Gun)
    (effectiveRange ?GUN ?LM)
    (distance ?GUN ?O ?LM1)
    (not
      (exists (?O2)
        (between ?O ?O2 ?GUN)))
    (lessThanOrEqualTo ?LM1 ?LM))
  (capability
    (KappaFn ?DAMAGING
      (and
        (instance ?DAMAGING Damaging)
        (patient ?DAMAGING ?O)))
     instrument ?GUN))

;; effectiveRange of weapons means the ability to kill living things

(=>
  (and
    (instance ?GUN Gun)
    (effectiveRange ?GUN ?LM)
    (distance ?GUN ?O ?LM1)
    (instance ?O Organism)
    (not
      (exists (?O2)
        (between ?O ?O2 ?GUN)))
    (lessThanOrEqualTo ?LM1 ?LM))
  (capability
    (KappaFn ?KILLING
      (and
        (instance ?KILLING Killing)
        (patient ?KILLING ?O)))
     instrument ?GUN))

(subclass GunStock EngineeringComponent)
(documentation GunStock EnglishLanguage "The part of a &%Gun that is placed against the 
shoulder to absorb some of the recoil action when it is fired.")

(=>
   (instance ?STOCK GunStock)
   (exists (?GUN)
      (and
         (instance ?GUN Gun)
         (part ?STOCK ?GUN))))

(subclass WeaponOfMassDestruction Weapon)
(documentation WeaponOfMassDestruction EnglishLanguage "Nuclear, chemical and biological weapons.  
What these weapons have in common is that they are designed to kill large numbers 
of people indiscriminately.")

(subclass RadioactiveWeapon WeaponOfMassDestruction)
(disjoint RadioactiveWeapon BiochemicalWeapon)
(documentation RadioactiveWeapon EnglishLanguage "A &%WeaponOfMassDestruction which 
achieves its effect through radioactivity, either by an explosion resulting 
from nuclear fission or by a conventional explosive device that scatters 
radioactive debris.")

(documentation RadiologicalWeapon EnglishLanguage "&%Weapons which are designed to spread 
radioactive particles over a large area by means of a conventional 
explosive device rather than a nuclear reaction.  These weapons are often 
referred to as 'dirty bombs'.")
(subclass RadiologicalWeapon RadioactiveWeapon)

(subclass NuclearWeapon ExplosiveDevice)
(subclass NuclearWeapon Weapon)
(documentation NuclearWeapon EnglishLanguage "An &%ExplosiveDevice and &%RadioactiveWeapon
which achieves its effect by means of a critical mass of a radioactive substance.")
(subclass NuclearWeapon RadioactiveWeapon)

(=>
  (instance ?W NuclearWeapon)
  (capability RadiatingNuclear instrument ?W))

(subclass Handle EngineeringComponent)
(documentation Handle EnglishLanguage "The part or parts of an &%Artifact that are designed to be 
held with the &%Hands when the &%Artifact is used or carried.")
(=>
  (instance ?H Handle)
  (hasPurpose 
    ?H
    (exists (?G ?D)
      (and
        (instance ?G Grabbing)
        (patient ?G ?H)
        (instrument ?G ?D)
        (instance ?D Hand)))))

(subclass Shield Artifact)
(documentation Shield EnglishLanguage "An &%Artifact that is held by the hand or whole arm and 
is used to prevent injuries from &%Weapons.")

(=>
  (instance ?S Shield)
  (hasPurpose ?S (prevents ?S Injuring)))

(=>
  (instance ?S Shield)
  (exists (?H)
    (and
      (instance ?H Handle)
      (part ?H ?S)))) 

(subclass Mast Artifact)
(documentation Mast EnglishLanguage "A large pole in the center of a &%WaterVehicle which is 
used to support a sail.")

(=>
   (instance ?M Mast)
   (exists (?W)
      (and
         (instance ?W WaterVehicle)
         (part ?M ?W))))

(subclass Rotor EngineeringComponent)
(documentation Rotor EnglishLanguage "The &%Rotating component of a motor, generator or similar 
&%Device.")

(=>
  (instance ?R Rotor)
  (capability Rotating instrument ?R))

(subclass WingDevice EngineeringComponent)
(documentation WingDevice EnglishLanguage "The wings of &%Aircraft, i.e. the parts of &%Aircraft 
that allow them to become and remain airborne.")

(=>
  (instance ?W WingDevice)
  (exists (?A)
    (and
      (instance ?A Aircraft)
      (part ?W ?A))))

(subclass Tube Artifact)
(documentation Tube EnglishLanguage "A long, narrow, and hollow &%Artifact that is designed for 
moving &%Fluids from place to another.")

(=>
  (instance ?T Tube)
  (hasPurpose ?T 
    (exists (?P ?OBJ)
      (and
        (instance ?P Transfer)
        (path ?P ?T)
        (patient ?P ?OBJ)
        (attribute ?OBJ Fluid)))))

(subclass PhotographicFilm Artifact)
(documentation PhotographicFilm EnglishLanguage "Celluloid covered with an emulsion which can 
be converted into photographic negatives by a &%Camera.")

(subclass PictureFrame Artifact)
(documentation PictureFrame EnglishLanguage "A frame which surrounds a &%PaintedPicture or 
&%Photograph and has the function of protecting and accenting the picture.")

(=>
  (instance ?F PictureFrame)
  (hasPurpose ?F 
    (exists (?P)
      (and
        (meetsSpatially ?F ?P)
        (or
          (instance ?P PaintedPicture)
          (instance ?P Photograph))))))

(subclass Shelf Artifact)
(documentation Shelf EnglishLanguage "An piece of &%Furniture or part of a piece of &%Furniture 
that is used for keeping or displaying things.")

(=>
  (instance ?S Shelf)
  (exists (?F)
    (and
      (instance ?F Furniture)
      (part ?S ?F))))

(subclass AutomaticGun Gun)
(documentation AutomaticGun EnglishLanguage "A &%Gun that fires a burst of &%Projectiles 
with each pull of the trigger.  Also known as a machine gun.")
(=>
  (and
    (instance ?G AutomaticGun)
    (instance ?P Pulling)
    (patient ?T GunTrigger)
    (part ?T ?G))
  (exists (?S1 ?S2)
    (and
      (instance ?S1 Shooting)
      (instance ?S2 Shooting)
      (instrument ?S1 ?G)
      (instrument ?S2 ?G)
      (not (equal ?S1 ?S2))
      (causes ?P ?S1)
      (causes ?P ?S2))))

(subclass ArtilleryGun Gun)
(documentation ArtilleryGun EnglishLanguage "A &%Gun that is too large to be carried and fired 
by a single &%Human.  Typically, &%ArtilleryGuns are on wheels.") 

(subclass Firearm Gun)
(partition Firearm Rifle Pistol)
(documentation Firearm EnglishLanguage "A &%Gun that is small enough to be carried and fired by 
a single &%Human.")

(subclass Rifle Firearm)
(documentation Rifle EnglishLanguage "A &%Firearm with a long barrel that is intended to be fired 
from the shoulder.")

(subclass Pistol Firearm)
(documentation Pistol EnglishLanguage "A &%Firearm that is intended to be aimed and fired with a 
single hand.")

(subclass RevolverGun Pistol)
(documentation RevolverGun EnglishLanguage "A &%Pistol whose magazine is a revolving cylinder with 
six chambers for &%Bullets.")

(instance ContainerEmpty RelationalAttribute)
(documentation ContainerEmpty EnglishLanguage "&%ContainerEmpty is the &%Attribute of a 
&%Container that is empty.")

(=>
  (attribute ?C ContainerEmpty)
  (instance ?C Container))

(=>
  (attribute ?C ContainerEmpty)
  (not 
    (exists (?H ?S)
      (and 
        (instance ?H Hole)
        (hole ?H ?C)
        (partiallyFills ?S ?C)))))

(instance ContainerFull RelationalAttribute)
(contraryAttribute ContainerFull ContainerEmpty)
(documentation ContainerFull EnglishLanguage "&%ContainerFull is the &%Attribute of a 
&%Container that is full to capacity.")

(=>
  (attribute ?C ContainerFull)
  (instance ?C Container))

(=>
  (attribute ?C ContainerFull)
  (exists (?H ?S)
    (and 
      (instance ?H Hole)
      (hole ?H ?C)
      (fills ?S ?C))))

(subclass Holster Container)
(documentation Holster EnglishLanguage "A &%Container for a &%Pistol.  It is generally made of 
Leather and worn on either the shoulder or the waist.")

(=>
  (instance ?H Holster)
  (hasPurpose ?H 
    (exists (?P)
      (and
        (instance ?P Pistol)
        (contains ?H ?P)))))

(subclass Wastebasket Container)
(documentation Wastebasket EnglishLanguage "A &%Container which is used for trash.")

(subclass ArrowProjectile Projectile)
(documentation ArrowProjectile EnglishLanguage "A long, thin &%Projectile with a pointed tip is 
fired from a bow.")

(subclass DriveComponent EngineeringComponent)
(documentation DriveComponent EnglishLanguage "An &%EngineeringComponent whose purpose is to 
transfer force from one part of a &%Device to another part.")

(subclass PetroleumProduct MiningProduct)
(subclass PetroleumProduct Oil)
(documentation PetroleumProduct EnglishLanguage "&%PetroleumProduct is a broad class that 
includes both crude oil (Petroleum) and &%RefinedPetroleumProducts.")
(=>
  (instance ?PP PetroleumProduct)
  (exists (?O)
    (and
      (instance ?O OrganicCompound)
      (part ?O ?PP))))

(subclass FossilFuel Fuel)
(subclass FossilFuel PetroleumProduct)
(documentation FossilFuel EnglishLanguage "&%FossilFuel is the subclass of &%Fuel whose 
instances are derived from fossilized organic deposits, such as &%Coal 
and &%LiquefiedPetroleumGas.")

(subclass Fuel Substance)
(documentation Fuel EnglishLanguage "Fuel is the class of &%Substances that can be 
used as resources in Combustion processes in order to produce heat.  
Mechanical energy can be produced by burning fuel in an &%Engine.")
(=>
  (instance ?X FossilFuel)
  (attribute ?X Flammable))

(subclass Wax Substance)
(documentation Wax EnglishLanguage "Any &%Substance of high molecular weight that resembles 
beeswax.")

(subclass LiquefiedPetroleumGas FossilFuel)
(subclass LiquefiedPetroleumGas RefinedPetroleumProduct)
(documentation LiquefiedPetroleumGas EnglishLanguage "&%LiquefiedPetroleumGas is a 
compressed hydrocarbon gas.")

(subclass Engine Transducer)
(documentation Engine EnglishLanguage "Engine is a subclass of &%Transducer.  &%Engines 
are devices for converting some form of energy resource into mechanical 
power.")

(documentation powerPlant EnglishLanguage "(powerPlant ?GENERATOR ?THING) means that 
the &%Device ?GENERATOR is the power-producing component of the &%Artifact 
?THING which provides the energy for its operation.")
(instance powerPlant BinaryPredicate)
(instance powerPlant AsymmetricRelation)
(instance powerPlant IrreflexiveRelation)
(domain powerPlant 1 Device)
(domain powerPlant 2 Artifact)
(subrelation powerPlant component)

(=>
  (and
    (powerPlant ?G ?O)
    (instance ?O Device))
  (instance ?O SelfPoweredDevice))

(=>
  (powerPlant ?G ?O)
  (or 
    (instance ?G Engine)
    (instance ?G Battery)))

(subclass SteamEngine Engine)
(documentation SteamEngine EnglishLanguage "&%SteamEngine is the subclass of &%Engines 
that produce mechanical power from heat and steam pressure.")

(subclass Turbine Engine)
(documentation Turbine EnglishLanguage "An &%Engine which converts the kinetic energy of a moving 
&%Liquid (typically &%Water) into mechanical energy.")

(subclass Windmill Engine)
(documentation Windmill EnglishLanguage "Windmill is the subclass of &%Engines that 
produce mechanical power from &%Wind energy.")

(subclass InternalCombustionEngine Engine)
(documentation InternalCombustionEngine EnglishLanguage "&%InternalCombustionEngine is 
the subclass of &%Engines in which a heat reaction that occurs inside 
the engine is transformed into mechanical energy.")

(=>
  (instance ?R (OperatingFn InternalCombustionEngine))
  (exists (?E ?C ?F)
    (and
      (instance ?E InternalCombustionEngine)
      (instrument ?R ?E)
      (hole ?C ?E)
      (instance ?F Combustion)
      (completelyFills ?F ?C))))

(subclass Manifold EngineeringComponent)
(documentation Manifold EnglishLanguage "A pipe which has several outlets for other pipes that flow 
into or out of it.")

(subclass ElectricMotor Engine)
(subclass ElectricMotor ElectricDevice)
(documentation ElectricMotor EnglishLanguage "&%ElectricMotor is the subclass of &%Engines 
that produce mechanical power from electricity.")

(subclass SwitchDevice EngineeringComponent)
(documentation SwitchDevice EnglishLanguage "An &%EngineeringComponent which is capable of turning 
an &%ElectricDevice on and off.")

(=>
   (instance ?DEVICE SwitchDevice)
   (exists (?PROC1 ?PROC2 ?ELECTRIC)
      (and
         (instrument ?PROC1 ?DEVICE)
         (causes ?PROC1 ?PROC2)
         (instrument ?PROC2 ?ELECTRIC)
         (instance ?ELECTRIC ElectricDevice))))

(subclass Microphone ElectricDevice)
(subclass Microphone CommunicationDevice)
(documentation Microphone EnglishLanguage "A &%CommunicationDevice that converts sound 
into electrical energy.")

(subclass ReceiverDevice CommunicationDevice)
(subclass ReceiverDevice ElectricDevice)
(documentation ReceiverDevice EnglishLanguage "An &%ElectricDevice that is capable of 
receiving and decoding &%RadioEmissions, e.g. &%Radios and &%Televisions.")

(=>
  (and
    (instance ?E RadioEmission)
    (destination ?E ?D))
  (instance ?D ReceiverDevice))

(subclass Telegraph ElectricDevice)
(subclass Telegraph CommunicationDevice)
(documentation Telegraph EnglishLanguage "A &%Device that permits &%LinguisticCommunication 
between remote points by means of a code of aural dots and dashes that can 
be converted into letters of an alphabet.")

(subclass ProjectionScreen CommunicationDevice)
(documentation ProjectionScreen EnglishLanguage "A &%CommunicationDevice upon which images 
are projected so that they can be viewed.")

(subclass Pencil WritingDevice)
(documentation Pencil EnglishLanguage "A &%WritingDevice that consists of an encased piece of 
graphite.")

(subclass DeviceStateAttribute DeviceAttribute)
(documentation DeviceStateAttribute EnglishLanguage "&%DeviceStateAttribute is the class 
of attributes that represent different states that a Device may be in.  
Examples: &%DeviceOff, &%DeviceOn, &%DeviceOpen, and &%DeviceClosed.")

(instance DeviceClosed DeviceStateAttribute)
(instance DeviceClosed RelationalAttribute)
(instance DeviceOff DeviceStateAttribute)
(instance DeviceOff InternalAttribute)
(instance DeviceOn DeviceStateAttribute)
(instance DeviceOn InternalAttribute)
(instance DeviceOpen DeviceStateAttribute)
(instance DeviceOpen RelationalAttribute)

(documentation deviceState EnglishLanguage "(deviceState ?DEVICE ?STATE) means that 
the &%Object ?DEVICE is in the &%DeviceStateAttribute ?STATE.")
(instance deviceState BinaryPredicate)
(domain deviceState 1 Object)
(domain deviceState 2 DeviceStateAttribute)
(subrelation deviceState attribute)

(subclass Aerator Device)
(documentation Aerator EnglishLanguage "A &%Device whose purpose is to mix &%Substances with 
&%Air.")

(=>
   (instance ?AERATOR Aerator)
   (hasPurpose ?AERATOR (exists (?COMBINE ?AIR)
                           (and
                              (instance ?COMBINE Combining)
                              (resource ?COMBINE ?AIR)
                              (instance ?AIR Air)
                              (instrument ?COMBINE ?AERATOR)))))

(subclass Aerating Combining)
(documentation Aerating EnglishLanguage "Combining a substance with &%Air.")
(=>
   (instance ?AERATE Aerating)
   (exists (?AIR ?S)
      (and
         (patient ?AERATE ?AIR)
         (instance ?AIR Air)
         (patient ?AERATE ?S)
         (attribute ?S Liquid))))

(subclass Broom BrushOrComb)
(documentation Broom EnglishLanguage "A &%BrushOrComb whose purpose is to remove dirt and other 
small particles from floors.")

(=>
  (instance ?B Broom)
  (hasPurpose ?B 
    (exists (?S)
      (and
        (instance ?S Sweeping)
        (instrument ?S ?B)))))

(subclass BrushOrComb Device)
(documentation BrushOrComb EnglishLanguage "A &%Device which consists of a handle and bristles and 
whose purpose is to remove particles from something or to smooth something out.")

(=>
  (instance ?B BrushOrComb)
  (hasPurpose ?B 
    (exists (?S)
      (and
        (or
          (instance ?S Removing)
          (instance ?S SurfaceChange))
        (instrument ?S ?B)))))

(subclass MatchDevice Device)
(documentation MatchDevice EnglishLanguage "A small stick of carboard or wood that is treated 
with chemicals that can be easily ignited with friction.  MatchDevices are 
used for starting fires.")

(=>
  (instance ?M MatchDevice)
  (capability (StartFn Combustion) instrument ?M))

(subclass VendingDevice Device)
(documentation VendingDevice EnglishLanguage "A &%Device which is capable of &%Selling a &%Product 
to a customer automatically when the customer inserts the appropriate amount of 
Currency.")

(=>
  (instance ?VD VendingDevice)
  (hasPurpose ?VD 
    (exists (?T ?C ?P)
      (and
        (instance ?T FinancialTransaction)
        (instrument ?T ?VD)
        (patient ?T ?C)
        (instance ?C Currency)
        (patient ?T ?P)
        (instance ?P Product)))))

(subclass WritingDevice Device)
(documentation WritingDevice EnglishLanguage "A &%Device whose purpose is to be an instrument 
of &%Writing, e.g. pens, pencils, crayons, etc.")

(=>
  (instance ?D WritingDevice)
  (hasPurpose ?D 
    (exists (?W)
      (and
        (instance ?W Writing)
        (instrument ?W ?D)))))

(subclass SmokingDevice Device)
(documentation SmokingDevice EnglishLanguage "Any Device whose purpose is &%Smoking.")

(=>
  (instance ?D SmokingDevice)
  (hasPurpose ?D 
    (exists (?S) 
      (and 
        (instance ?S Smoking) 
        (instrument ?S ?D)))))

(subclass SmokingPipe SmokingDevice)
(documentation SmokingPipe EnglishLanguage "A &%SmokingDevice consisting of a tube and a small 
bowl.")

(subclass Toilet Device)
(documentation Toilet EnglishLanguage "A &%Device for the disposal of wastes resulting from
urination and defecation.")

(=>
  (and
    (located ?T ?R)
    (instance ?T Toilet)
    (instance ?R Room))
  (instance ?R Bathroom))

(subclass Toothbrush BrushOrComb)
(documentation Toothbrush EnglishLanguage "A small &%BrushOrComb with relatively soft
bristles and a long handle, used for brushing teeth (see &%Tooth).  It is
used help control bacteria living in the mouth.  While most typically used
by humans on themselves, it can be used by &%Dentists on their
patients and by adults assisting children.  They are occasionally used by
humans caring for pets, zoo animals and valuable livestock.")
(=>
  (instance ?T Toothbrush)
  (hasPurpose ?T
    (exists (?K)
      (and
        (instance ?TOOTH Tooth)
        (instrument ?K ?T)
        (instance ?K Killing)
        (instance ?B Bacterium)
        (patient ?K ?B)
        (patient ?K ?TOOTH)))))

(subclass CigarOrCigarette SmokingDevice)
(documentation CigarOrCigarette EnglishLanguage "A tube of thin paper containing finely ground 
tobacco that is smoked.")
(=>
  (instance ?C CigarOrCigarette)
  (exists (?T)
    (and
      (instance ?T Tobacco)
      (part ?T ?C))))

(subclass Tableware Device)
(documentation Tableware EnglishLanguage "&%Devices that are used in &%Ingesting (&%Eating 
and/or &%Drinking) a meal.  This coves dishware, flatware, and glassware.")

(=>
  (instance ?T Tableware)
  (hasPurpose ?T 
    (exists (?I)
      (and
        (instance ?I Ingesting)
        (instrument ?I ?T)))))

(subclass Compass Device)
(subclass Compass ContentBearingObject)
(documentation Compass EnglishLanguage "A &%Device that indicates the direction of the
various &%DirectionalAttributes with respect to the device.")
(=>
  (instance ?C Compass)
  (exists (?D)
    (and
      (instance ?D DirectionalAttribute)
      (represents ?C ?D))))

(subclass CoolingDevice Device)
(documentation CoolingDevice EnglishLanguage "A &%Device whose purpose is &%Cooling something, 
e.g. air conditioners, refrigerators, freezers, etc.")
(=>
  (instance ?D CoolingDevice)
  (hasPurpose ?D 
    (exists (?C)
      (and
        (instance ?C Cooling)
        (instrument ?C ?D))))) 

(subclass FanDevice CoolingDevice)
(documentation FanDevice EnglishLanguage "A &%CoolingDevice which consists simply of one or more 
blades that circulate the air.  Note that this class covers both electrical fans 
and fans that are manually operated.")
(=>
  (instance ?F FanDevice)
  (hasPurpose ?F 
    (exists (?W)
      (and
        (instance ?W Wind)
        (instrument ?W ?F)))))

(subclass HeatingDevice Device)
(documentation HeatingDevice EnglishLanguage "A &%Device whose purpose is &%Heating something, 
e.g. electric heaters, heat lamps, ovens, stoves, etc.")

(=>
  (instance ?D HeatingDevice)
  (hasPurpose ?D 
    (exists (?H)
      (and
        (instance ?H Heating)
        (instrument ?H ?D)))))

(subclass Oven HeatingDevice)
(subclass Oven Container)
(documentation Oven EnglishLanguage "A &%HeatingDevice with a door for inserting and removing 
&%Food that is to undergo &%Baking.")

(=>
  (instance ?O Oven)
  (hasPurpose ?O 
  (exists (?C)
    (and
      (instance ?C Cooking)
      (instrument ?C ?O)))))

(subclass Stove HeatingDevice)
(documentation Stove EnglishLanguage "A &%HeatingDevice which consists one or more burners for 
heating pots and pans of &%Food.")

(=>
  (instance ?S Stove)
  (hasPurpose ?S 
    (exists (?C)
      (and
        (instance ?C Cooking)
        (instrument ?C ?S)))))

(subclass Refrigerator CoolingDevice)
(subclass Refrigerator ElectricDevice)
(subclass Refrigerator Container)
(documentation Refrigerator EnglishLanguage "The intersection of &%Containers and &%ElectricDevices 
in which the temperature is reduced from that of the outside air by a &%Cooling 
process.")

(subclass PotOrPan Container)
(documentation PotOrPan EnglishLanguage "A &%Container which has a handle and is used for 
&%Cooking.")

(=>
  (instance ?P PotOrPan)
  (hasPurpose ?P 
    (exists (?C)
      (and
        (instance ?C Cooking)
        (instrument ?C ?P)))))

(subclass Ramp SelfConnectedObject)
(documentation Ramp EnglishLanguage "Ramp is the class of &%SelfConnectedObjects that are 
inclined planes used for moving objects from one level to another, 
especially used for wheeled vehicles and people who cannot climb stairs.")

(=>
  (instance ?R Ramp)
  (capability MotionUpward path ?R))

(=>
  (instance ?R Ramp)
  (capability MotionDownward path ?R))

(subclass RecreationOrExerciseDevice Device)
(documentation RecreationOrExerciseDevice EnglishLanguage "A &%Device whose purpose is &%RecreationOrExercise.")

(=>
  (instance ?D RecreationOrExerciseDevice)
  (hasPurpose ?D 
    (exists (?E)
      (and
        (instance ?E RecreationOrExercise)
        (instrument ?E ?D)))))

(subclass WalkingCane Device)
(documentation WalkingCane EnglishLanguage "A &%Device which has the form of a staff with 
a handle and which enables some people to walk with greater assurance.")

(=>
  (instance ?C WalkingCane)
  (hasPurpose ?C 
    (exists (?W)
      (and
        (instance ?W Walking)
        (instrument ?W ?C)))))

(subclass WashingDevice Device)
(documentation WashingDevice EnglishLanguage "Any &%Device whose purpose is &%Washing 
something, e.g. washing machines, dishwashers, bathtubs, etc.")

(=>
  (instance ?D WashingDevice)
  (hasPurpose ?D 
    (exists (?W)
      (and
        (instance ?W Washing)
        (instrument ?W ?D)))))

(subclass MaterialHandlingEquipment Device)
(documentation MaterialHandlingEquipment EnglishLanguage "&%MaterialHandlingEquipment 
is a class of &%Devices that are equipment used for handling goods and 
supplies more efficiently or safely.  Examples are cranes, hoists, 
forklifts, conveyors, racks, etc.")

(subclass Crane MaterialHandlingEquipment)
(documentation Crane EnglishLanguage "Crane is a subclass of mechanical &%Devices 
that consist of a &%HoistingDevice on a moveable boom, designed to 
assist in moving heavy loads.")

(=>
  (instance ?C Crane)
  (exists (?H)
    (and
      (instance ?H HoistingDevice)
      (component ?H ?C))))

(subclass HoistingDevice MaterialHandlingEquipment)
(documentation HoistingDevice EnglishLanguage "&%HoistingDevice is a subclass of 
mechanical &%Devices that provide a mechanical advantage in lifting 
and lowering, e.g., by use of a block and tackle.")

(=>
  (instance ?H HoistingDevice)
  (capability MotionUpward instrument ?H))

(=>
  (instance ?H HoistingDevice)
  (capability MotionDownward instrument ?H))

(subclass OpticalDevice Device)
(documentation OpticalDevice EnglishLanguage "A &%Device which enables someone to see something 
more clearly or with greater magnification.")

(=>
  (instance ?D OpticalDevice)
  (hasPurpose ?D 
    (exists (?S)
      (and
        (instance ?S Seeing)
        (instrument ?S ?D)))))

(subclass EyeGlass OpticalDevice)
(documentation EyeGlass EnglishLanguage "A lens or pair of lenses with or without frames whose 
purpose is to to correct a defect in vision.  This class covers ordinary eye 
glasses, reading glasses, contact lenses, monocles, etc.")

(subclass Lens OpticalDevice)
(documentation Lens EnglishLanguage "An &%OpticalDevice which consists of a polished, 
transparent piece of glass or plastic.  Lenses are often part of 
other &%OpticalDevices.")

(subclass Microscope OpticalDevice)
(documentation Microscope EnglishLanguage "An &%OpticalDevice with a stand and a focus, which is 
used for magnifying the images of things that are placed in the stand.")

(subclass Filter Device)
(documentation Filter EnglishLanguage "A &%Device whose purpose is to remove part of a 
&%Solution that is passed through the &%Filter.")

(=>
  (instance ?FILTER Filter)
  (hasPurpose ?FILTER 
    (exists (?REMOVE ?SOLUTION)
      (and
        (instance ?REMOVE Removing)
        (origin ?REMOVE ?SOLUTION)
        (instance ?SOLUTION Solution)
        (instrument ?REMOVE ?FILTER)))))

(subclass FileDevice Device)
(documentation FileDevice EnglishLanguage "A &%Device whose purpose is to make something 
smoother.  For example, a nail file is used to even out the tips of one's 
finger nails.")

(=>
  (instance ?DEVICE FileDevice)
  (capability SurfaceChange instrument ?DEVICE))

(subclass ElectricDevice Device)
(documentation ElectricDevice EnglishLanguage "A &%Device that uses &%Electricity as its
primary power source.")

(=>
  (and
    (instance ?DEV ElectricDevice)
    (instance ?EV Process)
    (patient ?EV ?DEV))
  (exists (?R)
    (and
      (instance ?R Electricity)
      (resource ?EV ?R))))

(documentation PowerSource EnglishLanguage "A source of electrical power.")
(termFormat EnglishLanguage PowerSource "power source")
(subclass PowerSource Device)
(=>
  (instance ?PS PowerSource)
  (exists (?RE)
    (and
      (instance ?RE RadiatingElectromagnetic)
      (origin ?RE ?PS))))

(documentation DCPowerSource EnglishLanguage "A direct-current &%PowerSource.")
(termFormat EnglishLanguage DCPowerSource "DC power source")
(subclass DCPowerSource PowerSource)

(subclass Battery DCPowerSource)
(subclass Battery ElectricDevice)
(documentation Battery EnglishLanguage "Battery is a subclass of &%Device.  Batteries are 
devices that use chemical means to store or produce electrical power.")

(subclass Radar ElectricDevice)
(documentation Radar EnglishLanguage "An &%ElectricDevice that emits and receives microwave 
radiation for the purpose of locating and tracking distant objects.")

(=>
  (instance ?RADAR Radar)
  (hasPurpose ?RADAR 
    (exists (?DISCOVER)
      (and
        (instance ?DISCOVER Discovering)
        (instrument ?DISCOVER ?RADAR)))))

(=>
  (instance ?RADAR Radar)
  (capability RadiatingElectromagnetic agent ?RADAR))

(subclass Sonar CorpuscularObject)
(documentation Sonar EnglishLanguage "Something that emits and receives sound 
for the purpose of locating and tracking distant objects.  Note that
this covers both manmade devices and &%BodyParts such as the sonar
of bats and dolphins.")

(=>
  (instance ?SONAR Sonar)
  (hasPurpose ?SONAR 
    (exists (?DISCOVER)
      (and
        (instance ?DISCOVER Discovering)
        (instrument ?DISCOVER ?SONAR)))))

(=>
  (instance ?SONAR Sonar)
  (capability RadiatingSound agent ?SONAR))

(subclass Echoing RadiatingSound)
(documentation Echoing EnglishLanguage "Instances of &%RadiatingSound where the instrument 
is a surface which bounces sound waves back to their origin, where they can 
be heard again.")

(subclass ElectricalSignalling Signalling)
(documentation ElectricalSignalling EnglishLanguage "&%ElectricalSignalling is the 
subclass of &%Signalling processes that involve control of an electrical 
current.  Cf. &%ElectronicSignalling.")
(=>
  (instance ?S ElectricalSignalling)
  (exists (?D)
    (and
      (instance ?D ElectricDevice)
      (instrument ?S ?D))))

(subclass ElectronicSignalling Signalling)
(documentation ElectronicSignalling EnglishLanguage "&%ElectronicSignalling is the 
subclass of &%Signalling processes that involve a signal generated by 
a computer.")
(=>
  (instance ?S ElectronicSignalling)
  (exists (?D)
    (and
      (instance ?D Computer)
      (instrument ?S ?D))))

(subclass SecurityDevice Device)
(documentation SecurityDevice EnglishLanguage "A &%Device whose purpose is to protect people or 
property from kidnappers and/or thieves.")

(subclass Lock SecurityDevice)
(documentation Lock EnglishLanguage "A &%Device, which, through a &%Key or a combination prevents 
access to a &%Container or &%StationaryArtifact.")

(subclass Key SecurityDevice)
(documentation Key EnglishLanguage "A &%Device which opens and closes a &%Lock.")

(=>
   (instance ?KEY Key)
   (exists (?LOCK)
      (and
         (instance ?LOCK Lock)
         (capability Opening instrument ?LOCK)
         (capability Closing instrument ?LOCK))))

(subclass SafeContainer Container)
(subclass SafeContainer SecurityDevice)
(documentation SafeContainer EnglishLanguage "A &%Container with a &%Lock which is intended 
to secure items from theft.  Note that this covers safes, lockers, and locked 
storage compartments.")

(=>
  (instance ?S SafeContainer)
  (exists (?L)
    (and
      (instance ?L Lock)
      (part ?L ?S))))

(subclass AnimalController Device)
(documentation AnimalController EnglishLanguage "Any &%Device which is used to control the 
movements of an &%Animal or &%Human, e.g. leashes, reins, harnesses, muzzles, 
bridles, shackles, handcuffs, etc.")
(=>
  (instance ?AC AnimalController)
  (hasPurpose ?AC 
    (exists (?G ?A)
      (and
        (instance ?G Guiding)
        (instrument ?G ?AC)
        (instance ?A Animal)
        (patient ?G ?A)))))
             
(subclass SelfPoweredDevice Device)
(documentation SelfPoweredDevice EnglishLanguage "&%SelfPoweredDevice is 
the subclass of &%Devices whose action is powered by some kind of on-board 
component or power source (not the user, which would be a 
&%UserPoweredVehicle).") 

(=>
  (instance ?DEVICE SelfPoweredDevice)
  (exists (?SOURCE)
    (and
      (instance ?SOURCE Device)
      (powerPlant ?DEVICE ?SOURCE))))

(subclass AnimalPoweredDevice Device)
(documentation AnimalPoweredDevice EnglishLanguage "&%AnimalPoweredDevice is the subclass 
of &%Devices that function with power supplied by animals.  Examples: 
oxcarts, horse-drawn plows, mule-driven mills.")
(=>
  (and
    (instance ?D AnimalPoweredDevice)
    (instrument ?P ?D))
  (exists (?A)
    (and
      (instance ?A Animal)
      (instrument ?P ?A))))

(documentation Flat EnglishLanguage "A three dimensional object for which
two dimensions are markedly larger than the third the
two larger dimensions are also not of markedly different length.")
(instance Flat ShapeAttribute)
(=>
  (attribute ?F Flat)
  (and
    (side ?S1 ?F)
    (side ?S2 ?F)
    (meetsSpatially ?S1 ?S2)
    (not (equal ?S1 ?S2))
    (height ?F ?H)
    (greaterThan ?S1 (MultiplicationFn 2 ?H))
    (greaterThan ?S2 (MultiplicationFn 2 ?H))))

(documentation LongAndThin EnglishLanguage "A three dimensional object that
has two thin dimensions and one markedly larger one.") 
(instance LongAndThin ShapeAttribute)
(=>
  (attribute ?F LongAndThin)
  (and
    (side ?S1 ?F)
    (side ?S2 ?F)
    (meetsSpatially ?S1 ?S2)
    (not (equal ?S1 ?S2))
    (length ?F ?H)
    (lessThan ?S1 (MultiplicationFn 3 ?H))
    (lessThan ?S2 (MultiplicationFn 3 ?H))))

(instance LevelShape ShapeAttribute)
(documentation LevelShape EnglishLanguage "The &%ShapeAttribute of &%Objects where no 
part of the top of the &%Object is higher than any other part of the 
top.")

(=>
  (attribute ?O LevelShape)
  (not 
    (exists (?P1 ?P2 ?T ?H1 ?H2)
      (and
        (top ?T ?O)
        (part ?P1 ?T)
        (part ?P2 ?T)
        (height ?P1 ?H1)
        (height ?P2 ?H2)
        (greaterThan ?H1 ?H2)))))

(instance SymmetricShape ShapeAttribute)
(documentation SymmetricShape EnglishLanguage "A &%ShapeAttribute that applies to a 
&%SelfConnectedObject that can be divided into two copies of each other.")

(=>
  (attribute ?T SymmetricShape)
  (exists (?C1 ?C2)
    (and
      (copy ?C1 ?C2)
      (equal ?T 
        (MereologicalSumFn ?C1 ?C2)))))

(subclass Baton Device)
(documentation Baton EnglishLanguage "A thin stick which is used for &%OrchestralConducting.")
(=>
  (instance ?B Baton)
  (hasPurpose ?B 
    (exists (?IM ?G)
      (and
        (instance ?IM InstrumentalMusic)
        (instance ?G Guiding)
        (instrument ?G ?B)
        (causes ?G ?IM)))))

(=>
  (instance ?B Baton)
  (attribute ?B LongAndThin))

(subclass SecurityAlarm SecurityDevice)
(subclass SecurityAlarm ElectricDevice)
(documentation SecurityAlarm EnglishLanguage "A &%SecurityDevice that detects intrusions to 
a &%StationaryArtifact and issues a warning of some sort.")

(partition Quantity FiniteQuantity InfiniteQuantity)

(subclass FiniteQuantity Quantity)
(documentation FiniteQuantity EnglishLanguage "Any &%Quantity that is limited or bounded in 
magnitude.")

(subclass InfiniteQuantity Quantity)
(documentation InfiniteQuantity EnglishLanguage "Any &%Quantity that is not limited or bounded 
in magnitude.")

(subclass Clock MeasuringDevice)
(documentation Clock EnglishLanguage "Any &%Device that measures and represents &%TimeDuration 
or &%TimePosition.")

(=>
  (and
    (instance ?M Measuring)
    (result ?M ?Q)
    (instrument ?M ?C)
    (instance ?C Clock))
  (instance ?Q TimeMeasure))

(subclass WatchClock Clock)
(documentation WatchClock EnglishLanguage "A &%Clock that can be worn on the &%Wrist.")

(=>
  (and
    (wears ?P ?C)
    (located ?C ?W)
    (instance ?C Clock)
    (instance ?W Wrist))
  (instance ?C WatchClock))

(subclass Thermometer MeasuringDevice)
(documentation Thermometer EnglishLanguage "Any &%Device that measures and represents 
&%TemperatureMeasure.")

(=>
   (and
      (instance ?MEASURE Measuring)
      (result ?MEASURE ?QUANTITY)
      (instrument ?MEASURE ?THERMOMETER)
      (instance ?THERMOMETER Thermometer))
   (instance ?QUANTITY TemperatureMeasure))

(subclass PressureMeasuringDevice MeasuringDevice)
(documentation PressureMeasuringDevice EnglishLanguage "Any &%Device that measures and 
represents &%PressureMeasure.")

(=>
   (and
      (instance ?M Measuring)
      (result ?M ?Q)
      (instrument ?M ?T)
      (instance ?T Thermometer))
   (instance ?Q TemperatureMeasure))

(documentation measurementReading EnglishLanguage "(measurementReading ?DEVICE ?QUANTITY) 
means that ?QUANTITY is a reading of the &%MeasuringDevice ?DEVICE, e.g. if 
?THERMOMETER is a &%Thermometer, (measurementReading ?THERMOMETER 
(&%MeasureFn 42 &%CelsiusDegree)) would mean that ?THERMOMETER registers 42 
degrees &%Celsius.")
(instance measurementReading BinaryPredicate)
(domain measurementReading 1 MeasuringDevice)
(domain measurementReading 2 ConstantQuantity)

(subclass Telephone ElectricDevice)
(subclass Telephone CommunicationDevice)
(documentation Telephone EnglishLanguage "A &%Device that permits &%LinguisticCommunication 
between remote points by converting sound into electrical signals that are 
then transmitted.  When the signals are received, they are converted back 
into sound.")

(subclass BroadcastNetwork CommunicationSystem)
(documentation BroadcastNetwork EnglishLanguage "&%BroadcastNetwork is the subclass of 
&%CommunicationSystems consisting of &%BroadcastingStations that are linked 
electronically and managed or owned by one organization.")
(=>
  (instance ?N BroadcastNetwork)
  (forall (?M)
    (=>
      (member ?M ?N)
      (instance ?M BroadcastingStation))))

(=>
  (instance ?N BroadcastNetwork)
  (exists (?O)
    (and
      (instance ?O Organization)
      (forall (?M)
        (=>
          (member ?M ?N)
          (possesses ?O ?M))))))

(subclass LightFixture Device)
(documentation LightFixture EnglishLanguage "Any &%Device whose purpose is to be a source of 
visible light.")

(=>
  (instance ?F LightFixture)
  (capability RadiatingLight instrument ?F))

(subclass Candle LightFixture)
(documentation Candle EnglishLanguage "An &%LightFixture that consists of &%Wax and a wick, 
which is lit with a flame.")

(=>
  (instance ?CANDLE Candle)
  (material Wax ?CANDLE))

(=>
  (instance ?C Candle)
  (hasPurpose ?C
    (exists (?F)
      (and
        (instance ?F Fire)
        (resource ?F ?C)))))

(subclass Lightning WeatherProcess)
(subclass Lightning Radiating)
(documentation Lightning EnglishLanguage "A &%WeatherProcess which involves a significant 
release of electricity from a &%Cloud.")

(=>
   (instance ?L Lightning)
   (exists (?C)
      (and
         (instance ?C Cloud)
         (located ?L ?C))))

(subclass Thunder WeatherProcess)
(subclass Thunder RadiatingSound)
(documentation Thunder EnglishLanguage "Any instance of &%RadiatingSound which is caused by 
an instance of Lightning.")

(=>
  (instance ?T Thunder)
  (exists (?L)
    (and
      (instance ?L Lightning)
      (causes ?L ?T))))

(subclass VehicleLight LightFixture)
(documentation VehicleLight EnglishLanguage "Any &%LightFixture which is attached to the 
surface of a &%Vehicle and whose purpose is to illuminate that area around 
the &%Vehicle so that potential obstacles can be detected or alerted.")

(=>
  (instance ?L VehicleLight)
  (exists (?V)
    (and
      (instance ?V Vehicle)
      (part ?L ?V))))

(subclass Headlight VehicleLight)
(documentation Headlight EnglishLanguage "Any &%VehicleLight which is attached to the front 
of a &%Vehicle.")

(=>
  (instance ?L Headlight)
  (exists (?V)
    (and
      (instance ?V Vehicle)
      (part ?L (FrontFn ?V)))))

(subclass Taillight VehicleLight)
(documentation Taillight EnglishLanguage "Any &%VehicleLight which is attached to the back 
of a &%Vehicle.")

(=>
  (instance ?L Taillight)
  (exists (?V)
    (and
      (instance ?V Vehicle)
      (part ?L (BackFn ?V)))))

(subclass PaintedPicture ArtWork)
(documentation PaintedPicture EnglishLanguage "Any &%ArtWork which is produced by &%Painting.")

(=>
  (instance ?PP PaintedPicture)
  (exists (?PAINT ?PAINTING)
    (and
      (instance ?PAINT Paint)
      (instance ?PAINTING Painting)
      (resource ?PAINTING ?PAINT)
      (result ?PAINTING ?PP))))

(subclass WatercolorPicture PaintedPicture)
(documentation WatercolorPicture EnglishLanguage "Any &%PaintedPicture which is created 
with water-based paints.")

(=>
  (instance ?W WatercolorPicture)
  (exists (?WP ?P)
    (and
      (instance ?WP WatercolorPaint)
      (instance ?P Painting)
      (resource ?P ?WP)
      (result ?P ?W))))

(subclass Wallpaper ArtWork)
(documentation Wallpaper EnglishLanguage "Any decorative paper that is used to cover the 
&%Walls of &%Rooms.")

(=>
  (and
    (instance ?W Wallpaper)
    (part ?P ?W))
  (instance ?P Paper))

(=>
  (instance ?P Wallpaper)
  (hasPurpose ?P 
    (exists (?W ?R ?C)
      (and
        (instance ?W Wall)
        (part ?W ?R)
        (instance ?R Room)
        (instance ?C Covering)
        (patient ?C ?W)
        (instrument ?C ?P)))))

(subclass Sculpture ArtWork)
(documentation Sculpture EnglishLanguage "Any &%ArtWork which is not constructed on the 
two-dimensional surface of a canvas, piece of paper, etc.")

(subclass Portrait PaintedPicture)
(documentation Portrait EnglishLanguage "A &%PaintedPicture that represents someone's &%Face.")

(=>
  (instance ?P Portrait)
  (exists (?F)
    (and
      (instance ?F Face)
      (represents ?P ?F))))

(subclass Sketch ArtWork)
(documentation Sketch EnglishLanguage "Any &%ArtWork which is produced by a pencil or 
piece of charcoal.")

(subclass Collage ArtWork)
(documentation Collage EnglishLanguage "Any abstract &%ArtWork that is produced by arranging 
bits of paper or photographs.")

(subclass Folding ShapeChange)
(documentation Folding EnglishLanguage "Bending something in such a way that one part of 
it covers another part.")
(=>
  (and
    (instance ?F Folding)
    (patient ?F ?O))
  (exists (?C ?P1 ?P2)
    (and
      (instance ?C Covering)
      (subProcess ?C ?F)
      (part ?P1 ?O)
      (part ?P2 ?O)
      (instrument ?C ?P1)
      (patient ?C ?P2))))

(subclass Painting Covering)
(subclass Painting Coloring)
(documentation Painting EnglishLanguage "The application of &%Paint to a &%surface.  Note that 
this class covers both &%ArtPainting (the creation of &%PaintedPictures), as 
well as painting one's kitchen, for example.")

(=>
   (and
      (instance ?PAINT Painting)
      (patient ?PAINT ?SURFACE)
      (instrument ?PAINT ?STUFF))
   (exists (?OBJ)
      (and
         (surface ?SURFACE ?OBJ)
         (instance ?STUFF Paint))))

(subclass Paint Solution)
(documentation Paint EnglishLanguage "Any &%Solution which is capable of &%Coloring something.")

(=>
   (instance ?PAINT Paint)
   (capability Coloring instrument ?PAINT))

(subclass WatercolorPaint Paint)
(documentation WatercolorPaint EnglishLanguage "Any &%Paint which is a water-based &%Solution.")

(=>
  (instance ?P WatercolorPaint)
  (exists (?W)
    (and
      (instance ?W Water)
      (part ?W ?P))))

(subclass OilPaint Paint)
(disjoint OilPaint WatercolorPaint)
(documentation OilPaint EnglishLanguage "Any &%Paint which is an oil-based &%Solution.")

(=>
  (instance ?P OilPaint)
  (exists (?O)
    (and
      (instance ?O Oil)
      (part ?O ?P))))

(subclass OilPicture PaintedPicture)
(disjoint OilPicture WatercolorPicture)
(documentation OilPicture EnglishLanguage "Any &%OilPicture which is created 
with oil-based paints.")

(=>
  (instance ?P OilPicture)
  (exists (?O ?PAINTING)
    (and
      (instance ?O OilPaint)
      (instance ?PAINTING Painting)
      (resource ?PAINTING ?O)
      (result ?PAINTING ?P))))

(subclass ArtPainting ContentDevelopment)
(subclass ArtPainting Painting)
(documentation ArtPainting EnglishLanguage "Any &%ContentDevelopment that results in a 
&%PaintedPicture.")

(=>
   (instance ?PAINT ArtPainting)
   (exists (?PICTURE)
      (and
         (instance ?PICTURE PaintedPicture)
         (result ?PAINT ?PICTURE))))

(documentation potentialOfHydrogen EnglishLanguage "(potentialOfHydrogen ?SOLUTION ?NUMBER) 
means that the Solution ?SOLUTION has a pH value of ?NUMBER.  The ph varies 
between 0 and 14, and it is a measure of the acidity or alkalinity of ?SOLUTION.  
More precisely,  and it is the logarithm of the reciprocal of the quantity of 
&%AtomGrams of &%Hydrogen ions.")
(instance potentialOfHydrogen BinaryPredicate)
(domain potentialOfHydrogen 1 Solution)
(domain potentialOfHydrogen 2 RealNumber)

(=>
  (potentialOfHydrogen ?S ?N)
  (and
    (greaterThanOrEqualTo ?N 0)
    (lessThanOrEqualTo ?N 14)))

(subclass Tracing ContentDevelopment)
(subclass Tracing SurfaceChange)
(documentation Tracing EnglishLanguage "Any &%ContentDevelopment that results in a &%Blueprint.")

(=>
  (instance ?T Tracing)
  (exists (?P)
    (and
      (instance ?P Blueprint)
      (result ?T ?P))))

(subclass ComposingMusic ContentDevelopment)
(documentation ComposingMusic EnglishLanguage "&%ContentDevelopment which results in a 
&%MusicalComposition.")
(=>
  (instance ?C ComposingMusic)
  (exists (?M)
    (and
      (instance ?M MusicalComposition)
      (result ?C ?M)))) 

(subclass Drawing ContentDevelopment)
(subclass Drawing SurfaceChange)
(documentation Drawing EnglishLanguage "Any &%ContentDevelopment that results in a &%Sketch.")

(=>
   (instance ?DRAW Drawing)
   (exists (?SKETCH)
      (and
         (instance ?SKETCH Sketch)
         (result ?DRAW ?SKETCH))))

(subclass Focusing QuantityChange)
(documentation Focusing EnglishLanguage "Altering the relative distance of a &%Lens 
so that a visual image is sharper.")
(=>
  (instance ?F Focusing)
  (exists (?L)
    (and
      (instance ?L Lens)
      (patient ?F ?L))))

(subclass DisplayArtifact Artifact)
(documentation DisplayArtifact EnglishLanguage "Something for posting content so 
that it can be disseminated to the public.")
(=>
  (instance ?DA DisplayArtifact)
  (hasPurpose ?DA 
    (exists (?P ?D ?I)
      (and
        (instance ?P Putting)
        (instance ?D Disseminating)
        (instance ?I ContentBearingObject)
        (patient ?P ?I)
        (patient ?D ?I)
        (destination ?P ?DA)))))

(subclass VehicleWheel Wheel)
(documentation VehicleWheel EnglishLanguage "&%VehicleWheel is a class of cylindrical 
&%Devices used to move a &%RoadVehicle along a road, running either  
directly on the wheel rims or on tires attached to the wheel rims.")

(subclass Wheel Artifact)
(documentation Wheel EnglishLanguage "A circular &%Artifact which is a component of 
&%LandVehicles and of some &%Devices.")

(=>
   (instance ?VEHICLE LandVehicle)
   (exists (?WHEEL)
      (and
         (instance ?WHEEL Wheel)
         (part ?WHEEL ?VEHICLE))))

(subclass VehicleController Device)
(documentation VehicleController EnglishLanguage "Any &%Device which is used to start, stop or 
control the movements of a &%Vehicle.  This class covers steering wheels, brakes, 
acceleration pedals, airplane sticks, etc.")

(=>
  (instance ?C VehicleController)
  (exists (?V)
    (and
      (instance ?V Vehicle)
      (part ?C ?V))))

(=>
  (instance ?C VehicleController)
  (hasPurpose ?C 
    (exists (?D)
      (and
        (instance ?D Driving)
        (instrument ?D ?C)))))

(subclass SteeringWheel VehicleController)
(documentation SteeringWheel EnglishLanguage "A &%VehicleController which enables one to steer 
a &%Vehicle.")

(=>
  (instance ?W SteeringWheel)
  (capability DirectionChange instrument ?W))

(subclass VehicleBrake VehicleController)
(documentation VehicleBrake EnglishLanguage "A &%VehicleController which is capable of stopping 
the motion of a &%Vehicle.")

(=>
  (and
    (instance ?B VehicleBrake)
    (instance ?V Vehicle)
    (part ?B ?V))
  (hasPurpose ?B 
    (exists (?S)
      (and
        (instance ?S (StopFn Motion))
        (patient ?S ?V)
        (instrument ?S ?B))))) 

(subclass VehicleThrottle VehicleController)
(documentation VehicleThrottle EnglishLanguage "A &%VehicleController which controls the amount 
of &%Fuel which is supplied to the &%Engine.")

(subclass VehicleTire Artifact)
(documentation VehicleTire EnglishLanguage "A covering, usually made of rubber, for a &%Wheel.")

(=>
  (instance ?T VehicleTire)
  (exists (?W)
    (and
      (instance ?W VehicleWheel)
      (part ?T ?W))))

(subclass VehicleWindow Window)
(documentation VehicleWindow EnglishLanguage "Any &%Window which is part of a &%Vehicle, 
e.g. the &%Windshield and &%RearWindow of an &%Automobile.")

(=>
  (instance ?W VehicleWindow)
  (exists (?V)
    (and
      (instance ?V Vehicle)
      (part ?W ?V))))

(subclass Windshield VehicleWindow)
(disjoint Windshield RearWindow)
(documentation Windshield EnglishLanguage "Any &%VehicleWindow which located at the front 
of an &%Automobile.")

(=>
  (instance ?W Windshield)
  (exists (?A)
    (and
      (instance ?A Automobile)
      (part ?W (FrontFn ?A)))))

(subclass RoadVehicle LandVehicle)
(documentation RoadVehicle EnglishLanguage "The class of &%LandVehicles that are not 
&%RollingStock.")
(disjoint RoadVehicle RollingStock)

(subclass Axle Device)
(documentation Axle EnglishLanguage "Axle is a class of &%Devices each of which can 
be paired with two &%VehicleWheels to rotate and move a &%RoadVehicle.")
(=>
  (and
    (instance ?W VehicleWheel)
    (instance ?V RoadVehicle)
    (part ?W ?V))
  (exists (?A)
    (and
      (instance ?A Axle)
      (component ?A ?V)
      (connected ?W ?A))))

(documentation Paper EnglishLanguage "Paper is a flat sheet of fibers, usually produced by 
spreading a wet solution of wood particles on a flat surface and drying 
through heat and pressure.  The fibers can also be cotton or a synthetic, 
although typically the bulk of the substance is wood fibers.  This 
includes all manner of paper products that may have varying thicknesses, 
colors or texture.  This is distinguished from flat wood products such as 
plywood and particle board which have a structural function in building 
construction.  Sheathing materials such as the paper affixed to sheetrock 
however, is paper.") 
(subclass Paper Artifact)

(=>
  (instance ?PAPER Paper)
  (hasPurpose ?PAPER 
    (exists (?TEXT) 
      (and
        (instance ?TEXT Text)
        (part ?TEXT ?PAPER)))))

(subclass CurrencyBill Currency)
(partition Currency CurrencyBill CurrencyCoin)
(documentation CurrencyBill EnglishLanguage "Any instance of &%Currency that is made 
of paper.")

(=>
  (instance ?BILL CurrencyBill)
  (exists (?PAPER)
    (and
      (instance ?PAPER Paper)
      (part ?PAPER ?BILL)))) 

(subclass CurrencyCoin Currency)
(documentation CurrencyCoin EnglishLanguage "Any instance of &%Currency that is made 
of &%Metal.")

(=>
  (instance ?COIN CurrencyCoin)
  (exists (?METAL)
    (and
      (subclass ?METAL Metal)
      (material ?METAL ?COIN)))) 

(subclass Wire Artifact)
(documentation Wire EnglishLanguage "A long, thin strand of &%Metal that is used in a wide 
range of applications, including the wiring of electrical systems, creating 
bundles and the construction of cages.")

(=>
   (instance ?WIRE Wire)
   (material Metal ?WIRE))

(subclass WireSpring Wire)
(subclass WireSpring EngineeringComponent)
(documentation WireSpring EnglishLanguage "An &%EngineeringComponent consisting of a coil of 
&%Wire that returns to its original shape when pulled apart or pressed together.")

(subclass WireLine Wire)
(subclass WireLine EngineeringComponent)
(documentation WireLine EnglishLanguage "A &%Wire that is designed for conducting electricity.")

(=>
  (instance ?WL WireLine)
  (hasPurpose ?WL 
    (exists (?R)
      (and
        (instance ?R Radiating)
        (path ?R ?WL)))))

(subclass TelephoneLine WireLine)
(documentation TelephoneLine EnglishLanguage "A &%WireLine that carries telephone signals and 
allows users of &%Telephones or &%Telegraphs to communicate with one another.")

(=>
  (instance ?TL TelephoneLine)
  (exists (?T1 ?T2)
    (and
      (or
        (instance ?T1 Telephone)
        (instance ?T1 Telegraph))
      (or 
        (instance ?T2 Telephone)
        (instance ?T2 Telegraph))
      (not (equal ?T1 ?T2))
        (connects ?TL ?T1 ?T2))))

(subclass String Artifact)
(documentation String EnglishLanguage "A long, thin strand of Fabric that is used for &%Tying 
things together, etc.  Note that this class covers a cord of any width, including 
rope, twine, and thread, for example.")

(=>
  (instance ?S String)
  (material Fabric ?S))

(subclass Plug Artifact)
(documentation Plug EnglishLanguage "An &%Artifact which is designed to fit snugly within 
a &%Hole.")

(=>
   (instance ?PLUG Plug)
   (exists (?HOLE)
      (completelyFills ?PLUG ?HOLE)))

(subclass Pottery Artifact)
(documentation Pottery EnglishLanguage "Household &%Artifacts that are made out of baked &%Clay.")

(=>
   (instance ?POTTERY Pottery)
   (exists (?CLAY)
      (and
         (instance ?CLAY Clay)
         (part ?CLAY ?POTTERY))))

(subclass Pillow Artifact)
(documentation Pillow EnglishLanguage "A soft, stuffed &%Artifact to support the &%Head or 
the body as a whole while one is sleeping or relaxing.")

(subclass Mirror Artifact)
(documentation Mirror EnglishLanguage "An &%Artifact with a surface that is capable of 
displaying an image by reflecting light.")

(=>
  (instance ?M Mirror)
  (capability ReflectingLight instrument ?M))

(subclass Furniture Artifact)

(documentation Furniture EnglishLanguage "Any free-standing and movable 
&%Artifacts which which are used in day-to-day living and designed 
to rest on the &%Floor of a &%Room.  This excludes items such as a rock 
which may serve as a chair but was not created with the intention of 
supporting that use.") 

(=>
  (instance ?F Furniture)
  (hasPurpose ?F
    (exists (?FL)
      (and
        (instance ?FL Floor)
	(meetsSpatially ?F ?FL)))))

(=>
  (instance ?F Furniture)
  (capability Translocation moves ?F))
                                  
(disjoint Furniture EngineeringComponent)
(disjoint Furniture Machine)
(disjoint Furniture MusicalInstrument)
(disjoint Furniture TransportationDevice)
(disjoint Furniture Weapon)

(subclass Seat Furniture)
(documentation Seat EnglishLanguage "Any instance of &%Furniture which is designed to 
accommodate &%Humans who are &%Sitting.")

(=>
  (instance ?SEAT Seat)
  (hasPurpose ?SEAT 
    (exists (?PERSON)
      (and
        (instance ?PERSON Human)
        (located ?PERSON ?SEAT)
        (attribute ?PERSON Sitting)))))

(subclass Chair Seat)
(documentation Chair EnglishLanguage "A &%Seat that is designed to accommodate a single 
&%Human.")

(=>
   (instance ?CHAIR Chair)
   (not (exists (?PERSON1 ?PERSON2)
      (and
         (instance ?PERSON1 Human)
         (instance ?PERSON2 Human)
         (attribute ?PERSON1 Sitting)
         (attribute ?PERSON2 Sitting)
         (located ?PERSON1 ?CHAIR)
         (located ?PERSON2 ?CHAIR)
         (not (equal ?PERSON1 ?PERSON2)))))) 

(subclass Sofa Seat)
(documentation Sofa EnglishLanguage "A padded &%Seat that is designed to accommodate more 
than one &%Human.")

(=>
  (instance ?S Sofa)
  (hasPurpose ?S
    (exists (?P1 ?P2 ?T)
      (and
        (not (equal ?P1 ?P2))
	(instance ?P1 Human)
	(instance ?P2 Human)
	(holdsDuring ?T
	  (and
	    (attribute ?P1 Sitting)
	    (attribute ?P2 Sitting)
	    (located ?P1 ?S)
	    (located ?P2 ?S)))))))

(subclass Bed Furniture)
(documentation Bed EnglishLanguage "A piece of &%Furniture which is primarily for sleeping.")

(=>
   (instance ?BED Bed)
   (hasPurpose ?BED (exists (?PERSON)
                       (and
                          (attribute ?PERSON Asleep)
                          (located ?PERSON ?BED)))))

(subclass Table Furniture)
(documentation Table EnglishLanguage "A piece of &%Furniture with four legs and a flat top.  
It is used either for eating, paperwork or meetings.")

(subclass Desk Table)
(documentation Desk EnglishLanguage "A &%Table for a single person which is intended to be 
used for paperwork.")

(subclass ChestOrCabinet Furniture)
(subclass ChestOrCabinet Container)
(documentation ChestOrCabinet EnglishLanguage "Any piece of &%Furniture which is also a 
Container, e.g. a chest of drawers, a memory chest, an armoire, etc.")

(subclass Coffin Container)
(documentation Coffin EnglishLanguage "A &%Container for a &%HumanCorpse.")
(=>
  (instance ?C Coffin)
  (hasPurpose ?C 
    (exists (?HC)
      (and
        (instance ?HC HumanCorpse)
        (located ?HC ?C)))))

(subclass Compartment Container)
(documentation Compartment EnglishLanguage "A &%Container which is part of another &%Container, 
e.g. a drawer, a zippered pouch in a piece of luggage, a compartment in a TV 
dinner, etc.")
(=>
  (instance ?C Compartment)
  (exists (?O)
    (and
      (instance ?O Container)
      (not (equal ?C ?O))
      (part ?C ?O))))

(subclass Mailbox Container)
(documentation Mailbox EnglishLanguage "A &%Container whose purpose is to receive items 
that are mailed to the address associated with the &%Mailbox.")

(=>
  (instance ?B Mailbox)
  (hasPurpose ?B 
    (exists (?M ?I)
      (and
        (instance ?M Mailing)
        (patient ?M ?I)
        (destination ?M ?B)))))

(subclass Screw AttachingDevice)
(documentation Screw EnglishLanguage "An &%AttachingDevice which contains a spiral of grooves to 
hold it in place and which is fastened with a &%Screwdriver.")

(subclass Screwdriver Device)
(documentation Screwdriver EnglishLanguage "A &%Device that is used to rotate
a &%Screw, which by the action of its helical threads is driven into a 
medium that is softer than the material of the screw itself.")

(=>
  (instance ?D Screwdriver)
  (hasPurpose ?D
    (exists (?R ?S)
      (and
        (instance ?R Rotating)
        (instrument ?R ?D)
        (patient ?R ?S)
        (instance ?S Screw)))))

(subclass Hammer Device)
(documentation Hammer EnglishLanguage "A &%Device that is used to pound &%Nails into a 
surface, where they become firmly attached.")

(=>
  (instance ?H Hammer)
  (hasPurpose ?H
    (exists (?I ?N)
      (and
        (instance ?I Impelling)
        (instrument ?I ?H)
        (patient ?I ?N)
        (instance ?N Nail)))))

(subclass Nail AttachingDevice)
(documentation Nail EnglishLanguage "An &%AttachingDevice which is a thin piece of metal, with 
or without a head, that is fastened with a &%Hammer.")

(subclass Tape AttachingDevice)
(documentation Tape EnglishLanguage "A thin strip of &%Fabric or &%Paper that is used to attach 
two things.")

(=>
   (instance ?TAPE Tape)
   (exists (?PART)
      (and
         (part ?PART ?TAPE)
         (or
            (instance ?PART Paper)
            (instance ?PART Fabric)))))

(subclass Anchor AttachingDevice)
(documentation Anchor EnglishLanguage "An &%AttachingDevice which is large hook or set of hooks 
that are used to secure a &%Ship on the open water.")
(=>
   (and
      (instance ?A Anchor)
      (instance ?ATTACH Attaching)
      (instrument ?ATTACH ?A)
      (patient ?ATTACH ?S))
   (instance ?S Ship))

(subclass Button AttachingDevice)
(documentation Button EnglishLanguage "An &%AttachingDevice that is used on &%Clothing.")
(=>
  (instance ?B Button)
  (hasPurpose ?B
    (exists (?C)
      (and
        (instance ?C Clothing)
        (part ?B ?C)))))

(subclass Clamp AttachingDevice)
(documentation Clamp EnglishLanguage "An &%AttachingDevice which is designed to attach 
two things together by means of a movable part which can be tightened 
or loosened.")

(subclass Holder Device)
(documentation Holder EnglishLanguage "A large class of &%Devices whose purpose is to hold 
something else, i.e. be the &%instrument of a &%Keeping.")

(=>
   (instance ?DEVICE Holder)
   (capability Keeping instrument ?DEVICE))

(subclass Saddle Holder)
(documentation Saddle EnglishLanguage "A &%Device which allows a &%Human to ride on a 
&%Horse.")

(=>
   (instance ?SADDLE Saddle)
   (capability 
     (KappaFn ?RIDE         
         (exists (?HUMAN ?HORSE) 
           (and
             (instance ?RIDE Carrying)
             (patient ?RIDE ?HUMAN)
             (instance ?HUMAN Human)
             (agent ?RIDE ?HORSE)
             (instance ?HORSE Horse)))) instrument ?SADDLE))

(subclass HorseRiding Carrying)
(documentation HorseRiding EnglishLanguage "Any instance of &%Carrying where the &%Animal is 
a &%Horse.")

(=>
  (instance ?R HorseRiding) 
  (exists (?H)
    (and
      (instance ?H Horse)
      (instrument ?R ?H))))

(subclass Tray Holder)
(documentation Tray EnglishLanguage "A &%Holder that is designed for &%Food, dishes, and 
flatware.")

(subclass Dish Holder)
(documentation Dish EnglishLanguage "A &%Holder for &%Food while the &%Food is being eaten.")
(=>
  (instance ?D Dish)
  (hasPurpose ?D 
    (exists (?F ?E)
      (and
        (instance ?E Eating)
        (patient ?E ?F)
        (patient ?E ?D)
        (instance ?F (FoodForFn Animal))
        (located ?F ?D)))))

(subclass Container Holder)
(documentation Container EnglishLanguage "Any &%Holder whose purpose is to contain 
something else.  Note that &%Container is more specific in meaning 
than &%Holder, because a &%Container must have a &%Hole that is at 
least partially filled by the thing contained.")

(=>
   (instance ?CONTAINER Container)
   (hasPurpose ?CONTAINER (exists (?OBJ) (contains ?CONTAINER ?OBJ))))

(subclass Bag Container)
(documentation Bag EnglishLanguage "Any &%Container which is made of &%Fabric.")
(=>
  (instance ?BAG Bag)
  (exists (?PART)
    (and
      (part ?PART ?BAG)
      (instance ?PART Fabric))))

(subclass Box Container)
(documentation Box EnglishLanguage "Any six-sided &%Container whose sides are 
rectangular in shape.")

(=>
  (instance ?BOX Box)
  (equal 
    (CardinalityFn 
      (KappaFn ?SIDE 
        (or
          (top ?SIDE ?BOX)
          (bottom ?SIDE ?BOX)
          (side ?SIDE ?BOX)))) 6))   

(subclass TrapOrCage Holder)
(documentation TrapOrCage EnglishLanguage "A &%Device whose purpose is to trap or cage &%Animals, 
i.e. to be in instrument in an act of &%Confining.")

(=>
  (instance ?T TrapOrCage)
  (hasPurpose ?T 
    (exists (?C)
      (and
        (instance ?C Confining)
        (instrument ?C ?T)))))

(subclass Envelope Container)
(documentation Envelope EnglishLanguage "A sealable &%Container for one or more pieces of 
paper which is designed to protect the papers while they are transferred to 
someone.")

(subclass TravelContainer Container)
(documentation TravelContainer EnglishLanguage "Any &%Container which is intended to be 
used for carrying clothing, toiletries, and other personal effects that 
would be needed on a overnight trip.")   

(subclass FluidContainer Container)
(documentation FluidContainer EnglishLanguage "A &%Container which is used to store &%Fluids,  
i.e. &%Liquids and &%Gases.")

(=>
   (and
      (instance ?TANK FluidContainer)
      (contains ?TANK ?STUFF))
   (attribute ?STUFF Fluid))

(subclass Bottle FluidContainer)
(documentation Bottle EnglishLanguage "A &%Container whose top is narrower than its bottom, which 
has no handle, and which is intended to store &%Liquids.")

(=>
   (and
      (instance ?BOTTLE Bottle)
      (contains ?BOTTLE ?STUFF))
   (attribute ?STUFF Liquid))

(=>
   (and
      (instance ?BOTTLE Bottle)
      (bottom ?BOTTOM ?BOTTLE)
      (top ?TOP ?BOTTLE)
      (width ?BOTTOM ?WIDTH1)
      (width ?TOP ?WIDTH2))
   (lessThan ?WIDTH2 ?WIDTH1))

(subclass DrinkingCup FluidContainer)
(documentation DrinkingCup EnglishLanguage "An open &%FluidContainer that is intended to serve a &%Beverage 
to a single person.  Note that this class includes both cups with handles and 
drinking glasses.")

(=>
   (and
      (instance ?CUP DrinkingCup)
      (contains ?CUP ?STUFF))
   (instance ?STUFF Beverage))
      
(subclass SwimmingPool FluidContainer)
(subclass SwimmingPool StationaryArtifact)
(documentation SwimmingPool EnglishLanguage "A &%FluidContainer that is filled with &%Water 
and that is used for &%Swimming.")

(=>
  (instance ?P SwimmingPool)
  (hasPurpose ?P 
    (exists (?S)
      (and
        (instance ?S Swimming)
        (located ?S ?P)))))

(=>
  (and
    (instance ?P SwimmingPool)
    (contains ?P ?F)
    (instance ?F Fluid))
  (instance ?F Water))

(subclass BoardOrBlock Artifact)
(documentation BoardOrBlock EnglishLanguage "A piece of material with flat, rectangular sides.  
Note that boards and blocks are lumped into a single concept, because the 
difference between these notions cannot be precisely defined.")

(=>
   (instance ?BOARD BoardOrBlock)
   (capability Constructing resource ?BOARD))

(subclass Brick BoardOrBlock)
(documentation Brick EnglishLanguage "A block of fired &%Clay that is used in &%Constructing.")
(=>
  (instance ?B Brick)
  (material Clay ?B))

(=>
  (instance ?B Brick)
  (hasPurpose ?B 
    (exists (?C)
       (and
         (instance ?C Constructing)
         (resource ?C ?B)))))

(subclass TwoDimensionalObject Object)
(documentation TwoDimensionalObject EnglishLanguage "A real-world physical object
with a very flat aspect.  This includes drawings on paper, cave
paintings and other surface alterations.")
(=>
  (instance ?T TwoDimensionalObject)
  (attribute ?T TwoDimensionalFigure))

(instance sliceOfFigure BinaryPredicate)
(documentation sliceOfFigure EnglishLanguage "(sliceOfFigure ?SLICE ?FIGURE) indicates
that ?SLICE is a 2-d section of the 3-d figure ?FIGURE.  Or, more 
formally, ?SLICE is 2-d figure formed by the intersection of a plane
with the 3-d figure ?FIGURE.")
(domain sliceOfFigure 1 TwoDimensionalObject)
(domain sliceOfFigure 2 CorpuscularObject)

(instance RoundShape ThreeDimensionalFigure)
(documentation RoundShape EnglishLanguage "Any &%ThreeDimensionalFigure that has a single
tangent at every point on its surface.")
(=>
  (attribute ?O RoundShape)
  (forall (?P)
    (and
      (pointOfFigure ?P ?O)
      (instance ?TO TwoDimensionalObject)
      (meetsSpatially ?P ?O)
      (meetsSpatially ?P ?TO)
      (exists (?T)       
        (tangent ?T ?TO)))))

(subclass Cone ThreeDimensionalFigure)
(documentation Cone EnglishLanguage "The class of &%ThreeDimensionalFigures which are 
produced by rotating a &%RightTriangle around its &%RightAngle.")

(subclass Cylinder ThreeDimensionalFigure)
(documentation Cylinder EnglishLanguage "The class of &%ThreeDimensionalFigures such that 
all &%GeometricPoints that make up the &%Cylinder are equidistant from a 
&%OneDimensionalFigure, known as the axis of the &%Cylinder.")

(subclass Sphere ThreeDimensionalFigure)
(documentation Sphere EnglishLanguage "The class of &%ThreeDimensionalFigures such that 
all &%GeometricPoints that make up the &%Sphere are equidistant from a 
single &%GeometricPoint, known as the center of the &%Sphere.")

(=>
  (and
    (attribute ?OBJ ?SPHERE)
    (instance ?SPHERE Sphere))
  (exists (?CENTER ?DIST)
    (forall (?PT)
      (=>
        (pointOfFigure ?PT ?OBJ)
        (geometricDistance ?PT ?CENTER ?DIST)))))

(subclass Bubble CorpuscularObject)
(documentation Bubble EnglishLanguage "A globe which has a &%Liquid surface and which contains 
a &%Gas.")
(=>
  (instance ?B Bubble)
  (exists (?S)
    (and
      (surface ?S ?B)
      (attribute ?S Liquid))))

(=>
  (and
    (instance ?B Bubble)
    (interiorPart ?P ?B))
  (attribute ?P Gas))

(=>
  (and
    (instance ?B Bubble)
    (not
      (exists (?X ?S)
        (and
          (attribute ?X ?S)
          (not (equal ?X Gas))
          (meetsSpatially ?X ?B)))))
  (attribute ?B RoundShape))

(subclass Nest CorpuscularObject)
(documentation Nest EnglishLanguage "Any structure which is created by nonhuman &%Animals for 
the purpose of giving birth to their offspring.")

(=>
   (instance ?NEST Nest)
   (hasPurpose ?NEST (exists (?BIRTH)
                        (and
                           (instance ?BIRTH Birth)
                           (located ?BIRTH ?NEST)))))

(subclass OutdoorClothing Clothing)
(documentation OutdoorClothing EnglishLanguage "&%Clothing that is intended to be worn 
outdoors.")  

(=>
   (and
      (instance ?CLOTHING OutdoorClothing)
      (holdsDuring ?TIME (wears ?PERSON ?CLOTHING)))
   (not (exists (?BUILDING)
      (and
         (instance ?BUILDING Building)
         (holdsDuring ?TIME (located ?PERSON ?BUILDING))))))
   
(subclass Apron Clothing)
(documentation Apron EnglishLanguage "An item of &%Clothing that protects the front and 
middle part of the body while one is &%Cooking or doing other work.")
(=>
  (instance ?A Apron)
  (hasPurpose ?A
    (exists (?P ?C)
      (and
        (instance ?C Cooking)
        (agent ?C ?P)
        (holdsDuring 
          (WhenFn ?C)
          (wears ?P ?A))))))

(subclass Belt Clothing)
(documentation Belt EnglishLanguage "A piece of &%Clothing that is worn around the waist 
to restrain another piece of clothing.")
(=>
  (and
    (wears ?P ?B)
    (instance ?B Belt))
  (exists (?C)
    (and
      (instance ?C Clothing)
      (wears ?P ?C)
      (meetsSpatially ?B ?C))))

(subclass TieClothing Clothing)
(documentation TieClothing EnglishLanguage "&%Clothing that is intended to be worn around the 
&%Neck and knotted at the front.")

(=>
  (and
    (wears ?P ?T)
    (instance ?T TieClothing))
  (exists (?N)
    (and
      (instance ?N Neck)
      (part ?N ?P)
      (contains ?T ?N))))

(subclass Cloak Clothing)
(documentation Cloak EnglishLanguage "A piece of &%Clothing that covers the whole body 
except the face (and possibly entire head), hands, and feet.")

(subclass Collar Clothing)
(documentation Collar EnglishLanguage "A piece of &%Clothing that fits around the &%Neck.  
A &%Collar is always part of a &%Coat or a &%Shirt.")
(=>
  (instance ?C Collar)
  (exists (?OC)
    (and
      (part ?C ?OC)
      (or
        (instance ?OC Coat)
        (instance ?OC Shirt)))))

(=>
  (and
    (wears ?P ?C)
    (instance ?C Collar))
  (exists (?N)
    (and
      (instance ?N Neck)
      (part ?N ?P)
      (meetsSpatially ?C ?N))))

(subclass Glove Clothing)
(documentation Glove EnglishLanguage "Clothing that is intended to be worn on the &%Hand.  
Note that this class covers both gloves which have individual compartments 
for each of the &%Fingers and mittens.")
(=>
  (and
    (wears ?P ?G)
    (instance ?G Glove))
  (exists (?H)
    (and
      (instance ?H Hand)
      (part ?H ?P)
      (meetsSpatially ?G ?H))))

(subclass Hat Clothing)
(documentation Hat EnglishLanguage "A type of &%Clothing that is worn on the &%Head.  Note 
that this class covers caps, bonnets, berets, etc.")

(=>
   (and
      (wears ?PERSON ?HAT)
      (instance ?HAT Hat))
   (exists (?HEAD)
      (and
         (instance ?HEAD Head)
         (part ?HEAD ?PERSON)
         (meetsSpatially ?HAT ?HEAD))))

(subclass Coat OutdoorClothing)
(documentation Coat EnglishLanguage "&%Clothing that has sleeves and covers from the neck 
down.  &%Coats are intended to be worn outdoors.")

(subclass Trousers Clothing)
(documentation Trousers EnglishLanguage "A piece of &%Clothing that covers each &%Leg 
of a person separately.")

(=>
  (and
    (wears ?H ?T)
    (instance ?T Trousers))
  (exists (?L)
    (and
      (instance ?L Leg)
      (part ?L ?H)
      (contains ?T ?L))))

(subclass Shoe Clothing)
(documentation Shoe EnglishLanguage "&%Clothing that is intended to be worn on the &%Foot.  
It consists of an upper, a sole, and a heel.")

(=>
   (and
      (wears ?PERSON ?SHOE)
      (instance ?SHOE Shoe))
   (exists (?FOOT)
      (and
         (instance ?FOOT Foot)
         (part ?FOOT ?PERSON)
         (meetsSpatially ?SHOE ?FOOT))))

(documentation ShoeSole EnglishLanguage "The bottom part of a &%Shoe that is
intended to be the point of contact with the ground while the
shoe is being worn.")
(subclass ShoeSole Artifact)
(=>
  (instance ?SS ShoeSole)
  (exists (?S)
    (and 
      (instance ?S Shoe)
      (bottom ?SS ?S))))

(documentation Sandal EnglishLanguage "Any kind of open shoe lacking a markedly
thicker heel.  The toe may be enclosed, but there must be some opening in the upper
other than the &%Hole through which the foot is inserted.")
(subclass Sandal Shoe)
(=>
  (instance ?S Sandal)
  (exists (?H1 ?H2)
    (and
      (instance ?H1 Hole)
      (instance ?H2 Hole)
      (not (equal ?H1 ?H2))
      (hasPurpose ?H1
        (exists (?D ?F ?A ?I)
          (and 
            (instance ?D Dressing)
            (instance ?F Foot)
            (agent ?D ?A)
            (part ?F ?A)
            (subProcess ?I ?D)
            (instance ?I Inserting)
            (patient ?I ?F)
            (destination ?I ?H1)))))))

(=>
  (instance ?S Sandal)
  (exists (?SOLE)
    (and
      (instance ?SOLE ShoeSole)
      (part ?SOLE ?S)
      (attribute ?SOLE Flat)))) 

(subclass Sock Clothing)
(documentation Sock EnglishLanguage "A piece of &%Clothing that is made of a soft &%Fabric 
like &%Cotton and that is intended to be worn on the &%Foot.")
(=>
  (and
    (wears ?P ?S)
    (instance ?S Sock))
  (exists (?F)
    (and
      (instance ?F Foot)
      (part ?F ?P)
      (contains ?S ?F))))

(subclass Shirt Clothing)
(documentation Shirt EnglishLanguage "An item of &%Clothing which covers the upper body of a 
&%Human.")

(=>
   (and
      (instance ?SHIRT Shirt)
      (wears ?PERSON ?SHIRT))
   (instance ?PERSON Human))

(subclass Sleeve Clothing)
(documentation Sleeve EnglishLanguage "A piece of &%Clothing that covers the Arm.  A 
&%Sleeve is always part of a &%Coat or a &%Shirt.")
(=>
  (and
    (wears ?P ?S)
    (instance ?S Sleeve))
  (exists (?A)
    (and
      (instance ?A Arm)
      (part ?A ?P)
      (contains ?S ?A))))

(=>
  (instance ?S Sleeve)
  (exists (?C)
    (and
      (part ?S ?C)
      (or
        (instance ?C Shirt)
        (instance ?C Coat)))))

(subclass Dress Clothing)
(documentation Dress EnglishLanguage "An item of &%Clothing which covers the lower body of a 
&%Woman.")

(=>
   (and
      (instance ?DRESS Dress)
      (wears ?PERSON ?DRESS))
   (instance ?PERSON Woman))

(subclass ClothingSuit Collection)
(documentation ClothingSuit EnglishLanguage "A &%Collection of instances of &%Clothing that 
are designed to be worn together.")

(=>
   (and
      (instance ?SUIT ClothingSuit)
      (member ?ITEM1 ?SUIT)
      (holdsDuring ?TIME (wears ?PERSON ?ITEM1)))
   (forall (?ITEM2)
      (=>
         (member ?ITEM2 ?SUIT)
         (holdsDuring ?TIME (wears ?PERSON ?ITEM2)))))

(subclass Rug Fabric)
(documentation Rug EnglishLanguage "A piece of &%Fabric whose purpose is to cover a &%Floor.")

(=>
  (instance ?R Rug)
  (hasPurpose ?R 
    (exists (?C)
      (and
        (instance ?C Covering)
        (instrument ?C ?R)
        (patient ?C ?F)
        (instance ?F Floor)))))

(documentation Towel EnglishLanguage "A piece of &%Fabric which is used for &%Drying.")
(subclass Towel Fabric)

(=>
  (instance ?X Towel)
  (capability Drying instrument ?X))

(=>
  (instance ?T Towel)
  (hasPurpose ?T 
    (exists (?D)
      (and
        (instance ?D Drying)
        (instrument ?D ?T)))))

(subclass Leather Fabric)
(documentation Leather EnglishLanguage "A &%Fabric that is the result of tanning an &%Animal 
&%Skin.")

(=>
   (instance ?LEATHER Leather)
   (exists (?MAKE ?SKIN)
      (and
         (instance ?MAKE Making)
         (resource ?MAKE ?SKIN)
         (instance ?SKIN Skin)
         (result ?MAKE ?LEATHER))))
      
(subclass Silk Fabric)
(documentation Silk EnglishLanguage "Fabric that is woven from the strands produced by 
certain &%Larval &%Insects.")

(=>
  (instance ?S Silk)
  (exists (?M ?T ?I)
    (and
      (instance ?M Making)
      (result ?M ?S)
      (resource ?M ?T)
      (part ?T ?I)
      (instance ?I Insect)
      (attribute ?I Larval))))

(subclass Wool Fabric)
(documentation Wool EnglishLanguage "&%Fabric that is made from the &%Hair of &%Sheep.")

(=>
  (instance ?W Wool)
  (exists (?M ?H ?S)
    (and
      (instance ?M Making)
      (result ?M ?W)
      (resource ?M ?H)
      (part ?H ?S)
      (instance ?H Hair)
      (instance ?S Sheep))))

(subclass Pocket Fabric)
(documentation Pocket EnglishLanguage "A pouch of &%Fabric in an instance of &%Clothing where 
something can be kept.")

(=>
   (instance ?POCKET Pocket)
   (exists (?CLOTHING)
      (and
         (instance ?CLOTHING Clothing)
         (part ?POCKET ?CLOTHING))))

(=>
   (instance ?POCKET Pocket)
   (capability Keeping instrument ?POCKET))

(subclass Blanket Fabric)
(documentation Blanket EnglishLanguage "A piece of &%Fabric whose purpose is to keep a 
person who is in bed warm.")

(=>
   (instance ?BLANKET Blanket)
   (hasPurpose ?BLANKET 
     (exists (?HEAT ?PERSON ?BED)
       (and
         (instance ?HEAT Heating)
         (patient ?HEAT ?PERSON)
         (instance ?PERSON Human)
         (located ?PERSON ?BED)
         (instance ?BED Bed)))))

(subclass Cotton PlantAnatomicalStructure)
(documentation Cotton EnglishLanguage "Fibers from the cotton plant that are used in 
&%Making &%CottonFabric.")

(subclass CottonFabric Fabric)
(documentation CottonFabric EnglishLanguage "Any &%Fabric that is made entirely out of &%Cotton.")
(=>
  (instance ?F CottonFabric)
  (exists (?M ?C)
    (and
      (instance ?M Making)
      (resource ?M ?C)
      (instance ?C Cotton)
      (result ?M ?F))))

(=>
  (instance ?F CottonFabric)
  (material Cotton ?F))

(documentation Veil EnglishLanguage "A piece of &%Clothing intended to cover or
obscure the face or hair of a &%Woman.  This is commonly worn for
religious reasons when in public in &%Muslim countries, but is also customary
at &%Weddings and &%Funerals among some non-Muslim women.")
(subclass Veil Clothing)
(=>
  (instance ?V Veil)
  (hasPurpose ?V
    (exists (?F ?H)
      (or
        (and
          (instance ?F Face)
          (meetsSpatially ?V ?F))
        (and
          (instance ?H Hair)
          (meetsSpatially ?V ?H)))))) 

(subclass Concealing Putting)
(documentation Concealing EnglishLanguage "Locating something in such a way that it cannot 
be seen.")
(=>
  (and
    (instance ?C Concealing)
    (patient ?C ?OBJ))
  (hasPurpose ?C 
    (not 
      (exists (?S)
        (and
          (instance ?S Seeing)
          (patient ?S ?OBJ))))))

(subclass Dressing Putting)
(documentation Dressing EnglishLanguage "The &%Process of putting on &%Clothing.")

(=>
   (and
      (instance ?DRESS Dressing)
      (agent ?DRESS ?AGENT)
      (patient ?DRESS ?CLOTHING))
   (and
      (instance ?CLOTHING Clothing)
      (holdsDuring 
        (BeginFn 
          (WhenFn ?DRESS)) 
        (not 
          (wears ?AGENT ?CLOTHING)))
      (holdsDuring 
        (EndFn 
          (WhenFn ?DRESS)) 
        (wears ?AGENT ?CLOTHING))))

(subclass ChangingClothing Transfer)
(documentation ChangingClothing EnglishLanguage "Any instance of a process of 
&%RemovingClothing and &%Dressing.")
(=>
  (instance ?CC ChangingClothing)
  (exists (?R ?D)
    (and
      (instance ?R RemovingClothing)
      (instance ?D Dressing)
      (subProcess ?R ?CC)
      (subProcess ?D ?CC))))

(subclass RemovingClothing Uncovering)
(documentation RemovingClothing EnglishLanguage "Removing &%Clothing from a &%Human or 
&%Animal in such a way that the &%Human or &%Animal no longer wears the 
&%Clothing.")

(=>
  (and
    (instance ?REMOVE RemovingClothing)
    (agent ?REMOVE ?AGENT)
    (patient ?REMOVE ?CLOTHING))
  (and
    (holdsDuring 
      (BeginFn (WhenFn ?REMOVE)) 
      (wears ?AGENT ?CLOTHING))
    (holdsDuring 
      (EndFn (WhenFn ?REMOVE)) 
      (not (wears ?AGENT ?CLOTHING)))))

(subclass Sweeping Removing)
(documentation Sweeping EnglishLanguage "Removing small particles from the floor by means of 
a &%Broom.")

(=>
  (and
    (instance ?S Sweeping)
    (instrument ?S ?B))
  (instance ?B Broom))

(subclass Washing Removing)
(documentation Washing EnglishLanguage "&%Removing small particles from something by means of 
a &%Detergent and &%Water.")

(=>
  (instance ?P Washing)
  (exists (?D ?W)
    (and
      (instance ?D Detergent)
      (instrument ?P ?D)
      (instance ?W Water)
      (instrument ?P ?W))))

(subclass Tying Attaching) 
(documentation Tying EnglishLanguage "The &%Process of tying two things, or two strands of 
the same thing, together.") 

(subclass Untying Detaching) 
(disjoint Untying Tying)
(documentation Untying EnglishLanguage "The &%Process of untying two things, or two strands 
of the same thing.") 

(subclass Sewing Attaching)
(documentation Sewing EnglishLanguage "Attaching two pieces of &%Fabric or one part of a 
piece of &%Fabric to another part by means of needle and thread.")

(=>
  (instance ?S Sewing)
  (exists (?F)
    (and
      (instance ?F Fabric)
      (patient ?S ?F))))

(subclass Soldering Attaching)
(documentation Soldering EnglishLanguage "Attaching two things by means of a &%MetallicAlloy.")

(=>
  (and
    (instance ?S Soldering)
    (patient ?S ?OBJ1)
    (patient ?S ?OBJ2)
    (not (equal ?OBJ1 ?OBJ2)))
  (exists (?A)
    (and
      (instance ?A MetallicAlloy)
      (connects ?A ?OBJ1 ?OBJ2))))

(instance patientMedical BinaryPredicate)
(domain patientMedical 1 Human)
(domain patientMedical 2 CognitiveAgent)
(documentation patientMedical EnglishLanguage "The relation of receiving medical care 
from a recognized medical practitioner.  (&%patientMedical ?PATIENT 
?DOCTOR) means that ?PATIENT is the patient of ?DOCTOR.  Note that 
argument type restriction on the second argument is &%CognitiveAgent 
to allow for cases where someone is the patient of an &%Organization, 
viz. a &%CareOrganization.")

(=>
   (patientMedical ?PATIENT ?DOCTOR)
   (exists (?PROCESS)
      (and
         (patient ?PROCESS ?PATIENT)
         (agent ?PROCESS ?DOCTOR)
         (or
            (instance ?PROCESS DiagnosticProcess)
            (instance ?PROCESS TherapeuticProcess)))))

(subclass Bandage Fabric)
(documentation Bandage EnglishLanguage "A piece of &%Fabric that is used in &%Covering an 
open wound.")
(=>
  (instance ?B Bandage)
  (hasPurpose ?B 
    (exists (?TP ?C)
      (and
        (instance ?TP TherapeuticProcess)
        (instance ?C Covering)
        (subProcess ?C ?TP)
        (instrument ?C ?B)))))

(subclass Massaging TherapeuticProcess)
(subclass Massaging Touching)
(documentation Massaging EnglishLanguage "A subclass of &%TherapeuticProcess which involves 
kneading &%Muscles in such a way that tension is relieved, blood circulation 
is increased, etc.")

(subclass RelievingPain TherapeuticProcess)
(documentation RelievingPain EnglishLanguage "Any &%TherapeuticProcess that removes &%Pain from 
the patient of the process.")

(=>
  (and
    (instance ?R RelievingPain)
    (patient ?R ?P))
  (and
    (holdsDuring 
      (BeginFn (WhenFn ?R)) 
      (attribute ?P Pain))
    (holdsDuring 
      (EndFn (WhenFn ?R)) 
      (not 
        (attribute ?P Pain)))))

(subclass NonspecificDisease DiseaseOrSyndrome)
(documentation NonspecificDisease EnglishLanguage "The class of &%DiseaseOrSyndromes that are not 
caused by a single type of &%Microorganism.")

(=>
   (instance ?DISEASE NonspecificDisease)
   (not (exists (?AGENT)
      (and
         (instance ?AGENT Microorganism)
         (forall (?VICTIM)
            (=>
               (attribute ?VICTIM ?DISEASE)
               (located ?AGENT ?VICTIM)))))))

(subclass InfectiousDisease DiseaseOrSyndrome)
(documentation InfectiousDisease EnglishLanguage "Any &%DiseaseOrSyndrome that is caused by a 
&%Microorganism.")

(=>
  (and
    (instance ?I InfectiousDisease)
    (attribute ?A ?I))
  (exists (?P)
    (and
      (instance ?P PathologicProcess)
      (agent ?P ?O)
      (instance ?O Microorganism)
      (experiencer ?P ?A))))

(subclass ViralDisease InfectiousDisease)
(documentation ViralDisease EnglishLanguage "A disease that is caused by instances of 
&%Virus.")

(instance Diarrhea DiseaseOrSyndrome)
(documentation Diarrhea EnglishLanguage "A &%DiseaseOrSyndrome of frequent, watery bowel 
movements.  Severe cases can be fatal for the young or weak.  It is
a common cause of death for the very young in poor developing countries.")

(instance Emphysema DiseaseOrSyndrome)
(documentation Emphysema EnglishLanguage "A &%DiseaseOrSyndrome that affects the &%Lungs and 
which results in a decrease of breathing ability.")

(=>
  (attribute ?H Emphysema)
  (exists (?L ?P)
    (and
      (instance ?L Lung)
      (part ?L ?H)
      (instance ?P PathologicProcess)
      (located ?P ?L))))

(instance Fever DiseaseOrSyndrome)
(documentation Fever EnglishLanguage "Having a body temperature which is (significantly) greater 
than 98.6 degrees Fahrenheit.  This syndrome is often associated with dehydration 
and chills.")

(=>
  (and
    (instance ?H Human)
    (attribute ?H Fever))
  (exists (?N)
    (and
      (measure ?H 
        (MeasureFn ?N FahrenheitDegree))
      (greaterThan ?N 98.6))))

(instance Goiter DiseaseOrSyndrome)
(documentation Goiter EnglishLanguage "A &%DiseaseOrSyndrome which is due to an &%Iodine deficiency 
and which results in an enlarged &%ThyroidGland.")

(=>
  (attribute ?H Goiter)
  (exists (?G ?P)
    (and
      (instance ?G ThyroidGland)
      (part ?G ?H)
      (instance ?P PathologicProcess)
      (located ?P ?G))))

(instance Tuberculosis BacterialDisease)
(documentation Tuberculosis EnglishLanguage "A &%BacterialDisease caused by the &%Tubercle &%Bacillus 
that results in lesions on various body parts, especially the &%Lungs.")

(subclass RecoveringFromIllness OrganismProcess)
(documentation RecoveringFromIllness EnglishLanguage "Any &%Process where the experiencer recovers 
from a &%DiseaseOrSyndrome.")

(=>
  (and
    (instance ?R RecoveringFromIllness)
    (experiencer ?R ?A))
  (exists (?D)
    (and
      (instance ?D DiseaseOrSyndrome)
      (holdsDuring 
        (BeginFn (WhenFn ?T)) 
	(attribute ?A ?D))
      (holdsDuring 
        (EndFn (WhenFn ?T)) 
	(not (attribute ?A ?D))))))

(subclass Protozoa Microorganism)
(documentation Protozoa EnglishLanguage "Unicellular &%Organisms that are capable of movement 
and that are found in almost every part of the world.  This class includes 
amoebas, sporozoans, and paramecia.")

(documentation parasite EnglishLanguage "(parasite ?ORGANISM1 ?ORGANISM2) means that there 
is a parasitic relationship between ?ORGANISM1 and ?ORGANISM2, i.e. ?ORGANISM1 
inhabits and obtains nourishment from ?ORGANISM2 in such a way that 
?ORGANISM2 is injured.")
(instance parasite BinaryPredicate)
(domain parasite 1 Organism)
(domain parasite 2 Organism)

(=>
  (parasite ?O1 ?O2)
  (inhabits ?O1 ?O2))

(=>
  (parasite ?O1 ?O2)
  (exists (?I)
    (and
      (instance ?I Injuring)
      (agent ?I ?O1)
      (patient ?I ?O2))))

(instance Cancer DiseaseOrSyndrome)
(documentation Cancer EnglishLanguage "A &%DiseaseOrSyndrome characterized by pathologic and 
uncontrolled cell division that results in a &%Tumor.")

(=>
   (attribute ?PERSON Cancer)
   (exists (?TUMOR)
      (and
         (instance ?TUMOR Tumor)
         (part ?TUMOR ?PERSON))))

(subclass BacterialDisease InfectiousDisease)
(documentation BacterialDisease EnglishLanguage "A disease that is caused by instances of 
&%Bacterium.")

(subclass Neurosis PsychologicalDysfunction)
(documentation Neurosis EnglishLanguage "Any &%PsychologicalDysfunction which is not due 
to an organic impairment of the &%NervousSystem.")

(subclass Psychosis PsychologicalDysfunction)
(documentation Psychosis EnglishLanguage "Any &%PsychologicalDysfunction which is the 
result of an organic impairment of the &%NervousSystem.")

(subclass ConjugatedSubstance CompoundSubstance)
(documentation ConjugatedSubstance EnglishLanguage "A &%CompoundSubstance that results from 
the &%ChemicalSynthesis of two or more &%CompoundSubstances.")
(<=> 
  (instance ?CS ConjugatedSubstance) 
  (exists (?C1 ?C2 ?P) 
    (and 
      (instance ?C1 CompoundSubstance) 
      (instance ?C2 CompoundSubstance) 
      (not (equal ?C1 ?C2)) 
      (instance ?P ChemicalSynthesis)
      (resource ?P ?C1)
      (resource ?P ?C2)
      (result ?P ?CS))))

(instance conjugate BinaryPredicate)
(instance conjugate IrreflexiveRelation)
(instance conjugate SymmetricRelation)
(instance conjugate TransitiveRelation)
(domain conjugate 1 CompoundSubstance)
(domain conjugate 2 CompoundSubstance)
(documentation conjugate EnglishLanguage "(&%conjugate ?COMPOUND1 ?COMPOUND2) means that 
?COMPOUND1 and ?COMPOUND2 are identical &%CompoundSubstances except that 
one has one more &%Proton than the other.")

(=>
   (conjugate ?COMPOUND1 ?COMPOUND2)
   (exists (?NUMBER1 ?NUMBER2)
      (and
         (protonNumber ?COMPOUND1 ?NUMBER1) 
         (protonNumber ?COMPOUND2 ?NUMBER2)
         (or
            (equal ?NUMBER1 (AdditionFn ?NUMBER2 1))
            (equal ?NUMBER2 (AdditionFn ?NUMBER1 1))))))

(subclass Biting Grabbing)
(documentation Biting EnglishLanguage "Any instance of &%Grabbing where the &%instrument is 
the &%Mouth of the &%agent.")

(=>
   (and
      (instance ?BITE Biting)
      (agent ?BITE ?ANIMAL))
   (exists (?MOUTH)
      (and
         (instance ?MOUTH Mouth)
         (part ?MOUTH ?ANIMAL)
         (instrument ?BITE ?MOUTH))))

(subclass Strangling Grabbing)
(documentation Strangling EnglishLanguage "Any instance of &%Grabbing where the patient is 
someone else's throat and the intention is to make it impossible for the other 
person to breathe.")

(=>
  (instance ?S Strangling)
  (exists (?T)
    (and
      (patient ?S ?T)
      (instance ?T Throat))))

(=>
  (and
    (instance ?S Strangling)
    (agent ?S ?A)
    (patient ?S ?V)
    (instance ?V Human))
  (hasPurpose ?S 
    (not 
      (exists (?B)
        (and
          (instance ?B Breathing)
          (agent ?B ?V))))))

(subclass Spitting Impelling)
(documentation Spitting EnglishLanguage "Any instance of &%Impelling where the &%origin is 
the &%Mouth of the &%agent.")

(=>
   (and
      (instance ?SPIT Spitting)
      (agent ?SPIT ?ANIMAL))
   (exists (?MOUTH)
      (and
         (instance ?MOUTH Mouth)
         (part ?MOUTH ?ANIMAL)
         (origin ?SPIT ?MOUTH))))

(subclass Punching Impelling)
(documentation Punching EnglishLanguage "Any instance of &%Impelling where the instrument 
is a &%Fist of the agent.")

(=>
  (and
    (instance ?P Punching)
    (agent ?P ?A))
  (exists (?H)
    (and
      (instance ?H Hand)
      (attribute ?H Fist)
      (part ?H ?A)
      (instrument ?P ?H))))

(subclass Kicking Impelling)
(documentation Kicking EnglishLanguage "Any instance of &%Impelling where the &%instrument 
is a &%Foot of the &%agent.")

(=>
  (and
    (instance ?KICK Kicking)
    (agent ?KICK ?ANIMAL))
  (exists (?FOOT)
    (and
      (instance ?FOOT Foot)
      (part ?FOOT ?ANIMAL)
      (instrument ?KICK ?FOOT))))

(subclass Mailing Transfer)
(documentation Mailing EnglishLanguage "Any instance of &%Transfer where a postal system is 
used to move the &%patient, either a letter or a package.")

(subclass Messaging Transfer)
(documentation Messaging EnglishLanguage "Any instance of &%Transfer where a &%Message is the 
patient.")

(=>
  (instance ?MESSAGING Messaging)
  (exists (?M)
    (and
      (instance ?M Message)
      (patient ?MESSAGING ?M))))

(instance neighbor BinaryPredicate)
(instance neighbor SymmetricRelation)
(instance neighbor IrreflexiveRelation)
(domain neighbor 1 Human)
(domain neighbor 2 Human)
(documentation neighbor EnglishLanguage "(&%neighbor ?PERSON1 ?PERSON2) means that ?PERSON1 is 
a neighbor of ?PERSON2, i.e. ?PERSON1 and ?PERSON2 have their &%homes &%Near 
one another.")

(=>
   (neighbor ?PERSON1 ?PERSON2)
   (exists (?HOME1 ?HOME2)
      (and
         (home ?PERSON1 ?HOME1)
         (home ?PERSON2 ?HOME2)
         (not (equal ?HOME1 ?HOME2))
         (orientation ?HOME1 ?HOME2 Near))))

(instance capacity BinaryPredicate)
(domain capacity 1 SelfConnectedObject)
(domain capacity 2 ConstantQuantity)
(relatedInternalConcept capacity humanCapacity)
(documentation capacity EnglishLanguage "(&%capacity ?OBJ ?QUANTITY) means that ?OBJ can contain 
something that has the &%measure of ?QUANTITY.  This predicate denotes maximal 
capacity, i.e. ?OBJ can hold no more than ?QUANTITY.  Note, however, that this 
does not mean that &%capacity is a &%SingleValuedRelation, since an object may 
have various maximal capacities across different dimensions, e.g. a particular 
box may have a &%capacity of 3 pounds and a &%capacity of 1 liter.")

(=>
   (and
      (measure ?OBJ1 ?MEAS)
      (contains ?OBJ2 ?OBJ1))
   (capacity ?OBJ2 ?MEAS)) 

(instance humanCapacity BinaryPredicate)
(instance humanCapacity SingleValuedRelation)
(domain humanCapacity 1 StationaryArtifact)
(domain humanCapacity 2 PositiveInteger)
(documentation humanCapacity EnglishLanguage "(&%humanCapacity ?CONSTRUCT ?NUMBER) means that the 
&%StationaryArtifact ?CONSTRUCT, e.g. a &%Building or a &%Room, can hold a maximum 
of ?NUMBER &%Humans without crowding.")

(instance LastFn UnaryFunction)
(domain LastFn 1 List)
(range LastFn Entity)
(documentation LastFn EnglishLanguage "(&%LastFn ?LIST) returns the last item in the 
&%List ?LIST.  For example, (&%LastFn (&%ListFn &%Monday &%Tuesday 
&%Wednesday)) would return the value of &%Wednesday.")

(<=>
   (equal (LastFn ?LIST) ?ITEM)
   (exists (?NUMBER)
      (and
         (equal (ListLengthFn ?LIST) ?NUMBER)
         (equal (ListOrderFn ?LIST ?NUMBER) ?ITEM))))

(instance FirstFn UnaryFunction)
(domain FirstFn 1 List)
(range FirstFn Entity)
(documentation FirstFn EnglishLanguage "(&%FirstFn ?LIST) returns the first item in 
the &%List ?LIST.  For example, (&%FirstFn (&%ListFn &%Monday &%Tuesday 
&%Wednesday)) would return the value of &%Monday.")

(=>
   (and
     (instance ?LIST List)
     (not
       (equal ?LIST NullList)))
   (equal (FirstFn ?LIST) (ListOrderFn ?LIST 1)))

(instance StreetAddressFn QuaternaryFunction)
(domain StreetAddressFn 1 StationaryArtifact)
(domain StreetAddressFn 2 Roadway)
(domain StreetAddressFn 3 City)
(domain StreetAddressFn 4 Nation)
(range StreetAddressFn Agent)
(documentation StreetAddressFn EnglishLanguage "(&%StreetAddressFn ?BUILDING ?ROAD ?CITY 
?COUNTRY) returns the &%Agent, e.g. a family, an organization, a person, 
etc. that resides or conducts business at the corresponding &%postContactSite.")

(=>
   (equal (StreetAddressFn ?PLACE ?ROAD ?CITY ?COUNTRY) ?AGENT)
   (postContactSite ?PLACE ?AGENT))

(=>
   (equal (StreetAddressFn ?PLACE ?ROAD ?CITY ?COUNTRY) ?AGENT)
   (exists (?BUILDING)
      (and
         (instance ?BUILDING Building)
         (part ?PLACE ?BUILDING))))
 
(subclass ArtStudio Workshop)
(documentation ArtStudio EnglishLanguage "A &%Workshop, which is 
devoted to the creation of &%ArtWorks.")

(=>
  (instance ?STUDIO ArtStudio)
  (hasPurpose ?STUDIO 
  (exists (?MAKE ?WORK)
    (and
      (instance ?MAKE Making)
      (result ?MAKE ?WORK)
      (instance ?WORK ArtWork)))))

(subclass Mine StationaryArtifact)
(documentation Mine EnglishLanguage "An construction in the earth from which &%Minerals are removed, 
either in pure form or as part of ores.")

(=>
  (instance ?MINE Mine)
  (hasPurpose ?MINE 
    (exists (?REMOVE ?MINERAL)
      (and
        (instance ?REMOVE Removing)
        (patient ?REMOVE ?MINERAL)
        (instance ?MINERAL Mineral)
        (origin ?REMOVE ?MINE)))))

(subclass MobileResidence Artifact)
(disjoint MobileResidence Residence)
(documentation MobileResidence EnglishLanguage "Anything which serves to house people but 
which changes its location from time to time, e.g. a motorhome, a mobile 
home, a camp, etc.  Note that &%MobileResidence is disjoint from &%Residence, 
because the latter is a subclass of &%StationaryArtifact.")

(subclass Prison StationaryArtifact)
(documentation Prison EnglishLanguage "An installation which is owned and maintained by 
a &%Government for the purpose of &%Confining people.  This class covers 
jails, federal prisons, concentration camps, gulags, etc.")

(=>
  (instance ?P Prison)
  (hasPurpose ?P 
    (exists (?C ?H ?G)
      (and
        (instance ?C Confining)
        (located ?C ?P)
        (patient ?C ?H)
        (instance ?H Human)
        (agent ?C ?G)
        (instance ?G Government)))))

(documentation detainee EnglishLanguage "(detainee ?EVENT ?OBJECT) means that in the 
Confining ?EVENT, the Object ?OBJECT is restrained by force, threat, or 
other form of intimidation.")
(instance detainee CaseRole)
(domain detainee 1 Confining)
(domain detainee 2 Animal)
(subrelation detainee patient)

(instance targetInAttack BinaryPredicate)
(instance targetInAttack CaseRole)
(domain targetInAttack 1 Process)
(domain targetInAttack 2 Object)
(subrelation targetInAttack patient)
(documentation targetInAttack EnglishLanguage "This relation 
identifies the &%patient in the event that is the object of the
attack.")

(subclass VotingPoll StationaryArtifact)
(documentation VotingPoll EnglishLanguage "A structure where Voting for an &%Election 
takes place.")

(=>
  (instance ?P VotingPoll)
  (exists (?V)
    (and
      (instance ?V Voting)
      (located ?V ?P))))

(subclass Camp MobileResidence)
(documentation Camp EnglishLanguage "A &%MobileResidence consisting of tents and other temporary 
living quarters that is constructed on an undeveloped &%LandArea.")

(=>
   (instance ?CAMP Camp)
   (exists (?TENT)
      (and
         (instance ?TENT Tent)
         (part ?TENT ?CAMP))))

(subclass Tent MobileResidence)
(documentation Tent EnglishLanguage "A &%MobileResidence that is made of &%Fabric and poles and 
can be easily assembled and disassembled.")

(=>
   (instance ?TENT Tent)
   (exists (?FABRIC)
      (and
         (instance ?FABRIC Fabric)
         (part ?FABRIC ?TENT))))        

(subclass Monument StationaryArtifact)
(documentation Monument EnglishLanguage "A &%StationaryArtifact whose purpose is to commemorate 
a person, animal or event.")

(=>
  (instance ?M Monument)
  (hasPurpose ?M 
    (exists (?R)
      (and
        (instance ?R Remembering)
        (instrument ?R ?M)))))

(subclass Tomb StationaryArtifact)
(documentation Tomb EnglishLanguage "A &%StationaryArtifact which is meant to contain someone 
who is &%Dead.")

(=>
  (instance ?T Tomb)
  (hasPurpose ?T 
    (exists (?P)
      (and
        (attribute ?P Dead)
        (contains ?T ?P)))))

(subclass Workshop StationaryArtifact)
(documentation Workshop EnglishLanguage "A &%Room, suite of &%Rooms or &%Building which is 
devoted to hand-crafting &%Artifacts.")

(=>
  (instance ?S Workshop)
  (hasPurpose ?S 
    (exists (?M)
      (and
        (instance ?M Making)
        (result ?M ?W)
        (instance ?W Artifact)))))

(subclass ExecutiveResidence PermanentResidence)
(documentation ExecutiveResidence EnglishLanguage "A &%Residence of a &%chiefOfState, e.g. 
the White House, a state governor's mansion, Buckingham Palace, etc.")

(=>
   (instance ?RESIDENCE ExecutiveResidence)
   (exists (?AREA ?POSITION ?PERSON)
      (and
         (located ?RESIDENCE ?AREA)
         (home ?PERSON ?RESIDENCE)
         (chiefOfState ?AREA ?POSITION ?PERSON)))) 

(subclass ApartmentUnit SingleFamilyResidence)
(documentation ApartmentUnit EnglishLanguage "A &%SingleFamilyResidence that is not owned 
by any member of the &%SocialUnit that lives there.")

(=>
   (and
      (instance ?UNIT ApartmentUnit)
      (home ?PERSON ?UNIT))
   (not (possesses ?PERSON ?UNIT)))

(=>
   (instance ?UNIT ApartmentUnit)
   (exists (?BUILDING)
      (and
         (instance ?BUILDING ApartmentBuilding)
         (part ?UNIT ?BUILDING))))

(documentation landlord EnglishLanguage "(landlord ?PERSON ?UNIT) means that ?PERSON is a 
landlord of the &%Residence ?UNIT, i.e. he or she owns ?UNIT and is renting 
the unit to someone else.")
(instance landlord BinaryPredicate)
(domain landlord 1 Agent)
(domain landlord 2 PermanentResidence)

(=>
   (landlord ?L ?U)
   (exists (?R ?P)
      (and
         (possesses ?L ?U)
	 (tenant ?P ?U)
	 (agent ?R ?P)
         (instance ?R Renting)
         (patient ?R ?U))))

(documentation tenant EnglishLanguage "(tenant ?PERSON ?UNIT) means that ?PERSON is a tenant 
of the &%Residence ?UNIT, i.e. he or she is renting the unit.")
(instance tenant BinaryPredicate)
(domain tenant 1 Agent)
(domain tenant 2 Residence)

(=>
  (tenant ?P ?U)
  (exists (?L ?R)
    (and
      (instance ?R Renting)
      (landlord ?L ?U)
      (agent ?R ?P)
      (possesses ?L ?U)
      (patient ?R ?U))))

(subclass CondominiumUnit SingleFamilyResidence)
(documentation CondominiumUnit EnglishLanguage "A &%SingleFamilyResidence that may be owned 
by a member of the &%SocialUnit that lives there.")

(=>
   (and
      (instance ?UNIT SingleFamilyResidence)
      (home ?PERSON ?UNIT)
      (possesses ?PERSON ?UNIT)
      (not (instance ?UNIT Building)))
   (instance ?UNIT CondominiumUnit))

(=>
   (instance ?UNIT CondominiumUnit)
   (exists (?BUILDING)
      (and
         (instance ?BUILDING CondominiumBuilding)
         (part ?UNIT ?BUILDING))))

(subclass ApartmentBuilding ResidentialBuilding)
(disjoint ApartmentBuilding SingleFamilyResidence)
(documentation ApartmentBuilding EnglishLanguage "A &%ResidentialBuilding containing 
&%ApartmentUnits.")

(=>
   (instance ?BUILDING ApartmentBuilding)
   (exists (?UNIT1 ?UNIT2)
      (and
         (instance ?UNIT1 ApartmentUnit)
         (instance ?UNIT2 ApartmentUnit)
         (part ?UNIT1 ?BUILDING)
         (part ?UNIT2 ?BUILDING)
         (not (equal ?UNIT1 ?UNIT2)))))

(subclass CondominiumBuilding ResidentialBuilding)
(disjoint CondominiumBuilding SingleFamilyResidence)
(documentation CondominiumBuilding EnglishLanguage "A &%ResidentialBuilding containing 
&%CondominiumUnits.")

(=>
   (instance ?BUILDING CondominiumBuilding)
   (exists (?UNIT1 ?UNIT2)
      (and
         (instance ?UNIT1 CondominiumUnit)
         (instance ?UNIT2 CondominiumUnit)
         (part ?UNIT1 ?BUILDING)
         (part ?UNIT2 ?BUILDING)
         (not (equal ?UNIT1 ?UNIT2)))))

(subclass Dormitory ResidentialBuilding)
(subclass Dormitory TemporaryResidence)
(disjoint Dormitory SingleFamilyResidence)
(documentation Dormitory EnglishLanguage "A &%TemporaryResidence which is owned by a &%School 
and which is used to house students while they take classes at the &%School.")
(=>
  (instance ?D Dormitory)
  (exists (?S)
    (and
      (instance ?S School)
      (possesses ?S ?D))))

(subclass AnimalResidence Artifact)
(documentation AnimalResidence EnglishLanguage "An &%Artifact which is intended to house 
&%Animals and not &%Humans.  Note that an &%AnimalResidence may or may not 
be a &%StationaryArtifact, e.g. a horse stall is stationary while a doghouse 
generally is not.")

(=>
   (instance ?RESIDENCE AnimalResidence)
   (hasPurpose ?RESIDENCE (exists (?ANIMAL)
                             (and
                                (instance ?ANIMAL Animal)
                                (not (instance ?ANIMAL Human))
                                (inhabits ?ANIMAL ?RESIDENCE)))))

(subclass SportsFacility StationaryArtifact)
(documentation SportsFacility EnglishLanguage "A specially designated and maintained facility 
where &%Sports are played.  Note that this covers sports fields, stadiums, 
and gymnasiums.")

(=>
  (instance ?G SportsFacility)
  (hasPurpose ?G 
    (exists (?S) 
      (and
        (instance ?S Sport)
        (located ?S ?G)))))

(subclass SportsLeague Organization)
(documentation SportsLeague EnglishLanguage "An &%Organization whose members are &%SportsTeams 
and whose purpose is to set up games between its members.")

(=>
  (and
    (instance ?L SportsLeague)
    (member ?M ?L))
  (instance ?M SportsTeam))

(subclass PlaceOfCommerce StationaryArtifact)
(partition PlaceOfCommerce CommercialBuilding CommercialUnit)
(documentation PlaceOfCommerce EnglishLanguage "A &%Building or part of a &%Building which is 
intended for organizational activities, e.g. retail or wholesale selling, 
manufacturing, office work, etc.")

(=>
   (instance ?OFFICE PlaceOfCommerce)
   (or
      (instance ?OFFICE CommercialBuilding)
      (exists (?BUILDING)
         (and
            (instance ?BUILDING CommercialBuilding)
            (part ?OFFICE ?BUILDING)))))

(subclass CommercialBuilding Building)
(subclass CommercialBuilding PlaceOfCommerce)
(documentation CommercialBuilding EnglishLanguage "A &%Building which is intended for 
organizational activities, e.g. retail or wholesale selling, manufacturing, 
office work, etc.")

(subclass Warehouse CommercialBuilding)
(documentation Warehouse EnglishLanguage "A very large &%CommercialBuilding whose purpose is 
to store commodities.")

(=>
  (instance ?B Warehouse)
  (hasPurpose ?B
    (exists (?K ?P)
      (and
        (instance ?K Keeping)
        (patient ?K ?P)
        (instance ?P Product)
        (located ?K ?B)))))

(subclass CommercialUnit PlaceOfCommerce)
(documentation CommercialUnit EnglishLanguage "A &%Room or suite of &%Rooms intended for 
clerical and/or professional work of a single &%Organization.")

(=>
   (and
      (instance ?UNIT CommercialUnit)
      (instance ?ORG Organization)
      (located ?ORG ?UNIT))
   (not (exists (?OTHER)
      (and
         (instance ?OTHER Organization)
         (located ?OTHER ?UNIT)
         (not (equal ?OTHER ?ORG))))))

(subclass Store Building)
(documentation Store EnglishLanguage "A &%Building that has the purpose of housing
&%FinancialTransactions.")

(=>
  (instance ?X Store)
  (hasPurpose ?X
    (exists (?EV)
      (and
        (instance ?EV FinancialTransaction)
        (located ?EV ?X)))))

(subclass Factory Building)
(documentation Factory EnglishLanguage "A &%Building intended to house a process that 
produces goods of some value.")
(=>
  (instance ?X Factory)
  (hasPurpose ?X
    (exists (?EV)
      (and
        (instance ?EV Manufacture)
        (located ?EV ?X)))))

(subclass FarmBuilding Building)
(disjoint FarmBuilding ResidentialBuilding)
(documentation FarmBuilding EnglishLanguage "A &%Building on a &%Farm that is used for keeping 
&%DomesticAnimals, &%Fodder or harvested crops.")
(=>
  (instance ?B FarmBuilding)
  (exists (?F)
    (and
      (instance ?F Farm)
      (located ?B ?F))))

(subclass Agency CommercialAgent)
(documentation Agency EnglishLanguage "A &%CommercialAgent whose customers are all other 
&%CommercialAgents, e.g. staffing agencies, food-service providers, etc.")
(=>
   (and
      (instance ?A Agency)
      (customer ?C ?A))
   (instance ?C CommercialAgent))

(subclass EntertainmentCompany CommercialAgent)
(documentation EntertainmentCompany EnglishLanguage "Any business whose services include 
&%Performances.  This class covers nightclubs, commercial live theaters, 
and comedy clubs.")
(=>
  (instance ?C EntertainmentCompany)
  (exists (?P)
    (and
      (instance ?P CommercialService)
      (agent ?P ?C)
      (instance ?P Performance))))

(subclass FinancialCompany CommercialAgent)
(documentation FinancialCompany EnglishLanguage "The class &%FinancialCompany 
includes, as subclasses, &%FinancialBank, &%CreditUnion and &%SavingsAndLoan.")

(documentation stockHolder EnglishLanguage "(stockHolder ?Stock ?Agent) means that 
?Agent possesses the &%Stock ?Stock.")
(instance stockHolder BinaryPredicate)
(domain stockHolder 1 Stock)
(domain stockHolder 2 CognitiveAgent)

(subclass SavingsAndLoan FinancialCompany)
(documentation SavingsAndLoan EnglishLanguage "A federally or state chartered 
&%FinancialCompany that takes &%Deposits from individuals, funds 
&%Mortgages, and pays &%Dividends.")

(subclass InsuranceCompany CommercialAgent)
(documentation InsuranceCompany EnglishLanguage "A &%CommercialAgent that insures &%Agents 
for the payment of a premium or premiums.")

(=>
  (instance ?I InsuranceCompany)
  (exists (?C)
    (insured ?C ?I)))

(subclass TransportationCompany CommercialAgent)
(documentation TransportationCompany EnglishLanguage "A &%CommercialAgent whose services 
include &%Transportation, e.g. a &%RailroadCompany, an airline, a cruise 
ship line, etc.")

(=>
  (instance ?C TransportationCompany)
  (exists (?S)
    (and
      (instance ?S CommercialService)
      (agent ?S ?C)
      (instance ?S Transportation))))

(subclass FinancialService CommercialService)
(documentation FinancialService EnglishLanguage "&%Services performed by a
&%FinancialCompany.")
(=>
  (instance ?S FinancialService)
  (exists (?O)
    (and
      (instance ?O FinancialCompany)
      (agent ?S ?O))))

(documentation loss EnglishLanguage "The amount by which the cost of an investment or 
business operation exceeds its return, i.e. the negative quantity left 
after subtracting for all expenses.")
(instance loss AsymmetricRelation)
(domain loss 1 FinancialTransaction)
(domain loss 2 CurrencyMeasure)

(subclass Financing Funding)
(subclass Financing FinancialService)
(documentation Financing EnglishLanguage "Any &%Funding which is provided by a &%FinancialCompany 
with the aim of making a profit on the money invested.")

(subclass Partnership CommercialAgent)
(documentation Partnership EnglishLanguage "A &%CommercialAgent that is owned by more 
than one person.")

(=>
  (instance ?C Partnership)
  (exists (?H1 ?H2)
    (and
      (instance ?H1 Human)
      (instance ?H2 Human)
      (not (equal ?H1 ?H2))
      (possesses ?H1 ?C)
      (possesses ?H2 ?C))))

(subclass Scholarship Funding)
(documentation Scholarship EnglishLanguage "Any &%Funding which is made on the basis of merit 
and whose purpose is to allow the destination to realize an 
&%EducationalProgram.")

(=>
  (and
    (instance ?F Scholarship)
    (destination ?F ?H))
  (hasPurpose ?F 
    (exists (?EP ?P)
      (and
        (instance ?EP EducationalProgram)
        (realization ?P ?EP)
        (experiencer ?P ?H)))))

(subclass Welfare Funding)
(documentation Welfare EnglishLanguage "Any &%Funding which is provided by a &%ServiceOrganization 
to people in need.")

(=>
  (instance ?W Welfare)
  (exists (?O)
    (and
      (instance ?O ServiceOrganization)
      (agent ?W ?O))))

(subclass Publisher CommercialAgent)
(documentation Publisher EnglishLanguage "Any business whose services include the 
&%Publication of &%Texts.")

(=>
  (instance ?C Publisher)
  (exists (?S)
    (and
      (instance ?S CommercialService)
      (agent ?S ?C)
      (instance ?S Publication))))

(subclass FamilyBusiness Partnership)
(documentation FamilyBusiness EnglishLanguage "A &%Partnership that is owned by a single 
family.")
(=>
  (and
    (instance ?C FamilyBusiness)
    (possesses ?P1 ?C)
    (possesses ?P2 ?C))
  (familyRelation ?P1 ?P2))

(subclass Restaurant CommercialAgent)
(documentation Restaurant EnglishLanguage "Any &%CommercialAgent whose services include selling 
Food to customers which is intended to be eaten on the premises.")

(=>
  (instance ?C Restaurant)
  (exists (?S ?F ?B)
    (and
      (instance ?S CommercialService)
      (agent ?S ?C)
      (instance ?S Selling)
      (instance ?B RestaurantBuilding)        
      (located ?S ?B)
      (patient ?S ?F)
      (instance ?F (FoodForFn Human)))))

(=>
  (instance ?PLACE RestaurantBuilding)
  (capability Cooking located ?PLACE))

(=>
  (instance ?R Restaurant)
  (and
    (possesses ?R ?D)
    (instance ?D DiningArea)))  
  
(subclass Proprietorship CommercialAgent)
(documentation Proprietorship EnglishLanguage "A &%CommercialAgent that is owned by a 
single person.")

(=>
  (instance ?C Proprietorship)
  (exists (?H)
    (and
      (instance ?H Human)
      (possesses ?H ?C)
      (not
        (exists (?H2)
	  (and
	    (not (equal ?H ?H2))
	    (possesses ?H2 ?C)))))))

(subclass Auditorium Building)
(documentation Auditorium EnglishLanguage "Any &%Building whose purpose is to hold concerts, 
sports events, plays, etc. before an audience.  This class includes theaters, 
sports stadiums, university auditoriums, etc.")

(=>
   (instance ?AUDITORIUM Auditorium)
   (hasPurpose ?AUDITORIUM (exists (?DEMO)
                              (and
                                 (instance ?DEMO Demonstrating)
                                 (located ?DEMO ?AUDITORIUM)))))

(=>
   (instance ?AUDITORIUM Auditorium)
   (exists (?STAGE)
      (and
         (instance ?STAGE PerformanceStage)
         (part ?STAGE ?AUDITORIUM))))

(subclass AuditoriumSeat Seat)
(documentation AuditoriumSeat EnglishLanguage "A &%Seat within an &%Auditorium from which one 
can observe the &%PerformanceStage.")

(=>
   (instance ?SEAT AuditoriumSeat)
   (exists (?AUDITORIUM)
      (and
         (instance ?AUDITORIUM Auditorium)
         (part ?SEAT ?AUDITORIUM))))

(=>
   (and
      (instance ?SEAT AuditoriumSeat)
      (part ?SEAT ?AUDITORIUM)
      (part ?STAGE ?AUDITORIUM)
      (instance ?AUDITORIUM Auditorium)
      (instance ?STAGE PerformanceStage)
      (located ?PERSON ?SEAT)
      (instance ?PERSON Human)
      (subclass ?SEE Seeing)
      (forall (?INST)
         (=>
            (instance ?INST ?SEE)
            (patient ?INST ?STAGE))))
   (capability ?SEE agent ?PERSON))

(subrelation enjoys inScopeOfInterest)
(domain enjoys 1 CognitiveAgent)
(domainSubclass enjoys 2 IntentionalProcess)
(documentation enjoys EnglishLanguage "(&%enjoys ?AGENT ?PROCESS) means that the 
&%CognitiveAgent ?AGENT tends to enjoy actions of type ?PROCESS, 
i.e. tends to enjoy being the &%agent or &%experiencer of such 
actions.")

(=>
   (enjoys ?AGENT ?PROCESS)
   (desires ?AGENT (exists (?INSTANCE)
                      (and
                         (instance ?INSTANCE ?PROCESS)
                         (or
                            (agent ?INSTANCE ?AGENT)
                            (experiencer ?INSTANCE ?AGENT))))))

(subrelation expects believes)
(documentation expects EnglishLanguage "Any belief about the future.  (&%expects 
?AGENT ?BELIEF) means that (&%believes ?AGENT ?BELIEF) and, if 
?BELIEF happens, it will happen in the future, i.e. after the 
expectation.")

(=>
   (and
      (holdsDuring ?TIME1 (expects ?AGENT ?FORMULA))
      (holdsDuring ?TIME2 (truth ?FORMULA True)))
   (earlier ?TIME1 ?TIME2))

(subrelation fears expects)
(documentation fears EnglishLanguage "(&%fears ?AGENT ?FORMULA) means that ?AGENT fears that 
the proposition ?FORMULA will be true, i.e. he/she believes that it will 
come to pass in the future and that it will be undesirable for ?AGENT.")

(=>
   (fears ?AGENT ?FORMULA)
   (not (desires ?AGENT ?FORMULA)))

(subrelation hopes expects)
(documentation hopes EnglishLanguage "(&%hopes ?AGENT ?FORMULA) means that ?AGENT hopes that 
the proposition ?FORMULA will be true, i.e. he/she believes that it will 
come to pass in the future and that it will be desirable for ?AGENT.")

(=>
   (hopes ?AGENT ?FORMULA)
   (desires ?AGENT ?FORMULA))

(instance doubts PropositionalAttitude)
(instance doubts BinaryPredicate)
(domain doubts 1 CognitiveAgent)
(domain doubts 2 Formula)
(documentation doubts EnglishLanguage "(&%doubts ?AGENT ?FORMULA) means that ?AGENT is unsure 
about the truth of ?FORMULA, in particular ?AGENT does not believe that 
?FORMULA is true.")

(=>
   (doubts ?AGENT ?FORMULA)
   (not (believes ?AGENT ?FORMULA)))

(instance dislikes ObjectAttitude)
(subrelation dislikes inScopeOfInterest)
(disjointRelation dislikes wants)
(relatedInternalConcept dislikes disapproves)
(domain dislikes 1 CognitiveAgent)
(domain dislikes 2 Object)
(documentation dislikes EnglishLanguage "(&%dislikes ?AGENT ?OBJECT) means that ?AGENT has a 
feeling of antipathy to ?OBJECT, i.e. ?AGENT believes that ?OBJECT will 
thwart one of his/her goals.  Note that there is no implication that what 
is hated by an agent is not already possessed by the agent.")

(=>
   (dislikes ?AGENT ?OBJECT)
   (desires ?AGENT (not (possesses ?AGENT ?OBJECT))))

(instance disapproves PropositionalAttitude)
(instance disapproves BinaryPredicate)
(subrelation disapproves inScopeOfInterest)
(disjointRelation disapproves desires)
(domain disapproves 1 CognitiveAgent)
(domain disapproves 2 Formula)
(documentation disapproves EnglishLanguage "(&%disapproves ?AGENT ?FORMULA) means that 
?AGENT has a feeling of antipathy to the state of affairs represented by 
?FORMULA, i.e. ?AGENT believes that the realization of ?FORMULA will 
thwart one of his/her goals.  Note that there is no implication that what 
is disapproved of by an agent is not already true.")

(subrelation lacks needs)
(documentation lacks EnglishLanguage "(&%lacks ?AGENT ?OBJECT) means that ?AGENT &%needs 
?OBJECT and it is not currently the case that ?AGENT &%possesses ?OBJECT.")

(=>
   (holdsDuring ?TIME (lacks ?AGENT ?OBJECT))
   (holdsDuring ?TIME (not (possesses ?AGENT ?OBJECT))))

(instance Antisemitism BeliefGroup)
(documentation Antisemitism EnglishLanguage "The &%BeliefGroup that is characterized by a 
dislike for &%Judaism.")

(=>
   (member ?MEMBER Antisemitism)
   (dislikes ?MEMBER Judaism))

(instance DescendantsFn UnaryFunction)
(domain DescendantsFn 1 Human)
(range DescendantsFn FamilyGroup)
(documentation DescendantsFn EnglishLanguage "(&%DescendantsFn ?PERSON) denotes all and only 
the descendants of ?PERSON, i.e. the &%Group consisting of ?OFFSPRING who 
satisfy the following formula:  (&%ancestor ?OFFSPRING ?PERSON).")

(=>
   (and
      (instance ?PERSON Human)
      (equal (DescendantsFn ?PERSON) ?DESCENDANTS))
   (forall (?MEMBER)
      (<=>
         (member ?MEMBER ?DESCENDANTS)
         (ancestor ?MEMBER ?PERSON))))

(subclass GroupOfAnimals Group)
(documentation GroupOfAnimals EnglishLanguage "Any &%Group which contains exclusively 
non-human &%members.")

(=>
   (instance ?GROUP GroupOfAnimals)
   (forall (?MEMBER)
      (=>
         (member ?MEMBER ?GROUP)
         (and
            (instance ?MEMBER Animal)
            (not (instance ?MEMBER Human))))))

(subclass Brood GroupOfAnimals)
(documentation Brood EnglishLanguage "A &%GroupOfAnimals that are all born at the same time 
and to the same parents.")

(=>
   (instance ?BROOD Brood)
   (forall (?MEMBER1 ?MEMBER2)
      (=>
         (and
            (member ?MEMBER1 ?BROOD)
            (member ?MEMBER2 ?BROOD))
         (sibling ?MEMBER1 ?MEMBER2))))

(=>
   (instance ?BROOD Brood)
   (exists (?TIME)
      (forall (?MEMBER)
         (=>
            (member ?MEMBER ?BROOD)
            (exists (?BIRTH)
               (and
                  (instance ?BIRTH Birth)
                  (experiencer ?BIRTH ?MEMBER)
                  (equal ?TIME (WhenFn ?BIRTH))))))))
      
(subclass AnimalTeam GroupOfAnimals)
(documentation AnimalTeam EnglishLanguage "A &%GroupOfAnimals which are &%Pulling something.")
(=>
   (instance ?AT AnimalTeam)
   (exists (?P)
      (and
         (instance ?P Pulling)
         (agent ?P ?AT))))

(subclass SportsTeam GroupOfPeople)
(documentation SportsTeam EnglishLanguage "A &%GroupOfPeople who habitually play a &%Sport 
together, either as an occupation or as a leisure activity.")

(=>
   (instance ?TEAM SportsTeam)
   (exists (?SPORT)
      (and
         (subclass ?SPORT Sport)
         (capability ?SPORT agent ?TEAM))))

(subclass TeamSport Sport)
(documentation TeamSport EnglishLanguage "Any &%Sport which is played by &%SportsTeams, e.g. 
&%Baseball and &%Football.")

(=>
  (instance ?S TeamSport)
  (exists (?T1 ?T2)
    (and
      (contestParticipant ?S ?T1)
      (contestParticipant ?S ?T2)
      (instance ?T1 SportsTeam)
      (instance ?T2 SportsTeam)
      (not (equal ?T1 ?T2)))))

(subclass Racing Sport)
(documentation Racing EnglishLanguage "A sport which involves a contest of speed between the 
participants.  Note that this covers a variety of things, including auto 
racing, running competitions, etc.")

(subrelation groupMember member)
(domain groupMember 1 Human)
(domain groupMember 2 GroupOfPeople)
(documentation groupMember EnglishLanguage "A &%subrelation of &%member, &%groupMember 
is used to relate a &%Human to a &%GroupOfPeople of which he/she is a 
&%member.")

(documentation headquartersOfOrganization EnglishLanguage "(headquartersOfOrganization ?ORG ?AREA) 
means that the &%Organization ?ORG is headquartered in ?AREA.")
(domain headquartersOfOrganization 1 Organization)
(domain headquartersOfOrganization 2 GeopoliticalArea)
(subrelation headquartersOfOrganization located)

(subclass CommunicationOrganization Organization)
(documentation CommunicationOrganization EnglishLanguage "&%CommunicationOrganization is 
the subclass of &%Organizations that manage &%Communications over physical 
infrastructure owned or leased by the organization.  Such organizations 
may also produce and disseminate information, entertainment, or other 
content.  Also see &%MediaOrganization.")
(=>
  (instance ?CO CommunicationOrganization)
  (exists (?CS ?C)
    (and
      (instance ?C Communication)
      (instance ?CS CommunicationSystem)
      (instrument ?C ?CS)
      (agent ?C ?CO))))

(subclass MediaOrganization Organization)
(documentation MediaOrganization EnglishLanguage "&%MediaOrganization is the subclass 
of &%Organization for groups whose primary purpose is the production or 
dissemination of media content.  For organizations that physically enable 
&%Communication, see &%CommunicationOrganization.")

(subclass NonprofitOrganization Organization)
(disjoint NonprofitOrganization CommercialAgent)
(documentation NonprofitOrganization EnglishLanguage "Any &%Organization whose purpose is something 
other than making a profit.")

(=>
  (instance ?N NonprofitOrganization)
  (not 
    (hasPurpose ?N 
      (exists (?P)
        (profit ?N ?P)))))

(subclass CommunicationSystem Collection)
(documentation CommunicationSystem EnglishLanguage "&%CommunicationSystem is a complex 
system with various components, enabling communication (in some 
medium) between points in a specific area, whether local or worldwide.")

(subclass CareOrganization Organization)
(partition CareOrganization HospitalOrganization MedicalClinic)
(documentation CareOrganization EnglishLanguage "Any &%Organization whose purpose is to 
provide medical care for for &%Humans who reside there, either permanently 
or temporarily.")  

(instance medicalPatient BinaryPredicate)
(documentation medicalPatient EnglishLanguage "A relation between a &%Human and a 
&%CareOrganization that treats the patient.")
(domain medicalPatient 1 Human)
(domain medicalPatient 2 CareOrganization)

(=>
   (instance ?ORG CareOrganization)
   (hasPurpose ?ORG (exists (?PATIENT) (medicalPatient ?PATIENT ?ORG))))

(subclass HospitalOrganization CareOrganization)
(documentation HospitalOrganization EnglishLanguage "A &%CareOrganization where patients reside for a 
short period of time while they undergo treatment for a disease or disorder.")

(subclass HospitalBuilding TemporaryResidence)
(documentation HospitalBuilding EnglishLanguage "A &%Building that serves at the place of business for
a &%HospitalOrganization.")

(subclass MedicalClinic CareOrganization)
(disjoint MedicalClinic Residence)
(documentation MedicalClinic EnglishLanguage "A &%CareOrganization which provides medical care 
on an out-patient basis only, i.e. there are no rooms where patients may take 
up residence for a period of time while they receive care.")

(subclass MedicalClinicBuilding (ComplementFn Residence))
(subclass MedicalClinicBuilding Building)

(subclass Cafeteria Restaurant)
(documentation Cafeteria EnglishLanguage "Any &%Restaurant which does not offer table service.  
&%Food is selected and purchased at a central counter.")

(=>
  (instance ?COMP Cafeteria)
  (not
    (exists (?PERSON)
      (occupiesPosition ?PERSON Waiter ?COMP))))

(subclass Tavern Restaurant)
(documentation Tavern EnglishLanguage "A &%Restaurant whose primary service is selling 
&%AlcoholicBeverages to customers.")

(=>
   (instance ?COMPANY Tavern)
   (exists (?SERVICE ?BEVERAGE)
      (and
         (instance ?SERVICE CommercialService)
         (agent ?SERVICE ?COMPANY)
         (instance ?SERVICE Selling)
         (patient ?SERVICE ?BEVERAGE)
         (instance ?BEVERAGE AlcoholicBeverage))))

(subclass Shipping Transportation)
(documentation Shipping EnglishLanguage "Shipping is the subclass of &%Transportation 
events in which goods are transported from one place to another by an 
agent who is entrusted with the goods temporarily just in order to move 
them.  Shipping may be done within an organization or it may be done 
by an outside commercial agent.  See &%CommercialShipping.")

(subclass CommercialShipping Shipping)
(subclass CommercialShipping CommercialService)
(documentation CommercialShipping EnglishLanguage "&%CommercialShipping is the subclass of 
&%Transportation events in which a commercial agent provides transportation 
of goods for remuneration.")

(subclass Smuggling Transportation)
(subclass Smuggling CriminalAction)
(documentation Smuggling EnglishLanguage "Any instance &%Transportation which is also a 
&%CriminalAction.")

(subclass RailroadCompany TransportationCompany)
(documentation RailroadCompany EnglishLanguage "Any &%TransportationCompany whose services 
include &%Transportation by &%Train.")

(=>
   (instance ?COMPANY RailroadCompany)
   (exists (?SERVICE ?TRAIN)
      (and
         (instance ?SERVICE CommercialService)
         (agent ?SERVICE ?COMPANY)
         (instance ?SERVICE Transportation)
         (instrument ?SERVICE ?TRAIN)
         (instance ?TRAIN Train))))
                       
(subclass WholesaleStore MercantileOrganization)
(subclass WholesaleStore Agency)
(documentation WholesaleStore EnglishLanguage "A &%MercantileOrganization that sells its 
goods exclusively to &%Corporations.")

(=>
   (and
      (instance ?STORE WholesaleStore)
      (customer ?CUSTOMER ?STORE))
   (instance ?CUSTOMER Corporation))

(subclass RetailStore MercantileOrganization)
(disjoint RetailStore WholesaleStore)
(documentation RetailStore EnglishLanguage "A &%Store where individuals who are not
representing an &%Organization purchase items.  This distinguishes
retail stores from wholesale establishments, where the purchasers
are businesses or their representatives, as well as mail order or
office buildings where transactions are facillitated but the consumer
does not take possession of the item on the premises.  More succinctly,
the complement of &%WholesaleStore, i.e. 
&%MercantileOrganizations that sell their goods to the general public.")
                                                    
(=>
  (instance ?X RetailStore)
  (and 
    (capability Selling located ?X)
    (exists (?P ?EV)
      (and
        (instance ?EV Selling)
        (destination ?EV ?P)
        (instance ?P Human)))))
        
(subclass ShoppingMall MercantileOrganization)
(documentation ShoppingMall EnglishLanguage "A &%MercantileOrganization which is a collection 
of various shops gathered together in a single, modern development.")

(=>
  (and
    (instance ?M ShoppingMall)
    (member ?S ?M))
  (instance ?S RetailStore)) 

(subclass DrugStore RetailStore)
(documentation DrugStore EnglishLanguage "A &%RetailStore that sells &%Medicine, and 
perhaps other items as well.")
(=>
  (instance ?D DrugStore)
  (exists (?S)
    (and
      (instance ?S Selling)
      (agent ?S ?D)
      (patient ?S ?M)
      (instance ?M Medicine))))

(subclass GroceryStore RetailStore)
(documentation GroceryStore EnglishLanguage "A &%RetailStore that sells food, and perhaps 
other items as well.")

(=>
   (instance ?STORE GroceryStore)
   (exists (?SELL ?FOOD)
      (and
         (instance ?SELL Selling)
         (agent ?SELL ?STORE)
         (patient ?SELL ?FOOD)
         (instance ?FOOD (FoodForFn Animal)))))

(subclass OfferingForSale Offering)
(documentation OfferingForSale EnglishLanguage "&%Offering to sell something to someone.")

(=>
  (and
    (instance ?SALE OfferingForSale)
    (agent ?SALE ?AGENT))
  (patient ?SALE 
    (exists (?SELL ?OBJ)
      (and
        (instance ?SELL Selling)
        (patient ?SELL ?OBJ)
        (agent ?SELL ?AGENT)))))

(subclass OfferingToPurchase Offering)
(documentation OfferingToPurchase EnglishLanguage "&%Offering to buy something from someone.")

(subclass BargainSale Offering)
(documentation BargainSale EnglishLanguage "&%Offering to sell something to someone at a 
reduced price.")

(=>
   (and
      (instance ?SALE BargainSale)
      (patient ?SALE ?OBJ)
      (instance ?OBJ Object))
   (exists (?PRICE1 ?PRICE2 ?AGENT1 ?AGENT2)
      (and
         (holdsDuring (ImmediatePastFn (WhenFn ?SALE)) (price ?OBJ ?PRICE1 ?AGENT1))
         (holdsDuring (WhenFn ?SALE) (price ?OBJ ?PRICE2 ?AGENT2))
         (lessThan ?PRICE2 ?PRICE1))))

(subclass ChristianBible Book)
(subsumesContentClass ChristianBible NewTestament)
(subsumesContentClass ChristianBible OldTestament)
(documentation ChristianBible EnglishLanguage "Any instance of the collection of writings which 
is regarded as scripture by those who embrace &%Christianity.")

(subclass ChristianGospel Text)
(documentation ChristianGospel EnglishLanguage "Four books in the New Testament of the 
&%ChristianBible that describe the life and teachings of Jesus Christ 
and that are referred to, respectively, as Matthew, Mark, Luke, and John.")

(subclass OldTestament Book)
(documentation OldTestament EnglishLanguage "The first part of the &%ChristianBible, describing 
the history of the Hebrew people.")

(subclass NewTestament Book)
(subsumesContentClass NewTestament ChristianGospel)
(documentation NewTestament EnglishLanguage "The second and final part of the &%ChristianBible,
describing the life and teachings of Jesus Christ and the activities of his 
apostles.")

(instance BeforeCommonEra TimeInterval)
(documentation BeforeCommonEra EnglishLanguage "The &%TimeInterval that runs from 
&%NegativeInfinity to the time of the birth of Christ.")

(equal (BeginFn BeforeCommonEra) NegativeInfinity)

(not (overlapsTemporally CommonEra BeforeCommonEra))

(instance CommonEra TimeInterval)
(documentation CommonEra EnglishLanguage "The &%TimeInterval that runs from the supposed 
time of the death of Christ to &%PositiveInfinity.")

(equal (EndFn CommonEra) PositiveInfinity)

(subclass MissionOrganization ReligiousOrganization)
(documentation MissionOrganization EnglishLanguage "The class of &%ReligiousOrganizations 
that send members to foreign countries with the aim of coverting citizens 
of those countries to the beliefs of the &%ReligiousOrganization.")

(subclass ReligiousBuilding Building)
(documentation ReligiousBuilding EnglishLanguage "A &%Building which is intended to be 
used for religious worship.  This class covers churches, temples, 
religious shrines, etc.")

(=>
   (instance ?BUILDING ReligiousBuilding)
   (hasPurpose ?BUILDING (exists (?SERVICE)
                            (and
                               (instance ?SERVICE ReligiousService)
                               (located ?SERVICE ?BUILDING)))))

(subclass Steeple StationaryArtifact)
(documentation Steeple EnglishLanguage "A component of a &%ReligiousBuilding that is tall 
and narrow and symbolizes the connection between humanity and a deity.")

(=>
   (instance ?STEEPLE Steeple)
   (exists (?BUILDING)
      (and
         (instance ?BUILDING Building)
         (part ?STEEPLE ?BUILDING))))

(subclass ReligiousService ReligiousProcess)
(subclass ReligiousService Demonstrating)
(documentation ReligiousService EnglishLanguage "A formal process of public worship which is 
typically carried out in a church, temple or other sanctified building and 
which typically accords with a prescribed set of rules.")

(subclass ChristianService ReligiousService)
(documentation ChristianService EnglishLanguage "Any &%ReligiousService that is conducted by 
&%members of &%Christianity.")

(=>
   (and
      (instance ?SERVICE ChristianService)
      (agent ?SERVICE ?PERSON)
      (instance ?PERSON Human))
   (member ?PERSON Christianity))

(subclass Baptizing ReligiousProcess)
(subclass Baptizing JoiningAnOrganization)
(documentation Baptizing EnglishLanguage "A &%ReligiousProcess which marks the acceptance of 
the person being baptized into the &%ReligiousOrganization.")
(=>
  (and
    (instance ?B Baptizing)
    (patient ?B ?H))
  (instance ?H Human))

(subclass Praying ReligiousProcess)
(subclass Praying Requesting)
(documentation Praying EnglishLanguage "A formal or informal process of private worship which 
may or may not be carried out in a &%ReligiousBuilding.")

(subclass PartyPlatform FactualText)
(documentation PartyPlatform EnglishLanguage "A &%Text which is authored by a &%PoliticalParty 
and which contains the core goals and principles of the &%PoliticalParty for a 
particular year or election cycle.")

(=>
   (and
      (subclass ?PLATFORM PartyPlatform)
      (authors ?PARTY ?PLATFORM)
      (instance ?PARTY Organization))
   (instance ?PARTY PoliticalParty))

(=>
   (and
      (subclass ?PLATFORM PartyPlatform)
      (authors ?PARTY ?PLATFORM)
      (instance ?PARTY PoliticalParty)
      (instance ?INST ?PLATFORM)
      (containsInformation ?INST ?PROP))
   (believes ?PARTY ?PROP))

(subclass CommunistParty PoliticalParty)
(documentation CommunistParty EnglishLanguage "Any &%PoliticalParty that advocates for a 
&%CommunistState.")
(=>
  (and
    (instance ?P CommunistParty)
    (member ?M ?P)
    (instance ?A GeopoliticalArea))
  (desires ?M (governmentType ?A CommunistState)))

(instance DemocraticParty PoliticalParty)
(documentation DemocraticParty EnglishLanguage "One of the two major political parties in 
the &%UnitedStates.  The &%DemocraticParty represents liberal values.")

(instance NaziParty PoliticalParty)
(documentation NaziParty EnglishLanguage "The German &%PoliticalParty headed by Adolf Hitler.")

(instance RepublicanParty PoliticalParty)
(documentation RepublicanParty EnglishLanguage "One of the two major political parties in 
the &%UnitedStates.  The &%RepublicanParty represents traditional, 
conservative values.")

(subclass ReferenceText FactualText)
(documentation ReferenceText EnglishLanguage "A &%Text which is not intended to be read 
from beginning to end, but which is meant to be consulted to answer specific 
factual questions, e.g. about the meaning of a word, the location of a 
country, etc.")

(instance ResidentFn UnaryFunction)
(domain ResidentFn 1 GeopoliticalArea)
(range ResidentFn GroupOfPeople)
(documentation ResidentFn EnglishLanguage "(&%ResidentFn ?AREA) denotes the 
&%GroupOfPeople who have their &%home in ?AREA.")

(=>
   (member ?PERSON (ResidentFn ?AREA))
   (home ?PERSON ?AREA))

(subrelation CitizenryFn ResidentFn)
(domain CitizenryFn 1 GeopoliticalArea)
(range CitizenryFn GroupOfPeople)
(documentation CitizenryFn EnglishLanguage "(&%CitizenryFn ?AREA) denotes the 
&%GroupOfPeople who are legal and permanent residents of the 
&%GeopoliticalArea ?AREA.")

(=>
   (and
      (instance ?AREA GeopoliticalArea)
      (equal ?CITIZENRY (CitizenryFn ?AREA))
      (equal ?POPULATION (ResidentFn ?AREA)))
   (greaterThanOrEqualTo (CardinalityFn ?POPULATION) (CardinalityFn ?CITIZENRY)))

(subclass CityDistrict GeopoliticalArea)
(documentation CityDistrict EnglishLanguage "Any &%geopoliticalSubdivision of a &%City.")

(<=>
   (instance ?DISTRICT CityDistrict)
   (exists (?CITY)
      (and
         (instance ?CITY City)
         (geopoliticalSubdivision ?DISTRICT ?CITY))))

(subclass Downtown CityDistrict)
(documentation Downtown EnglishLanguage "The commercial center of a &%City.  The part of the 
&%City that contains more shops and offices than any other part.")

(subclass Cemetery LandArea)
(documentation Cemetery EnglishLanguage "A &%LandArea which is used for burying the dead.")
(=>
  (instance ?C Cemetery)
  (exists (?T)
    (and
      (instance ?T Tomb)
      (located ?T ?C))))

(subclass Park LandArea)
(documentation Park EnglishLanguage "A publicly owned &%LandArea which is intended to be used 
for recreation and/or exercise.")

(=>
  (instance ?PARK Park)
  (exists (?GOV)
    (and
      (instance ?GOV Government)
      (possesses ?GOV ?PARK))))

(=>
  (instance ?PARK Park)
  (hasPurpose ?PARK 
    (exists (?REC)
      (and
        (instance ?REC RecreationOrExercise)
        (located ?REC ?PARK)))))

(subclass PublicPark Park)
(documentation PublicPark EnglishLanguage "A &%Park that is publicly owned, i.e. owned 
by a &%Government.")

(=>
  (instance ?P PublicPark)
  (exists (?G)
    (and
      (instance ?G Government)
      (possesses ?G ?P))))

(subclass CultivatedLandArea LandArea)
(documentation CultivatedLandArea EnglishLanguage "A &%LandArea which is dedicated to 
&%Agriculture, e.g. &%Lawns, gardens, and fields for growing crops.")

(=>
  (instance ?A CultivatedLandArea)
  (exists (?C)
    (and
      (instance ?C Agriculture)
      (located ?C ?A))))

(subclass Campground LandArea)
(documentation Campground EnglishLanguage "A &%LandArea whose purpose is to have &%MobileResidences 
(e.g. recreational vehicles, mobile homes, &%Tents, etc.) located there.")
(=>
  (instance ?C Campground)
  (hasPurpose ?C 
  (exists (?R)
    (and
      (instance ?R MobileResidence)
      (located ?R ?C)))))

(subclass CityBlock LandArea)
(documentation CityBlock EnglishLanguage "A square-shaped area surrounded by &%Roadways 
which is part of a &%City and typically contains &%Buildings.")
(=>
  (instance ?B CityBlock)
  (exists (?C)
    (and
      (instance ?C City)
      (part ?B ?C))))

(subclass AerobicExerciseDevice RecreationOrExerciseDevice)
(documentation AerobicExerciseDevice EnglishLanguage "An &%RecreationOrExerciseDevice whose purpose is 
to develop the cardiovascular system.")

(subclass AnaerobicExerciseDevice RecreationOrExerciseDevice)
(disjoint AnaerobicExerciseDevice AerobicExerciseDevice)
(documentation AnaerobicExerciseDevice EnglishLanguage "An &%RecreationOrExerciseDevice whose purpose 
is to develop &%Muscles without also developing the cardiovascular system.")

(instance UnitedStatesDepartmentOfState GovernmentOrganization)
(subOrganization UnitedStatesDepartmentOfState (GovernmentFn UnitedStates))
(documentation UnitedStatesDepartmentOfState EnglishLanguage "The &%subOrganization of the US 
government that sets and enforces foreign policy.")

(instance UnitedStatesDepartmentOfInterior GovernmentOrganization)
(subOrganization UnitedStatesDepartmentOfInterior (GovernmentFn UnitedStates))
(documentation UnitedStatesDepartmentOfInterior EnglishLanguage "Manages and preserves public lands 
and natural resources in the &%UnitedStates.")

(documentation GovernmentSecretary EnglishLanguage "The class of &%Positions where the position holder 
is head of an adminstrative department of &%Government.")
(subclass GovernmentSecretary SkilledOccupation)

(instance SecretaryOfTheInterior GovernmentSecretary)
(documentation SecretaryOfTheInterior EnglishLanguage "The head of the &%UnitedStatesDepartmentOfInterior.")

(=>
   (occupiesPosition ?PERSON SecretaryOfTheInterior UnitedStatesDepartmentOfInterior)
   (leader UnitedStatesDepartmentOfInterior ?PERSON))

(instance SecretaryOfTheTreasury GovernmentSecretary)
(documentation SecretaryOfTheTreasury EnglishLanguage "The head of the United States Treasury Department.")

(subclass StateGovernment Government)
(documentation StateGovernment EnglishLanguage "The class of &%Governments whose 
jurisdictions are &%StateOrProvinces.")

(=>
   (instance ?GOVERNMENT StateGovernment)
   (exists (?STATE)
      (and
         (instance ?STATE StateOrProvince)
         (equal (GovernmentFn ?STATE) ?GOVERNMENT))))

(subclass CityGovernment Government)
(documentation CityGovernment EnglishLanguage "&%CityGovernment is the class of 
governments of &%Cities.")
(<=>
  (instance ?C City)
  (instance (GovernmentFn ?C) CityGovernment))

(subclass PopularElection GeneralElection)
(documentation PopularElection EnglishLanguage "&%PopularElection is the class of 
&%Elections in which office-holders and issues are determined directly by 
the outcome of the votes cast by the enfranchised.  For example, U.S.  
Senators and Representatives are elected by &%PopularElection.  By 
contrast, U.S. Presidents are elected by indirect elections, in which the 
Electoral College determines the actual outcome.  See &%electionWinner.")

(subclass ElectoralCollegeElection GeneralElection)
(documentation ElectoralCollegeElection EnglishLanguage "&%ElectoralCollegeElection is 
the class of &%Elections in which the outcome is decided by the votes of 
electors who have been chosen by popular vote, rather than directly by 
results of the popular vote.  &%ElectoralCollegeElection is an indirect 
method of election, in contrast to &%PopularElection.")

(instance UnitedStatesCongress LegislativeOrganization)
(subOrganization UnitedStatesCongress (GovernmentFn UnitedStates))
(documentation UnitedStatesCongress EnglishLanguage "The legislative branch of the government 
of the &%UnitedStates.")

(subclass MilitaryService MilitaryOrganization)
(documentation MilitaryService EnglishLanguage "A branch of the armed forces of a &%Nation.  
For example, there are five military services in the United States, the army, 
the navy, the air force, the marines, and the coast guard.")

(subclass CoastGuard GovernmentOrganization)
(documentation CoastGuard EnglishLanguage "&%CoastGuard is the subclass of &%GovernmentOrganizations 
that enforce the maritime laws of a &%Nation and guard its &%Seacoast.  This may be 
a military or quasi-military organization.")

(subclass Army MilitaryService)
(documentation Army EnglishLanguage "&%MilitaryServices that are land forces.")

(subclass AirForce MilitaryService)
(documentation AirForce EnglishLanguage "&%AirForce is the subclass of &%MilitaryService 
that comprises military air forces.")

(subclass MilitaryUnit MilitaryOrganization)
(documentation MilitaryUnit EnglishLanguage "Any &%MilitaryOrganization that can be dispatched 
to an area of operations.")
(=>
  (instance ?B Battle)
  (exists (?U1 ?U2)
    (and
      (instance ?U1 MilitaryUnit)
      (instance ?U2 MilitaryUnit)
      (not 
        (equal ?U1 ?U2))
      (agent ?B ?U1)
      (agent ?B ?U2))))

(subclass MilitaryReserveForce MilitaryOrganization)
(documentation MilitaryReserveForce EnglishLanguage "&%MilitaryReserveForce is the subclass 
of &%MilitaryOrganizations that consist of forces trained in military 
procedures and activities, which are subject to being called to active duty 
if needed.")

(subclass MilitarySquad MilitaryUnit)
(documentation MilitarySquad EnglishLanguage "Any &%MilitaryUnit with the rank of squad.")

(subclass MilitaryCorps MilitaryUnit)
(documentation MilitaryCorps EnglishLanguage "Any &%MilitaryUnit with the rank of 
corps.")

(subclass CavalryUnit MilitaryUnit)
(documentation CavalryUnit EnglishLanguage "&%MilitaryUnits composed primarily of &%Soldiers 
who are mounted, i.e. who perform their combat missions from a &%Horse or 
&%Vehicle.")
(=>
  (and
    (instance ?C CavalryUnit)
    (instance ?B Battle)
    (agent ?B ?C))
  (exists (?P ?V ?T)
    (and
      (attribute ?P Soldier)
      (member ?P ?C)
      (instance ?T Transportation)
      (agent ?T ?P)
      (instance ?V Vehicle)
      (patient ?T ?V)
      (during ?T ?B))))

(subclass MilitarySurplus MilitaryArtifact)
(documentation MilitarySurplus EnglishLanguage "Any &%MilitaryArtifact which is sold to a 
non-military customer.")

(=>
  (instance ?S MilitarySurplus)
  (exists (?A ?B)
    (and
      (instance ?B Buying)
      (patient ?B ?A)
      (not 
        (instance ?A MilitaryOrganization))
      (not 
        (member ?A MilitaryOrganization)))))

(documentation hostileForces EnglishLanguage "(hostileForces ?UNIT1 ?UNIT2) means that 
the &%MilitaryUnits ?UNIT1 and ?UNIT2 are, respectively, allied with 
&%GeopoliticalAreas that are at war with one another.")
(instance hostileForces BinaryPredicate)
(instance hostileForces IrreflexiveRelation)
(domain hostileForces 1 MilitaryUnit)
(domain hostileForces 2 MilitaryUnit)

(=>
  (hostileForces ?U1 ?U2)
  (exists (?W ?A1 ?A2)
    (and
      (instance ?WAR War)
      (contestParticipant ?W ?A1)
      (contestParticipant ?W ?A2)
      (subOrganization ?U1 (GovernmentFn ?A1))
      (subOrganization ?U2 (GovernmentFn ?A2)))))

(subclass Commission Organization)
(documentation Commission EnglishLanguage "A small, temporary &%Organization whose purpose 
is to investigate some issue.")

(=>
   (instance ?COMMISSION Commission)
   (hasPurpose ?COMMISSION (exists (?INVESTIGATE ?ISSUE)
                              (and
                                 (instance ?INVESTIGATE Investigating)
                                 (patient ?INVESTIGATE ?ISSUE)
                                 (agent ?INVESTIGATE ?COMMISSION)))))

(subclass ServiceOrganization Organization)
(documentation ServiceOrganization EnglishLanguage "An &%Organization that performs 
a public service and is regulated by the &%Government.")

(=>
   (instance ?ORG ServiceOrganization)
   (exists (?PROC ?GOV)
      (and
         (instance ?PROC RegulatoryProcess)
         (patient ?PROC ?ORG)
         (agent ?PROC ?GOV)
         (instance ?GOV Government)))) 

(subclass Library EducationalOrganization)
(documentation Library EnglishLanguage "An &%EducationalOrganization which is a repository 
of &%Texts which have been classified for efficient retrieval.")

(=>
  (instance ?L Library)
  (exists (?B ?T)
    (and
      (possesses ?L ?B)
      (instance ?B Building)
      (instance ?T Text)
      (located ?T ?B))))

(subclass PublicLibrary Library)
(subclass PublicLibrary GovernmentOrganization)
(documentation PublicLibrary EnglishLanguage "A &%Library which is financed by taxes and which 
is open to everyone.")

(subclass Museum EducationalOrganization)
(documentation Museum EnglishLanguage "An &%EducationalOrganization where &%Artifacts of historic, 
scientific or aesthetic value are collected and exhibited.")

(subclass OrganizationalBoard Organization)
(documentation OrganizationalBoard EnglishLanguage "Part of an &%Organization that 
is responsible for managing the &%Organization.")

(=>
   (instance ?BOARD OrganizationalBoard)
   (exists (?ORG ?MANAGE)
      (and
         (subOrganization ?BOARD ?ORG)
         (instance ?MANAGE Managing)
         (agent ?MANAGE ?BOARD)
         (patient ?MANAGE ?ORG))))

(subclass SecurityUnit Organization)
(documentation SecurityUnit EnglishLanguage "The &%Organization that is charged with 
ensuring the security of members of the overall &%Organization and the 
property of the &%Organization.")

(=>
  (and
    (instance ?UNIT SecurityUnit)
    (subOrganization ?UNIT ?ORG))
  (holdsObligation 
    (exists (?MAINTAIN)
      (and
        (instance ?MAINTAIN Maintaining)
        (agent ?MAINTAIN ?UNIT)
        (patient ?MAINTAIN ?ORG)))
    ?UNIT))

(subclass UnionOrganization Organization)
(documentation UnionOrganization EnglishLanguage "An &%Organization comprised of workers from 
the same &%Corporation or &%Industry.  The purpose of the &%UnionOrganization 
is to strengthen its representation in bargaining with the &%Corporation or 
&%Industry.")

(=>
   (instance ?ORG UnionOrganization)
   (exists (?COLL)
      (=>
         (member ?MEMBER ?ORG)
         (or
            (and
               (instance ?COLL Corporation)
               (employs ?COLL ?MEMBER)) 
            (exists (?CORP)
               (and
                  (instance ?COLL Industry)
                  (member ?CORP ?COLL)
                  (employs ?CORP ?MEMBER)))))))

(subclass OrganicCompound CompoundSubstance)
(documentation OrganicCompound EnglishLanguage "Any &%CompoundSubstance that has a &%Carbon 
base.")

(=>
   (and
      (instance ?COMPOUND OrganicCompound)
      (instance ?COMPOUND Molecule))
   (exists (?CARBON)
      (and
         (instance ?CARBON Carbon)
         (part ?CARBON ?COMPOUND))))

(subclass Urea OrganicCompound)
(documentation Urea EnglishLanguage "Urea is a dry, soluble, nitrogenous substance 
that is the major solid component of the urine of mammals, and which 
may be synthesized from ammonia and carbon dioxide, it is useful in the 
production of &%Plastic and in &%Fertilizers.") 

(subclass Steroid OrganicCompound)
(subclass Steroid BiologicallyActiveSubstance)
(documentation Steroid EnglishLanguage "A class of &%OrganicCompounds having the same basic 
chemical structure and having significant physiological effects.")

(subclass Hydrocarbon OrganicCompound)
(documentation Hydrocarbon EnglishLanguage "Any &%OrganicCompound that contains only &%Carbon 
and &%Hydrogen.")

(=>
  (instance ?S Hydrocarbon)
  (exists (?P1 ?P2)
    (and
      (equal ?S (MereologicalSumFn ?P1 ?P2))
      (instance ?P1 Carbon)
      (instance ?P2 Hydrogen))))

(subclass Alcohol OrganicCompound)
(documentation Alcohol EnglishLanguage "&%OrganicCompounds that are produced from hydrocarbons 
by distillation.")

(subclass Alkaloid ChemicalBase)
(subclass Alkaloid BiologicallyActiveSubstance)
(documentation Alkaloid EnglishLanguage "A &%ChemicalBase found in some &%Plants that has 
physiological and psychological effects.")
(=>
   (instance ?A Alkaloid)
   (exists (?N)
      (and
         (instance ?N Nitrogen)
         (part ?N ?A))))

(instance Drugged ConsciousnessAttribute)
(contraryAttribute Drugged Sober)
(documentation Drugged EnglishLanguage "The &%ConsciousnessAttribute of someone whose motor and/or 
cognitive faculties are significantly impaired by a &%BiologicallyActiveSubstance.")

(=>
  (holdsDuring ?T 
    (attribute ?P Drugged))
  (holdsDuring ?T 
    (exists (?B)
      (and
        (instance ?B BiologicallyActiveSubstance)
        (part ?B ?P)))))

(subAttribute Drunk Drugged)
(documentation Drunk EnglishLanguage "The &%ConsciousnessAttribute of someone whose motor and 
cognitive faculties are significantly impaired by &%Alcohol.")

(=>
  (holdsDuring ?TIME (attribute ?PERSON Drunk))
  (exists (?PAST)
    (and
      (temporalPart ?PAST (PastFn ?TIME))
      (holdsDuring ?PAST 
        (exists (?DRINK ?ALCOHOL)
          (and
            (instance ?DRINK Drinking)
            (patient ?DRINK ?ALCOHOL)
            (instance ?ALCOHOL AlcoholicBeverage)
            (agent ?DRINK ?PERSON)))))))

(=>
  (holdsDuring ?T 
    (attribute ?P Drunk))
  (holdsDuring ?T 
    (exists (?B)
      (and
        (instance ?B Alcohol)
        (part ?B ?P)))))

(subclass AminoAcid ChemicalAcid)
(subclass AminoAcid OrganicCompound)
(documentation AminoAcid EnglishLanguage "Organic acids that are the building blocks of 
&%Proteins.")
(=>
  (instance ?P Protein)
  (exists (?A)
    (and
      (instance ?A AminoAcid)
      (part ?A ?P))))

(subclass Cholesterol Steroid)
(subclass Cholesterol AnimalSubstance)
(documentation Cholesterol EnglishLanguage "A &%Steroid that is produced by the &%Liver and that 
is believed to be closely associated with various cardiological disorders.")
(=>
  (instance ?S Cholesterol)
  (exists (?P ?L)
    (and
      (instance ?P BiologicalProcess)
      (located ?P ?L)
      (instance ?L Liver)
      (result ?P ?S))))

(subclass ChemicalAcid CompoundSubstance)
(disjoint ChemicalAcid ChemicalBase)
(documentation ChemicalAcid EnglishLanguage "A bitter &%CompoundSubstance that is capable of 
reacting with a &%ChemicalBase and forming a &%ChemicalSalt.")
(=>
  (and
    (instance ?CA ChemicalAcid)
    (instance ?CB ChemicalBase)
    (instance ?C ChemicalProcess)
    (patient ?C ?CA)
    (patient ?C ?CB))
  (exists (?CS)
    (and 
      (instance ?CS ChemicalSalt)
      (result ?C ?CS))))

(subclass ChemicalBase CompoundSubstance)
(documentation ChemicalBase EnglishLanguage "A &%CompoundSubstance that is capable of 
reacting with a &%ChemicalAcid and forming a &%ChemicalSalt.")

(subclass ChemicalSalt CompoundSubstance)
(documentation ChemicalSalt EnglishLanguage "A bitter &%CompoundSubstance that is formed in a 
chemical reaction of a &%ChemicalBase with a &%ChemicalAcid.")

(subclass SodiumChloride ChemicalSalt)
(names "salt" SodiumChloride)
(names "NaCl" SodiumChloride)

(documentation SodiumChloride EnglishLanguage "&%SodiumChloride is the compound of &%Sodium and &%Chloride,
which may appear in crystalline form or in solution with water or other substances.
It is a compound found in solution in significant quantities in sea water.")

(=>
   (instance ?SALT SodiumChloride)
   (exists (?SYNTHESIS ?SODIUM ?CHLORINE)
      (and
         (instance ?SYNTHESIS ChemicalSynthesis)
         (resource ?SYNTHESIS ?SODIUM)
         (instance ?SODIUM Sodium)
         (resource ?SYNTHESIS ?CHLORINE)
         (instance ?CHLORINE Chlorine)
         (result ?SYNTHESIS ?SALT))))

(subclass Diamond Mineral)
(subclass Diamond Carbon)
(documentation Diamond EnglishLanguage "Crystalline &%Carbon that is valued as a gem and 
used in industrial applications.")

(subclass SalineSolution Solution)
(documentation SalineSolution EnglishLanguage "A &%Solution consisting of &%SodiumChloride and 
&%Water.")

(=>
   (and
      (instance ?SOLUTION SalineSolution)
      (part ?PART ?SOLUTION))
   (or
      (instance ?PART SodiumChloride)
      (instance ?PART Water)))

(=>
   (instance ?WATER Water)
   (exists (?SYNTHESIS ?HYDROGEN ?OXYGEN)
      (and
         (instance ?SYNTHESIS ChemicalSynthesis)
         (resource ?SYNTHESIS ?HYDROGEN)
         (instance ?HYDROGEN Hydrogen)
         (resource ?SYNTHESIS ?OXYGEN)
         (instance ?OXYGEN Oxygen)
         (result ?SYNTHESIS ?WATER))))

(documentation reactant EnglishLanguage "(reactant ?PROCESS ?SUBSTANCE) means that ?SUBSTANCE 
is a chemical reactant in the chemical reaction ?PROCESS, i.e. ?SUBSTANCE is 
present at the beginning of the chemical reaction ?PROCESS.")
(domain reactant 1 ChemicalProcess)
(domain reactant 2 Substance)
(subrelation reactant resource)

(documentation reagent EnglishLanguage "(reagent ?PROCESS ?SUBSTANCE) means that ?SUBSTANCE 
is a chemical agent in the chemical reaction ?PROCESS.")
(domain reagent 1 ChemicalProcess)
(domain reagent 2 Substance)
(subrelation reagent instrument)

(subclass Oil Solution)
(documentation Oil EnglishLanguage "A greasy, viscous &%Solution that cannot be mixed with &%Water.  
Note that this general class covers petroleum oil, vegetable oil, animal fat, etc.")

(=>
   (instance ?OIL Oil)
   (not (exists (?MIX ?WATER)
      (and
         (instance ?MIX Mixture)
         (part ?WATER ?MIX)
         (instance ?WATER Water)
         (part ?OIL ?MIX)))))

(subclass Surfactant Substance)

(documentation Surfactant EnglishLanguage "Surfactants, also known as &%Wetting agents, 
lower the surface tension of a &%Liquid, allowing easier spreading.  The 
term surfactant is a compression of 'Surface active agent'.  Surfactants 
are usually organic compounds that contain both hydrophobic and 
hydrophilic groups, and are thus semi-soluble in both organic and aqueous 
solvents.") 

(subclass Detergent Surfactant)
(subclass Detergent Mixture)
(documentation Detergent EnglishLanguage "A detergent is a compound, or a mixture of 
compounds, whose molecules have two distinct regions: one that is 
hydrophilic, and dissolves easily in water, and another region that is 
hydrophobic, with little (if any) affinity for water.  As a consequence, 
these compounds can aid in the solubilization of hydrophobic compounds in 
water, and usually are optimized for this property.  Though &%Soap also has 
these properties, soaps in general are not considered detergents.  &%Soap is 
a particular type of surfactant that is derived from oils and fats.  They 
are created through the saponification process whereby the ester linkage 
in a vegetable oil or fat is hydrolytically cleaved, creating a sodium or 
potassium salt of a fatty acid (i.e.  soap).  Both detergents and soaps 
are considered to be surfactants.  &%Surfactants that are not soaps are 
considered to be detergents.  Detergents are also commonly known as any 
cleaning mixture containing surfactants.  (from Wikipedia)") 

(=>
  (instance ?DETERGENT Detergent)
  (hasPurpose ?DETERGENT 
    (exists (?REMOVE ?SUBSTANCE ?SURFACE ?OBJECT)
      (and
        (instance ?REMOVE Removing)
        (instance ?SUBSTANCE Substance)
        (patient ?REMOVE ?SUBSTANCE)
        (origin ?REMOVE ?SURFACE)
        (surface ?SURFACE ?OBJECT)))))

(subclass Bathing Washing)
(documentation Bathing EnglishLanguage "&%Washing the entire body of a &%Human or &%Animal.")
(=>
  (and
    (instance ?B Bathing)
    (patient ?B ?A))
  (instance ?A Animal))

(subclass BathingDevice WashingDevice)
(documentation BathingDevice EnglishLanguage "A &%WashingDevice which is intended to be used by 
Humans for washing their bodies.  Note that this class covers bathtubs, showers, 
etc.")
(=>
  (instance ?BD BathingDevice)
  (hasPurpose ?BD 
    (exists (?B ?P)
      (and
        (instance ?B Bathing)
        (instrument ?B ?BD)
        (patient ?B ?PERSON)
        (instance ?P Human)))))

(subclass WashBasin WashingDevice)
(documentation WashBasin EnglishLanguage "A &%WashingDevice which is intended to be used by 
&%Humans for washing their &%Hands.")

(=>
  (instance ?B WashBasin)
  (hasPurpose ?B 
    (exists (?W ?H)
      (and
        (instance ?W Washing)
        (instrument ?W ?B)
        (patient ?W ?H)
        (instance ?H Hand)))))

(subclass Glue Mixture)
(documentation Glue EnglishLanguage "Any &%Mixture whose purpose is to be used as the &%instrument 
of &%Attaching one thing to another.")

(=>
   (instance ?GLUE Glue)
   (hasPurpose ?GLUE (exists (?ATTACH)
                            (and
                               (instance ?ATTACH Attaching)
                               (instrument ?ATTACH ?GLUE)))))

(subclass Glass Mixture)
(documentation Glass EnglishLanguage "A transparent or translucent &%Mixture of silicates.")

(subclass MetallicAlloy Mixture)
(documentation MetallicAlloy EnglishLanguage "A &%Mixture of two or more &%Metals, and possibly 
nonmetallic elements as well.  For example, steel is an alloy containing iron 
and manganese.")

(=>
   (instance ?ALLOY MetallicAlloy)
   (exists (?METAL1 ?METAL2)
      (and
         (instance ?METAL1 Metal)
         (instance ?METAL2 Metal)
         (not (equal ?METAL1 ?METAL2))
         (part ?METAL1 ?ALLOY)
         (part ?METAL2 ?ALLOY))))

(subclass Steel MetallicAlloy)
(documentation Steel EnglishLanguage "A &%MetallicAlloy made from &%Iron and other elements.")

(=>
   (instance ?STEEL Steel)
   (exists (?IRON)
      (and
         (instance ?IRON Iron)
         (part ?IRON ?STEEL))))

(subclass Brass MetallicAlloy)
(documentation Brass EnglishLanguage "A &%MetallicAlloy made from &%Copper and &%Zinc.")

(=>
   (instance ?BRASS Brass)
   (exists (?COPPER ?ZINC)
      (and
         (instance ?COPPER Copper)
         (instance ?ZINC Zinc)
         (part ?COPPER ?BRASS)
         (part ?ZINC ?BRASS))))

(subclass Powder Substance)
(documentation Powder EnglishLanguage "Any &%Solid &%Substance which consists of loose, 
identical, and very small particles.")

(=>
   (instance ?POWDER Powder)
   (attribute ?POWDER Solid))

(subclass Fallout Powder)
(documentation Fallout EnglishLanguage "Radioactive powder that is typically dispersed by 
the explosion of a nuclear weapon.")

(=>
   (instance ?POWDER Fallout)
   (capability RadiatingNuclear instrument ?POWDER)) 

(subclass Fog WaterCloud)
(documentation Fog EnglishLanguage "Any &%WaterCloud that is in contact with the ground.")

(=>
   (instance ?FOG Fog)
   (exists (?LAND)
      (and
         (instance ?LAND LandArea)
         (meetsSpatially ?FOG ?LAND))))

(subclass Ice Water)
(documentation Ice EnglishLanguage "&%Water that has the &%PhysicalState of &%Solid.")

(<=>
   (instance ?ICE Ice)
   (and
      (instance ?ICE Water)
      (attribute ?ICE Solid)))

(=>
   (and
      (instance ?ICE Ice)
      (measure ?ICE (MeasureFn ?NUMBER CelsiusDegree)))
   (lessThanOrEqualTo ?NUMBER 0))

(subclass Field LandArea)
(documentation Field EnglishLanguage "A &%LandArea that has been cleared of &%BotanicalTrees.  
Note that a &%Field is not necessarily used for the cultivation of crops and 
that a &%Field may be very small, e.g. &%Lawn is a subclass of &%Field.")

(=>
   (instance ?FIELD Field)
   (not (exists (?TREE)
      (and
         (instance ?TREE BotanicalTree)
         (located ?TREE ?FIELD)))))

(subclass Lawn CultivatedLandArea)
(subclass Lawn Field)
(documentation Lawn EnglishLanguage "A &%CultivatedLandArea and &%Field 
containing mowed &%Grass.")

(=>
  (instance ?L Lawn)
  (exists (?G)
    (and
      (instance ?G Grass)
      (located ?G ?L))))

(subclass MilitaryFront GeographicArea)
(documentation MilitaryFront EnglishLanguage "A &%GeographicArea along which opposing military 
forces confront one another in a &%Battle.")

(=>
   (holdsDuring ?TIME (instance ?AREA MilitaryFront))
   (exists (?BATTLE)
      (and
         (instance ?BATTLE Battle)
         (located ?BATTLE ?AREA)
         (temporalPart ?TIME ?BATTLE))))

(partition Region GeographicArea SpaceRegion)

(subclass Meteorite Meteoroid)
(documentation Meteorite EnglishLanguage "Any &%Meteoroid that leaves traces on the surface 
of &%Earth.")

(=>
  (instance ?M Meteorite)
  (exists (?T)
    (holdsDuring ?T
      (meetsSpatially ?M PlanetEarth))))

(subclass Meteoroid AstronomicalBody)
(documentation Meteoroid EnglishLanguage "Any &%AstronomicalBody that breaks through the 
&%Atmosphere of &%Earth.")

(=>
  (instance ?M Meteorite)
  (exists (?T)
    (holdsDuring ?T
      (meetsSpatially ?M EarthsAtmosphere))))

(subclass AtmosphericRegion SpaceRegion)
(documentation AtmosphericRegion EnglishLanguage "&%AtmosphericRegion is the class of 
all subregions of &%EarthsAtmosphere.")
(=>
  (instance ?AR AtmosphericRegion)
  (geographicSubregion ?AR EarthsAtmosphere))

(subclass Creek BodyOfWater)
(subclass Creek StreamWaterArea)
(subclass Creek FreshWaterArea)
(disjoint Creek River)
(documentation Creek EnglishLanguage "Creek is the class of small streams of fresh 
water flowing through land, usually into a &%River.")
(=>
  (and
    (instance ?C Creek)
    (instance ?R River))
  (smaller ?C ?R))

(subclass SpaceRegion Region)
(disjoint SpaceRegion GeographicArea)
(partition SpaceRegion AtmosphericRegion OuterSpaceRegion)
(documentation SpaceRegion EnglishLanguage "The class of all &%Regions which are not 
&%GeographicAreas.")

(subclass OuterSpaceRegion SpaceRegion)
(documentation OuterSpaceRegion EnglishLanguage "The class of all &%Regions which are 
neither &%GeographicAreas nor &%AtmosphericRegions.")

(subclass StormFront AtmosphericRegion)
(documentation StormFront EnglishLanguage "The &%Region where two or more unstable air 
masses meet.")

(=>
   (instance ?FRONT StormFront)
   (exists (?AIR1 ?AIR2)
      (and
         (instance ?AIR1 Air)
         (instance ?AIR2 Air)
         (between ?AIR1 ?FRONT ?AIR2))))

(instance Inside PositionalAttribute)
(documentation Inside EnglishLanguage "&%Inside is a &%PositionalAttribute used to 
describe the relative location of one object or region to another 
region.  For example, (&%orientation &%Virginia &%UnitedStates &%Inside).")

(=>
   (instance ?INSIDE Inside)
   (exists (?OBJECT)
     (forall (?THING)
       (=>
         (located ?THING ?INSIDE)
         (exists (?PART)
           (and
             (interiorPart ?PART ?OBJECT)
             (located ?THING ?PART)))))))

(instance Outside PositionalAttribute)
(contraryAttribute Inside Outside)
(documentation Outside EnglishLanguage "&%Outside is a &%PositionalAttribute used to 
describe the relative location of one object or region to another 
region.  For example, (&%orientation &%Cuba &%UnitedStates &%Outside).")

(=>
  (orientation ?OBJ ?REGION Outside)
  (instance ?REGION Region))

(=>
  (orientation ?OBJ ?REGION Inside)
  (instance ?REGION Region))

(<=>
  (orientation ?OBJ ?REGION Outside)
  (not 
    (partlyLocated ?OBJ ?REGION)))

(=>
  (part ?OBJ ?REGION)
  (not 
    (orientation ?OBJ ?REGION Outside)))

(subclass Outdoors Region)
(documentation Outdoors EnglishLanguage "Any &%Region which is not enclosed by a &%Building 
or part of a &%Building.")

(=>
   (instance ?OUTDOORS Outdoors)
   (not
     (exists (?BUILDING)
       (and
         (instance ?BUILDING Building)
         (orientation ?OUTDOORS ?BUILDING Inside)))))

(subclass Indoors Region)
(disjoint Indoors Outdoors)
(documentation Indoors EnglishLanguage "Any &%Region which is enclosed by a &%Building.")

(=>
   (instance ?INDOORS Indoors)
   (exists (?BUILDING)
     (and
       (instance ?BUILDING Building)
       (orientation ?INDOORS ?BUILDING Inside))))

(subclass BiologicalSpecies Class)
(documentation BiologicalSpecies EnglishLanguage "The &%Class of all biological species, i.e. 
the class of all classes of &%Organism whose instances can interbreed.")

(=>
   (instance ?SPECIES BiologicalSpecies)
   (subclass ?SPECIES Organism))

(subclass Wood Tissue)
(subclass Wood PlantSubstance)
(documentation Wood EnglishLanguage "&%Tissue that comprises the inner trunk of &%Trees.  
It is often used in constructing &%Buildings and other &%Artifacts.
Wood is the principal substance making up a tree, and is distinguished from the
bark, roots, flowers, seeds, fruit and leaves.") 

(relatedInternalConcept Wood WoodArtifact)
(subclass WoodArtifact Artifact)
(documentation WoodArtifact EnglishLanguage "&%Wood that has been cut (and perhaps treated) for 
some purpose, e.g. &%Constructing or &%Combustion.  Note that this class covers 
both lumber and firewood.")

(=>
  (instance ?W WoodArtifact)
  (material Wood ?W))

(subclass OakWood WoodArtifact)
(documentation OakWood EnglishLanguage "Any &%WoodArtifact that is made from the &%Wood of an 
oak tree.")

(subclass Rubber PlantSubstance)
(subclass Rubber BodySubstance)
(subclass Rubber Hydrocarbon)
(documentation Rubber EnglishLanguage "Natural rubber, as opposed to synthetic rubber
which is a &%Plastic, is a product of the rubber tree, genus Hevea or Ficus. 
It is the coagulated form of the milky juice secreted by these trees.  
Rubber is essentially a polymer of isoprene. ")
(=>
  (instance ?R Rubber)
  (exists (?T)
    (and
      (instance ?T BotanicalTree)
      (part ?R ?T))))

(subclass Cellulose PlantSubstance)
(subclass Cellulose Carbohydrate)
(documentation Cellulose EnglishLanguage "The main component of &%Plant &%Tissue.")

(subclass Starch Carbohydrate)
(documentation Starch EnglishLanguage "A complex &%Carbohydrate that is the main form in 
which &%Carbohydrates are stored.")

(subclass Sugar Carbohydrate)
(documentation Sugar EnglishLanguage "A simple &%Carbohydrate that has a sweet taste and 
consists mostly or entirely of sucrose.")

(subclass Nectar PlantSubstance)
(documentation Nectar EnglishLanguage "A sweet, sticky liquid that is produced by &%Plants 
and that attracts Insects.")

(subclass Medicine BiologicallyActiveSubstance)
(documentation Medicine EnglishLanguage "Any &%BiologicallyActiveSubstance which has a 
therapeutic effect under certain conditions.")

(=>
  (instance ?M Medicine)
  (exists (?P)
    (and
      (instance ?P TherapeuticProcess)
      (instrument ?P ?M))))

(subclass Stimulant BiologicallyActiveSubstance)
(disjoint Stimulant Depressant)
(documentation Stimulant EnglishLanguage "Any &%BiologicallyActiveSubstance which has 
the effect of stimulating the central nervous system, i.e. increasing 
function or activity in the &%Brain or &%SpinalCord.")

(subclass Depressant BiologicallyActiveSubstance)
(documentation Depressant EnglishLanguage "Any &%BiologicallyActiveSubstance which has 
the effect of depressing the central nervous system, i.e. decreasing 
function or activity in the &%Brain or &%SpinalCord.")

(subclass Opium BiologicallyActiveSubstance)
(subclass Opium PlantSubstance)
(documentation Opium EnglishLanguage "A substance harvested from the seed capsules of the 
opium poppy that contains various powerful alkaloids.")
(subclass Opium Depressant)

(=>
  (instance ?O Opium)
  (exists (?A)
    (and
      (instance ?A Alkaloid)
      (part ?A ?O))))

(subclass Grass FloweringPlant)
(documentation Grass EnglishLanguage "&%FloweringPlants with green, narrow leaves that are 
used for lawns and &%Fields. &%Grass includes any plant of the family 
Gramineae, a widely distributed group of mostly annual and perennial 
herbs.")

(subclass Lilac FloweringPlant)
(documentation Lilac EnglishLanguage "A &%FloweringPlant that has large clusters of 
aromatic flowers.")

(subclass Tobacco FloweringPlant)
(documentation Tobacco EnglishLanguage "A &%FloweringPlant containing nicotine whose leaves 
are dried and then smoked or ingested.")

(subclass Lesion AbnormalAnatomicalStructure)
(documentation Lesion EnglishLanguage "Any &%AbnormalAnatomicalStructure which is the result of 
an Injuring and which is found on the surface of an &%Organ.")

(=>
  (instance ?L Lesion)
  (exists (?I)
    (and
      (instance ?I Injuring)
      (result ?I ?L))))

(=>
  (instance ?L Lesion)
  (exists (?S ?O)
    (and
      (surface ?S ?O)
      (instance ?O Organ)
      (located ?L ?S))))

(subclass Scar AbnormalAnatomicalStructure)
(documentation Scar EnglishLanguage "Any &%AbnormalAnatomicalStructure which results from the 
healing of a &%Lesion.")

(=>
  (instance ?S Scar)
  (exists (?T ?OP ?L)
    (and
      (temporalPart ?T 
        (PastFn (WhenFn ?S)))
      (holdsDuring ?T 
        (instance ?L Lesion))
      (instance ?OP OrganOrTissueProcess)
      (patient ?OP ?L)
      (result ?OP ?S))))

(subclass Tumor AbnormalAnatomicalStructure)
(documentation Tumor EnglishLanguage "Any &%AbnormalAnatomicalStructure which consists of a 
mass of &%Tissue.  Note that this class covers both malignant (i.e. cancerous) 
and benign tumors.")

(=>
   (instance ?TUMOR Tumor)
   (forall (?PART)
      (=>
         (part ?PART ?TUMOR)
         (instance ?PART Tissue))))

(subclass Tendon Tissue)
(subclass Tendon AnimalSubstance)
(documentation Tendon EnglishLanguage "&%Tissue that connects &%Muscle to &%Bone.")

(=>
  (instance ?T Tendon)
  (exists (?M ?B)
    (and
      (instance ?M Muscle)
      (instance ?B Bone)
      (connects ?T ?M ?B))))

(subclass Sweat AnimalSubstance)
(documentation Sweat EnglishLanguage "An &%AnimalSubstance that contains &%SodiumChloride 
and is produced by the sweat glands.")

(=>
   (instance ?SWEAT Sweat)
   (exists (?PART)
      (and
         (instance ?PART SodiumChloride)
         (part ?PART ?SWEAT))))

(subclass Leaf PlantAnatomicalStructure)
(subclass Leaf Organ)
(documentation Leaf EnglishLanguage "An &%Organ of &%Plants whose main purpose is 
photosynthesis.")

(subclass PlantBranch PlantAnatomicalStructure)
(subclass PlantBranch BodyPart)
(documentation PlantBranch EnglishLanguage "The stem of a &%Plant or any shoot arising from 
the stem of a &%Plant.")

(subclass TreeBranch PlantBranch)
(documentation TreeBranch EnglishLanguage "Any &%PlantBranch which is part of a 
&%BotanicalTree.")

(=>
  (instance ?B TreeBranch)
  (exists (?T)
    (and
      (instance ?T BotanicalTree)
      (part ?B ?T))))

(subclass WillowTree BotanicalTree)
(documentation WillowTree EnglishLanguage "A &%BotanicalTree of the genus &%Salix.")

(subclass PlantRoot PlantAnatomicalStructure)
(subclass PlantRoot Organ)
(documentation PlantRoot EnglishLanguage "An &%Organ of &%Plants whose main purpose is 
twofold, viz. to absorb nutrients from the ground and to anchor the &%Plant 
in place.")

(subclass Flower PlantAnatomicalStructure)
(subclass Flower Organ)
(documentation Flower EnglishLanguage "The reproductive organ of &%FloweringPlants.")

(=>
  (instance ?FLOWER Flower)
  (exists (?PLANT ?TIME)
    (and
      (instance ?PLANT FloweringPlant)
      (holdsDuring ?TIME
        (part ?FLOWER ?PLANT)))))

(subclass Antibody Protein)
(documentation Antibody EnglishLanguage "An immunoglobulin which is produced by the body 
and which has the ability to neutralize &%Antigens.")

(=>
   (instance ?BODY Antibody)
   (hasPurpose ?BODY (exists (?DEST ?ANTI)
                        (and
                           (instance ?DEST Destruction)
                           (agent ?DEST ?BODY)
                           (patient ?DEST ?ANTI)
                           (instance ?ANTI Antigen)))))

(subclass Antibiotic BiologicallyActiveSubstance)
(documentation Antibiotic EnglishLanguage "A &%BiologicallyActiveSubstance than can kill 
instances of &%Bacterium.")

(subclass Antigen BiologicallyActiveSubstance)
(documentation Antigen EnglishLanguage "Any &%BiologicallyActiveSubstance that has the 
capacity to stimulate the production of &%Antibodies.")

(subclass AntigenA Antigen)
(documentation AntigenA EnglishLanguage "An &%Antigen that is carried in the red blood 
cells of those with &%BloodTypeA.")

(subclass AntigenB Antigen)
(documentation AntigenB EnglishLanguage "An &%Antigen that is carried in the red blood 
cells of those with &%BloodTypeB.")

(subclass BloodCell Cell)
(subclass BloodCell AnimalAnatomicalStructure)
(documentation BloodCell EnglishLanguage "A &%Cell that is normally present in &%Blood.")
(=>
  (instance ?C BloodCell)
  (exists (?B)
    (and
      (instance ?B Blood)
      (part ?C ?B))))

(subclass RedBloodCell BloodCell)
(documentation RedBloodCell EnglishLanguage "&%BloodCells that contain hemoglobin, lack a 
&%CellNucleus, and carry &%Oxygen to the tissues of the body.")
(=>
  (instance ?C RedBloodCell)
  (not 
    (exists (?N)
      (and
        (instance ?N CellNucleus)
        (part ?N ?C)))))

(subclass WhiteBloodCell BloodCell)
(documentation WhiteBloodCell EnglishLanguage "&%BloodCells that lack hemoglobin, contain a 
&%CellNucleus, and have no color.")
(=>
  (instance ?C WhiteBloodCell)
  (exists (?N)
    (and
      (instance ?N CellNucleus)
      (part ?N ?C))))

(subclass BloodTypeA Blood)
(documentation BloodTypeA EnglishLanguage "The subclass of &%Blood that contains &%AntigenA 
and does not contain &%AntigenB.")
(=>
  (instance ?B BloodTypeA)
  (exists (?A)
    (and
      (instance ?A AntigenA)
      (part ?A ?B))))

(=> 
  (instance ?B BloodTypeA)
  (not 
    (exists (?A)
      (and
        (instance ?A AntigenB)
        (part ?A ?B)))))

(subclass BloodTypeAB Blood)
(documentation BloodTypeAB EnglishLanguage "The subclass of &%Blood that contains both 
&%AntigenA and &%AntigenB.")
(=>
  (instance ?B BloodTypeAB)
  (exists (?A)
    (and
      (instance ?A AntigenA)
      (part ?A ?B))))

(=>
   (instance ?B BloodTypeAB)
   (exists (?A)
      (and
         (instance ?A AntigenB)
         (part ?A ?B))))

(subclass BloodTypeB Blood)
(documentation BloodTypeB EnglishLanguage "The subclass of &%Blood that contains &%AntigenB 
and does not contain &%AntigenA.")
(=>
  (instance ?B BloodTypeB)
  (exists (?A)
    (and
      (instance ?A AntigenB)
      (part ?A ?B))))

(=> 
  (instance ?B BloodTypeB)
  (not 
    (exists (?A)
      (and
        (instance ?A AntigenA)
        (part ?A ?B)))))

(subclass BloodTypeO Blood)
(documentation BloodTypeO EnglishLanguage "The subclass of &%Blood that contains neither 
&%AntigenA nor &%AntigenB.")
(=> 
  (instance ?B BloodTypeO)
  (not 
    (exists (?A)
      (and
        (instance ?A AntigenA)
        (part ?A ?B)))))

(=> 
  (instance ?B BloodTypeO)
  (not 
    (exists (?A)
      (and
        (instance ?A AntigenB)
        (part ?A ?B)))))

(subclass NerveCell Cell)
(subclass NerveCell AnimalAnatomicalStructure)
(documentation NerveCell EnglishLanguage "The &%Cells that make up a &%NervousSystem.")
(=>
  (instance ?N NerveCell)
  (exists (?S)
    (and
      (instance ?S NervousSystem)
      (part ?N ?S))))

(subclass LiquidBodySubstance BodySubstance)
(documentation LiquidBodySubstance EnglishLanguage "Any &%BodySubstance which is &%Liquid 
under normal circumstances.")

(=>
   (instance ?SUBSTANCE LiquidBodySubstance)
   (attribute ?SUBSTANCE Liquid))

(subclass Serum LiquidBodySubstance)
(disjoint Serum Blood)
(documentation Serum EnglishLanguage "Any &%LiquidBodySubstance other than &%Blood.")

(subclass TearSubstance LiquidBodySubstance)
(subclass TearSubstance SalineSolution)
(documentation TearSubstance EnglishLanguage "The class of &%SalineSolutions produced by 
the lacrimal glands of the &%Eyes.")

(=>
  (instance ?T TearSubstance)
  (exists (?P ?E)
    (and
      (instance ?E Eye)
      (instrument ?P ?E)
      (result ?P ?T))))

(subclass Milk LiquidBodySubstance)
(subclass Milk Beverage)
(documentation Milk EnglishLanguage "A nutritious &%BodySubstance produced by &%Mammals
that has evolved to provide nourishment for their offspring during
their initial period of life.")

(=>
   (instance ?MILK Milk)
   (exists (?PROCESS ?MAMMAL)
      (and
         (instance ?MAMMAL Mammal)
         (attribute ?MAMMAL Female)
         (instrument ?PROCESS ?MAMMAL)
         (result ?PROCESS ?MILK))))

(subclass Tea Beverage)
(subclass Tea PreparedFood)
(documentation Tea EnglishLanguage "A &%Beverage which is prepared by infusing tea leaves 
into hot water.")

(=>
   (instance ?NUCLEUS CellNucleus)
   (exists (?CELL)
      (and
         (instance ?CELL Cell)
         (part ?NUCLEUS ?CELL))))

(subclass AlcoholicBeverage Beverage)
(subclass AlcoholicBeverage Depressant)
(documentation AlcoholicBeverage EnglishLanguage "Any &%Beverage that contains &%Alcohol.")

(=>
   (instance ?BEVERAGE AlcoholicBeverage)
   (exists (?ALCOHOL)
      (and
         (instance ?ALCOHOL Alcohol)
         (part ?ALCOHOL ?BEVERAGE))))

(subclass Wine AlcoholicBeverage)
(documentation Wine EnglishLanguage "An &%AlcoholicBeverage that is prepared by fermenting 
the juice of grapes.")

(subclass DistilledAlcoholicBeverage AlcoholicBeverage)
(documentation DistilledAlcoholicBeverage EnglishLanguage "An &%AlcoholicBeverage that has 
had some part of its &%Water content removed by distillation.  This class 
covers drinks of unmixed, hard liquor.")

(=>
   (instance ?BEVERAGE DistilledAlcoholicBeverage)
   (exists (?REMOVE ?WATER)
      (and
         (instance ?REMOVE Removing)
         (patient ?REMOVE ?WATER)
         (instance ?WATER Water)
         (origin ?REMOVE ?BEVERAGE))))

(subclass Whiskey DistilledAlcoholicBeverage)
(documentation Whiskey EnglishLanguage "A &%DistilledAlcoholicBeverage that is prepared by 
distilling fermented grain mash.")

(subclass Beer AlcoholicBeverage)
(documentation Beer EnglishLanguage "An &%AlcoholicBeverage that is prepared by fermenting 
malt and hops.") 

(subclass Brandy DistilledAlcoholicBeverage)
(documentation Brandy EnglishLanguage "A &%DistilledAlcoholicBeverage that is prepared by 
distilling &%Wine.")
(=>
  (instance ?B Brandy)
  (exists (?D ?W)
    (and
      (instance ?D Distilling)
      (instance ?W Wine)
      (resource ?D ?W)
      (result ?D ?B))))

(subclass SpinalColumn Organ) 
(subclass SpinalColumn AnimalAnatomicalStructure)
(documentation SpinalColumn EnglishLanguage "A flexible column made out of bones called 
vertebrae. The main function of the &%SpinalColumn is to protect the 
spinal cord.") 

(<=> 
   (instance ?VERT Vertebrate) 
   (exists (?SPINE) 
      (and 
         (instance ?VERT Animal)
         (component ?SPINE ?VERT) 
         (instance ?SPINE SpinalColumn)))) 

(subclass SpinalCord Organ)
(documentation SpinalCord EnglishLanguage "A cord of nerves that carries impulses to 
and from the &%Brain.  It is contained within the &%SpinalColumn.")

(=>
  (instance ?C SpinalCord)
  (exists (?S)
    (and
      (instance ?S NervousSystem)
      (part ?C ?S))))

(=>
  (instance ?C SpinalCord)
  (exists (?S)
    (and
      (instance ?S SpinalColumn)
      (contains ?S ?C)))) 

(subclass Tail AnimalAnatomicalStructure)
(documentation Tail EnglishLanguage "A &%BodyPart which extends from the rear of the 
main body of some &%Vertebrates.")

(=>
  (instance ?T Tail)
  (exists (?V)
    (and
      (instance ?V Vertebrate)
      (part ?T ?V)
      (not (instance ?V Human)))))

(subclass Wing Limb)
(documentation Wing EnglishLanguage "Any &%Limb which is capable of being an 
instrument in &%Flying.")

(subclass Skin BodyCovering)
(subclass Skin AnimalAnatomicalStructure)
(documentation Skin EnglishLanguage "A BodyCovering that comprises part of the surface 
of &%Animals.")

(=>
   (instance ?SKIN Skin)
   (exists (?SURFACE ?ANIMAL)
      (and
         (surface ?SURFACE ?ANIMAL)
         (part ?SKIN ?ANIMAL)
         (instance ?ANIMAL Animal)
         (overlapsSpatially ?SKIN ?SURFACE)))) 

(subclass Retina Organ)
(subclass Retina AnimalAnatomicalStructure)
(documentation Retina EnglishLanguage "A membrane that covers the &%Eye and converts 
the image formed by the lens of the &%Eye into neurochemical impulses 
which can be processed by the &%Brain.")

(=>
  (instance ?R Retina)
  (exists (?E)
    (and
      (instance ?E Eye)
      (part ?R ?E))))

(subclass BronchialDuct BodyVessel)
(subclass BronchialDuct AnimalAnatomicalStructure)
(documentation BronchialDuct EnglishLanguage "Any &%BodyVessel which is located in a 
&%Lung and which carries oxygen from the trachea to the alveoli.")

(=>
   (instance ?DUCT BronchialDuct)
   (exists (?LUNG)
      (and
         (instance ?LUNG Lung)
         (located ?DUCT ?LUNG))))

(subclass BloodVessel BodyVessel)
(subclass BloodVessel AnimalAnatomicalStructure)
(documentation BloodVessel EnglishLanguage "Any &%BodyVessel which is used to circulate 
&%Blood from one part of the body to another.")

(=>
   (instance ?VESSEL BloodVessel)
   (exists (?BLOOD ?TRANSFER)
      (and
         (instance ?BLOOD Blood)
         (instance ?TRANSFER Transfer)
         (patient ?TRANSFER ?BLOOD)
         (instrument ?TRANSFER ?VESSEL))))

(subclass Alveolus BodyVessel)
(subclass Alveolus AnimalAnatomicalStructure)
(documentation Alveolus EnglishLanguage "Tiny sacs in the &%Lung which absorb &%Oxygen 
which is delivered to them by the &%BronchialDucts.")
(=>
   (instance ?A Alveolus)
   (exists (?L)
      (and
         (instance ?L Lung)
         (located ?A ?L))))

(subclass Artery BloodVessel)
(documentation Artery EnglishLanguage "Any &%BloodVessel which transfers &%Blood from 
the &%Heart to the extremities of the body.")

(=>
   (and
      (instance ?ARTERY Artery)
      (instance ?TRANSFER Transfer)
      (patient ?TRANSFER ?BLOOD)
      (instrument ?TRANSFER ?ARTERY)
      (instance ?BLOOD Blood))
   (exists (?HEART)
      (and
         (instance ?HEART Heart)
         (origin ?TRANSFER ?HEART))))

(subclass Vein BloodVessel)
(relatedInternalConcept Vein Artery)
(documentation Vein EnglishLanguage "Any &%BloodVessel which transfers &%Blood from 
the extremities of the body to the &%Heart.")

(=>
  (and
    (instance ?V Vein)
    (instance ?T Transfer)
    (patient ?T ?B)
    (instrument ?T ?V)
    (instance ?B Blood))
  (exists (?H)
    (and
      (instance ?H Heart)
      (destination ?T ?H))))

(subclass PulmonaryArtery Artery)
(documentation PulmonaryArtery EnglishLanguage "An &%Artery that carries &%Blood from 
the &%Heart to a &%Lung.")

(=>
  (and
    (instance ?ARTERY PulmonaryArtery)
    (instance ?TRANSFER Transfer)
    (patient ?TRANSFER ?BLOOD)
    (instrument ?TRANSFER ?ARTERY)
    (instance ?BLOOD Blood))
  (exists (?LUNG)
    (and
      (instance ?LUNG Lung)
      (destination ?TRANSFER ?LUNG))))

(subclass PulmonaryVein Vein)
(documentation PulmonaryVein EnglishLanguage "A &%Vein that carries &%Blood from the 
&%Lungs to the &%Heart.")

(=>
  (and
    (instance ?V PulmonaryVein)
    (instance ?T Transfer)
    (patient ?T ?B)
    (instrument ?T ?V)
    (instance ?B Blood))
  (exists (?L ?H ?P)
    (and
      (instance ?L Lung)
      (origin ?T ?L)
      (part ?V ?P)
      (part ?L ?P)
      (part ?H ?P)
      (instance ?H Heart)
      (destination ?T ?H))))

(subclass Capillary BloodVessel)
(documentation Capillary EnglishLanguage "A very small &%BloodVessel that connects arterioles 
(very small &%Arteries) with venules (very small &%Veins).")
(=>
  (instance ?C Capillary)
  (exists (?A ?V)
    (and
      (instance ?A Artery)
      (instance ?V Vein)
      (connects ?C ?A ?V))))

(subclass Lung Organ)
(subclass Lung AnimalAnatomicalStructure)
(documentation Lung EnglishLanguage "A respiratory organ of &%Vertebrates.  Its function is 
to furnish the blood with oxygen and to remove carbon dioxide.")

(=>
   (capability Breathing experiencer ?ANIMAL)
   (exists (?LUNG)
      (and
         (component ?LUNG ?ANIMAL)
         (instance ?LUNG Lung))))

(subclass Kidney Organ)
(subclass Kidney AnimalAnatomicalStructure)
(documentation Kidney EnglishLanguage "An &%Organ that separates urine from other 
&%BodySubstances and passes it to the bladder.")

(subclass Spleen Organ)
(subclass Spleen AnimalAnatomicalStructure)
(documentation Spleen EnglishLanguage "An &%Organ on the left side of the body that produces 
&%Cells that play a crucial role in immune response.")

(subclass Inhaling Breathing)
(documentation Inhaling EnglishLanguage "Any instance of &%Breathing where the breath is 
taken into the &%Lungs.")

(=>
  (instance ?I Inhaling)
  (exists (?L)
    (and
      (instance ?L Lung)
      (destination ?I ?L))))

(subclass Exhaling Breathing)
(documentation Exhaling EnglishLanguage "Any instance of &%Breathing where the breath is 
expelled from the &%Lungs.")
(=>
  (instance ?E Exhaling)
  (exists (?L)
    (and
      (instance ?L Lung)
      (origin ?E ?L))))

(subclass Nostril BodyVessel)
(subclass Nostril AnimalAnatomicalStructure)
(documentation Nostril EnglishLanguage "Either of two &%BodyVessels that run through the &%Nose 
and connect it to the &%Throat.")

(=>
  (instance ?NOSE Nose)
  (exists (?N1 ?N2)
    (and
      (instance ?N1 Nostril)
      (instance ?N2 Nostril)
      (not (equal ?N1 ?N2))
      (connected ?N1 ?NOSE)
      (connected ?N2 ?NOSE))))

(=>
  (instance ?NOS Nostril)
  (exists (?N ?T)
    (and
      (instance ?N Nose)
      (instance ?T Throat)
      (connects ?NOS ?N ?T))))

(subclass Heart Organ)
(subclass Heart AnimalAnatomicalStructure)
(documentation Heart EnglishLanguage "The &%Organ that pumps &%Blood throughout the body.")

(=>
   (instance ?HEART Heart)
   (exists (?TRANSFER ?BLOOD)
      (and
         (instance ?TRANSFER Transfer)
         (instance ?BLOOD Blood)
         (instrument ?TRANSFER ?HEART)
         (patient ?TRANSFER ?BLOOD)))) 

(subclass Liver Organ)
(subclass Liver AnimalAnatomicalStructure)
(documentation Liver EnglishLanguage "An &%Organ that secretes bile and serves metabolic 
functions.")

(subclass Mouth AnimalAnatomicalStructure)
(subclass Mouth BodyPart)
(documentation Mouth EnglishLanguage "Part of the &%Face, used for &%Ingesting &%Food 
and &%Vocalizing.")

(=>
   (instance ?MOUTH Mouth)
   (exists (?FACE)
      (and
         (instance ?FACE Face)
         (part ?MOUTH ?FACE))))

(subclass Tongue AnimalAnatomicalStructure)
(subclass Tongue BodyPart)
(documentation Tongue EnglishLanguage "Part of the &%Mouth, used for &%Tasting &%Food, 
&%Vocalizing, and the initial stage of &%Digesting.")

(=>
   (instance ?TONGUE Tongue)
   (exists (?MOUTH)
      (and
         (instance ?MOUTH Mouth)
         (part ?TONGUE ?MOUTH))))

(subclass NervousSystem Organ)
(subclass NervousSystem AnimalAnatomicalStructure)
(documentation NervousSystem EnglishLanguage "A system in &%Vertebrates that is made up of 
the &%Brain, the spinal cord, nerves, etc.")

(=>
  (and
    (instance ?S NervousSystem)
    (part ?S ?A)
    (instance ?A Animal))
  (instance ?A Vertebrate))

(subclass Hoof AnimalAnatomicalStructure)
(subclass Hoof BodyPart)
(documentation Hoof EnglishLanguage "The hard end of the &%Foot of a &%HoofedMammal.")

(=>
  (instance ?H Hoof)
  (exists (?F ?A)
    (and
      (instance ?F Foot)
      (instance ?A HoofedMammal)
      (part ?H ?F)
      (part ?F ?A))))

(subclass Tooth Bone)
(documentation Tooth EnglishLanguage "Part of the &%Mouth, used for biting and chewing.")

(=>
   (instance ?TOOTH Tooth)
   (exists (?MOUTH)
      (and
         (instance ?MOUTH Mouth)
         (part ?TOOTH ?MOUTH))))

(subclass Skull Bone)
(documentation Skull EnglishLanguage "The &%Bone that is found in the &%Heads of &%Vertebrates.")
(=>
  (instance ?S Skull)
  (exists (?V ?H)
    (and
      (instance ?V Vertebrate)
      (instance ?H Head)
      (part ?H ?V)
      (part ?S ?H))))

(subclass Horn Bone)
(documentation Horn EnglishLanguage "An outgrowth of &%Bone that is found on some 
&%HoofedMammals.")

(=>
  (instance ?H Horn)
  (exists (?M)
    (and
      (instance ?M HoofedMammal)
      (part ?H ?M))))

(subclass Ivory Bone)
(documentation Ivory EnglishLanguage "A very hard substance that makes up the tusks of 
elephants and walruses.")

(subclass AnimalShell AnimalAnatomicalStructure)
(subclass AnimalShell BodyPart)
(documentation AnimalShell EnglishLanguage "A hard shell of calcium that serves as a 
supporting structure for some &%Invertebrates.")
(=>
  (and
    (instance ?A Animal)
    (instance ?S AnimalShell)
    (part ?S ?A))
  (or
    (instance ?A Invertebrate)
    (instance ?A Reptile)))

(subclass Ossification OrganOrTissueProcess)
(documentation Ossification EnglishLanguage "Any &%BiologicalProcess that results in the 
formation of &%Bones.")

(=>
  (instance ?O Ossification)
  (exists (?B)
    (and
      (instance ?B Bone)
      (result ?O ?B))))

(subclass Chewing BodyMotion)
(documentation Chewing EnglishLanguage "Breaking up or mashing &%Food with one's teeth.")

(=>
   (and
      (instance ?CHEW Chewing)
      (resource ?CHEW ?FOOD))
   (instance ?FOOD (FoodForFn Organism)))

(=>
   (and
      (instance ?CHEW Chewing)
      (instrument ?CHEW ?TOOTH))
   (instance ?TOOTH Tooth))

(=>
   (instance ?CHEW Chewing)
   (exists (?EAT)
      (and
         (instance ?EAT Eating)
         (subProcess ?CHEW ?EAT))))

(subclass Feeding Giving)
(documentation Feeding EnglishLanguage "Giving &%Food to a &%Human or &%Animal.")
(=>
  (and
    (instance ?F Feeding)
    (patient ?F ?O))
  (instance ?O (FoodForFn Animal)))

(=>
  (and
    (instance ?F Feeding)
    (destination ?F ?A))
  (instance ?A Animal))

(subclass Lip AnimalAnatomicalStructure)
(subclass Lip BodyPart)
(documentation Lip EnglishLanguage "Folds of &%Tissue surrounding the mouths of some 
&%Vertebrates.")

(=>
   (instance ?LIP Lip)
   (exists (?MOUTH)
      (and
         (instance ?MOUTH Mouth)
         (part ?LIP ?MOUTH))))

(subclass Kissing Touching)
(documentation Kissing EnglishLanguage "The class of &%Touching processes where the lips 
of two persons are brought into contact with each other.")

(=>
  (instance ?KISS Kissing)
  (exists (?PERSON1 ?PERSON2 ?LIP1 ?LIP2)
    (and
      (agent ?KISS ?PERSON1)
      (agent ?KISS ?PERSON2)
      (instance ?PERSON1 Human)
      (instance ?PERSON2 Human)
      (not (equal ?PERSON1 ?PERSON2))
      (instance ?LIP1 Lip)
      (instance ?LIP2 Lip)
      (part ?LIP1 ?PERSON1)
      (part ?LIP2 ?PERSON2)
      (holdsDuring 
	(BeginFn (WhenFn ?KISS)) 
	(not 
	  (meetsSpatially ?LIP1 ?LIP2)))
      (holdsDuring 
	(EndFn (WhenFn ?KISS)) 
	(meetsSpatially ?LIP1 ?LIP2)))))

(subclass Licking Touching)
(documentation Licking EnglishLanguage "The class of &%Touching processes where the &%Tongue 
is brought into contact with something else.")

(=>
  (instance ?L Licking)
  (exists (?T)
    (and
      (instance ?T Tongue)
      (instrument ?L ?T))))

(subclass Embracing Touching)
(documentation Embracing EnglishLanguage "The class of &%Touching processes where one &%Human 
hugs another one.")
(=>
  (instance ?E Embracing)
  (exists (?P1 ?P2)
    (and
      (instance ?P1 Human)
      (instance ?P2 Human)
      (not (equal ?P1 ?P2))
      (agent ?E ?P1)
      (agent ?E ?P2))))

(subclass Skeleton AnimalAnatomicalStructure)
(subclass Skeleton BodyPart)
(documentation Skeleton EnglishLanguage "The system of &%Bones that make up the supporting structure 
of &%Vertebrates.")

(=>
   (and
      (instance ?ANIMAL Animal)
      (instance ?SKELETON Skeleton)
      (part ?SKELETON ?ANIMAL))
   (instance ?ANIMAL Vertebrate))

(=>
  (instance ?S Skeleton)
  (exists (?A)
    (and
      (instance ?A Animal)
      (interiorPart ?S ?A))))

(subclass Exoskeleton AnimalAnatomicalStructure)
(subclass Exoskeleton BodyPart)
(documentation Exoskeleton EnglishLanguage "The system of &%Bones that are on the &%Outside of an organism and 
make up the supporting structure of many &%Invertebrates.")

(=>
   (and
      (instance ?ANIMAL Animal)
      (instance ?SKELETON Exoskeleton)
      (part ?SKELETON ?ANIMAL))
   (instance ?ANIMAL Invertebrate))

(<=>
   (instance ?BONE Bone)
   (exists (?SKELETON)
      (and
        (or
           (instance ?SKELETON Skeleton)
           (instance ?SKELETON Exoskeleton))
         (part ?BONE ?SKELETON))))

(=>
  (instance ?X Exoskeleton)
  (exists (?A)
    (and
      (instance ?A Animal)
      (part ?X ?A)
      (not
        (exists (?PART)
          (and
            (properPart ?PART ?A)
            (orientation ?PART ?X Outside)))))))

(subclass BodyJoint BodyJunction)
(documentation BodyJoint EnglishLanguage "&%BodyJunctions where different parts of the same 
&%Skeleton come together.")
(=>
  (instance ?J BodyJoint)
  (exists (?S ?P1 ?P2)
    (and
      (instance ?S Skeleton)
      (component ?P1 ?S)
      (component ?P2 ?S)
      (component ?J ?S)
      (connects ?J ?P1 ?P2))))

(subclass Airway Transitway)
(documentation Airway EnglishLanguage "The class of Transitways used to transport Air 
to a location where it is absent or insufficient.")
(subclass Throat AnimalAnatomicalStructure)
(subclass Throat BodyVessel)
(subclass Throat Airway)
(documentation Throat EnglishLanguage "A &%BodyVessel which connects the &%Mouth to the 
lungs and stomach.")

(=>
   (instance ?THROAT Throat)
   (exists (?MOUTH)
      (and
         (instance ?MOUTH Mouth)
         (connected ?THROAT ?MOUTH))))

(subclass Hair AnimalAnatomicalStructure)
(documentation Hair EnglishLanguage "A filament that covers part of the body of many 
&%Mammals.")

(=>
   (instance ?HAIR Hair)
   (exists ( ?MAMMAL ?TIME)
      (and
         (instance ?MAMMAL Mammal)
         (holdsDuring ?TIME (part ?HAIR ?MAMMAL)))))

(subclass FacialHair Hair)
(documentation FacialHair EnglishLanguage "&%Hair that grows on the &%Face.  This 
class covers beards, mustaches, sideburns, midnight shadow, etc.")
(=>
  (instance ?H FacialHair)
  (exists (?F)
    (and
      (instance ?F Face)
      (part ?H ?F))))

(subclass HairRemoval Removing)
(documentation HairRemoval EnglishLanguage "&%Removing (some or all) the &%Hair from the 
body of an &%Animal.  Note that this covers shaving hair, cutting hair, 
pulling hair out by the roots, etc.")

(=>
   (and
      (instance ?REMOVE HairRemoval)
      (resource ?REMOVE ?HAIR))
   (instance ?HAIR Hair))
      
(subclass Peeling Removing)
(documentation Peeling EnglishLanguage "Removing the &%Skin (or part of the &%Skin) from 
an &%Organism.  Note that this class covers a large range of cases, including 
peeling an orange, pelting an &%Animal, etc.")

(=>
  (and
    (instance ?P Peeling)
    (patient ?P ?O))
  (exists (?S)
    (and
      (instance ?S Skin)
      (part ?O ?S))))

(subclass Brain Organ)
(subclass Brain AnimalAnatomicalStructure)
(documentation Brain EnglishLanguage "The seat of the central nervous system.")

(documentation intelligenceQuotient EnglishLanguage "(intelligenceQuotient ?PERSON ?NUMBER) 
means that ?NUMBER is the I.Q. of ?PERSON.  The I.Q. of a person is the ratio 
of their mental age (determined by a standardized test) divided by their 
chronological age, multiplied by 100.")
(instance intelligenceQuotient BinaryPredicate)
(instance intelligenceQuotient SingleValuedRelation)
(domain intelligenceQuotient 1 Human)
(domain intelligenceQuotient 2 RationalNumber)

(subclass Stomach Organ)
(subclass Stomach AnimalAnatomicalStructure)
(documentation Stomach EnglishLanguage "A muscular sac that is the principal organ of 
digestion.")

(=>
  (instance ?S Stomach)
  (capability Digesting instrument ?S))

(subclass Intestine BodyVessel)
(subclass Intestine AnimalAnatomicalStructure)
(documentation Intestine EnglishLanguage "A &%BodyVessel which connects the &%Stomach to 
the anus and which is used in digesting &%Food.")

(=>
  (instance ?I Intestine)
  (capability Digesting instrument ?I))

(=>
  (instance ?I Intestine)
  (exists (?S)
    (and
      (instance ?S Stomach)
      (connected ?I ?S))))

(subclass Hypothalamus BodyPart)
(subclass Hypothalamus AnimalAnatomicalStructure)
(documentation Hypothalamus EnglishLanguage "The part of the &%Brain lying below the 
thalamus that serves to regulate &%AutonomicProcesses.")

(=>
   (instance ?HYPO Hypothalamus)
   (exists (?BRAIN)
      (and
         (instance ?BRAIN Brain)
         (part ?HYPO ?BRAIN))))

(subclass Eye Organ)
(subclass Eye AnimalAnatomicalStructure)
(documentation Eye EnglishLanguage "The &%Organ of sight.")

(=>
   (instance ?EYE Eye)
   (capability Seeing instrument ?EYE))

(=>
   (instance ?EYE Eye)
   (exists (?HEAD)
      (and
         (instance ?HEAD Head)
         (part ?EYE ?HEAD))))

(subclass Ear Organ)
(subclass Ear AnimalAnatomicalStructure)
(documentation Ear EnglishLanguage "The &%Organ of hearing.")

(=>
   (instance ?EAR Ear)
   (capability Hearing instrument ?EAR))

(=>
   (instance ?EAR Ear)
   (exists (?HEAD)
      (and
         (instance ?HEAD Head)
         (part ?EAR ?HEAD))))

(subclass Eyelid Skin)
(documentation Eyelid EnglishLanguage "Folds of &%Skin that can be manipulated to 
cover or uncover &%Eyes.")
(=>
  (instance ?E Eyelid)
  (capability Covering instrument ?E))

(=>
  (instance ?E Eyelid)
  (hasPurpose ?E
    (exists (?EYE ?P ?C)
      (and
        (instance ?C Covering)
        (instance ?C BodyMotion)
        (instance ?P Human)
        (experiencer ?C ?P)
        (instrument ?C ?E)
        (patient ?C ?EYE)
        (instance ?EYE Eye)
        (part ?EYE ?P)
        (part ?E ?P)))))

(subclass Nose Organ)
(subclass Nose AnimalAnatomicalStructure)
(documentation Nose EnglishLanguage "The &%Organ of &%Smelling.")

(=>
   (instance ?NOSE Nose)
   (capability Smelling instrument ?NOSE))

(=>
   (instance ?NOSE Nose)
   (exists (?FACE)
      (and
         (instance ?FACE Face)
         (part ?NOSE ?FACE))))

(documentation secretesSubstance EnglishLanguage "(secretesSubstance ?OBJ ?STUFF) means 
that the subclass of &%OrganicObject (either &%Organism or &%BodyPart) 
?OBJ produces the subclass of &%Substance ?STUFF.")
(instance secretesSubstance BinaryPredicate)
(domainSubclass secretesSubstance 1 OrganicObject)
(domainSubclass secretesSubstance 2 NaturalSubstance)

(=>
  (secretesSubstance ?O ?S)
  (forall (?I1)
    (=>
      (instance ?I1 ?O)
      (exists (?P ?I2)
        (and
          (instance ?P BiologicalProcess)
          (located ?P ?I1)
          (result ?P ?I2)
          (instance ?I2 ?S))))))

(subclass ThyroidGland Gland)
(documentation ThyroidGland EnglishLanguage "A &%Gland in the neck that produces 
&%HormoneTSH, which regulates body weight, metabolic rate, etc.")

(subclass ThyroidHormone Hormone)
(documentation ThyroidHormone EnglishLanguage "A &%Hormone secreted by the &%ThyroidGland.")

(subclass PituitaryGland Gland)
(documentation PituitaryGland EnglishLanguage "The main &%Gland of the endocrine system.")

(subclass HormoneTSH Hormone)
(documentation HormoneTSH EnglishLanguage "A &%Hormone secreted by the &%ThyroidGland.")

(=>
   (instance ?HORMONE HormoneTSH)
   (exists (?PROC ?GLAND)
      (and
         (instance ?GLAND ThyroidGland)
         (instrument ?PROC ?GLAND)
         (result ?PROC ?HORMONE))))

(subclass Pancreas Gland)
(documentation Pancreas EnglishLanguage "A large &%Gland that secretes &%Insulin and other 
substances.")

(=>
  (instance ?P Pancreas)
  (secretesSubstance ?P Insulin))

(subclass Insulin Hormone)
(documentation Insulin EnglishLanguage "A &%Hormone secreted by the &%Pancreas that is used 
to regulate the metabolism of &%Carbohydrates.")

(subclass Arm Limb)
(documentation Arm EnglishLanguage "The upper &%Limbs of a &%Primate.")

(=>
   (instance ?ARM Arm)
   (exists (?PRIMATE)
      (and
         (instance ?PRIMATE Primate)
         (part ?ARM ?PRIMATE))))

(subclass Hand AnimalAnatomicalStructure)
(subclass Hand BodyPart)
(documentation Hand EnglishLanguage "The grasping, fingered part of an upper limb of a 
&%Primate.")

(=>
   (instance ?HAND Hand)
   (exists (?ARM)
      (and
         (instance ?ARM Arm)
         (part ?HAND ?ARM))))

(subclass DigitAppendage AnimalAnatomicalStructure)
(subclass DigitAppendage BodyPart)
(documentation DigitAppendage EnglishLanguage "Any of the extremities of &%Limbs that are 
found in the higer &%Vertebrates and the &%Amphibians.")
(=>
  (instance ?DIGIT DigitAppendage)
  (exists (?LIMB)
    (and
      (instance ?LIMB Limb)
      (part ?DIGIT ?LIMB))))

(subclass Toad Amphibian)
(documentation Toad EnglishLanguage "An &%Amphibian that lacks a &%Tail and lives at least 
partially outside of water.")

(=>
  (instance ?A Toad)
  (not 
    (exists (?T)
      (and
        (instance ?T Tail)
        (part ?T ?A)))))

(initialPart DigitAppendage Limb)

(subclass Finger DigitAppendage)
(documentation Finger EnglishLanguage "The five extremities of &%Hands.")

(=>
   (instance ?FINGER Finger)
   (exists (?HAND)
      (and
         (instance ?HAND Hand)
         (part ?FINGER ?HAND))))

(subclass Thumb Finger)
(documentation Thumb EnglishLanguage "The thick, short &%Finger of each &%Hand.")

(subclass NailDigit AnimalAnatomicalStructure)
(documentation NailDigit EnglishLanguage "Any of the horny structures which are found 
on the &%DigitAppendages of &%Primates and other &%Animals.  This includes
fingernails and toenails.")

(=>
   (instance ?N NailDigit)
   (exists (?D)
      (and
         (instance ?D DigitAppendage)
         (part ?N ?D))))

(subclass Fingerprint ShapeAttribute)
(subclass Fingerprint BiologicalAttribute)
(documentation Fingerprint EnglishLanguage "That pattern of arches, loops, and whorls that mark 
the imprint of a &%Finger.")
(=>
  (and
    (attribute ?O ?P)
    (instance ?P Fingerprint)
    (not (instance ?O Finger)))
  (exists (?PR ?F)
    (and
      (instrument ?PR ?F)
      (instance ?F Finger)
      (attribute ?F ?P)
      (patient ?PR ?O)))) 

(subclass Limb AnimalAnatomicalStructure)
(subclass Limb BodyPart)
(documentation Limb EnglishLanguage "Any of the limbs of a &%Vertebrate.
Animal Appendages with joints that are used for movement 
and grasping.")

(=>
   (instance ?LIMB Limb)
   (exists (?VERTEBRATE)
      (and
         (instance ?VERTEBRATE Vertebrate)
         (part ?LIMB ?VERTEBRATE))))

(subclass Snake Reptile)
(documentation Snake EnglishLanguage "A long and narrow &%Reptile which lacks &%Limbs.")

(=>
   (instance ?SNAKE Snake)
   (not (exists (?LIMB)
      (and
         (instance ?LIMB Limb)
         (part ?LIMB ?SNAKE)))))

(subclass Rattlesnake Snake)
(documentation Rattlesnake EnglishLanguage "A subclass of &%Snake which derives its name 
from the fact that it can manipulate its tail in such a way as to produce 
a sound like that of a rattle.")

(subclass ConstrictorSnake Snake)
(documentation ConstrictorSnake EnglishLanguage "A &%Snake that lacks venom and kills its 
prey by crushing it to death.")

(subclass AntInsect Insect)
(documentation AntInsect EnglishLanguage "A colony &%Insect of three types:  males, 
&%QueenInsect, and worker ants.")

(subclass Anaconda ConstrictorSnake)
(documentation Anaconda EnglishLanguage "A very large Boa that is found in South America.")

(subclass Bee Insect)
(documentation Bee EnglishLanguage "A hairy &%Insect, some species of which produce honey 
and/or sting.")

(subclass BumbleBee Bee)
(documentation BumbleBee EnglishLanguage "A large &%Bee which lacks a stinger.")

(subclass FlyInsect Insect)
(documentation FlyInsect EnglishLanguage "&%Insects with &%Wings and two &%Limbs.")

(subclass Grasshopper Insect)
(documentation Grasshopper EnglishLanguage "&%Insects with enormous legs that are used for 
jumping.")

(subclass Moth Insect)
(documentation Moth EnglishLanguage "Nocturnal &%Insect with a large body and antennae.")

(subclass QueenInsect Insect)
(disjoint QueenInsect WorkerInsect)
(documentation QueenInsect EnglishLanguage "A &%Female &%Insect which is the sole member of 
her colony with the capability to reproduce.") 

(=>
  (instance ?INSECT QueenInsect)
  (and
    (attribute ?INSECT Female)
    (capability Replication agent ?INSECT)))

(=>
  (instance ?INSECT QueenInsect)
  (exists (?GROUP)
    (and
      (instance ?GROUP Group)
      (member ?INSECT ?GROUP)
      (not 
        (exists (?MEMBER) 
          (and
            (member ?MEMBER ?GROUP)
            (capability Replication agent ?MEMBER)
            (not 
	      (equal ?MEMBER ?INSECT))))))))

(subclass WorkerInsect Insect)
(documentation WorkerInsect EnglishLanguage "Sterile members of an &%Insect colony which 
are responsible for locating food and caring for eggs, larvae, etc.")

(subclass Leg Limb)
(documentation Leg EnglishLanguage "The lower &%Limbs of &%Primates.")

(=>
  (instance ?LEG Leg)
  (exists (?PRIMATE)
    (and
      (instance ?PRIMATE Primate)
      (part ?LEG ?PRIMATE))))

(subclass Foot AnimalAnatomicalStructure)
(subclass Foot BodyPart)
(documentation Foot EnglishLanguage "The lower part of a &%Limb, the part which makes contact 
with the ground in locomotion of the &%Animal.")

(=>
  (instance ?FOOT Foot)
  (exists (?LIMB)
    (and
      (instance ?LIMB Leg)
      (part ?FOOT ?LIMB))))

(subclass Ankle BodyJoint)
(documentation Ankle EnglishLanguage "The joint in the &%Leg that connects the tibia and the 
fibula to the talus.")
(=>
  (instance ?A Ankle)
  (exists (?L)
    (and
      (instance ?L Leg)
      (part ?A ?L))))

(subclass Elbow BodyJoint)
(documentation Elbow EnglishLanguage "The joint in the &%Arm connecting the forearm and the 
upper arm.")
(=>
  (instance ?E Elbow)
  (exists (?A)
    (and
      (instance ?A Arm)
      (part ?E ?A))))

(subclass Wrist BodyJoint)
(documentation Wrist EnglishLanguage "The joint in the &%Arm connecting the radius and carpal 
bones.")

(=>
  (instance ?W Wrist)
  (exists (?A)
    (and
      (instance ?A Arm)
      (part ?W ?A))))

(subclass Toe AnimalAnatomicalStructure)
(subclass Toe BodyPart)
(subclass Toe DigitAppendage)
(documentation Toe EnglishLanguage "The five extremities of a &%Foot.")

(=>
   (instance ?TOE Toe)
   (exists (?FOOT)
      (and
         (instance ?FOOT Foot)
         (part ?TOE ?FOOT))))

(subclass Knee BodyJoint)
(documentation Knee EnglishLanguage "The joint in the &%Leg connecting the tibia and fibula 
with the femur.")

(=>
   (instance ?KNEE Knee)
   (exists (?LEG)
      (and
         (instance ?LEG Leg)
         (part ?KNEE ?LEG))))

(subclass Shoulder AnimalAnatomicalStructure)
(subclass Shoulder BodyPart)
(documentation Shoulder EnglishLanguage "The part of a &%Primate between the &%Arm and 
the neck.")

(=>
   (instance ?SHOULDER Shoulder)
   (exists (?PRIMATE)
      (and
         (instance ?PRIMATE Primate)
         (part ?SHOULDER ?PRIMATE))))

(subclass Knuckle BodyJoint)
(documentation Knuckle EnglishLanguage "Any joint in a &%Finger.")

(=>
   (instance ?KNUCKLE Knuckle)
   (exists (?FINGER)
      (and
         (instance ?FINGER Finger)
         (part ?KNUCKLE ?FINGER))))

(subclass Torso AnimalAnatomicalStructure)
(subclass Torso BodyPart)
(documentation Torso EnglishLanguage "The body of a &%Primate excluding its &%Limbs.")

(=>
   (and
      (instance ?TORSO Torso)
      (instance ?LIMB Limb))
   (not (overlapsSpatially ?TORSO ?LIMB)))

(subclass Breast AnimalAnatomicalStructure)
(subclass Breast Organ)
(documentation Breast EnglishLanguage "The paired &%Organs which are part of the chests 
of &%Primates.")
(=>
  (instance ?B Breast)
  (exists (?T)
    (and
      (instance ?T Torso)
      (part ?B 
        (FrontFn ?T)))))

(=>
  (instance ?B Breast)
  (exists (?P)
    (and
      (instance ?P Primate)
      (part ?B ?P))))

(subclass Head AnimalAnatomicalStructure)
(subclass Head BodyPart)
(documentation Head EnglishLanguage "The part of the body containing the sense organs and 
the brain.")

(subclass Neck AnimalAnatomicalStructure)
(subclass Neck BodyPart)
(documentation Neck EnglishLanguage "The part of the body that connects the &%Head to the 
rest of the body.")

(=>
   (instance ?NECK Neck)
   (exists (?HEAD)
      (and
         (instance ?HEAD Head)
         (connected ?NECK ?HEAD))))

(subclass Face AnimalAnatomicalStructure)
(subclass Face BodyPart)
(documentation Face EnglishLanguage "The part of the &%Head from forehead to chin and 
from ear to ear.")

(=>
   (instance ?FACE Face)
   (exists (?HEAD)
      (and
         (instance ?HEAD Head)
         (part ?FACE ?HEAD))))

(=>
   (instance ?FACE Face)
   (exists (?VERTEBRATE)
      (and
         (instance ?VERTEBRATE Vertebrate)
         (part ?FACE ?VERTEBRATE))))

(subclass Chin AnimalAnatomicalStructure)
(subclass Chin BodyPart)
(documentation Chin EnglishLanguage "A part of the &%Face which protrudes slightly and which 
is lower than all other parts of the &%Face.")

(=>
   (instance ?CHIN Chin)
   (exists (?FACE)
      (and
         (instance ?FACE Face)
         (part ?CHIN ?FACE))))

(=>
   (instance ?CHIN Chin)
   (forall (?PART)
      (=>
         (and
            (part ?PART ?FACE)
            (not (part ?PART ?CHIN)))
         (orientation ?PART ?CHIN Below))))
   

(partition Expressing Gesture ExpressingInLanguage)
(disjointDecomposition Expressing ExpressingApproval ExpressingDisapproval)

(subclass ExpressingApproval Expressing)
(documentation ExpressingApproval EnglishLanguage "&%Expressing favor about a physical thing 
or a state of affairs.")

(=>
   (and
      (instance ?EXPRESS ExpressingApproval)
      (agent ?EXPRESS ?AGENT)
      (patient ?EXPRESS ?THING))
   (or
      (wants ?AGENT ?THING)
      (desires ?AGENT ?THING)))

(subclass ExpressingDisapproval Expressing)
(documentation ExpressingDisapproval EnglishLanguage "&%Expressing disfavor about a physical 
thing or a state of affairs.")

(=>
   (and
      (instance ?EXPRESS ExpressingDisapproval)
      (agent ?EXPRESS ?AGENT)
      (patient ?EXPRESS ?THING))
   (or
      (dislikes ?AGENT ?THING)
      (disapproves ?AGENT ?THING)))

(subclass Thanking ExpressingApproval)
(documentation Thanking EnglishLanguage "Any &%ExpressingApproval to a person for something 
that the person did in the past and that is regarded as being to the thanker's 
benefit.")

(=>
  (and
    (instance ?THANK Thanking)
    (agent ?THANK ?AGENT)
    (refers ?THANK ?THING)
    (destination ?THANK ?PERSON))
  (and
    (instance ?PERSON Human)
    (or
      (holdsDuring 
        (WhenFn ?THANK) 
	(wants ?AGENT ?THING))
      (holdsDuring 
        (WhenFn ?THANK) 
	(desires ?AGENT ?THING)))))

(=>
  (and
    (instance ?T Thanking)
    (destination ?T ?P))
  (instance ?P Human))

(subclass Regretting ExpressingDisapproval)
(documentation Regretting EnglishLanguage "&%ExpressingDisapproval about a state of affairs 
that has already occurred.")

(=>
  (and
    (instance ?R Regretting)
    (patient ?R ?T)
    (instance ?T Sentence))
  (truth ?T True))

(subclass ExpressingFarewell Expressing)
(documentation ExpressingFarewell EnglishLanguage "Any instance of &%Expressing an acknowledgment 
of a person's departure.  Note that this class is not a subclass of 
&%LinguisticCommunication, because it covers gestures of departure, e.g. &%Waving 
and &%Nodding in certain circumstances.")

(subclass Congratulating ExpressingApproval)
(documentation Congratulating EnglishLanguage "Any &%ExpressingApproval to a person for 
something that the person did in the past and that is regarded as being 
to the benefit of the person congratulated.")
(=>
  (instance ?C Congratulating)
    (exists (?P ?A ?D)
      (and
        (destination ?C ?D)
        (agent ?P ?D)
        (agent ?C ?A)
        (wants ?A ?P)
        (refers ?C ?P))))

(subclass FacialExpression Gesture)
(documentation FacialExpression EnglishLanguage "Any &%Gesture whose &%instrument is the &%Face.")

(=>
   (and
      (instance ?EXPRESS FacialExpression)
      (agent ?EXPRESS ?AGENT))
   (exists (?FACE)
      (and
         (part ?FACE ?AGENT)
         (instance ?FACE Face)
         (instrument ?EXPRESS ?FACE))))

(subclass Smiling FacialExpression)
(documentation Smiling EnglishLanguage "Spreading the lips in such a way as to convey 
happiness.")

(=>
   (and
      (instance ?SMILE Smiling)
      (agent ?SMILE ?AGENT))
   (holdsDuring (WhenFn ?SMILE) (attribute ?AGENT Happiness)))

(subclass Frowning FacialExpression)
(documentation Frowning EnglishLanguage "Furrowing the forehead in such a way as to convey 
unhappiness.")

(=>
   (and
      (instance ?FROWN Frowning)
      (agent ?FROWN ?AGENT))
   (holdsDuring (WhenFn ?FROWN) (attribute ?AGENT Unhappiness)))

(subclass Laughing Vocalizing)
(subclass Laughing FacialExpression)
(documentation Laughing EnglishLanguage "Expressing happiness by &%Vocalizing in a 
certain way.")

(=>
   (instance ?LAUGH Laughing)
   (exists (?SMILE)
      (and
         (instance ?SMILE Smiling)
         (subProcess ?SMILE ?LAUGH))))

(subclass Weeping FacialExpression)
(documentation Weeping EnglishLanguage "&%Expressing unhappiness by shedding tears.")

(=>
   (and
      (instance ?WEEP Weeping)
      (agent ?WEEP ?AGENT))
   (holdsDuring (WhenFn ?WEEP) (attribute ?AGENT Unhappiness))) 

(subclass Nodding Gesture)
(documentation Nodding EnglishLanguage "Moving the &%Head up and down or side to side 
to indicate approval or disapproval.")

(=>
   (and
      (instance ?NOD Nodding)
      (patient ?NOD ?HEAD))
   (instance ?HEAD Head))

(subclass HandGesture Gesture)
(documentation HandGesture EnglishLanguage "Any &%Gesture which involves the &%Hands.")
(=>
  (instance ?HG HandGesture)
  (exists (?H)
    (and
      (instance ?H Hand)
      (patient ?HG ?H))))

(subclass Waving HandGesture)
(documentation Waving EnglishLanguage "Moving a &%Hand to indicate a greeting, farewell, 
recognition, goodwill, etc.")

(=>
   (and
      (instance ?WAVE Waving)
      (patient ?WAVE ?HAND))
   (instance ?HAND Hand))

(subclass Clapping HandGesture)
(subclass Clapping RadiatingSound)
(subclass Clapping Impacting)
(documentation Clapping EnglishLanguage "Bringing the &%Hands together repeatedly to make 
a loud noise.")

(subclass Inclining BodyMotion)
(subclass Inclining MotionDownward)
(documentation Inclining EnglishLanguage "Moving one's body downward from a vertical position.  
Note that this class covers cases of leaning forward, as well as those of 
reclining backwards.")

(subclass Bowing Gesture)
(subclass Bowing Inclining)
(subclass Bowing MotionDownward)
(documentation Bowing EnglishLanguage "Any downward motion of the body that indicates respect 
for or submission to another &%Agent.")

(subclass Sinking Falling)
(documentation Sinking EnglishLanguage "The class of &%Falling processes that occur 
in a &%BodyOfWater.")

(=>
  (instance ?S Sinking)
  (exists (?W)
    (and
      (instance ?W BodyOfWater)
      (located ?S ?W))))

(subclass Ducking IntentionalProcess)
(subclass Ducking BodyMotion)
(subclass Ducking MotionDownward)
(documentation Ducking EnglishLanguage "Purposely moving one's body downward in such a way as 
to avoid being hit by something.")

(=>
   (and
      (instance ?MOTION Ducking)
      (agent ?MOTION ?AGENT))
   (hasPurpose ?MOTION (not (exists (?IMPACT)
                          (and
                             (instance ?IMPACT Impacting)
                             (patient ?IMPACT ?AGENT))))))

(subclass ExpressingInLanguage Expressing)
(subclass ExpressingInLanguage LinguisticCommunication)
(documentation ExpressingInLanguage EnglishLanguage "Any instance of &%Expressing that is also an 
instance of &%LinguisticCommunication, e.g. thanking someone, expressing condolence, 
expressing disapproval with an utterance rather than a &%Gesture, etc.")

(subclass TonalLanguage SpokenHumanLanguage)
(documentation TonalLanguage EnglishLanguage "Any &%SpokenHumanLanguage that uses pitch to 
differentiate otherwise identical words, e.g. Chinese.")

(subclass Greeting Expressing)
(documentation Greeting EnglishLanguage "Any instance of &%Expressing an acknowledgment of a 
person's arrival.  Note that this class is not a subclass of &%ExpressingInLanguage, 
because it covers gestures of greeting, e.g. &%Waving and &%Nodding in certain 
circumstances.")

(subrelation half part)
(documentation half EnglishLanguage "(&%half ?HALF ?WHOLE) means that ?HALF is one half 
of ?WHOLE.")

(=>
   (half ?HALF ?WHOLE)
   (exists (?OTHER)
      (and
         (half ?OTHER ?WHOLE)
         (not (equal ?OTHER ?HALF))
         (equal ?WHOLE (MereologicalSumFn ?HALF ?OTHER)))))

(documentation third EnglishLanguage "(third ?THIRD ?WHOLE) means that ?THIRD is one 
third of ?WHOLE.")
(subrelation third part)

(=>
  (third ?T ?W)
  (exists (?O1 ?O2)
    (and
      (third ?O1 ?W)
      (third ?O2 ?W)
      (not (equal ?O1 ?T))
      (not (equal ?O2 ?T))
      (not (equal ?O1 ?O2))
      (equal ?W 
        (MereologicalSumFn ?T 
	  (MereologicalSumFn ?O1 ?O2))))))

(documentation quarter EnglishLanguage "(quarter ?QUART ?WHOLE) means that ?QUART is a 
quarter of ?WHOLE.")
(subrelation quarter part)

(<=>
  (quarter ?Q ?W)
  (exists (?H)
    (and
      (half ?H ?W)
      (half ?Q ?H))))

(subrelation most part)
(documentation most EnglishLanguage "(&%most ?MOST ?WHOLE) means that ?MOST is a &%part 
of ?WHOLE that is greater than &%half of ?WHOLE.")

(=>
   (most ?MOST ?WHOLE)
   (exists (?HALF ?NUMBER1 ?NUMBER2 ?UNIT)
      (and
         (half ?HALF ?WHOLE)
         (measure ?HALF (MeasureFn ?NUMBER1 ?UNIT))
         (measure ?MOST (MeasureFn ?NUMBER2 ?UNIT))
         (greaterThan ?NUMBER2 ?NUMBER1))))

(subclass ArrowFigure Icon)
(documentation ArrowFigure EnglishLanguage "An &%Icon which has the shape of an arrow and 
which is used to indicate direction or a relationship betwee two things.")

(subclass Blueprint Icon)
(documentation Blueprint EnglishLanguage "An &%Icon which is a scale model of an &%Artifact, 
whether the &%Artifact actually exists or not.")

(=>
  (instance ?PLAN Blueprint)
  (hasPurpose ?PLAN
    (exists (?ARTIFACT)
      (and
        (instance ?ARTIFACT Artifact)
        (represents ?PLAN ?ARTIFACT)))))

(subclass Chart Icon)
(documentation Chart EnglishLanguage "An &%Icon which depicts one or more quantities.")

(=>
   (instance ?CHART Chart)
   (exists (?QUANTITY)
      (and
         (instance ?QUANTITY PhysicalQuantity)
         (refers ?CHART ?QUANTITY))))

(subclass Flag Icon)
(documentation Flag EnglishLanguage "An &%Icon made of &%Fabric that refers to a particular 
&%GeopoliticalArea.")

(=>
   (instance ?FLAG Flag)
   (exists (?FABRIC)
      (and
         (instance ?FABRIC Fabric)
         (part ?FABRIC ?FLAG))))

(=>
   (instance ?FLAG Flag)
   (exists (?AREA)
      (and
         (instance ?AREA GeopoliticalArea)
         (refers ?FLAG ?AREA))))

(subclass NationalFlag Flag)
(documentation NationalFlag EnglishLanguage "A &%Flag that is the official flag of a &%Nation, 
e.g. the stars and stripes is the &%NationalFlag of the &%UnitedStates.")

(=>
  (instance ?F Flag)
  (exists (?N)
    (and
      (instance ?N Nation)
      (refers ?F ?N))))

(subclass GraphDiagram Icon)
(documentation GraphDiagram EnglishLanguage "An &%Icon which depicts one or more quantities.")
(=>
  (instance ?G GraphDiagram)
  (exists (?Q)
    (and
      (instance ?Q PhysicalQuantity)
      (refers ?G ?Q))))

(subclass ArrowIcon Icon)
(documentation ArrowIcon EnglishLanguage "An &%Icon which has the shape of an arrow and which 
is used to indicate direction or a relationship betwee two things.")

(subclass Map Icon)
(documentation Map EnglishLanguage "An &%Icon which represents one or more &%GeographicAreas 
(or even the entire &%Earth).")

(=>
  (instance ?M Map)
  (exists (?A)
    (and
      (instance ?A GeographicArea)
      (represents ?M ?A))))

(subclass Photograph Icon)
(documentation Photograph EnglishLanguage "An &%Icon that is the result of a process of 
&%Photographing.")

(subclass Photographing ContentDevelopment)
(documentation Photographing EnglishLanguage "&%ContentDevelopment where the &%instrument 
is a camera and the &%result is a &%Photograph.")

(=>
   (instance ?SHOOT Photographing)
   (exists (?PHOTO ?CAMERA)
      (and
         (instance ?PHOTO Photograph)
         (result ?SHOOT ?PHOTO)
         (instrument ?SHOOT ?CAMERA)
         (instance ?CAMERA Camera))))

(subclass Camera Device)
(documentation Camera EnglishLanguage "A &%Device which is capable of &%Photographing.")

(=>
   (instance ?CAMERA Camera)
   (capability Photographing instrument ?CAMERA))

(subclass Composing ContentDevelopment)
(documentation Composing EnglishLanguage "&%ContentDevelopment which results in a 
&%MusicalComposition.")

(=>
   (instance ?COMPOSE Composing)
   (exists (?MUSIC)
      (and
         (instance ?MUSIC MusicalComposition)
         (result ?COMPOSE ?MUSIC)))) 

(instance TonMass UnitOfMass)
(documentation TonMass EnglishLanguage "English mass unit that is equal to 2000 pounds.")

(=>
   (equal ?NUMBER (MultiplicationFn 1 ?NUMBER))
   (equal
        (MeasureFn ?NUMBER TonMass)
        (MeasureFn (MultiplicationFn ?NUMBER 2000) PoundMass)))

(instance MetricTon UnitOfMass)
(documentation MetricTon EnglishLanguage "&%MetricTon is a &%UnitOfMeasure that represents 
a weight of 2,205 &%PoundMass.")

(equal 
  (MeasureFn 1 MetricTon) 
  (MeasureFn 2205 PoundMass))

(<=>
  (measure ?O 
    (MeasureFn ?A MetricTon))
  (measure ?O 
    (MeasureFn 
      (MultiplicationFn ?A 2205) 
    PoundMass)))

(instance Page UnitOfInformation)
(documentation Page EnglishLanguage "A single page of &%Text.")

(instance LiquorShot UnitOfVolume)
(documentation LiquorShot EnglishLanguage "A &%UnitOfMeasure used in preparing &%AlcoholicBeverages.")

(instance MillenniumDuration UnitOfDuration)
(documentation MillenniumDuration EnglishLanguage "The &%UnitOfDuration of 1000 years.")

(=>
  (equal ?N (MultiplicationFn 1 ?N))
  (equal
    (MeasureFn ?N MillenniumDuration)
    (MeasureFn (MultiplicationFn ?N 1000) YearDuration)))

(instance Acre UnitOfArea)
(documentation Acre EnglishLanguage "A &%UnitOfMeasure equal to 4840 square yards.")

(instance SquareMile UnitOfArea)
(documentation SquareMile EnglishLanguage "&%SquareMile represents a &%UnitOfMeasure 
equal to one square &%Mile.")

(equal 
  (MeasureFn 1 SquareMile) 
  (PerFn 
    (MeasureFn 1 Mile)
    (MeasureFn 1 Mile)))

(instance SquareYard UnitOfArea)
(documentation SquareYard EnglishLanguage "&%SquareYard represents a &%UnitOfMeasure 
equal to one square &%YardLength.")

(equal 
  (MeasureFn 1 SquareYard) 
  (PerFn 
    (MeasureFn 1 YardLength)
    (MeasureFn 1 YardLength)))

(subclass MusicalComposition Text)
(documentation MusicalComposition EnglishLanguage "A &%Text that expresses the notes, 
words, etc. of a song or other sort of &%Music.")

(=>
   (and
      (instance ?COMP MusicalComposition)
      (containsInformation ?COMP ?INFO)
      (realization ?MUSIC ?INFO))
   (instance ?MUSIC Music))

(subclass Song MusicalComposition)
(documentation Song EnglishLanguage "Any &%MusicalComposition which contains &%Lyrics.")

(=>
  (instance ?S Song)
  (exists (?L)
    (and
      (instance ?L Lyrics)
      (part ?L ?S))))

(subclass DeviceAttribute ObjectiveNorm)
(documentation DeviceAttribute EnglishLanguage "This class contains two &%Attributes to 
indicate whether a &%Device is or is not behaving as it is intended to 
behave, &%Functioning and &%Malfunctioning.")

(=>
   (and
      (attribute ?DEVICE ?ATTRIBUTE)
      (instance ?ATTRIBUTE DeviceAttribute))
   (instance ?DEVICE Device))

(instance Functioning DeviceAttribute)
(contraryAttribute Functioning Malfunctioning)
(documentation Functioning EnglishLanguage "Indicates that a &%Device is performing its 
intended function.")

(=>
   (and
      (attribute ?DEVICE Functioning)
      (hasPurpose ?DEVICE ?PROP))
   (truth ?PROP True))

(instance Malfunctioning DeviceAttribute)
(documentation Malfunctioning EnglishLanguage "Indicates that a &%Device is not performing 
its intended function.")

(=>
   (and
      (attribute ?DEVICE Malfunctioning)
      (hasPurpose ?DEVICE ?PROP))
   (truth ?PROP False))

(instance LegislativeBill DeonticAttribute)
(documentation LegislativeBill EnglishLanguage "The &%Attribute of being a proposed law, 
i.e. being under consideration by a legislative body of &%Government.")

(=>
   (holdsDuring ?TIME1 (modalAttribute ?TEXT Law))
   (exists (?TIME2)
      (and
         (holdsDuring ?TIME2 (attribute ?TEXT LegislativeBill))
         (earlier ?TIME2 ?TIME1))))

(subclass PassingABill PoliticalProcess)
(subclass PassingABill Declaring)
(documentation PassingABill EnglishLanguage "The &%Process of converting a &%LegislativeBill 
into &%Law by a duly authorized legislative body of &%Government.")

(=>
   (instance ?ORG LegislativeOrganization)
   (capability PassingABill agent ?ORG))

(=>
   (and
      (instance ?PASS PassingABill)
      (patient ?PASS ?TEXT))
   (and
      (holdsDuring (BeginFn (WhenFn ?PASS)) (attribute ?TEXT LegislativeBill))
      (holdsDuring (EndFn (WhenFn ?PASS)) (attribute ?TEXT Law))))

(subAttribute InternationalLaw Law)
(documentation InternationalLaw EnglishLanguage "An &%Attribute that applies to &%Propositions 
that express &%Laws concerning the relations between &%Nations.")

(subAttribute Illegal Prohibition)
(contraryAttribute Illegal Legal)
(documentation Illegal EnglishLanguage "A proposition is &%Illegal just in case it is inconsistent 
with any proposition that is a &%Law.")

(subAttribute Legal Permission)
(documentation Legal EnglishLanguage "A proposition is &%Legal just in case it is not inconsistent 
with any proposition that is a &%Law.")

(<=>
  (modalAttribute ?F1 Legal)
  (not 
    (exists (?F2)
      (and
        (modalAttribute ?F2 Law)
        (not 
	  (consistent ?F1 ?F2))))))

(subclass CriminalAction IntentionalProcess)
(documentation CriminalAction EnglishLanguage "Any &%IntentionalProcess that violates a &%Law.")

(=>
   (instance ?ACTION CriminalAction)
   (exists (?LAW ?CONTENT ?CRIME)
      (and
         (modalAttribute ?LAW Law)
         (containsInformation ?LAW ?CONTENT)
         (realization ?ACTION ?CRIME)
         (not (consistent ?CONTENT ?CRIME)))))

(subclass PoliticalCrime CriminalAction)
(subclass PoliticalCrime PoliticalProcess)
(documentation PoliticalCrime EnglishLanguage "A &%CriminalAction that is committed by or 
against &%Governments.")

(subclass PoliticalRevolution PoliticalProcess)
(subclass PoliticalRevolution ViolentContest)
(documentation PoliticalRevolution EnglishLanguage "The violent overthrow of one &%Government 
and its replacement by another.  This covers grass-roots revolutions, as well 
as coups d'etat.")

(=>
  (and
    (instance ?R PoliticalRevolution)
    (patient ?R ?A))
  (instance ?A GeopoliticalArea))

(=>
  (and
    (instance ?R PoliticalRevolution)
    (patient ?R ?A))
  (exists (?GOV1 ?GOV2)
    (and
      (holdsDuring 
        (BeginFn (WhenFn ?R)) 
	(equal ?GOV1 (GovernmentFn ?A)))
      (holdsDuring 
        (EndFn (WhenFn ?R)) 
	(equal ?GOV2 (GovernmentFn ?A)))
      (not (equal ?GOV1 ?GOV2)))))

(subclass LawEnforcement PoliticalProcess)
(subclass LawEnforcement RegulatoryProcess)
(documentation LawEnforcement EnglishLanguage "Any &%RegulatoryProcess where the agent is either 
a &%PoliceOrganization or a member of a &%PoliceOrganization.  This covers 
everything from issuing a traffic ticket to arresting someone on suspicion of having 
committed a &%Murder.")

(=>
  (and
    (instance ?L LawEnforcement)
    (agent ?L ?AGENT))
  (or
    (instance ?P PoliceOrganization)
    (exists (?O)
      (and
        (instance ?O PoliceOrganization)
        (member ?P ?O)))))

(subclass Imprisoning Confining)
(documentation Imprisoning EnglishLanguage "The class of &%Confining processes where the detainee 
is put in &%Prison.")

(=>
  (instance ?I Imprisoning)
  (exists (?P)
    (and
      (instance ?P Prison)
      (located ?I ?P))))

(subclass Espionage PoliticalProcess)
(subclass Espionage Investigating)
(documentation Espionage EnglishLanguage "Any Investigating by one &%Government of another 
&%Government where the second Government does not know that it is being 
spied upon.")
(=>
  (instance ?S Espionage)
  (exists (?GOV1 ?GOV2)
    (and
      (instance ?GOV1 Government)
      (instance ?GOV2 Government)
      (agent ?S ?GOV1)
      (patient ?S ?GOV2)
      (not 
        (equal ?GOV1 ?GOV2))))) 

(subclass Polling Investigating)
(documentation Polling EnglishLanguage "Investigating what people believe (and in what proportions) 
by asking a set of structured questions to a random sample of people.")

(subclass MilitaryInfiltration MilitaryProcess)
(subclass MilitaryInfiltration Translocation)
(documentation MilitaryInfiltration EnglishLanguage "Any &%Process by a &%MilitaryOrganization 
which involves moving through enemy positions without detection by the enemy.")

(subAttribute MarriageContract Contract)
(documentation MarriageContract EnglishLanguage "A &%Contract between a married couple 
about how assets owned by the couple will be distributed in the event 
that the couple is divorced.")

(=>
  (instance ?C MarriageContract)
  (exists (?P1 ?P2)
    (and
      (agreementMember ?C ?P1)
      (agreementMember ?C ?P2)
      (spouse ?P1 ?P2)))) 

(subAttribute Treaty Contract)
(documentation Treaty EnglishLanguage "Any &%Contract which holds between two or more 
&%Nations.")

(=>
  (attribute ?T Treaty)
  (exists (?N1 ?N2)
    (and
      (agreementMember ?T ?N1)
      (agreementMember ?T ?N2)
      (instance ?N1 Nation)
      (instance ?N2 Nation)
      (not 
        (equal ?N1 ?N2)))))

(disjointDecomposition Sentence Statement Supposition Question Request Order)

(subclass Statement Sentence)
(documentation Statement EnglishLanguage "A &%Sentence that is stated to be true.")

(=>
   (and
      (instance ?STATE Stating)
      (result ?STATE ?SENTENCE)
      (instance ?SENTENCE Sentence))
   (instance ?SENTENCE Statement))

(subclass Fact Statement)
(documentation Fact EnglishLanguage "The class of &%Statements that are &%True.")

(=>
   (instance ?FACT Fact)
   (truth ?FACT True))

(subclass Question Sentence)
(documentation Question EnglishLanguage "An interrogative &%Sentence, a &%Sentence that 
poses a question.")

(=>
   (and
      (instance ?QUESTION Questioning)
      (result ?QUESTION ?SENTENCE)
      (instance ?SENTENCE Sentence))
   (instance ?SENTENCE Question))

(subclass Supposition Sentence)
(documentation Supposition EnglishLanguage "A &%Sentence that is assumed to be true, possibly 
just for the sake of argument.")

(=>
   (and
      (instance ?SUPPOSE Supposing)
      (result ?SUPPOSE ?SENTENCE)
      (instance ?SENTENCE Sentence))
   (instance ?SENTENCE Supposition))

(subclass Request Sentence)
(documentation Request EnglishLanguage "A &%Sentence that expresses a request for something or 
that something be done.")

(=>
   (and
      (instance ?REQUEST Requesting)
      (result ?REQUEST ?SENTENCE)
      (instance ?SENTENCE Sentence))
   (instance ?SENTENCE Request))

(subclass Order Sentence)
(documentation Order EnglishLanguage "A &%Sentence that expresses an order for something or 
that something be done.")

(=>
   (and
      (instance ?ORDER Ordering)
      (result ?ORDER ?SENTENCE)
      (instance ?SENTENCE Sentence))
   (instance ?SENTENCE Order))

(subclass AudioRecording ContentBearingObject)
(subclass AudioRecording Text)
(documentation AudioRecording EnglishLanguage "A representation of sound on some medium such
as wax cylinder, vinyl record, magnetic tape, CD or flash memory, that is 
intended to be used in some machine to reproduce that sound.")

(=>
  (instance ?X AudioRecording)
  (exists (?EV)
    (and
      (instance ?EV RadiatingSound)
      (represents ?X ?EV)
      (capability RadiatingSound patient ?X))))

(=>
  (instance ?RECORD AudioRecording)
  (exists (?INFO ?SOUND)
    (and
      (containsInformation ?RECORD ?INFO)
      (realization ?SOUND ?INFO)
      (instance ?SOUND RadiatingSound))))

(subclass MusicText Text)

(documentation MusicText EnglishLanguage "A &%Text in a &%Language that represents a form
of music.")

(=>
  (instance ?X MusicText)
  (capability Music patient ?X))

(subclass VideoRecording ContentBearingObject)

(documentation VideoRecording EnglishLanguage "A representation of video on some medium such
as film, videotape or DVD.  Instances of this class are also commonly
instances of &%AudioRecording.")

(=>
  (instance ?X VideoRecording)
  (exists (?EV)
    (and
      (instance ?EV Process)
      (represents ?X ?EV)
      (capability RadiatingLight patient ?X))))

(subclass MusicRecording AudioRecording)

(documentation MusicRecording EnglishLanguage "An &%AudioRecording of &%Music.  Note that
many &%AudioRecordings contain music, so only an audio recording without
any music would be an instance of &%AudioRecording rather than of 
&%MusicRecording.")

(=>
  (instance ?X MusicRecording)
  (exists (?EV)
    (and
      (instance ?EV Music)
      (represents ?X ?EV)
      (capability Music patient ?X))))

(subclass Label Text)
(documentation Label EnglishLanguage "A very brief &%Text that is attached to an &%Object 
and that indicates very specific information about the &%Object, e.g. its 
name, its &%monetaryValue, etc.")

(=>
   (instance ?LABEL Label)
   (exists (?OBJ)
      (and
         (instance ?OBJ SelfConnectedObject)
         (connected ?LABEL ?OBJ)
         (refers ?LABEL ?OBJ))))

(subclass FormText Text)
(documentation FormText EnglishLanguage "A page or set of pages containing spaces where 
information is to be entered by an &%Agent.")

(subclass TestForm FormText)
(subclass TestForm MeasuringDevice)
(documentation TestForm EnglishLanguage "A &%FormText which is intended to measure some aspect 
of the cognitive capabilities, e.g. intelligence or knowledge of a domain, 
of the person taking the test.")

(subclass TaxReturn FormText)
(documentation TaxReturn EnglishLanguage "A &%FormText that is used for calculating the amount 
of income tax owed in a given year.")

(=>
   (instance ?RETURN TaxReturn)
   (hasPurpose ?RETURN (exists (?CALCULATE ?AMOUNT ?TAX)
                          (and
                             (instance ?CALCULATE Calculating)
                             (instrument ?CALCULATE ?RETURN)
                             (result ?CALCULATE ?AMOUNT)
                             (transactionAmount ?TAX ?AMOUNT)
                             (instance ?TAX Tax)))))

(subclass IncomeTax Tax)
(documentation IncomeTax EnglishLanguage "A &%Tax on annual income.")

(subclass DutyTax Tax)
(documentation DutyTax EnglishLanguage "A &%Tax that is levied on imports and/or exports.")
(=>
  (and
    (instance ?X DutyTax)
    (refers ?X ?OBJ)
    (patient ?X ?OBJ))
  (exists (?T ?N1 ?N2)
    (and
      (instance ?T Transfer)
      (patient ?T ?OBJ)
      (origin ?T ?N1)
      (destination ?T ?N2)
      (instance ?N1 Nation)
      (instance ?N2 Nation)
      (not (equal ?N1 ?N2))
      (earlier (WhenFn ?T) (WhenFn ?X))
      (causes ?T ?X))))

(subclass Ballot FormText)
(documentation Ballot EnglishLanguage "A &%FormText which is used in &%Voting.  A list of 
candidates is printed on the form, and the voter selects the candidate he/she 
wants to vote for.")
(=>
  (instance ?B Ballot)
  (hasPurpose ?B 
    (exists (?V)
      (and
        (instance ?V Voting)
        (instrument ?V ?B)))))

(subclass Application FormText)
(documentation Application EnglishLanguage "A &%FormText whose purpose is to obtain admission 
to an &%Organization or to receive assistance from an &%Organization.")

(=>
  (instance ?APP Application)
  (hasPurpose ?APP 
    (exists (?JOIN ?GIVE) 
      (or 
        (instance ?JOIN JoiningAnOrganization)
        (instance ?GIVE UnilateralGiving)))))

(subclass Announcement FactualText)
(documentation Announcement EnglishLanguage "Any &%Text which contains information about 
an event in the future.")
(=>
  (instance ?A Announcement)
  (exists (?CD ?I)
    (and
      (instance ?CD ContentDevelopment)
      (result ?CD ?A)
      (refers ?A ?I)
      (instance ?I SocialInteraction)
      (earlier 
        (WhenFn ?CD) 
        (WhenFn ?I)))))

(subclass PerformanceProgram FactualText)
(documentation PerformanceProgram EnglishLanguage "A &%Text that describes the events 
and/or participants in a &%Performance.")

(=>
  (instance ?PROG PerformanceProgram)
  (exists (?PERF ?PROP)
    (and
      (containsInformation ?PROG ?PROP)
      (realization ?PERF ?PROP)
      (instance ?PERF Performance))))

(subclass MotionPictureShot MotionPicture)
(documentation MotionPictureShot EnglishLanguage "A unit of action in a &%MotionPicture, a 
&%MotionPictureShot is a sequence of images which are captured by a single 
camera without interruption.")

(=>
   (instance ?SHOT MotionPictureShot)
   (exists (?MOVIE)
      (and
         (instance ?MOVIE MotionPicture)
         (subsumesContentInstance ?MOVIE ?SHOT))))

(=>
  (instance ?SHOT MotionPictureShot)
  (exists (?C)
    (and
      (instance ?C Camera)
      (instrument ?SHOT ?C)
      (not
        (exists (?C2)
	  (and
	    (instance ?C2 Camera)
	    (not (equal ?C ?C2))
	    (instrument ?SHOT ?C2)))))))

(subclass MotionPictureScene MotionPicture)
(documentation MotionPictureScene EnglishLanguage "A discrete part of a &%MotionPicture which is set in 
the same time period and the same location.")

(=>
  (instance ?S MotionPictureScene)
  (exists (?M)
    (and
      (instance ?M MotionPicture)
      (subsumesContentInstance ?MOVIE ?S))))

(subclass Documentary MotionPicture)
(subclass Documentary FactualText)
(documentation Documentary EnglishLanguage "A &%MotionPicture which purports to represent the 
facts about a person, event, etc.")

(subclass BroadcastProgram Series)
(documentation BroadcastProgram EnglishLanguage "A &%Series of episodes that are broadcast 
on television or radio.")

(=>
   (instance ?PROGRAM BroadcastProgram)
   (exists (?BROADCAST)
      (and
         (instance ?BROADCAST Broadcasting)
         (patient ?BROADCAST ?PROGRAM))))

(subclass NewsProgram BroadcastProgram)
(documentation NewsProgram EnglishLanguage "A &%BroadcastProgram that is devoted to 
reporting the latest events in a city, region, nation or the world at 
large.")

(=>
   (and
      (subclass ?PROGRAM NewsProgram)
      (equal ?EPISODE (SeriesVolumeFn ?PROGRAM ?NUMBER)))
   (exists (?DISSEMINATE)
      (and
         (instance ?DISSEMINATE Disseminating)
         (patient ?DISSEMINATE ?EPISODE))))

(subclass Chapter Article)
(documentation Chapter EnglishLanguage "A numbered and/or titled
section of a &%Book, which is typically indicated in a table of
contents for the &%Book.")

(=>
   (instance ?CHAPTER Chapter)
   (exists (?BOOK)
      (and
         (instance ?BOOK Book)
         (subsumesContentInstance ?BOOK ?CHAPTER))))

(subrelation titles names)
(domain titles 1 SymbolicString)
(domainSubclass titles 2 ContentBearingPhysical)
(documentation titles EnglishLanguage "A &%BinaryPredicate used to
indicate the title of a &%ContentBearingPhysical.  Note that the
second argument type restriction is a subclass, rather than an
instance, of &%ContentBearingPhysical.  Thus, the title
Murder_on_the_Orient_Express corresponds to a large class of &%Books,
and not just to a single copy of the book.")

(documentation brandName EnglishLanguage "(brandName ?NAME ?PRODUCT) means that ?NAME 
is the brand name for the product class ?PRODUCT, e.g. Cheerios is the 
brand name for a certain class of breakfast cereal.")
(domain brandName 2 Product)
(subrelation brandName names)
(domain brandName 1 SymbolicString)

(documentation formerName EnglishLanguage "(formerName ?NAME ?THING) means that the 
string ?NAME is a name formerly used for ?THING.")
(subrelation formerName names)

(instance familyName BinaryPredicate)
(instance familyName AsymmetricRelation)
(domain familyName 1 SymbolicString)
(domain familyName 2 Human)
(subrelation familyName names)
(documentation familyName EnglishLanguage "(&%familyName ?STRING
?HUMAN) means that the &%SymbolicString ?STRING denotes a non-optional
name that ?HUMAN has inherited by virtue of being born into a
particular family (kin group).  surname is another word for this type
of name.  Cf. &%givenName.")

(documentation middleName EnglishLanguage "(middleName ?CHAR ?INDIV) means that the 
&%SymbolicString ?CHAR contains the middle name of the &%Human ?INDIV.")
(instance middleName BinaryPredicate)
(instance middleName AsymmetricRelation)
(domain middleName 1 SymbolicString)
(domain middleName 2 Human)
(subrelation middleName names)

(instance givenName BinaryPredicate)
(instance givenName AsymmetricRelation)
(subrelation givenName names)
(domain givenName 1 SymbolicString) 
(domain givenName 2 Human)
(documentation givenName EnglishLanguage "(&%givenName ?STRING ?HUMAN)
means that ?STRING is a name selected for ?HUMAN, usually from among
many options, as opposed to a mandatory name (cf. &%familyName) that
?HUMAN has inherited by virtue of being born into a certain kin group,
caste, or occupation.")

(instance localLongName BinaryPredicate)
(domain localLongName 1 SymbolicString)
(domain localLongName 2 Entity)
(documentation localLongName EnglishLanguage "(localLongName ?NAME ?THING) means that 
the string ?NAME is the long form of the name used for ?THING in its local 
area or language.")
(subrelation localLongName names)

(documentation localShortName EnglishLanguage "(localShortName ?NAME ?THING) means that 
the string ?NAME is the short form of the name used for ?THING in its 
local area or language.")
(subrelation localShortName names)
(instance localShortName BinaryPredicate)
(domain localShortName 1 SymbolicString)
(domain localShortName 2 Entity)

(subclass Report FactualText)
(subclass Report Article)
(documentation Report EnglishLanguage "A relatively brief &%FactualText, often it 
describes the findings of a study or experiment, or a series of 
observations.")

(subclass Message FactualText)
(documentation Message EnglishLanguage "A &%FactualText which is intended to be delivered to 
and read by a &%Human, &%GroupOfPeople or &%Organization.")

(subclass Newspaper Periodical)
(documentation Newspaper EnglishLanguage "A &%Periodical that is published on a daily or 
weekly basis, that contains &%Reports, and whose issues are printed on 
newsprint paper.")

(=>
  (instance ?PAPER Newspaper)
  (exists (?REPORT)
    (and
      (instance ?REPORT Report)
      (subsumesContentInstance ?PAPER ?REPORT))))

(=>
  (and
    (subclass ?PAPER Newspaper)
    (instance ?WEEK Week))
  (exists (?PUBLICATION ?ISSUE)
    (and
      (instance ?PUBLICATION Publication)
      (temporalPart (WhenFn ?PUBLICATION) ?WEEK)
      (result ?PUBLICATION ?ISSUE)
      (instance ?ISSUE ?PAPER))))

(subclass Magazine Periodical)
(documentation Magazine EnglishLanguage "A &%Periodical that is softbound and printed on 
glossy paper.")
   
(subclass Letter FactualText)
(documentation Letter EnglishLanguage "A brief message which is intended to be mailed 
to a person or &%Organization.")

(subclass HolidayCard ContentBearingObject)
(documentation HolidayCard EnglishLanguage "A stiff piece of &%Paper, usually folded over, which 
is sent to others in celebration of a &%Holiday.")

(=>
  (instance ?C HolidayCard)
  (material Paper ?C))

(=>
  (instance ?C HolidayCard)
  (exists (?H)
    (and
      (instance ?H Holiday)
      (refers ?C ?H))))

(subclass HistoricalAccount NarrativeText)
(subclass HistoricalAccount FactualText)
(documentation HistoricalAccount EnglishLanguage "A &%FactualAccount that describes 
significant events that occurred in the past.")

(=>
   (instance ?ACCOUNT HistoricalAccount)
   (exists (?EVENT)
      (and
         (represents ?ACCOUNT ?EVENT)
         (earlier (WhenFn ?EVENT) (WhenFn ?ACCOUNT)))))

(subclass Biography HistoricalAccount)
(documentation Biography EnglishLanguage "A &%HistoricalAccount which is concerned 
with the life of a single &%Human.")
(=>
  (instance ?B Biography)
  (exists (?H)
    (and
      (instance ?H Human)
      (refers ?B ?H))))

(subclass FinancialText Report)
(documentation FinancialText EnglishLanguage "A &%Report about monetary figures.  This 
class covers &%FinancialBills, balance sheets, account statements, etc.")

(subclass FinancialBill FinancialText)
(documentation FinancialBill EnglishLanguage "A brief statement that the stated amount 
of money is owed by the person to whom the bill is delivered.")

(subclass ReferenceBook Book)
(subclass ReferenceBook FactualText)
(documentation ReferenceBook EnglishLanguage "A &%Book which is not intended to be read 
from cover to cover, but which is meant to be consulted to answer specific 
factual questions, e.g. about the meaning of a word, the location of a 
country, etc.")

(subclass Dictionary ReferenceBook)
(documentation Dictionary EnglishLanguage "A &%ReferenceBook which specifies the meanings 
of the &%Words of a &%Language.")

(subclass NarrativeText Text)
(partition NarrativeText FictionalText HistoricalAccount)
(documentation NarrativeText EnglishLanguage "Any &%Text that tells a story, whether true 
or false.")

(subclass ShortStory FictionalText)
(subclass ShortStory Article)
(documentation ShortStory EnglishLanguage "A brief work of fiction, often bound with other 
short stories in a &%Book or &%Periodical.")

(subclass MysteryStory FictionalText)
(documentation MysteryStory EnglishLanguage "A &%FictionalText whose central plot element is a 
crime of some sort, usually a murder.")

(=>
  (instance ?S MysteryStory)
  (exists (?C)
    (and
      (instance ?C CriminalAction)
      (refers ?S ?C))))

(subclass Novel FictionalText)
(subclass Novel Book)
(documentation Novel EnglishLanguage "A &%FictionalText that is larger than a &%ShortStory 
and that is bound independently (i.e. it is a &%Book).")

(subclass DramaticPlay FictionalText)
(documentation DramaticPlay EnglishLanguage "A &%FictionalText that is intended to be realized 
as &%DramaticActing.")

(=>
   (instance ?PLAY DramaticPlay)
   (hasPurpose ?PLAY (exists (?ACT ?PROP)
                        (and
                           (instance ?ACT DramaticActing)
                           (containsInformation ?PLAY ?PROP)
                           (realization ?ACT ?PROP)))))

(subclass Opera DramaticPlay)
(documentation Opera EnglishLanguage "A &%DramaticPlay that is set to &%Music.")

(=>
   (and
      (instance ?OPERA Opera)
      (realization ?ACT ?OPERA))
   (instance ?ACT Music))

(instance FrenchLanguage SpokenHumanLanguage)
(instance FrenchLanguage NaturalLanguage)
(documentation FrenchLanguage EnglishLanguage "A &%Romance language that is the official language 
of France and Belgium, and is widely spoken in Africa.")

(instance GermanLanguage SpokenHumanLanguage)
(instance GermanLanguage NaturalLanguage)
(documentation GermanLanguage EnglishLanguage "A &%Germanic language that is spoken primarily in 
Germany and Austria.")

(instance GreekLanguage SpokenHumanLanguage)
(instance GreekLanguage NaturalLanguage)
(documentation GreekLanguage EnglishLanguage "An Indo-European language that is spoken in &%Greece.")

(instance LatinLanguage SpokenHumanLanguage)
(instance LatinLanguage NaturalLanguage)
(documentation LatinLanguage EnglishLanguage "A &%Language that was spoken in ancient &%Rome and 
is still the official language of the &%Vatican.")

(instance RussianLanguage SpokenHumanLanguage)
(instance RussianLanguage NaturalLanguage)
(documentation RussianLanguage EnglishLanguage "A Slavic language that is spoken in &%Russia.")

(instance SpanishLanguage SpokenHumanLanguage)
(instance SpanishLanguage NaturalLanguage)
(documentation SpanishLanguage EnglishLanguage "A &%Romance language that is the official language 
of Spain, Mexico, and many Central and South American countries.")

(<=>
  (attribute ?H LiteracyAttribute)
  (and
    (hasSkill Reading ?H)
    (hasSkill Writing ?H)))
(subclass LiteracyAttribute TraitAttribute)
(documentation LiteracyAttribute EnglishLanguage "If an individual has the &%attribute 
&%LiteracyAttribute, that individual is able to read and write.")

(subclass Industry Collection)
(documentation Industry EnglishLanguage "The class of &%Collections of &%Corporations 
which are in the same line of business.")

(=>
   (instance ?INDUSTRY Industry)
   (=>
      (and
         (member ?MEMB1 ?INDUSTRY)
         (member ?MEMB2 ?INDUSTRY))
      (exists (?CLASS)
         (and
            (subclass ?CLASS Corporation)
            (immediateInstance ?MEMB1 ?CLASS)
            (immediateInstance ?MEMB2 ?CLASS)))))

(subclass Chimney StationaryArtifact)
(subclass Chimney Device)
(documentation Chimney EnglishLanguage "A thin passageway through which &%Smoke from a controlled 
fire is conducted &%Outside of a &%Building or &%Room.")
(=>
  (instance ?C Chimney)
  (hasPurpose ?C 
    (exists (?S ?R)
      (and
        (instance ?S Smoke)
        (instance ?R Removing)
        (instrument ?R ?C)))))

(subclass Curb StationaryArtifact)
(documentation Curb EnglishLanguage "A raised concrete or asphalt structure that connects a 
&%Sidewalk with a &%Roadway.")
(=>
  (instance ?C Curb)
  (exists (?S ?R)
    (and
      (instance ?S Sidewalk)
      (instance ?R Roadway)
      (connects ?C ?S ?R))))

(subclass Stairway StationaryArtifact)
(documentation Stairway EnglishLanguage "A &%StationaryArtifact which allows one to climb, step 
by step, from one level to another.")

(=>
  (instance ?S Stairway)
  (and
    (capability MotionUpward instrument ?S)
    (capability MotionDownward instrument ?S)))

(=>
  (and
    (instance ?S Stairway)
    (part ?S ?B)
    (instance ?B Building))
  (hasPurpose ?S
    (exists (?L1 ?L2)
      (and
        (instance ?L1 BuildingLevel)
        (instance ?L2 BuildingLevel)
        (part ?L1 ?B)
        (part ?L2 ?B)
        (connects ?S ?L1 ?L2)))))

(subclass Steps StationaryArtifact)
(documentation Steps EnglishLanguage "A &%StationaryArtifact which allows one to climb, step 
by step, from one level to another.")

(=>
   (instance ?STEPS Steps)
   (and
      (capability MotionUpward instrument ?STEPS)
      (capability MotionDownward instrument ?STEPS)))

(=>
   (and
      (instance ?LEVEL1 BuildingLevel)
      (instance ?LEVEL2 BuildingLevel)
      (instance ?BUILDING Building)
      (part ?LEVEL1 ?BUILDING)
      (part ?LEVEL2 ?BUILDING))
   (exists (?STEPS)
      (and
         (instance ?STEPS Steps)
         (connects ?STEPS ?LEVEL1 ?LEVEL2))))
   
(subclass Ladder Artifact)
(documentation Ladder EnglishLanguage "An &%Artifact which consists of two parallel supports 
connected by a series of rungs which can be used to ascend or descend.")

(=>
   (instance ?LADDER Ladder)
   (and
      (capability MotionUpward instrument ?LADDER)
      (capability MotionDownward instrument ?LADDER)))

(instance Transparent InternalAttribute)
(documentation Transparent EnglishLanguage "A Transparent material is one through which
it is possible to have &%Seeing.")
(=>
  (attribute ?O Transparent)
  (modalAttribute
    (exists (?S)
      (and
        (instance ?S Seeing)
        (agent ?S ?A)
        (destination ?S ?D)
        (between ?O ?A ?D)))
    Possibility))

(subclass Curtain WindowCovering)
(subclass Curtain Fabric)
(documentation Curtain EnglishLanguage "A piece of &%Fabric whose purpose is &%Covering a 
&%Window so as to keep out the light or prevent people from seeing inside.")

(subclass Window Artifact)
(documentation Window EnglishLanguage "An &%Artifact composed of transparent material 
that admits light (and possibly air) into a &%Room, &%Building 
or &%Vehicle.")
(=>
  (instance ?W Window)
  (exists (?A)
    (and
      (part ?W ?A)
      (or
        (instance ?A Building)
        (instance ?A Room)
        (instance ?A Vehicle)))))

(=>
  (instance ?W Window)
  (attribute ?W Transparent))

(subclass Skylight Window)
(documentation Skylight EnglishLanguage "A &%Window that is part of the &%Ceiling of 
a &%Room.")

(=>
  (instance ?W Skylight)
  (exists (?C)
    (and
      (instance ?C Ceiling)
      (part ?W ?C))))

(subclass RearWindow VehicleWindow)
(documentation RearWindow EnglishLanguage "Any &%VehicleWindow which is located at the back 
of an &%Automobile.")

(=>
  (instance ?W RearWindow)
  (exists (?A)
    (and
      (instance ?A Automobile)
      (part ?W (BackFn ?A))))) 

(subclass WindowCovering Artifact)
(documentation WindowCovering EnglishLanguage "An &%Artifact that is used to cover 
Windows.  Note that this class includes blinds, drapes, shutters, 
etc.")
(=>
  (instance ?WC WindowCovering)
  (hasPurpose ?WC 
    (exists (?C ?W)
      (and
        (instance ?C Covering)
        (patient ?C ?W)
        (instance ?W Window)
        (instrument ?C ?WC)))))

(subclass Door Artifact)
(documentation Door EnglishLanguage "An &%Artifact that restricts and permits access to a 
&%StationaryArtifact (e.g. &%Building or &%Room) depending on whether the 
&%Door is open or locked.  Note that the class &%Door also covers gates, 
because it is not possible to define objective criteria that reliably 
distinguish doors from gates.")

(=>
   (instance ?DOOR Door)
   (exists (?WAY)
      (and
         (part ?DOOR ?WAY)
         (instance ?WAY Doorway))))

(subclass Hinge EngineeringComponent)
(documentation Hinge EnglishLanguage "An &%EngineeringComponent that connects one thing to 
another in such a way that they can move relative to one another.")
(=>
   (instance ?HINGE Hinge)
   (hasPurpose ?HINGE 
     (exists (?OBJ1 ?OBJ2)
       (and
         (connects ?HINGE ?OBJ1 ?OBJ2)
	 (or
	   (capability Rotating patient ?OBJ1)
	   (capability Rotating patient ?OBJ2))))))

(subclass Doorway StationaryArtifact)
(documentation Doorway EnglishLanguage "A &%StationaryArtifact consisting of a frame that 
holds a &%Door.")

(=>
   (instance ?WAY Doorway)
   (exists (?ARTIFACT)
      (and
         (part ?WAY ?ARTIFACT)
         (or
            (instance ?ARTIFACT Building)
            (instance ?ARTIFACT Room)))))

(=>
   (instance ?DOOR Door)
   (exists (?ARTIFACT)
      (and
         (part ?DOOR ?ARTIFACT)
         (or
            (instance ?ARTIFACT Building)
            (instance ?ARTIFACT Room)))))

(subclass Wall StationaryArtifact)
(documentation Wall EnglishLanguage "A &%StationaryArtifact that supports a &%Building or 
partitions it into &%Rooms.")

(=>
   (instance ?WALL Wall)
   (exists (?ARTIFACT)
      (and
         (part ?WALL ?ARTIFACT)
         (or
            (instance ?ARTIFACT Building)
            (instance ?ARTIFACT Room)))))

(subclass Floor StationaryArtifact)
(documentation Floor EnglishLanguage "A &%StationaryArtifact that is the bottom surface 
of a &%Room.")

(=>
   (instance ?FLOOR Floor)
   (exists (?ARTIFACT)
      (and
         (part ?FLOOR ?ARTIFACT)
         (instance ?ARTIFACT Room))))

(subclass Ceiling StationaryArtifact)
(documentation Ceiling EnglishLanguage "A &%StationaryArtifact that is the top surface 
of a &%Room.")
(=>
  (instance ?C Ceiling)
  (exists (?R)
    (and
      (top ?C ?R)
      (instance ?R Room))))

(subclass Roof StationaryArtifact)
(documentation Roof EnglishLanguage "The &%top of a &%Building.")

(=>
   (instance ?ROOF Roof)
   (exists (?BUILDING)
      (and
         (instance ?BUILDING Building)
         (top ?ROOF ?BUILDING))))

(subclass Fireplace StationaryArtifact)
(subclass Fireplace Device)
(documentation Fireplace EnglishLanguage "A &%Device in a &%Building which is used for burning wood, 
coal, etc for heat.")
(=>
  (instance ?F Fireplace)
  (hasPurpose ?F 
    (exists (?C)
      (and
        (instance ?C Combustion)
        (located ?C ?F)))))

(=>
  (instance ?F Fireplace)
  (exists (?C)
    (and
      (instance ?C Chimney)
      (connected ?F ?C))))
       
(=>
  (instance ?F Fireplace)
  (exists (?B)
    (and
      (instance ?B Building)
      (part ?F ?B)))) 

(subclass BuildingLevel StationaryArtifact)
(documentation BuildingLevel EnglishLanguage "The story or level of a building, e.g. the &%Basement, 
the &%Attic, the ground level, the fourteenth floor, etc.")

(=>
   (instance ?LEVEL BuildingLevel)
   (exists (?BUILDING)
      (and
         (instance ?BUILDING Building)
         (part ?LEVEL ?BUILDING))))

(subclass Basement BuildingLevel)
(documentation Basement EnglishLanguage "A &%BuildingLevel which satisfies two conditions, viz. it is 
lower than all of the other &%BuildingLevels in the same &%Building and it is below 
ground level.")

(=>
   (instance ?BASEMENT Basement)
   (not (exists (?LEVEL ?BUILDING)
      (and
         (instance ?LEVEL BuildingLevel)
         (instance ?BUILDING Building)
         (part ?LEVEL ?BUILDING)
         (part ?BASEMENT ?BUILDING)
         (not (equal ?LEVEL ?BASEMENT))
         (orientation ?LEVEL ?BASEMENT Below)))))

(subclass Elevator TransportationDevice)
(documentation Elevator EnglishLanguage "A &%TransportationDevice consisting of a car that moves 
up and down in a vertical shaft so that people or objects can move from one floor to another 
in a building.")
(=>
  (instance ?E Elevator)
  (hasPurpose ?E 
    (exists (?T ?L1 ?L2 ?B)
      (and
        (instance ?T Transportation)
        (origin ?T ?L1)
        (instance ?L1 BuildingLevel)
        (destination ?T ?L2)
        (instance ?L2 BuildingLevel)
        (not (equal ?L1 ?L2))
        (part ?L1 ?B)
        (part ?L2 ?B)
        (instance ?B Building)))))

(=>
  (instance ?E Elevator)
  (and
    (capability MotionUpward instrument ?E)
    (capability MotionDownward instrument ?E)))

(subclass Pump TransportationDevice)
(documentation Pump EnglishLanguage "A &%Device that moves &%Fluids by means of pressure or suction.")

(=> 
  (and
    (instance ?T Transportation)
    (instrument ?T ?D)
    (instance ?D Pump))
  (exists (?F)
    (and
      (instrument ?T ?F)
      (attribute ?F Fluid))))

(subclass Oar TransportationDevice)
(documentation Oar EnglishLanguage "A &%Device consisting of a flat, broad surface attached to a 
handle that is used as an instrument in &%Rowing.")

(=>
  (instance ?O Oar)
  (hasPurpose ?O 
    (exists (?R)
      (and
        (instance ?R Rowing)
        (instrument ?R ?O)))))

(subclass Garage StationaryArtifact)
(documentation Garage EnglishLanguage "A &%Building or part of a &%Building which is intended to house 
one or more &%Automobiles when they are not in use or are under
repair.")

(=>
  (instance ?GARAGE Garage)
  (hasPurpose ?GARAGE
    (exists (?AUTO)
      (and
        (instance ?AUTO Automobile)
        (contains ?GARAGE ?Automobile)))))

(=>
   (instance ?GARAGE Garage)
   (exists (?BUILDING)
      (and
         (instance ?BUILDING Building)
         (part ?GARAGE ?BUILDING))))

(subclass Bathroom Room)
(documentation Bathroom EnglishLanguage "A &%Room that contains a &%WashBasin and 
possibly a &%Toilet.")

(=>
  (instance ?B Bathroom)
  (exists (?W)
    (and
      (instance ?W WashBasin)
      (located ?W ?B))))

(subclass Closet Room)
(documentation Closet EnglishLanguage "A relatively small &%Room used for storage.")
(=>
  (instance ?R Closet)
  (hasPurpose ?R 
    (exists (?K ?O)
      (and
        (instance ?K Keeping)
        (patient ?K ?O)
        (located ?O ?R)))))

(subclass DressingRoom Room)
(documentation DressingRoom EnglishLanguage "Any &%Room which is intended for 
&%ChangingClothing.")
(=>
  (instance ?R DressingRoom)
  (hasPurpose ?R 
    (exists (?C)
      (and
        (instance ?C ChangingClothing)
        (located ?C ?R)))))
        
(subclass KitchenArea Region)
(documentation KitchenArea EnglishLanguage "a &%KitchenArea refers to a 
&%Region, either &%Outdoors or &%Indoors, that is intended for &%Cooking")
(termFormat EnglishLanguage KitchenArea "kitchen area")

(=>
  (instance ?K KitchenArea)
  (hasPurpose ?K 
    (exists (?C)
      (and
        (instance ?C Cooking)
        (located ?C ?K)))))
        
(subclass Kitchen Room)
(subclass Kitchen KitchenArea)
(documentation Kitchen EnglishLanguage "A &%Room intended for &%Cooking.")


(subclass DiningArea Region)
(documentation DiningArea EnglishLanguage "a &%DiningArea refers to a 
&%Region, either &%Outdoors or &%Indoors, that is intended for &%Eating")
(termFormat EnglishLanguage DiningArea "dining area")

(=> 
  (instance ?D DiningArea)
  (hasPurpose ?D
    (exists (?E)
      (and
        (instance ?E Eating)
        (located ?E ?D)))))                           
                           
(subclass DiningRoom Room)
(subclass DiningRoom DiningArea)
(documentation DiningRoom EnglishLanguage "A &%Room intended primarily for &%Eating.")

(subclass Bedroom Room)
(documentation Bedroom EnglishLanguage "A &%Room intended primarily for sleeping.")

(=>
   (instance ?ROOM Bedroom)
   (exists (?BED)
      (and
         (instance ?BED Bed)
         (located ?BED ?ROOM))))

(subclass Classroom Room)
(documentation Classroom EnglishLanguage "Any Room in a &%School where education 
takes place.")
(=>
  (instance ?R Classroom)
  (exists (?S)
    (and
      (instance ?S School)
      (part ?R ?S))))

(=>
  (instance ?R Classroom)
  (hasPurpose ?R 
    (exists (?E)
      (and
        (instance ?E EducationalProcess)
        (located ?E ?R)))))

(subclass Porch StationaryArtifact)
(documentation Porch EnglishLanguage "A &%StationaryArtifact which is &%connected to a 
&%Building and which provides some shelter in entering or leaving the 
&%Building or in sitting outside.")

(=>
   (instance ?PORCH Porch)
   (exists (?BUILDING)
      (and
         (instance ?BUILDING Building)
         (connected ?PORCH ?BUILDING)))) 

(subclass Sidewalk StationaryArtifact)
(documentation Sidewalk EnglishLanguage "A prepared path for pedestrians alongside a &%Roadway.")

(=>
   (instance ?SIDE Sidewalk)
   (hasPurpose ?SIDE (exists (?WALK)
                        (and
                           (instance ?WALK Walking)
                           (path ?WALK ?SIDE)))))

(=>
   (instance ?SIDE Sidewalk)
   (exists (?ROAD)
      (and
         (instance ?ROAD Roadway)
         (orientation ?SIDE ?ROAD Near))))

(subclass Fence StationaryArtifact)
(documentation Fence EnglishLanguage "A &%StationaryArtifact that serves to demarcate or
to prevent access to or from the area that the &%Fence surrounds.")

(subclass SportsGround StationaryArtifact)
(documentation SportsGround EnglishLanguage "A specially designated and maintained area 
where &%Sports are played.")

(=>
   (instance ?GROUND SportsGround)
   (hasPurpose ?GROUND (exists (?SPORT) 
                          (and
                             (instance ?SPORT Sport)
                             (located ?SPORT ?GROUND)))))

(subclass IndustrialPlant StationaryArtifact)
(documentation IndustrialPlant EnglishLanguage "A &%Building or part of a &%Building or group 
of &%Buildings whose purpose is to &%Manufacture something.")

(=>
   (instance ?PLANT IndustrialPlant)
   (or
      (instance ?PLANT Building)
      (exists (?BUILDING)
         (and
            (instance ?BUILDING Building)
            (located ?BUILDING ?PLANT)))))

(=>
   (instance ?PLANT IndustrialPlant)
   (hasPurpose ?PLANT (exists (?MANUFACTURE)
                         (and
                            (instance ?MANUFACTURE Manufacture)
                            (located ?MANUFACTURE ?PLANT)))))

(subclass Laboratory StationaryArtifact)
(documentation Laboratory EnglishLanguage "A &%Building, &%Room or suite of &%Rooms where 
scientific research, i.e. &%Experimenting, is conducted.")

(=>
   (instance ?LAB Laboratory)
   (exists (?EXPERIMENT)
      (and
         (instance ?EXPERIMENT Experimenting)
         (located ?EXPERIMENT ?LAB))))

(documentation experimentalControl EnglishLanguage "(experimentalControl ?EXPERIMENT ?OBJ) 
means that the Object ?OBJ serves as a control in the instance of 
&%Experimenting ?EXPERIMENT, i.e. ?OBJ is the standard against which something 
else in the experiment can be compared.")
(instance experimentalControl CaseRole)
(domain experimentalControl 1 Experimenting)
(domain experimentalControl 2 Object)
(subrelation experimentalControl patient)

(subclass MilitaryInstallation StationaryArtifact)
(subclass MilitaryInstallation MilitaryArtifact)
(documentation MilitaryInstallation EnglishLanguage "A &%StationaryArtifact consisting of grounds 
and &%Buildings that is intended to be used by a &%MilitaryOrganization.")

(subclass Farm StationaryArtifact)
(documentation Farm EnglishLanguage "A &%StationaryArtifact consisting of a cultivated 
&%LandArea and &%Buildings for maintaining the land and/or the &%Animals 
on the land.") 

(subclass Barn Building)
(disjoint Barn ResidentialBuilding)
(documentation Barn EnglishLanguage "A &%Building on a &%Farm that is used for keeping 
&%DomesticAnimals, &%Fodder or harvested crops.")

(=>
   (instance ?BARN Barn)
   (exists (?FARM)
      (and
         (instance ?FARM Farm)
         (located ?BARN ?FARM))))

(subclass PerformanceStage StationaryArtifact)
(documentation PerformanceStage EnglishLanguage "A large platform for theatrical plays, 
lectures, dances, music recitals, etc, which can be observed by an audience.")

(=>
  (instance ?STAGE PerformanceStage)
  (hasPurpose ?STAGE 
    (exists (?PERF)
      (and
        (instance ?PERF Demonstrating)
        (located ?PERF ?STAGE)))))

(=>
  (instance ?STAGE PerformanceStage)
  (exists (?AUDITORIUM)
    (and
      (instance ?AUDITORIUM Auditorium)
      (part ?STAGE ?AUDITORIUM))))

(subclass PerformanceStageWing StationaryArtifact)
(documentation PerformanceStageWing EnglishLanguage "A part of the &%PerformanceStage that is not 
visible to members of the audience.")

(=> 
  (instance ?W PerformanceStageWing)
  (exists (?S)
    (and
      (instance ?S PerformanceStage)
      (part ?W ?S))))

(subclass Magnetism RadiatingElectromagnetic)
(documentation Magnetism EnglishLanguage "Any instance of &%RadiatingElectromagnetic which 
involves the attraction of &%Iron.")

(subclass RadioEmission RadiatingElectromagnetic)
(documentation RadioEmission EnglishLanguage "Any instance of &%RadiatingElectromagnetic 
where the waves have a wavelength between 5 milimeters and 30,000 meters.")

(subclass Broadcasting Disseminating)
(subclass Broadcasting RadioEmission)
(documentation Broadcasting EnglishLanguage "&%Disseminating information by using a 
&%CommunicationDevice that radiates &%RadioEmissions.")

(=>
   (instance ?BROADCAST Broadcasting)
   (exists (?DEVICE)
      (and
         (instance ?DEVICE CommunicationDevice)
         (instrument ?BROADCAST ?DEVICE))))

(subclass RadioBroadcasting Broadcasting)
(documentation RadioBroadcasting EnglishLanguage "Any instance of &%Broadcasting which is 
intended to be received by a &%RadioReceiver.")

(=>
  (instance ?RADIO RadioReceiver)
  (capability RadioBroadcasting patient ?RADIO))

(subclass TelevisionBroadcasting Broadcasting)
(documentation TelevisionBroadcasting EnglishLanguage "Any instance of &%Broadcasting which 
is intended to be received by a &%Television.")

(=>
	(instance ?TELEVISION TelevisionReceiver)
      (capability TelevisionBroadcasting patient ?TELEVISION))

(subclass PeriodicalPublisher Publisher)
(documentation PeriodicalPublisher EnglishLanguage "Any &%Publisher that publishes 
&%Periodicals, e.g. newspaper and magazine publishing houses.")

(=>
  (instance ?C PeriodicalPublisher)
  (exists (?PUBLISH ?PERIODICAL)
    (and
      (instance ?PUBLISH Publication)
      (agent ?PUBLISH ?C)
      (patient ?PUBLISH ?PERIODICAL)
      (instance ?PERIODICAL Periodical))))

(subclass Antenna CommunicationDevice)
(documentation Antenna EnglishLanguage "A &%CommunicationDevice which enables or improves 
the reception of &%RadioEmissions by another &%CommunicationDevice (the 
radio or television receiver).")

(=>
   (instance ?ANTENNA Antenna)
   (capability RadioEmission instrument ?ANTENNA))

(subclass Explosion Radiating)
(documentation Explosion EnglishLanguage "Any sudden and massive release of energy that is 
the product of a chemical reaction.")

(=>
   (instance ?EXPLOSION Explosion)
   (exists (?PROC)
      (and
         (instance ?PROC ChemicalProcess)
         (causes ?PROC ?EXPLOSION))))

(=>
   (instance ?EXPLOSION Explosion)
   (capability Damaging instrument ?EXPLOSION))

(subclass Performance Demonstrating)
(documentation Performance EnglishLanguage "A &%Demonstrating which includes &%DramaticActing 
and/or &%Music and which is intended to entertain the audience.")

(=>
   (instance ?PERFORMANCE Performance)
   (exists (?SUB)
      (and
         (subProcess ?SUB ?PERFORMANCE)
         (or
            (instance ?SUB DramaticActing)
            (instance ?SUB Music)))))

(=>
   (instance ?PERFORMANCE Performance)
   (exists (?STAGE)
      (and
         (instance ?STAGE PerformanceStage)
         (located ?PERFORMANCE ?STAGE))))

(subclass PerformanceAct Performance)
(documentation PerformanceAct EnglishLanguage "A discrete &%subProcess of a &%Performance, 
e.g. an act of a play, a movement of a performance of a symphony, etc.")

(subclass Lecture Demonstrating)
(subclass Lecture Speaking)
(documentation Lecture EnglishLanguage "Any instance of &%Speaking which is done before an 
assembled audience.")

(subclass Proclaiming Lecture)
(subclass Proclaiming Declaring)
(documentation Proclaiming EnglishLanguage "Any instance of &%Speaking before an assembled 
audience that effects an institutional change, e.g. a change in the laws of 
the government.")

(subclass Reciting Speaking)
(documentation Reciting EnglishLanguage "Any instance of &%Speaking where what is 
uttered is contained within a &%Text.")

(=>
  (instance ?R Reciting)
  (exists (?P ?T)
    (and
      (realization ?R ?P)
      (containsInformation ?T ?P)
      (instance ?T Text))))

(subclass MusicalPerformance Performance)
(documentation MusicalPerformance EnglishLanguage "A &%Performance that consists exclusively of 
&%Music, e.g. an orchestra playing a symphony to an assembled audience.")

(=>
  (and
    (instance ?P MusicalPerformance)
    (subProcess ?M ?P))
  (instance ?M Music))

(subclass Sermon Lecture)
(documentation Sermon EnglishLanguage "A &%Lecture that is part of a &%ReligiousService.")

(=>
   (instance ?SERMON Sermon)
   (exists (?SERVICE)
      (and
         (instance ?SERVICE ReligiousService)
         (subProcess ?SERMON ?SERVICE))))

(subclass DramaticActing Pretending)
(documentation DramaticActing EnglishLanguage "Playing a character in a &%Performance, 
&%MotionPicture, etc.") 

(subclass DramaticPerformance Performance)
(documentation DramaticPerformance EnglishLanguage "A &%Performance that consists exclusively of 
&%DramaticActing, e.g. a live performance of Death_of_a_Salesman in front 
of an audience.")
(=>
  (and
    (instance ?P DramaticPerformance)
    (subProcess ?A ?P))
  (instance ?A DramaticActing))

(subclass DramaticCast GroupOfPeople)
(documentation DramaticCast EnglishLanguage "The &%GroupOfPeople who engage in &%DramaticActing 
as part of the realization of a single &%FictionalText.")

(=>
   (instance ?CAST DramaticCast)
   (exists (?TEXT ?PROP ?PROC)
      (and
         (instance ?TEXT FictionalText)
         (containsInformation ?TEXT ?PROP)
         (realization ?PROC ?PROP)
         (forall (?MEMBER)
            (=>
               (member ?MEMBER ?CAST)
               (exists (?SUB)
                  (and
                     (instance ?SUB DramaticActing)
                     (agent ?SUB ?MEMBER)
                     (subProcess ?SUB ?PROC))))))))

(subclass SocialParty Meeting)
(subclass SocialParty RecreationOrExercise)
(documentation SocialParty EnglishLanguage "Any &%Meeting where the intent is primarily 
to socialize and be entertained.")

(subclass FormalMeeting Meeting)
(disjoint FormalMeeting SocialParty)
(documentation FormalMeeting EnglishLanguage "Any &%Meeting which is the result of &%Planning 
and whose purpose is not socializing.")

(=>
   (instance ?MEETING FormalMeeting)
   (exists (?PLANNING)
      (and
         (instance ?PLANNING Planning)
         (result ?PLANNING ?MEETING)
         (earlier (WhenFn ?PLANNING) (WhenFn ?MEETING)))))

(subclass Funeral FormalMeeting)
(documentation Funeral EnglishLanguage "A &%FormalMeeting whose purpose is to commemorate 
the &%Death of someone.")
(=>
  (instance ?F Funeral)
  (exists (?D)
    (and
      (instance ?D Death)
      (refers ?F ?D))))

(subclass Interviewing FormalMeeting)
(documentation Interviewing EnglishLanguage "A &%FormalMeeting whose purpose is to acquire 
information from the interviewee that can be used in a media report.")

(subclass Choreographing Planning)
(documentation Choreographing EnglishLanguage "&%Planning that results in a sequence of dance 
steps that are executed as part of a &%Performance.")
(=>
  (and
    (instance ?G Choreographing)
    (result ?G ?P) 
    (realization ?D ?P))
  (instance ?D Dancing))

(subclass Resolution Deciding)
(documentation Resolution EnglishLanguage "Any instance of &%Deciding which is conducted at a 
&%FormalMeeting and where the &%agent is an &%Organization.")

(=>
   (instance ?RESOLUTION Resolution)
   (exists (?AGENT ?MEETING)
      (and
         (instance ?AGENT Organization)
         (agent ?RESOLUTION ?AGENT)
         (subProcess ?RESOLUTION ?MEETING)
         (instance ?MEETING FormalMeeting))))

(subclass Smoking RecreationOrExercise)
(documentation Smoking EnglishLanguage "Inhaling and exhaling &%Smoke produced by a 
&%CigarOrCigarette.")

(=>
   (instance ?SMOKING Smoking)
   (exists (?BURN ?CIGAR ?BREATHE ?SMOKE)
      (and
         (subProcess ?BURN ?SMOKING)
         (instance ?BURN Combustion)
         (resource ?BURN ?CIGAR)
         (instance ?CIGAR CigarOrCigarette)
         (result ?BURN ?SMOKE)
         (patient ?BREATHE ?SMOKE)
         (instance ?BREATHE Breathing)
         (subProcess ?BREATHE ?SMOKING))))
   
(subclass Jury GroupOfPeople)
(documentation Jury EnglishLanguage "A &%GroupOfPeople who are given the duty of rendering a 
verdict with respect to a &%LegalAction.")

(=>
   (instance ?JURY Jury)
   (holdsRight (exists (?DECISION)
                  (and
                     (instance ?DECISION LegalDecision)
                     (agent ?DECISION ?JURY))) ?JURY))

(subclass CriminalGang GroupOfPeople)
(documentation CriminalGang EnglishLanguage "A &%GroupOfPeople which exists (partially or 
wholly) for the purpose of CriminalAction.")
(=>
  (instance ?G CriminalGang)
  (hasPurpose ?G 
    (exists (?A)
      (and
        (instance ?A CriminalAction)
        (agent ?A ?G)))))

(subclass LegalCharge LegalAction)
(documentation LegalCharge EnglishLanguage "Any &%LegalAction of which a &%Government is 
the &%agent.")

(=>
   (instance ?CHARGE LegalCharge)
   (exists (?GOV)
      (and
         (instance ?GOV Government)
         (agent ?CHARGE ?GOV))))
   
(subclass LegalAppeal Requesting)
(subclass LegalAppeal JudicialProcess)
(documentation LegalAppeal EnglishLanguage "Asking that a higher court reconsider a &%LegalDecision 
of a lower court.")

(subclass Pleading Stating)
(documentation Pleading EnglishLanguage "&%Stating in a court of law a claim about whether or 
not one is guilty of the crime of which one has been accused.")

(=>
  (instance ?P Pleading)
  (exists (?J)
    (and
      (instance ?J JudicialProcess)
      (subProcess ?P ?J))))

(=>
  (instance ?P Pleading)
  (exists (?C)
    (and
      (instance ?C CriminalAction)
      (refers ?P ?C))))

(subclass Testifying Stating)
(documentation Testifying EnglishLanguage "Giving testimony as part of a &%JudicialProcess.")

(=>
   (instance ?TESTIFY Testifying)
   (exists (?PROC)
      (and
         (instance ?PROC JudicialProcess)
         (subProcess ?TESTIFY ?PROC))))

(subclass CourtRoom Room)
(documentation CourtRoom EnglishLanguage "Any &%Room whose purpose is to realize 
&%JudicialProcesses.")

(=>
   (instance ?PROCESS JudicialProcess)
   (exists (?ROOM)
      (and
         (instance ?ROOM CourtRoom)
         (located ?PROCESS ?ROOM))))

(subclass LegalOpinion Argument)
(documentation LegalOpinion EnglishLanguage "An &%Argument that explains the reasoning behind a 
&%LegalDecision.")

(=>
   (instance ?OPINION LegalOpinion)
   (exists (?DECISION ?TEXT ?PROPOSITION)
      (and
         (instance ?DECISION LegalDecision)
         (result ?DECISION ?TEXT)
         (containsInformation ?TEXT ?PROPOSITION)
         (conclusion ?PROPOSITION ?OPINION))))

(subclass LegalAward LegalDecision)
(documentation LegalAward EnglishLanguage "Any &%LegalDecision which gives to the plaintiff of 
the corresponding &%LegalAction some amount of monetary compensation.")

(=>
   (and
      (instance ?AWARD LegalAward)
      (refers ?AWARD ?ACTION)
      (instance ?ACTION LegalAction)
      (agent ?ACTION ?PLAINTIFF))
   (confersRight (exists (?GET ?OBJ ?VALUE)
                    (and
                       (instance ?GET Getting)
                       (experiencer ?GET ?PLAINTIFF)
                       (patient ?GET ?OBJ)
                       (monetaryValue ?OBJ ?VALUE))) ?AWARD ?PLAINTIFF))

(subclass LegalConviction LegalDecision)
(documentation LegalConviction EnglishLanguage "Any &%LegalDecision where the defendant is found 
guilty of the crime for which the corresponding trial was held.")

(subclass LegalDismissal LegalDecision)
(documentation LegalDismissal EnglishLanguage "Any &%LegalDecision where the &%LegalAction of 
the plaintiff is dismissed by the court, e.g. for lack of merit.")

(subclass LegalSummons Ordering)
(subclass LegalSummons JudicialProcess)
(documentation LegalSummons EnglishLanguage "An official order that a person appear at a 
&%CourtRoom at a specified time.")

(=>
  (and
    (instance ?S LegalSummons)
    (patient ?S ?O)
    (destination ?S ?P))
  (exists (?C ?T)
    (and
      (instance ?C CourtRoom)
      (containsInformation ?O 
        (modalAttribute 
	  (holdsDuring ?T 
	    (located ?P ?C)) 
	  Law))))) 

(subclass LegalAquittal LegalDecision)
(documentation LegalAquittal EnglishLanguage "Any &%LegalDecision where the defendant is found 
not to be guilty of the crime for which the corresponding trial was held.")

(subclass Sentencing LegalDecision)
(documentation Sentencing EnglishLanguage "Any &%LegalDecision where the defendant is assigned 
a punishment for a &%CriminalAction which was the subject of an earlier 
&%LegalConviction.")

(=>
  (and
    (instance ?S Sentencing)
    (patient ?S ?P))
  (exists (?C)
    (and
      (causes ?C ?S)
      (instance ?C LegalConviction)
      (patient ?C ?P)
      (earlier (WhenFn ?C) (WhenFn ?S)))))

(documentation defendant EnglishLanguage "(defendant ?AGENT ?ACTION) means the &%LegalAction 
?ACTION makes a legal claim against ?AGENT.")
(domain defendant 1 CognitiveAgent)
(domain defendant 2 LegalAction)
(subrelation defendant patient)

(documentation plaintiff EnglishLanguage "(plaintiff ?ACTION ?AGENT) means that ?AGENT 
is responsible for initiating the &%LegalAction ?ACTION.")
(domain plaintiff 1 LegalAction)
(domain plaintiff 2 CognitiveAgent)
(subrelation plaintiff agent)

(subclass GameCall Deciding)
(subclass GameCall Declaring)
(documentation GameCall EnglishLanguage "A decision issued by an official referee in a 
&%Game.  Note that &%GameCall is a subclass of &%Declaring, because these 
decisions have binding, normative force.")

(=>
   (instance ?CALL GameCall)
   (exists (?GAME)
      (and
         (instance ?GAME Game)
         (refers ?CALL ?GAME))))

(subclass BeginningOperations OrganizationalProcess)
(disjoint BeginningOperations CeasingOperations)
(documentation BeginningOperations EnglishLanguage "The process of an &%Organization 
commencing operations.  In the case of a &%Corporation, this would be 
the process of going into business.")

(=>
   (and
      (instance ?OP BeginningOperations)
      (instance ?ORG Organization)
      (agent ?OP ?ORG))
   (starts ?OP (WhenFn ?ORG)))

(subclass OrganizationalMerging OrganizationalProcess)
(documentation OrganizationalMerging EnglishLanguage "The process of two or more 
&%Organizations merging into a single &%Organization.")

(=>
  (and
    (instance ?M OrganizationalMerging)
    (patient ?M ?ORG1)
    (patient ?M ?ORG2)
    (not (equal ?ORG1 ?ORG2)))
  (and
    (holdsDuring 
      (BeginFn (WhenFn ?M)) 
      (not 
        (exists (?S1) 
          (and 
            (subOrganization ?ORG1 ?S1)
            (subOrganization ?ORG2 ?S1)))))
    (holdsDuring 
      (EndFn (WhenFn ?M)) 
      (exists (?S2) 
        (and 
          (subOrganization ?ORG1 ?S2)
          (subOrganization ?ORG2 ?S2))))))

(subclass CeasingOperations OrganizationalProcess)
(documentation CeasingOperations EnglishLanguage "The process of an &%Organization 
ceasing operations, i.e. its folding or going out of business in 
some other fashion.")

(=>
   (and
      (instance ?OP CeasingOperations)
      (instance ?ORG Organization)
      (agent ?OP ?ORG))
   (finishes ?OP (WhenFn ?ORG)))

(subclass BecomingDrunk PsychologicalProcess)
(disjoint BecomingDrunk SoberingUp)
(documentation BecomingDrunk EnglishLanguage "The process of transitioning from a state of 
being &%Sober to a state of being &%Drunk.")
(=>
  (and
    (instance ?B BecomingDrunk)
    (experiencer ?B ?A))
  (and
    (holdsDuring 
      (BeginFn (WhenFn ?B)) 
      (attribute ?A Sober))
    (holdsDuring 
      (EndFn (WhenFn ?B)) 
      (attribute ?A Drunk))))

(instance Sober ConsciousnessAttribute)
(documentation Sober EnglishLanguage "The &%ConsciousnessAttribute of someone whose motor and
cognitive faculties are not significantly impaired by a &%BiologicallyActiveSubstance.")

(subclass SoberingUp PsychologicalProcess)
(documentation SoberingUp EnglishLanguage "The process of transitioning from a state of 
being &%Drunk to a state of being &%Sober.")

(=>
  (and
    (instance ?B SoberingUp)
    (experiencer ?B ?A))
  (and
    (holdsDuring 
      (BeginFn (WhenFn ?B)) 
      (attribute ?A Drunk))
    (holdsDuring 
      (EndFn (WhenFn ?B)) 
      (attribute ?A Sober)))) 

(subclass FallingAsleep PsychologicalProcess)
(disjoint FallingAsleep WakingUp)
(documentation FallingAsleep EnglishLanguage "The process of transitioning from a state of 
being &%Awake to a state of being &%Asleep.")

(=>
   (and
      (instance ?FALL FallingAsleep)
      (experiencer ?FALL ?AGENT))
   (exists (?START ?FINISH)
      (and
         (starts ?START (WhenFn ?FALL))
         (finishes ?FINISH (WhenFn ?FALL))
         (holdsDuring ?START (attribute ?AGENT Awake))
         (holdsDuring ?FINISH (attribute ?AGENT Asleep))))) 

(subclass WakingUp PsychologicalProcess)
(documentation WakingUp EnglishLanguage "The process of transitioning from a state of being 
&%Asleep to a state of being &%Awake.")

(=>
   (and
      (instance ?WAKE WakingUp)
      (experiencer ?WAKE ?AGENT))
   (exists (?START ?FINISH)
      (and
         (starts ?START (WhenFn ?WAKE))
         (finishes ?FINISH (WhenFn ?WAKE))
         (holdsDuring ?START (attribute ?AGENT Asleep))
         (holdsDuring ?FINISH (attribute ?AGENT Awake))))) 

(subclass GainingConsciousness PsychologicalProcess)
(documentation GainingConsciousness EnglishLanguage "The process of transitioning from a state 
of being &%Unconscious to a state of being &%Awake.")
(=>
  (and
    (instance ?G GainingConsciousness)
    (experiencer ?G ?A))
  (and
    (holdsDuring 
      (BeginFn 
        (WhenFn ?G)) 
      (attribute ?A Unconscious))
    (holdsDuring 
      (EndFn 
        (WhenFn ?G)) 
      (attribute ?A Awake)))) 

(subclass LosingConsciousness PsychologicalProcess)
(disjoint LosingConsciousness GainingConsciousness)
(documentation LosingConsciousness EnglishLanguage "The process of transitioning from a state 
of being &%Awake to a state of being &%Unconscious.")

(=>
  (and
    (instance ?L LosingConsciousness)
    (experiencer ?L ?A))
  (and
    (holdsDuring 
      (BeginFn (WhenFn ?L)) 
      (attribute ?A Awake))
    (holdsDuring 
      (EndFn (WhenFn ?L)) 
      (attribute ?A Unconscious))))

(subclass AcademicDegree Certificate)
(documentation AcademicDegree EnglishLanguage "A &%Certificate that demonstrates that the holder 
of the &%Certificate has successfully completed an &%EducationalProgram.")

(=>
   (and
      (instance ?DEGREE AcademicDegree)
      (possesses ?AGENT ?DEGREE))
   (exists (?PROGRAM ?STUDY)
      (and
         (instance ?PROGRAM EducationalProgram)
         (realization ?STUDY ?PROGRAM)
         (experiencer ?STUDY ?AGENT))))

(subclass License Certificate)
(documentation License EnglishLanguage "License is the subclass of &%Certificates that 
are granted by a &%GovernmentOrganization and that authorize the performance 
of a kind of action, e.g., driving, exporting, travelling to another 
country, etc.")

(=>
  (instance ?L License)
  (exists (?ORG)
    (and
      (instance ?ORG GovernmentOrganization)
      (issuedBy ?L ?ORG))))

(subclass Ticket Certificate)
(documentation Ticket EnglishLanguage "A &%Certificate that allows the holder to perform 
a specified act once, e.g. ride a bus, attend a concert, obtain a prize, 
etc.")

(=>
  (and
    (instance ?T Ticket)
    (possesses ?A ?T))
  (confersNorm ?T 
    (exists (?P) 
      (agent ?P ?A)) 
    Permission))

(subclass Passport License)
(documentation Passport EnglishLanguage "A &%License which identifies the holder and permits 
travel between different countries.")

(=>
  (and
    (instance ?P Passport)
    (possesses ?A ?P))
  (confersNorm ?P 
    (exists (?T ?N1 ?N2)
      (and
        (instance ?T Translocation)
        (agent ?T ?A)
        (origin ?T ?N1)
        (destination ?T ?N2)
	(instance ?N1 Nation)
        (instance ?N2 Nation)
        (not (equal ?N1 ?N2))))
  Permission))

(subclass PassCertificate Certificate)
(documentation PassCertificate EnglishLanguage "A &%Certificate that allows the holder to 
be at or away from a specified location, e.g. a pass for a leave of 
absence, a hall pass, a pass to enter a cleared facility, etc.")

(=>
  (and
    (instance ?P PassCertificate)
    (possesses ?A ?P))
  (exists (?L)
    (confersNorm ?P 
      (located ?A ?L) 
      Permission)))

(subclass Testament Certificate)
(documentation Testament EnglishLanguage "A &%Certificate that describes how a person's 
property is to be distributed after the death of the person.")

(=>
  (instance ?T Testament)
  (exists (?B)
    (and
      (instance ?B Bequeathing)
      (refers ?T ?B))))

(documentation registeredItem EnglishLanguage "(registeredItem ?DOCUMENT ?ITEM) means 
that the &%Text ?DOCUMENT contains an official record of the &%Physical 
thing ?ITEM.  The registered item could be an object or an event, e.g., 
an automobile, a ship, a marriage, an adoption.")
(instance registeredItem BinaryPredicate)
(domain registeredItem 1 Text)
(domain registeredItem 2 Physical)
(subrelation registeredItem refers)

(subclass EducationalProgram Plan)
(documentation EducationalProgram EnglishLanguage "A series of &%EducationalCourses that must 
be completed to receive an &%AcademicDegree or other &%Certificate.  Note that 
an &%EducationalProgram, unlike an &%EducationalCourse, may be realized at more 
than one &%EducationalOrganization.")

(=>
   (instance ?PROGRAM EducationalProgram)
   (exists (?COURSE1 ?COURSE2)
      (and
         (instance ?COURSE1 EducationalCourse)
         (instance ?COURSE2 EducationalCourse)
         (not (equal ?COURSE1 ?COURSE2))
         (subPlan ?COURSE1 ?PROGRAM)
         (subPlan ?COURSE2 ?PROGRAM))))

(subclass EducationalCourse EducationalProgram)
(documentation EducationalCourse EnglishLanguage "A schedule of class meetings offered by an 
&%EducationalOrganization.")

(=>
   (instance ?COURSE EducationalCourse)
   (exists (?CLASS ?ORG)
      (and
         (realization ?CLASS ?COURSE)
         (instance ?CLASS EducationalProcess)
         (located ?CLASS ?ORG)
         (instance ?ORG EducationalOrganization))))  

(subclass SportsPlay Plan)
(documentation SportsPlay EnglishLanguage "A &%Plan for a &%Maneuver within a &%TeamSport.")

(=>
  (and
    (instance ?SP SportsPlay)
    (realization ?P ?SP))
  (exists (?S)
    (and
      (instance ?S TeamSport)
      (instance ?P Maneuver)
      (subProcess ?P ?S))))

(subclass School EducationalOrganization)
(documentation School EnglishLanguage "An &%EducationalOrganization with a curriculum, 
teachers, and students.  Most &%Schools are housed in a &%Building 
dedicated to the &%EducationalOrganization.")

(subclass PublicSchool School)
(subclass PublicSchool GovernmentOrganization)
(documentation PublicSchool EnglishLanguage "A &%School which is financed primarily by taxes.")

(subclass PrivateSchool School)
(disjoint PrivateSchool GovernmentOrganization)
(documentation PrivateSchool EnglishLanguage "A &%School which is not publicly owned.")

(subclass HighSchool School)
(documentation HighSchool EnglishLanguage "A &%School which admits &%students who have 
graduated from a middle school and which normally covers the ninth through 
twelfth grades.  A &%HighSchool confers a high school diploma.")

(=>
   (and
      (instance ?ENTER Matriculation)
      (agent ?ENTER ?COLLEGE)
      (patient ?ENTER ?STUDENT)
      (instance ?COLLEGE College))
   (exists (?GRAD ?SCHOOL)
      (and
         (instance ?GRAD Graduation)
         (agent ?GRAD ?SCHOOL)
         (patient ?GRAD ?STUDENT)
         (instance ?SCHOOL HighSchool)
         (earlier (WhenFn ?GRAD) (WhenFn ?ENTER)))))

(subclass SecondarySchool School)
(documentation SecondarySchool EnglishLanguage "A &%School which admits students who have 
graduated from a middle school and which normally covers the ninth through 
twelfth grades.  A &%SecondarySchool confers a high school diploma.")

(=>
  (and
    (instance ?E Matriculation)
    (agent ?E ?C)
    (patient ?E ?P)
    (instance ?C College))
  (exists (?G ?S)
    (and
      (instance ?G Graduation)
      (agent ?G ?S)
      (patient ?G ?P)
      (instance ?S SecondarySchool)
      (earlier (WhenFn ?G) (WhenFn ?E)))))

(subclass VocationalSchool School)
(documentation VocationalSchool EnglishLanguage "Any &%School whose aim is to teach 
students an &%OccupationalTrade.")

(=>
  (instance ?VS VocationalSchool)
  (hasPurpose ?VS 
    (exists (?P ?S ?T)
      (and
        (instance ?P EducationalProcess)
        (located ?P ?VS)
        (student ?S ?VS)
	(experiencer ?P ?S)
        (instance ?T OccupationalTrade)
        (holdsDuring 
	  (EndFn (WhenFn ?P)) 
	  (attribute ?S ?T))))))

(subclass PostSecondarySchool School)
(partition PostSecondarySchool JuniorCollege College University)
(documentation PostSecondarySchool EnglishLanguage "The class of &%Schools that offer 
an associate's degree or a bachelor's degree.")

(subclass JuniorCollege PostSecondarySchool)
(documentation JuniorCollege EnglishLanguage "The class of &%PostSecondarySchools that 
offer an associate's degree and do not offer a bachelor's degree.")
   
(subclass College PostSecondarySchool)
(documentation College EnglishLanguage "A &%School which admits &%students who have 
graduated from high school and which confers a bachelor's degree, 
normally requiring four years of study.  Note that a &%College does 
not confer any graduate degrees.  For institutions that confer both 
bachelor's and graduate degrees, the concept &%University should be 
used.") 

(subclass University PostSecondarySchool)
(documentation University EnglishLanguage "A &%School which admits &%students that 
have graduated from high school (known as undergraduate students) and 
&%students who have received a bachelor's degree (known as graduate 
students).  A &%University confers both bachelor's and graduate 
degrees.")

(subclass GraduateSchool PostSecondarySchool)
(documentation GraduateSchool EnglishLanguage "A &%School which is devoted to a specific 
subject area, which admits students that have a bachelor's degree, which 
grants masters and/or doctorate degrees, and which is part of a &%University.")
(=>
  (instance ?S GraduateSchool)
  (exists (?U)
    (and
      (instance ?U University)
      (part ?S ?U))))

(subclass MedicalSchool GraduateSchool)
(documentation MedicalSchool EnglishLanguage "A &%GraduateSchool that is devoted to 
&%MedicalScience and that grants the degree of doctor of medicine.")

(subclass ArtSchool School)
(documentation ArtSchool EnglishLanguage "Any &%School whose aim is to teach students 
how to create &%ArtWorks.")
(=>
   (and
      (instance ?AS ArtSchool)
      (instance ?P EducationalProcess)
      (located ?P ?AS))
   (exists (?M ?ART)
      (and
         (instance ?M Making)
         (patient ?M ?ART)
         (instance ?ART ArtWork)
         (refers ?P ?M))))

(subclass DaySchool School)
(documentation DaySchool EnglishLanguage "A &%School that does not board its students, i.e. 
students attend classes during the day and then return to a private residence 
for the night.")
(=>
  (instance ?DS DaySchool)
  (not 
    (exists (?S)
      (and
        (student ?S ?DS)
        (home ?S ?DS)))))

(subrelation student member)
(domain student 1 CognitiveAgent)
(domain student 2 EducationalOrganization)
(documentation student EnglishLanguage "(&%student ?AGENT ?ORG) means that ?AGENT is enrolled 
in the &%EducationalOrganization ?ORG.")

(=>
  (student ?AGENT ?ORG)
  (exists (?PROCESS)
    (and
      (instance ?PROCESS EducationalProcess)
      (located ?PROCESS ?ORG)
      (destination ?PROCESS ?AGENT))))

(subrelation teacher member)
(domain teacher 1 CognitiveAgent)
(domain teacher 2 EducationalOrganization)
(documentation teacher EnglishLanguage "(&%teacher ?AGENT ?ORG) means that ?AGENT is a 
teacher at the &%EducationalOrganization ?ORG.")

(=>
   (teacher ?AGENT ?ORG)
   (exists (?PROCESS)
      (and
         (instance ?PROCESS EducationalProcess)
	 (located ?PROCESS ?ORG)
         (agent ?PROCESS ?AGENT))))

(subclass DriversLicense License)
(documentation DriversLicense EnglishLanguage "A &%License which identifies the holder and 
indicates that he has the right to drive a certain class of &%RoadVehicle.")
(=>
  (and
    (instance ?L DriversLicense)
    (possesses ?A ?L))
  (confersNorm ?L 
    (exists (?D ?V)
      (and
        (instance ?D Driving)
        (agent ?D ?A)
        (patient ?D ?V)
        (instance ?V RoadVehicle))) 
    Permission))

(subclass InsurancePolicy Certificate)
(documentation InsurancePolicy EnglishLanguage "A &%Certificate that states the terms of an 
insurance contract.")

(subclass Telephoning Speaking)
(documentation Telephoning EnglishLanguage "Any instance of &%Speaking where the 
&%instrument of &%Communication is a &%Telephone.")

(=>
   (instance ?TEL Telephoning)
   (exists (?DEVICE)
      (and
         (instance ?DEVICE Telephone)
         (instrument ?TEL ?DEVICE))))

(subclass EconomicRelation BinaryRelation)
(documentation EconomicRelation EnglishLanguage "A class of &%Relations which are used 
to specify various economic measures, e.g. the GDP, the consumer price 
index, and the trade deficit.")

(=>
   (and
      (instance ?REL EconomicRelation)
      (domain ?REL 1 ?CLASS))
   (subclass ?CLASS GeopoliticalArea))

(subclass LaborStriking OrganizationalProcess)
(documentation LaborStriking EnglishLanguage "A &%Process in which some or all of the 
employees of an &%Organization refuse to work until their pay is 
increased or their working conditions are improved in some respect.")

(=>
   (and
      (instance ?STRIKE LaborStriking)
      (agent ?STRIKE ?PERSON)
      (instance ?PERSON Human)
      (patient ?STRIKE ?ORG)
      (instance ?ORG Organization))
   (employs ?ORG ?PERSON))

(subclass EmploymentFiring TerminatingEmployment)
(documentation EmploymentFiring EnglishLanguage "Involuntarily ending one's employment.  
Note that this covers termination due to inadequate performance, as 
well as layoffs.")

(subclass Resigning TerminatingEmployment)
(documentation Resigning EnglishLanguage "Voluntarily ending one's employment.")

(=>
  (and
    (instance ?R Resigning)
    (agent ?R ?A))
  (wants ?A ?R))

(subclass Retiring Resigning)
(documentation Retiring EnglishLanguage "Voluntarily leaving employment at the end of one's 
career in order to take time off in the later years of one's life.")

(subclass TransferringPosition OrganizationalProcess)
(documentation TransferringPosition EnglishLanguage "Any process within an &%Organization 
where a person is moved from one &%Position to another, e.g. the promotion 
or demotion of an employee.")

(=>
  (and
    (instance ?T TransferringPosition)
    (experiencer ?T ?H))
  (exists (?P1 ?P2 ?ORG)
    (and
      (holdsDuring 
        (BeginFn (WhenFn ?T)) 
	(occupiesPosition ?H ?P1 ?ORG))
      (holdsDuring 
        (EndFn (WhenFn ?T)) 
	(occupiesPosition ?H ?P2 ?ORG))
      (not 
        (equal ?P1 ?P2)))))

(instance monetaryWage QuaternaryPredicate)
(domain monetaryWage 1 Organization)
(domain monetaryWage 2 Human)
(domain monetaryWage 3 TimeDuration)
(domain monetaryWage 4 CurrencyMeasure)
(documentation monetaryWage EnglishLanguage "(&%monetaryWage ?ORG ?PERSON ?TIME ?MONEY) means 
that the &%Organization employs ?PERSON and pays him/her the amount of money 
?MONEY per &%TimeDuration ?TIME.") 

(=>
   (monetaryWage ?ORG ?PERSON ?TIME ?MONEY)
   (employs ?ORG ?PERSON))

(subclass GameArtifact Artifact)
(disjointDecomposition GameArtifact GameBoard GamePiece)
(documentation GameArtifact EnglishLanguage "An &%Artifact that is designed to be used as an 
&%instrument in a &%Game.")

(=>
   (instance ?ARTIFACT GameArtifact)
   (exists (?GAME)
      (and
         (subclass ?GAME Game)
         (capability ?GAME instrument ?ARTIFACT))))

(subclass GameBoard GameArtifact)
(documentation GameBoard EnglishLanguage "A &%GameArtifact which is intended to be used as the 
game area for playing a particular game.")

(=>
   (instance ?BOARD GameBoard)
   (hasPurpose ?BOARD (exists (?GAME)
                         (and
                            (instance ?GAME Game)
                            (located ?GAME ?BOARD)))))

(subclass GamePiece GameArtifact)
(documentation GamePiece EnglishLanguage "A &%GameArtifact that is moved around in a game 
area.")

(subclass GameDie GamePiece)
(documentation GameDie EnglishLanguage "A small cube with 1 to 6 dots on each face that is 
used to generate numbers at random in a &%Game.")

(subclass Ball GamePiece)
(documentation Ball EnglishLanguage "Any &%GamePiece which has the shape of a sphere.")
(=>
  (instance ?B Ball)
  (attribute ?B Sphere))

(subclass ShotBall Ball)
(documentation ShotBall EnglishLanguage "A solid metal &%Ball that is used in the sport of 
shotput.")

(subclass GameShot Impelling)
(subclass GameShot Maneuver)
(documentation GameShot EnglishLanguage "Impelling a &%GamePiece for the purpose of 
scoring a point or preventing the opposing player or team from scoring 
a point.  Note that this class does not cover shots which are disallowed 
by the rules of the game.")

(=>
   (instance ?SHOT GameShot)
   (exists (?PIECE)
      (and
         (instance ?PIECE GamePiece)
         (patient ?SHOT ?PIECE))))

(=>
   (instance ?SHOT GameShot)
   (exists (?GAME)
      (and
         (instance ?GAME Game)
         (subProcess ?SHOT ?GAME))))

(=>
   (instance ?SHOT GameShot)
   (hasPurpose ?SHOT (instance ?SHOT Score)))

(subclass Score GameShot)
(documentation Score EnglishLanguage "A successful attempt to score a point in a &%Game.")

(subclass GameGoal GameArtifact)
(documentation GameGoal EnglishLanguage "The location where a &%GameShot must end up if it 
is to constitute a &%Score.")

(=>
   (and
      (instance ?GOAL GameGoal)
      (instrument ?GAME ?GOAL)
      (instance ?GAME Game)
      (subProcess ?SCORE ?GAME)
      (instance ?SCORE Score))
   (exists (?PIECE ?TIME)
      (and
         (instance ?PIECE GamePiece)
         (patient ?SCORE ?PIECE)
         (temporalPart ?TIME (WhenFn ?SCORE))
         (holdsDuring ?TIME (located ?PIECE ?GOAL)))))

(subclass SportServe GameShot)
(documentation SportServe EnglishLanguage "A &%GameShot which is part of a &%Sport and 
which serves to start the &%Sport, e.g. the beginning shot in &%Tennis, 
&%Badminton or &%Squash.")

(=>
  (instance ?SS SportServe)
  (exists (?S)
    (and
      (instance ?S Sport)
      (subProcess ?SS ?S)
      (starts (WhenFn ?SS) (WhenFn ?S)))))

(subclass Throwing Impelling)
(subclass Throwing BodyMotion)
(documentation Throwing EnglishLanguage "Any instance of &%Impelling where the &%instrument is 
an &%Arm.")

(=>
   (instance ?THROW Throwing)
   (exists (?ARM)
      (and
         (instance ?ARM Arm)
         (instrument ?THROW ?ARM))))

(subclass Pitching Throwing)
(subclass Pitching GameShot)
(documentation Pitching EnglishLanguage "&%Throwing a &%Ball to the batter in a game of 
&%Baseball or softball.")

(=>
   (and
      (instance ?PITCH Pitching)
      (patient ?PITCH ?BALL))
   (instance ?BALL Ball))

(subclass Catching Touching)
(subclass Catching Maneuver)
(documentation Catching EnglishLanguage "Any &%Maneuver in a &%Game which results in a 
situation where the &%agent &%grasps the &%Ball.")

(=>
   (and
      (instance ?CATCH Catching) 
      (patient ?CATCH ?BALL))
   (instance ?CATCH Ball))

(=>
   (and
      (instance ?CATCH Catching)
      (agent ?CATCH ?AGENT)
      (patient ?CATCH ?BALL))
   (holdsDuring (EndFn (WhenFn ?CATCH)) (grasps ?AGENT ?BALL)))
         
(subclass HomeBase BaseballBase)
(documentation HomeBase EnglishLanguage "The final &%BaseballBase which must be touched 
by the batter in order to achieve a BaseballRun.")
(=>
  (and
    (instance ?R BaseballRun)
    (agent ?R ?A))
  (exists (?T ?H)
    (and
      (instance ?T Touching)
      (subProcess ?T ?R)
      (agent ?T ?A)
      (patient ?T ?H)
      (instance ?H HomeBase))))

(subclass PitchersMound GameArtifact)
(documentation PitchersMound EnglishLanguage "The place where the pitcher in &%Baseball 
stands when he is throwing balls to the batter.")

(=>
  (and
    (instance ?G Baseball)
    (instance ?P Pitching)
    (subProcess ?P ?G))
  (exists (?M)
    (and
      (instance ?M PitchersMound)
      (origin ?P ?M))))


(subclass PlayingCard GameArtifact)
(subclass PlayingCard ContentBearingObject)
(documentation PlayingCard EnglishLanguage "A small piece of stiff paper with markings 
which is intended to be used for playing card games.")

(=>
  (instance ?C PlayingCard)
  (material Paper ?C))

(subclass Stretching Motion)
(documentation Stretching EnglishLanguage "Moving two sides of an object in opposite 
directions so that the object becomes both longer and thinner.")
(subclass Stretching Lengthening)

(subclass Accelerating Translocation)
(subclass Accelerating Increasing)
(documentation Accelerating EnglishLanguage "Increasing the speed with which someone 
or something is moving.")

(=>
   (and
      (instance ?ACCELERATE Accelerating)
      (agent ?ACCELERATE ?AGENT))
   (exists (?LENGTH1 ?LENGTH2 ?TIME1 ?TIME2)
      (and
         (holdsDuring (BeginFn (WhenFn ?ACCELERATE)) (measure ?AGENT (SpeedFn ?LENGTH1 ?TIME1)))
         (holdsDuring (EndFn (WhenFn ?ACCELERATE)) (measure ?AGENT (SpeedFn ?LENGTH2 ?TIME2)))
         (or
            (greaterThan ?LENGTH2 ?LENGTH1)
            (greaterThan ?TIME2 ?TIME1))))) 

(subclass Decelerating Translocation)
(subclass Decelerating Decreasing)
(documentation Decelerating EnglishLanguage "Decreasing the speed with which someone 
or something is moving.")
(=>
  (and
    (instance ?D Decelerating)
    (agent ?D ?A))
  (exists (?L1 ?L2 ?T1 ?T2)
    (and
      (holdsDuring 
        (BeginFn (WhenFn ?D)) 
        (measure ?A (SpeedFn ?L1 ?T1)))
      (holdsDuring 
        (EndFn (WhenFn ?D)) 
        (measure ?A (SpeedFn ?L2 ?T2)))
      (or
        (greaterThan ?L1 ?L2)
        (greaterThan ?T1 ?T2))))) 

(=>
  (and
    (instance ?A Accelerating)
    (equal ?T (WhenFn ?A))
    (experiencer ?A ?E))
  (not
    (exists (?D)
      (and
        (instance ?D Decelerating)
        (experiencer ?D ?E)
        (equal ?T (WhenFn ?D))))))

(subclass Shortening Decreasing)
(documentation Shortening EnglishLanguage "Decreasing the length of something.")

(=>
  (and
    (instance ?S Shortening)
    (patient ?S ?O))
  (exists (?L1 ?L2)
    (and
      (holdsDuring 
        (BeginFn (WhenFn ?S)) 
	(length ?O ?L1))
      (holdsDuring 
        (EndFn (WhenFn ?S)) 
	(length ?O ?L2))
      (greaterThan ?L1 ?L2))))

(subclass Flying Translocation)
(documentation Flying EnglishLanguage "Any instance of &%Translocation which is through an 
&%AtmosphericRegion and which is powered by the wings of an &%Animal.")

(=>
   (instance ?FLY Flying)
   (exists (?REGION)
      (and
         (instance ?REGION AtmosphericRegion)
         (located ?FLY ?REGION))))

(subclass TakingOff Translocation)
(documentation TakingOff EnglishLanguage "Any instance of &%Translocation which starts on something other 
than an &%AtmosphericRegion and which has an instance of &%Flying as a &%subProcess.")

(=>
   (instance ?OFF TakingOff)
   (exists (?REGION ?FLYING)
      (and
         (not (instance ?REGION AtmosphericRegion))
         (holdsDuring (BeginFn (WhenFn ?OFF)) (located ?OFF ?REGION))
         (subProcess ?FLYING ?OFF)
         (instance ?FLYING Flying))))

(subclass Landing Translocation)
(documentation Landing EnglishLanguage "Any instance of &%Translocation which ends up on something other 
than an &%AtmosphericRegion and which has an instance of &%Flying as a &%subProcess.")

(=>
   (instance ?LAND Landing)
   (exists (?REGION ?FLYING)
      (and
         (not (instance ?REGION AtmosphericRegion))
         (holdsDuring (EndFn (WhenFn ?LAND)) (located ?LAND ?REGION))
         (subProcess ?FLYING ?LAND)
         (instance ?FLYING Flying))))

(subclass Returning Translocation)
(documentation Returning EnglishLanguage "Any instance of &%Translocation where the &%agent 
goes to a location where he/she had been before the &%Translocation took place.")

(=>
   (and
      (instance ?RETURN Returning)
      (experiencer ?RETURN ?AGENT)
      (destination ?RETURN ?DEST))
   (exists (?TIME)
      (and
         (earlier ?TIME (WhenFn ?RETURN))
         (holdsDuring ?TIME (located ?AGENT ?DEST)))))

(subclass Escaping Translocation)
(documentation Escaping EnglishLanguage "Any instance of &%Translocation where the &%agent brings 
it about that he/she is no longer confined without having the right to do 
so.")

(=>
   (and
      (instance ?ESCAPE Escaping)
      (agent ?ESCAPE ?AGENT))
   (exists (?CONFINE)
      (and
         (instance ?CONFINE Confining)
         (patient ?CONFINE ?AGENT)
         (meetsTemporally (WhenFn ?CONFINE) (WhenFn ?ESCAPE)))))

(=>
   (instance ?ESCAPE Escaping)
   (not (holdsRight (agent ?ESCAPE ?AGENT) ?AGENT)))
   
(subclass Leaving Translocation)
(documentation Leaving EnglishLanguage "The initial part of any instance of &%Translocation.")

(=>
   (instance ?LEAVE Leaving)
   (exists (?GO)
      (and
         (instance ?GO Translocation)
         (subProcess ?LEAVE ?GO)
         (starts (WhenFn ?LEAVE) (WhenFn ?GO)))))

(subclass Arriving Translocation)
(documentation Arriving EnglishLanguage "The final part of any instance of &%Translocation.")

(=>
   (instance ?ARRIVE Arriving)
   (exists (?GO)
      (and
         (instance ?GO Translocation)
         (subProcess ?ARRIVE ?GO)
         (finishes (WhenFn ?ARRIVE) (WhenFn ?GO)))))

(subclass Immigrating Translocation)
(documentation Immigrating EnglishLanguage "Any &%Translocation by a Human from one &%Nation 
to another &%Nation where the person is not a citizen for the purpose of taking 
up residence.")

(=>
  (and
    (instance ?I Immigrating)
    (agent ?I ?A))
  (exists (?N1 ?N2)
    (and
      (instance ?N1 Nation)
      (instance ?N2 Nation)
      (origin ?I ?N1)
      (destination ?I ?N2)
      (not 
        (equal ?N1 ?N2))
      (member ?A (CitizenryFn ?N1))
      (not 
        (member ?A (CitizenryFn ?N2)))
      (hasPurpose ?I 
        (inhabits ?A ?N2)))))

(=>
  (and
    (instance ?I Immigrating)
    (agent ?I ?A))
  (instance ?A Human))

(subclass Boarding Translocation)
(documentation Boarding EnglishLanguage "Getting on a &%Vehicle, e.g. getting into an 
&%Automobile, boarding an &%Aircraft, etc.")
(=>
  (and
    (instance ?B Boarding)
    (patient ?B ?V))
  (instance ?V Vehicle))

(=>
  (and
    (instance ?B Boarding)
    (agent ?B ?AGENT)
    (patient ?B ?V))
  (and
    (holdsDuring 
      (BeginFn (WhenFn ?B)) 
      (not (onboard ?A ?V)))
    (holdsDuring 
      (EndFn (WhenFn ?B)) 
      (onboard ?A ?V))))

(documentation onboard EnglishLanguage "(onboard ?OBJ ?VEHICLE) means that the 
&%SelfConnectedObject ?OBJ is inside the &%Vehicle ?VEHICLE.")
(domain onboard 1 SelfConnectedObject)
(domain onboard 2 Vehicle)
(subrelation onboard located)

(=>
  (onboard ?O ?V)
  (contains ?V ?O))

(=>
  (and
    (onboard ?O ?V)
    (instrument ?T ?V)
    (instance ?T Transportation))
  (patient ?T ?O))

(subclass Deboarding Translocation)
(relatedInternalConcept Boarding Deboarding)
(documentation Deboarding EnglishLanguage "Getting off a &%Vehicle, e.g. getting out of an 
Automobile, deplaning, getting off a &%WaterVehicle, etc.")
(=>
  (and
    (instance ?B Deboarding)
    (patient ?B ?VEHICLE))
  (instance ?VEHICLE Vehicle))

(=>
  (and
    (instance ?B Deboarding)
    (agent ?B ?A)
    (patient ?B ?V))
  (and
    (holdsDuring 
      (BeginFn (WhenFn ?B)) 
      (onboard ?A ?V))
    (holdsDuring 
      (EndFn (WhenFn ?B)) 
      (not 
        (onboard ?A ?V)))))

(subclass Rotating Motion)
(documentation Rotating EnglishLanguage "&%Motion that begins and ends at the same point, 
because the trajectory of the &%Motion is circular.")

(=>
  (and
    (instance ?R Rotating)
    (experiencer ?R ?A))
  (exists (?L)
    (and
      (holdsDuring 
        (BeginFn (WhenFn ?R)) 
	(located ?A ?L))
      (holdsDuring 
        (EndFn (WhenFn ?R)) 
	(located ?A ?L)))))

(subclass Tremor Rotating)
(documentation Tremor EnglishLanguage "&%Motion that involves rapidly &%Rotating between 
two positions.")

(instance axis BinaryPredicate)
(documentation axis EnglishLanguage "(axis ?A ?OBJ) means that a &%part ?A of an &%Object ?OBJ
is the axis of rotation in a &%Rotating.")
(domain axis 1 Object)
(domain axis 2 Object)

(=>
  (axis ?AXIS ?OBJ)
  (exists (?R)
    (and
      (instance ?R Rotating)
      (part ?AXIS ?OBJ)
      (experiencer ?R ?OBJ)
      (not
        (exists (?R2)
          (and
            (instance ?R2 Rotating)
            (subProcess ?R2 ?R)
            (experiencer ?R2 ?AXIS)))))))

(subclass CentrifugalMotion Rotating)
(disjoint CentrifugalMotion CentripetalMotion)
(documentation CentrifugalMotion EnglishLanguage "Any instance of &%Rotating where there is 
&%Motion away from the center of the &%Rotating.")
(=>
  (instance ?CM CentrifugalMotion)
  (exists (?R ?A ?O1 ?O2)
    (and
      (instance ?R Rotating)
      (axis ?A ?O1)
      (origin ?CM ?A)
      (experiencer ?R ?O1)
      (experiencer ?CM ?O2)
      (causes ?R ?CM))))

(subclass CentripetalMotion Rotating)
(documentation CentripetalMotion EnglishLanguage "Any instance of &%Rotating where there is 
&%Motion towards the center of the &%Rotating.")
(=>
  (instance ?CM CentripetalMotion)
  (exists (?R ?A ?O1 ?O2)
    (and
      (instance ?R Rotating)
      (axis ?A ?O1)
      (destination ?CM ?A)
      (experiencer ?R ?O1)
      (experiencer ?CM ?O2)
      (causes ?R ?CM))))

(subclass Reversing Motion)
(documentation Reversing EnglishLanguage "Moving something in such a way that its &%top 
becomes its &%bottom and vice versa.")

(=>
   (and
      (instance ?REVERSE Reversing)
      (patient ?REVERSE ?OBJ)
      (holdsDuring (BeginFn (WhenFn ?REVERSE)) (and (top ?TOP ?OBJ) (bottom ?BOTTOM ?OBJ))))
   (holdsDuring (EndFn (WhenFn ?REVERSE)) (and (top ?BOTTOM ?OBJ) (bottom ?TOP ?OBJ))))

(subclass Dripping LiquidMotion)
(documentation Dripping EnglishLanguage "Any &%LiquidMotion where the &%Liquid is moved 
drop by drop.")

(subclass Stirring LiquidMotion)
(subclass Stirring Combining)
(documentation Stirring EnglishLanguage "Any instance of &%LiquidMotion which is also an 
instance of &%Combining two or more &%Liquids.")

(subclass Pouring LiquidMotion)
(subclass Pouring Transfer)
(documentation Pouring EnglishLanguage "Any instance of &%Transfer from one &%Container to 
another, where the thing transferred is a &%Liquid.")

(=>
   (instance ?POUR Pouring)
   (exists (?LIQUID ?CONTAINER1 ?CONTAINER2)
      (and
         (origin ?POUR ?CONTAINER1)
         (destination ?POUR ?CONTAINER2)
         (instance ?CONTAINER1 Container)
         (instance ?CONTAINER2 Container)
         (not (equal ?CONTAINER1 ?CONTAINER2))
         (patient ?POUR ?LIQUID)
         (attribute ?LIQUID Liquid)
         (instance ?LIQUID Substance))))

(subclass WaterWave LiquidMotion)
(documentation WaterWave EnglishLanguage "A &%WaterWave is a raised ridge of water 
moving along the surface of a body of water.  The &%WaterWave moves 
in a direction approximately transverse to the crest line of the wave.  
The &%patient of the &%WaterWave is successive regions of water, which 
do not travel in the direction of the wave or with it.")

(=>
   (instance ?WAVE WaterWave)
   (exists (?AREA)
      (and
         (instance ?AREA WaterArea)
         (located ?WAVE ?AREA))))

(subclass Flooding LiquidMotion)
(documentation Flooding EnglishLanguage "Flooding is the subclass of &%LiquidMotion 
processes in which the water level of a &%Waterway rises or water spreads 
over a flood plain along a &%Waterway.")
(=>
  (and
    (instance ?F Flooding)
    (located ?F ?P))
  (exists (?L1 ?L2)
    (and
      (holdsDuring 
        (BeginFn (WhenFn ?F)) 
        (waterDepth ?P ?L1))
      (holdsDuring 
        (EndFn (WhenFn ?F)) 
        (waterDepth ?P ?L2))
      (greaterThan ?L2 ?L1))))

(subclass Spraying LiquidMotion)
(subclass Spraying Transfer)
(documentation Spraying EnglishLanguage "Any instance of &%Transfer of a &%Liquid which is 
accomplished by converting the &%Liquid into a mist.")

(subclass Pulling LandTransportation)
(documentation Pulling EnglishLanguage "Any instance of &%LandTransportation, where a 
&%TransportationDevice is dragged by something else, whether the something 
else is an &%Animal or a self-powered &%TransportationDevice.")

(=>
  (and
    (instance ?P Pulling)
    (patient ?P ?D))
  (instance ?D TransportationDevice))

(subclass MilitaryVehicle MilitaryPlatform)
(subclass MilitaryVehicle Vehicle)
(documentation MilitaryVehicle EnglishLanguage "Any &%Vehicle which is intended to be 
used by a &%MilitaryOrganization. Military platforms which are also vehicles. 
This would include things like airplanes and tanks, but exclude things like 
towed platforms.")

(=>
  (instance ?V MilitaryVehicle)
  (hasPurpose ?V 
    (exists (?O)
      (and
        (instance ?O MilitaryOrganization)
        (uses ?V ?O)))))

(subclass MilitaryWaterVehicle WaterVehicle)
(documentation MilitaryWaterVehicle EnglishLanguage "&%MilitaryWaterVehicle is the class of 
all &%WaterVehicle owned or leased for use by some &%MilitaryOrganization.")

(subclass Submarine WaterVehicle)
(documentation Submarine EnglishLanguage "A &%WaterVehicle which is capable of travelling 
under the water level by filling tanks with water.")

(subclass MilitaryTank MilitaryVehicle)
(subclass MilitaryTank RoadVehicle)
(subclass MilitaryTank ArtilleryGun)
(documentation MilitaryTank EnglishLanguage "A &%MilitaryVehicle that moves along the ground 
on treaded wheels and that contains a large cannon.")

(subclass TractorTrailer RoadVehicle)
(documentation TractorTrailer EnglishLanguage "&%TractorTrailer is the subclass of 
&%RoadVehicles that are truck tractor- truck trailer combinations.")

(=>
  (instance ?TT TractorTrailer)
  (exists (?TRAC ?TRAIL)
    (and
      (instance ?TRAC TruckTractor)
      (instance ?TRAIL TruckTrailer)
      (part ?TRAC ?TT)
      (part ?TRAIL ?TT)
      (connected ?TRAC ?TRAIL))))

(subclass TruckTractor Truck)
(documentation TruckTractor EnglishLanguage "&%TruckTractor is the subclass of &%Trucks 
that are used to tow truck trailers.  Truck tractors are the cab portions 
of tractor-trailers.  See also &%TruckTrailer.")

(subclass TruckTrailer Wagon)
(documentation TruckTrailer EnglishLanguage "&%TruckTrailer is the subclass of &%Wagons 
that are towed by &%TruckTractors.  These are the trailers used in 
tractor-trailer (semi-trailer) rigs.")

(subclass Aircraft Vehicle)
(documentation Aircraft EnglishLanguage "Any Vehicle which is capable of 
AirTransportation.  Note that this class covers both fixed-wing aircraft
and helicopters.")

(subclass AirTransportation Transportation)
(documentation AirTransportation EnglishLanguage "Any instance of &%Transportation where the 
&%instrument is an &%Aircraft and which is through an &%AtmosphericRegion.")

(=>
   (instance ?TRANSPORT AirTransportation)
   (exists (?CRAFT ?REGION)
      (and
         (instance ?CRAFT Aircraft)
         (instance ?REGION AtmosphericRegion)
         (instrument ?TRANSPORT ?CRAFT)
         (located ?TRANSPORT ?REGION))))

(=>
   (instance ?CRAFT Aircraft)
   (capability AirTransportation instrument ?CRAFT))

(=>
  (and
    (instance ?T AirTransportation)
    (path ?T ?P))
  (instance ?P AirTransitway))

(subclass Helicopter Aircraft)
(documentation Helicopter EnglishLanguage "Any &%Aircraft with rapidly rotating wings.")

(subclass MilitaryArtifact Artifact)
(documentation MilitaryArtifact EnglishLanguage "Any &%Artifact which is made for a 
&%MilitaryOrganization.")

(=>
  (instance ?A MilitaryArtifact)
  (hasPurpose ?A
    (exists (?O)
      (and
        (instance ?O MilitaryOrganization)
        (uses ?A ?O)))))

(subclass MilitaryAircraft Aircraft)
(subclass MilitaryAircraft MilitaryVehicle)
(documentation MilitaryAircraft EnglishLanguage "Any &%Aircraft which is made for a 
&%MilitaryOrganization.  This includes fighters, &%Bombers, attack 
helicopters, etc.")

(subclass Fighter MilitaryAircraft)
(documentation Fighter EnglishLanguage "Any high-speed &%MilitaryAircraft whose purpose is 
to destroy enemy &%MilitaryAircraft.")
(=>
  (instance ?F Fighter)
  (hasPurpose ?F 
    (exists (?A ?MA)
      (and
        (instance ?A Attack)
        (instrument ?A ?F)
        (patient ?A ?MA)
        (instance ?MA MilitaryAircraft)))))

(subclass Bomber MilitaryAircraft)
(documentation Bomber EnglishLanguage "Any &%MilitaryAircraft whose purpose is to deliver 
&%ExplosiveDevices.")
(=>
  (instance ?B Bomber)
  (hasPurpose ?B 
    (exists (?T ?D)
      (and
        (instance ?T Transportation)
        (instrument ?T ?B)
        (patient ?T ?D)
        (instance ?D ExplosiveDevice)))))

(subclass MilitaryShip Ship)
(subclass MilitaryShip MilitaryWaterVehicle)
(documentation MilitaryShip EnglishLanguage "Any &%Ship which is made for a &%MilitaryOrganization.  
This includes aircraft carriers, destroyers, etc.")

(subclass ExplosiveDevice Device)
(documentation ExplosiveDevice EnglishLanguage "A &%Device whose purpose is to explode.  Note 
that &%ExplosiveDevice is not a subclass of &%Weapon, since explosives can be 
used in demolition work and in fireworks displays, for example.")
(=>
  (instance ?ED ExplosiveDevice)
  (hasPurpose ?ED 
    (exists (?E ?D)
      (and
        (instance ?E Explosion)
        (instrument ?E ?ED)
        (causes ?E ?D)
        (instance ?D Damaging)))))

(subclass ExplosiveMine ExplosiveDevice)
(subclass ExplosiveMine Weapon)
(documentation ExplosiveMine EnglishLanguage "An &%ExplosiveDevice which is designed to 
explode when there is movement over it, e.g. by a person on foot, by a 
&%RoadVehicle, etc.")

(subclass ExplosiveSubstance Substance)
(documentation ExplosiveSubstance EnglishLanguage "Any &%Substance which is capable of exploding.")
(=>
  (instance ?S ExplosiveSubstance)
  (capability Explosion instrument ?S))

(=>
  (instance ?D ExplosiveDevice)
  (exists (?S)
    (and
      (instance ?S ExplosiveSubstance)
      (part ?S ?D))))

(subclass LandTransportation Transportation)
(documentation LandTransportation EnglishLanguage "Any instance of &%Transportation where the 
&%instrument is a &%LandVehicle.")

(=>
   (instance ?LAND LandTransportation)
   (exists (?CRAFT ?AREA)
      (and
         (instance ?CRAFT LandVehicle)
         (instance ?AREA LandArea)
         (instrument ?LAND ?CRAFT)
         (located ?CRAFT ?AREA))))

(=>
   (instance ?VEHICLE LandVehicle)
   (capability LandTransportation instrument ?VEHICLE))

(=>
  (and
    (instance ?T LandTransportation)
    (path ?T ?P))
  (instance ?P LandTransitway))

(subclass RoadTransportationSystem TransitSystem)
(documentation RoadTransportationSystem EnglishLanguage "&%RoadTransportationSystem 
is the subclass of &%TransportationSystems whose routes are &%Roadways.")

(=>
  (and
    (instance ?S RoadTransportationSystem)
    (routeInSystem ?P ?S))
  (instance ?P Roadway))

(subclass SubwaySystem RailTransportationSystem)
(documentation SubwaySystem EnglishLanguage "Any &%RailTransportationSystem that runs 
exclusively through &%Tunnels.")

(=>
  (and
    (instance ?S SubwaySystem)
    (routeInSystem ?P ?S))
  (instance ?P Tunnel))

(subclass Driveway Roadway)
(documentation Driveway EnglishLanguage "A small, private &%Roadway that is used for parking 
&%Automobiles or for connecting a &%Garage to a public &%Roadway.")

(subclass LandVehicle Vehicle)
(documentation LandVehicle EnglishLanguage 
"&%LandVehicle is the class of &%TransportationDevices that travel on 
land.  The two main types of &%LandVehicle are &%RoadVehicle and 
&%RailVehicle. Note that this includes vehicles which travel on any solid
surface, including a frozen body of water or snow.")
(=>
  (instance ?X LandVehicle)
  (hasPurpose ?X
    (exists (?EV ?SURF)
      (and
        (surface ?SURF PlanetEarth)
        (instance ?EV Transportation)
        (holdsDuring
          (WhenFn ?EV)
          (meetsSpatially ?X ?SURF))))))

(subclass SelfPoweredRoadVehicle RoadVehicle)
(subclass SelfPoweredRoadVehicle PoweredVehicle)
(documentation SelfPoweredRoadVehicle EnglishLanguage "&%SelfPoweredRoadVehicle is the 
class of &%RoadVehicles that are also &%PoweredVehicles.  
&%SelfPoweredRoadVehicle covers motorcycles, semi-trailers, RVs, etc., 
as well as Automobiles.  This class includes vehicles powered by 
electricity, gasoline, diesel, and other fuels.")

(subclass Automobile SelfPoweredRoadVehicle)
(subclass Automobile PassengerVehicle)
(documentation Automobile EnglishLanguage "Automobile is a subclass of 
&%SelfPoweredRoadVehicles including passenger cars, family vans, light 
trucks, and sport utility vehicles.  In general, this class covers 
four-wheeled passenger road vehicles.")

(=>
  (instance ?A Automobile)
  (equipmentCount ?A VehicleWheel 4))

(=>
  (instance ?A Automobile)
  (equipmentCount ?A Axle 2))

(subclass Taxicab Automobile)
(documentation Taxicab EnglishLanguage "An &%Automobile which is used to transport people 
on short trips in exchange for a fare.")

(subclass Truck SelfPoweredRoadVehicle)
(subclass Truck CargoVehicle)
(documentation Truck EnglishLanguage "Any &%Vehicle that is intended to carry substantial
amounts of cargo, in addition to passengers.  This includes 18-wheelers,
pickup trucks, tanker trucks etc.  Consumer vehicles with trunks or enclosed
rear areas that can carry cargo are not defined as trucks. Truck is the subclass of &%RoadVehicles that 
are designed primarily for transporting various kinds of non-passenger 
loads.  Truck is the class of single-bodied trucks or &%TruckTractors.  
Note: TractorTrailer combinations are a distinct class.")

(=>
  (instance ?T Truck)
  (exists (?L)
    (and
      (subclass ?L Object)
      (cargoType ?T ?L))))

(subclass Wagon LandVehicle)
(documentation Wagon EnglishLanguage "A &%Landcraft that is not self-propelled, but must be 
pulled by either an &%Animal or a self-propelled &%Vehicle to move along the 
ground.")

(=>
  (and
    (instance ?W Wagon)
    (instance ?T Transportation)
    (instrument ?T ?W))
  (exists (?P)
    (and
      (instance ?T Pulling)
      (agent ?T ?P)
      (patient ?T ?W)
      (or
        (instance ?P DomesticAnimal)
        (instance ?P Vehicle)))))

(subclass Motorcycle SelfPoweredRoadVehicle)
(documentation Motorcycle EnglishLanguage "Motorcycle is the subclass of &%RoadVehicles 
that have two wheels one behind the other in the frame, upon which the rider 
sits on a seat above the engine.")

(=>
  (instance ?M Motorcycle)
  (equipmentCount ?M VehicleWheel 2))

(subclass Bus SelfPoweredRoadVehicle)
(documentation Bus EnglishLanguage "Bus is the subclass of &%SelfPoweredRoadVehicles 
that can transport large numbers of passengers (i.e., dozens) at one 
time. It can be distinguished from a van which is
designed to carry less than a dozen people.")
(=>
  (instance ?B Bus)
  (hasPurpose ?B
    (exists (?C ?T ?N)
      (and
        (instance ?C Collection)
        (instance ?T Transportation)
        (memberCount ?C ?N)
        (greaterThan ?N 11)
        (=>
          (member ?P ?C)
          (patient ?T ?P))))))

(subclass MotorizedRailwayCar RollingStock)
(subclass MotorizedRailwayCar PoweredVehicle)
(documentation MotorizedRailwayCar EnglishLanguage "&%MotorizedRailwayCar is the subclass 
of railway cars that carry their own power source.")

(subclass Streetcar ElectrifiedRailwayCar)
(documentation Streetcar EnglishLanguage "Streetcar is the subclass of 
&%ElectrifiedRailwayCars that run on tracks laid into, along, or 
beside city &%Streets.")

(subclass StageCoach Wagon)
(documentation StageCoach EnglishLanguage "A &%Wagon that is pulled by &%Horses and whose purpose 
was to transport Humans and their luggage from one &%City to the next, especially 
in areas which did not have an established transportation system, e.g. the old west.")
   
(subclass WaterTransportation Transportation)
(documentation WaterTransportation EnglishLanguage "Any instance of &%Transportation where 
the &%instrument is a &%WaterVehicle.")

(=>
   (instance ?TRANSPORT WaterTransportation)
   (exists (?CRAFT ?AREA)
      (and
         (instance ?CRAFT WaterVehicle)
         (instance ?AREA WaterArea)
         (instrument ?TRANSPORT ?CRAFT)
         (located ?TRANSPORT ?AREA))))

(=>
   (instance ?CRAFT WaterVehicle)
   (capability WaterTransportation instrument ?CRAFT))

(=>
  (and
    (instance ?T WaterTransportation)
    (path ?T ?P))
  (instance ?P Waterway))

(subclass Rowing WaterTransportation)
(documentation Rowing EnglishLanguage "Any instance of &%WaterTransportation where the 
instrument is an &%Oar that is manually powered.")

(=>
  (instance ?R Rowing)
  (exists (?O)
    (and
      (instance ?O Oar)
      (instrument ?R ?O))))

(subclass BoatDeck Artifact)
(documentation BoatDeck EnglishLanguage "One of the levels of a &%WaterVehicle, e.g. the 
upper deck and lower deck of small boats with a cabin.")

(=>
  (instance ?D BoatDeck)
  (exists (?B)
    (and
      (instance ?B WaterVehicle)
      (part ?D ?B))))

(subclass SpaceTransportation Transportation)
(documentation SpaceTransportation EnglishLanguage "Any instance of &%Transportation where the 
&%instrument is a &%Spacecraft and which is through a &%SpaceRegion.")

(=>
   (instance ?TRANSPORT SpaceTransportation)
   (exists (?CRAFT ?REGION)
      (and
         (instance ?CRAFT Spacecraft)
         (instance ?REGION SpaceRegion)
         (instrument ?TRANSPORT ?CRAFT)
         (located ?TRANSPORT ?REGION))))

(subclass Spacecraft Vehicle)
(documentation Spacecraft EnglishLanguage "Any &%Vehicle which is capable of 
&%SpaceTransportation.")

(=>
   (instance ?CRAFT Spacecraft)
   (capability SpaceTransportation instrument ?CRAFT))

(subclass Rocket Spacecraft)
(documentation Rocket EnglishLanguage "A &%Spacecraft which has the shape of a cylinder 
with a cone on top and which is powered by a jet engine.")

(subclass Missile Projectile)
(subclass Missile SelfPoweredDevice)
(documentation Missile EnglishLanguage "A &%Projectile which is propelled by a rocket and 
contains a &%Bomb.") 

(=>
  (instance ?M Missile)
  (exists (?B)
    (and
      (instance ?B Bomb)
      (part ?B ?M))))

(subclass BallisticMissile Missile)
(documentation BallisticMissile EnglishLanguage "A &%Missile which is guided for the first stage 
of its flight but then falls to its target for the second stage.")

(subclass Installing Putting)
(documentation Installing EnglishLanguage "&%Putting a &%Device in a location and configuring 
the &%Device so that it can be used as intended after the installation.")

(=>
   (and
      (instance ?INSTALL Installing)
      (patient ?INSTALL ?DEVICE))
   (instance ?DEVICE Device))

(=>
   (and
      (instance ?INSTALL Installing)
      (patient ?INSTALL ?DEVICE)
      (hasPurpose ?DEVICE ?PURPOSE))
   (holdsDuring (EndFn (WhenFn ?INSTALL)) (truth ?PURPOSE True)))

(subclass Burying Putting)
(subclass Burying Digging)
(documentation Burying EnglishLanguage "Putting something in the ground and then covering 
it with &%Soil.")
(=>
  (instance ?B Burying)
  (exists (?C ?S)
    (and
      (instance ?C Covering)
      (instrument ?C ?S)
      (instance ?S Soil)
      (subProcess ?C ?B))))

(subclass Digging IntentionalProcess)
(subclass Digging SurfaceChange)
(documentation Digging EnglishLanguage "Any &%Process of removing or turning over the &%Soil.")
(=>
  (instance ?D Digging)
  (exists (?S)
    (and
      (patient ?D ?S)
      (instance ?S Soil))))

(subclass Tilling Digging)
(documentation Tilling EnglishLanguage "Any &%Process of &%Digging, e.g. breaking and 
turning over &%Soil that facilitates &%Agriculture.")
(subclass Tilling IntentionalProcess)
(subclass Tilling SurfaceChange)

(=>
   (and
      (instance ?TILL Tilling)
      (patient ?TILL ?SOIL))
   (instance ?SOIL Soil))

(=>
  (instance ?T Tilling)
  (exists (?A)
    (and
      (instance ?A Agriculture)
      (subProcess ?T ?A))))

(subclass Entombing Putting)
(documentation Entombing EnglishLanguage "More commonly known as burying, this is the class 
of processes of putting a &%HumanCorpse into a Tomb.  Note that this class 
is not a subclass of &%Burying, since some &%Tombs are not covered with &%Soil, 
e.g. those in a mausoleum.")
(=>
  (and
    (instance ?E Entombing)
    (patient ?E ?C))
  (instance ?C HumanCorpse))

(=>
  (and
    (instance ?E Entombing)
    (destination ?E ?T))
  (instance ?T Tomb))

(subclass MovingResidence Transfer)
(documentation MovingResidence EnglishLanguage "The process of changing one's residence, i.e. 
moving one's belongs to a new &%home.")

(=>
   (and
      (instance ?MOVE MovingResidence)
      (agent ?MOVE ?AGENT))
   (exists (?HOME1 ?HOME2)
      (and
         (holdsDuring (BeginFn (WhenFn ?MOVE)) (home ?AGENT ?HOME1))
         (holdsDuring (EndFn (WhenFn ?MOVE)) (home ?AGENT ?HOME2))
         (not (equal ?HOME1 ?HOME2)))))

(subclass Harvesting Removing)
(documentation Harvesting EnglishLanguage "Gathering the plant results of &%Agriculture.")

(=>
  (instance ?H Harvesting)
  (exists (?A)
    (and
      (instance ?A Agriculture) 
      (subProcess ?H ?A))))

(=>
  (and
    (instance ?H Harvesting)
    (result ?H ?C))
  (or
    (instance ?C PlantAnatomicalStructure)
    (instance ?C PlantSubstance)))

(subclass Drilling IntentionalProcess)
(subclass Drilling SurfaceChange)
(documentation Drilling EnglishLanguage "Any &%Process of producing a &%hole in a 
&%SelfConnectedObject which involves rotating a long, thin bit.")

(=>
   (and
      (instance ?DRILL Drilling)
      (patient ?DRILL ?OBJ))
   (exists (?HOLE)
      (and
         (holdsDuring (BeginFn (WhenFn ?DRILL)) (not (hole ?HOLE ?OBJ)))
         (holdsDuring (EndFn (WhenFn ?DRILL)) (hole ?HOLE ?OBJ)))))

(subclass Drill CuttingDevice)
(documentation Drill EnglishLanguage "A &%Device that has the purpose of creating a 
&%Hole.  This covers manual drills as well as electric or pneumatic
drills.")

(=>
  (instance ?D Drill)
  (hasPurpose ?D
    (exists (?E ?H)
      (and 
        (instance ?E Cutting)
        (instance ?H Hole)
        (result ?E ?H)
        (instrument ?E ?D)))))

(subclass Memorizing Learning)
(documentation Memorizing EnglishLanguage "The process of committing a &%Text to memory.")

(=>
  (and
    (instance ?M Memorizing)
    (patient ?M ?T))
  (instance ?T Text))

(subclass Imagining PsychologicalProcess)
(documentation Imagining EnglishLanguage "Forming a mental picture of something which 
is not present.")

(subclass Dreaming Imagining)
(disjoint Dreaming IntentionalProcess)
(documentation Dreaming EnglishLanguage "A &%Process of producing metal images which occurs 
while one is &%Asleep.")

(=>
   (and
      (instance ?DREAM Dreaming)
      (experiencer ?DREAM ?AGENT))
   (holdsDuring (WhenFn ?DREAM) (attribute ?AGENT Asleep)))

(subclass Frightening PsychologicalProcess)
(documentation Frightening EnglishLanguage "Any &%PsychologicalProcess where the &%patient 
comes to feel &%Anxiety.")

(=>
   (and
      (instance ?FRIGHTEN Frightening)
      (experiencer ?FRIGHTEN ?AGENT))
   (and
      (holdsDuring (BeginFn (WhenFn ?FRIGHTEN)) (not (attribute ?AGENT Anxiety)))
      (holdsDuring (EndFn (WhenFn ?FRIGHTEN)) (attribute ?AGENT Anxiety))))

(subclass Execution Killing)
(documentation Execution EnglishLanguage "&%Killing of a &%Human by a &%Government for 
the commission of a &%CriminalAction.")

(=>
  (instance ?E Execution)
  (exists (?G)
    (and
      (instance ?G Government)
      (agent ?E ?G))))

(=>
  (instance ?E Execution)
  (exists (?P)
    (and
      (patient ?E ?P)
      (instance ?P Human))))

(subclass Suicide Killing)
(documentation Suicide EnglishLanguage "Any instance of &%Killing where the &%agent and 
the &%experiencer are identical.")

(=>
  (instance ?S Suicide)
  (exists (?A)
    (and
      (agent ?S ?A)
      (experiencer ?S ?A))))

(subclass Trespassing CriminalAction)
(subclass Trespassing Translocation)
(documentation Trespassing EnglishLanguage "Entering property that does not belong to one and without 
the permission of the owner of the property.")

(=>
  (and
    (instance ?T Trespassing)
    (patient ?T ?P))
  (instance ?P RealEstate))

(=>
  (and
    (instance ?T Trespassing)
    (agent ?T ?H)
    (patient ?T ?P))
  (and
    (not 
      (possesses ?H ?P))
    (modalAttribute 
      (located ?H ?P) 
      Illegal)))

(subclass Murder Killing)
(subclass Murder CriminalAction)
(documentation Murder EnglishLanguage "Impermissible &%Killing of a &%Human.")

(=>
   (instance ?MURDER Murder)
   (exists (?PERSON)
      (and
         (patient ?MURDER ?PERSON)
         (instance ?PERSON Human))))

(subclass Suffocating Killing)
(documentation Suffocating EnglishLanguage "&%Killing someone by asphyxiation, i.e. by 
depriving them of &%Oxygen.")

(=>
  (instance ?S Suffocating)
  (holdsDuring (EndFn (WhenFn ?H))
    (and
      (experiencer ?S ?P)
      (not
        (exists (?B)
	  (and
	    (instance ?B Breathing)
	    (experiencer ?B ?P)))))))

(subclass Hanging Suffocating)
(documentation Hanging EnglishLanguage "Suffocating someone by suspending him/her from 
a rope wound around the neck until asphyxiation occurs.")
(=>
  (instance ?H Hanging)
  (exists (?S ?N ?P)
    (and
      (instance ?P Human)
      (instance ?S String)
      (instance ?N Neck)
      (experiencer ?H ?P)
      (properPart ?N ?P)
      (instrument ?H ?S)
      (holdsDuring (WhenFn ?H)
        (meetsSpatially ?S ?N)))))

(subclass Starving Killing)
(documentation Starving EnglishLanguage "&%Killing someone by depriving them of &%Food.")

(subclass OrchestralConducting Guiding)
(documentation OrchestralConducting EnglishLanguage "The &%Process of directing an orchestra.")

(=>
   (instance ?CONDUCT OrchestralConducting)
   (exists (?MUSIC)
      (and
         (instance ?MUSIC Music)
         (result ?CONDUCT ?MUSIC))))

(=>
   (and
      (instance ?CONDUCT OrchestralConducting)
      (patient ?CONDUCT ?ORCHESTRA))
   (instance ?ORCHESTRA Orchestra))


(subclass MusicalGroup GroupOfPeople)
(documentation MusicalGroup EnglishLanguage "A &%GroupOfPeople that create &%Music 
together.")

(=>
   (instance ?ORCHESTRA Orchestra)
   (hasPurpose ?ORCHESTRA (exists (?MUSIC)
                             (and
                                (instance ?MUSIC Music)
                                (agent ?MUSIC ?ORCHESTRA)))))

(subclass Orchestra MusicalGroup)
(documentation Orchestra EnglishLanguage "A &%GroupOfPeople that create &%InstrumentalMusic 
together.")

(=>
   (instance ?ORCHESTRA Orchestra)
   (hasPurpose ?ORCHESTRA (exists (?MUSIC)
                             (and
                                (instance ?MUSIC InstrumentalMusic)
                                (agent ?MUSIC ?ORCHESTRA)))))

(subclass DramaticDirecting Guiding)
(documentation DramaticDirecting EnglishLanguage "The process of directing a &%DramaticActing 
in a &%MotionPicture or the &%Performance of a &%DramaticPlay.")

(=>
   (and
      (instance ?DIRECT DramaticDirecting)
      (patient ?DIRECT ?ACT))
   (instance ?ACT DramaticActing))

(subclass Sharing ChangeOfPossession)
(documentation Sharing EnglishLanguage "The subclass of &%ChangeOfPossession where a 
&%properPart of the &%patient is given by the &%agent or the &%destination.")

(=>
   (and
      (instance ?SHARE Sharing)
      (agent ?SHARE ?AGENT1)
      (destination ?SHARE ?AGENT2)
      (patient ?SHARE ?OBJ))
   (exists (?GIVE ?PART)
      (and
         (instance ?GIVE Giving)
         (subProcess ?GIVE ?SHARE)
         (patient ?GIVE ?PART)
         (properPart ?PART ?OBJ)
         (agent ?GIVE ?AGENT1)
         (destination ?GIVE ?AGENT2))))

(subclass Stealing UnilateralGetting)
(subclass Stealing CriminalAction)
(documentation Stealing EnglishLanguage "Any &%UnilateralGetting which is not permitted by the 
&%origin of the &%UnilateralGetting.  These cases of &%UnilateralGetting are 
distinguished from ones where the &%destination is the subject of charity or 
other forms of benefaction.")

(=>
   (and
      (instance ?STEAL Stealing)
      (destination ?STEAL ?AGENT)
      (origin ?STEAL ?VICTIM)
      (instance ?VICTIM CognitiveAgent))
   (not (confersRight (destination ?STEAL ?AGENT) ?VICTIM ?AGENT)))

(subclass Robbing Stealing)
(documentation Robbing EnglishLanguage "Any instance of &%Stealing which involves the threat 
of the use of force.")

(=>
  (and
    (instance ?R Robbing)
    (agent ?R ?P)
    (patient ?R ?P2))
  (exists (?T)
    (and
      (instance ?T Threatening)
      (subProcess ?T ?R)
      (patient ?T 
        (exists (?A)
          (and
            (instance ?A Attack)
            (agent ?A ?P)
            (patient ?A ?P2))))))) 

(subclass Inheriting UnilateralGetting)
(documentation Inheriting EnglishLanguage "Any &%UnilateralGetting where the &%agent 
receives some part of the property of a person upon the death of the 
person.")

(=>
   (and
      (instance ?INHERIT Inheriting)
      (agent ?INHERIT ?HEIR)
      (origin ?INHERIT ?PERSON)
      (patient ?INHERIT ?PROPERTY))
   (exists (?DEATH)
      (and
         (instance ?DEATH Death)
         (experiencer ?DEATH ?PERSON)
         (earlier (WhenFn ?DEATH) (WhenFn ?INHERIT))
         (holdsDuring (ImmediatePastFn (WhenFn ?DEATH)) (possesses ?PERSON ?PROPERTY))
         (confersRight (possesses ?HEIR ?PROPERTY) ?PERSON ?HEIR))))

(subclass Bequeathing UnilateralGiving)
(documentation Bequeathing EnglishLanguage "Any &%UnilateralGiving where the agent 
wills some part of his/her property to someone else upon his/her death.")
(=>
  (and
    (instance ?B Bequeathing)
    (agent ?B ?P)
    (destination ?B ?H)
    (patient ?I ?PROP))
  (exists (?D)
    (and
      (instance ?D Death)
      (experiencer ?D ?P)
      (earlier (WhenFn ?D) (WhenFn ?B))
      (holdsDuring 
        (ImmediatePastFn (WhenFn ?D)) 
        (possesses ?P ?PROP))
      (confersNorm ?P 
        (possesses ?H ?PROP) 
        Permission))))

(subclass SeizingProperty UnilateralGetting)
(documentation SeizingProperty EnglishLanguage "Any instance of &%UnilateralGetting which is done by 
a &%Government and which is not permitted by the origin of the &%UnilateralGetting.")

(=>
  (and
    (instance ?S SeizingProperty)
    (destination ?S ?A)
    (origin ?S ?V)
    (instance ?V CognitiveAgent))
  (and
    (instance ?A Government)
    (not 
      (confersNorm ?V 
        (destination ?S ?A) 
	Permission))))

(partition Position FullTimePosition PartTimePosition)

(subclass FullTimePosition Position)
(documentation FullTimePosition EnglishLanguage "Any &%Position where the employee is either 
salaried or paid for at least 40 hour of work per week.")

(subclass PartTimePosition Position)
(documentation PartTimePosition EnglishLanguage "Any &%Position where the employee is not 
salaried and is paid for less than 40 hours of work per week.")

(instance CollegeFreshman CollegeStudentPosition)
(documentation CollegeFreshman EnglishLanguage "The &%Positions of a student at a &%PostSecondarySchool 
who has completed less than one year at the school.")

(=>
  (and
    (attribute ?P CollegeFreshman)
    (occupiesPosition ?H ?P ?ORG))
  (exists (?T)
    (and
      (holdsDuring ?T 
        (student ?H ?ORG))
      (lessThan ?T (MeasureFn 1 YearDuration)))))

(instance CollegeJunior CollegeStudentPosition)
(documentation CollegeJunior EnglishLanguage "The &%Positions of a student at a &%PostSecondarySchool 
who has completed at least two years and less than three years at the school.")

(=>
  (and
    (attribute ?P CollegeJunior)
    (occupiesPosition ?H ?P ?ORG))
  (exists (?T)
    (and
      (holdsDuring ?T 
        (student ?H ?ORG))
      (greaterThanOrEqualTo ?T 
        (MeasureFn 2 YearDuration))
      (lessThan ?T 
        (MeasureFn 3 YearDuration)))))

(instance CollegeSenior CollegeStudentPosition)
(documentation CollegeSenior EnglishLanguage "The &%Positions of a student at a &%PostSecondarySchool 
who has completed at least three years and less than four years at the school.")

(=>
  (and
    (attribute ?P CollegeJunior)
    (occupiesPosition ?H ?P ?ORG))
  (exists (?T)
    (and
      (holdsDuring ?T 
        (student ?H ?ORG))
      (greaterThanOrEqualTo ?T 
        (MeasureFn 3 YearDuration))
      (lessThan ?T 
        (MeasureFn 4 YearDuration)))))

(instance CollegeSophomore CollegeStudentPosition)
(documentation CollegeSophomore EnglishLanguage "The &%Positions of a student at a &%PostSecondarySchool 
who has completed at least one year and less than two years at the school.")

(=>
  (and
    (attribute ?P CollegeSophomore)
    (occupiesPosition ?H ?P ?ORG))
  (exists (?T)
    (and
      (holdsDuring ?T 
        (student ?H ?ORG))
      (greaterThanOrEqualTo ?T 
        (MeasureFn 2 YearDuration))
      (lessThan ?T 
        (MeasureFn 2 YearDuration)))))

(subclass CollegeStudentPosition Position)
(documentation CollegeStudentPosition EnglishLanguage "Any &%Position at a &%PostSecondarySchool 
which is occupied exclusively by students.")

(instance Comedian EntertainmentProfession)
(documentation Comedian EnglishLanguage "&%Positions which involve performing stand-up comedy, which 
is recorded and/or performed in front of live audiences.")

(=>
  (attribute ?P Comedian)
  (exists (?PERF ?L)
    (and
      (instance ?PERF Performance)
      (agent ?PREF ?P)
      (instance ?L Laughing)
      (causes ?PERF ?L))))

(instance Banker SkilledOccupation)
(documentation Banker EnglishLanguage "Any &%SkilledOccupation which involves working in a 
&%Bank-FinancialOrganization.")

(=>
   (and
      (instance ?H Human)
      (occupiesPosition ?H ?B ?O)
      (attribute ?B Banker))
   (instance ?O Bank-FinancialOrganization))

(instance PresidentOfTheUnitedStates Position)
(documentation PresidentOfTheUnitedStates EnglishLanguage "The position of being head of the 
&%UnitedStates.")

(=>
  (attribute ?H PresidentOfTheUnitedStates)
  (leader ?H UnitedStates))

(instance Treasurer SkilledOccupation)
(documentation Treasurer EnglishLanguage "Any &%SkilledOccupation which involves receiving 
and disbursing money.")

(instance Governor Position)
(documentation Governor EnglishLanguage "The head of the &%Government of an &%AmericanState.")

(=>
  (headOfGovernment ?A Governor ?H)
  (instance ?A AmericanState))

(instance LieutenantGovernor Position)
(documentation LieutenantGovernor EnglishLanguage "The second-in-command of some &%Governments 
of &%AmericanStates.")

(instance Mayor Position)
(documentation Mayor EnglishLanguage "The head of the &%Government of a &%City.")

(=>
  (headOfGovernment ?A Mayor ?H)
  (instance ?A City))

(instance Pope Position)
(documentation Pope EnglishLanguage "The &%Position of head of the &%RomanCatholicChurch.")

(=>
  (occupiesPosition ?H Pope RomanCatholicChurch)
  (leader RomanCatholicChurch ?H))

(subclass SalesPosition SkilledOccupation)
(documentation SalesPosition EnglishLanguage "Any &%Position which involves &%Selling 
or trying to sell items.")

(instance ClericalSecretary SkilledOccupation)
(documentation ClericalSecretary EnglishLanguage "The class of &%Positions where the position 
holder is responsible for clerical duties, e.g. typing documents, answering 
phones, keeping schedules, etc.")

(subclass ServicePosition SkilledOccupation)
(documentation ServicePosition EnglishLanguage "Any &%Position which involves working 
as a waiter or servant, either for an &%Organization (e.g. a restaurant) 
or for a person or family.")

(instance Dentist Profession)
(documentation Dentist EnglishLanguage "The &%Profession of being a dentist, i.e. diagnosing 
and treating problems related to the teeth.")

(=>
  (attribute ?P Dentist)
  (exists (?D ?T)
    (and
     (instance ?D DiagnosticProcess)
     (agent ?D ?P)
     (instance ?T Tooth)
     (patient ?D ?T))))

(instance Pharmacist Profession)
(documentation Pharmacist EnglishLanguage "The &%Profession of being a pharmacist, i.e. 
preparing and dispensing &%BiologicallyActiveSubstances.")

(instance Researcher Profession)
(documentation Researcher EnglishLanguage "The &%Profession of being a scientific 
researcher.")

(=>
   (attribute ?P Researcher)
   (exists (?S)
      (and
         (instance ?S Science)
         (hasExpertise ?P ?S))))

(instance Teacher Profession)
(documentation Teacher EnglishLanguage "The &%Profession of being a teacher.")

(=>
  (and
    (instance ?T Teacher)
    (occupiesPosition ?P ?T ?O))
  (teacher ?T ?O))

(=>
  (instance ?T Teacher)
  (hasSkill EducationalProcess ?T))

(subclass EntertainmentProfession SkilledOccupation)
(documentation EntertainmentProfession EnglishLanguage "Positions which involve creating 
content or performances that are intended to entertain.  This class covers 
the &%TheaterProfession, &%FilmMakingProfession, being a comedian, etc.")

(=>
  (and
    (instance ?EP EntertainmentProfession)
    (attribute ?P ?EP))
  (or
    (occupation ?P (OccupationFn Performance))
    (occupation ?P (OccupationFn ContentDevelopment))))

(subclass SportsPosition SkilledOccupation)
(documentation SportsPosition EnglishLanguage "A &%Position which is filled by someone on a 
&%SportsTeam and which represents the role played by the person on the team.")

(=>
  (and
    (occupiesPosition ?H ?P ?O)   
    (instance ?P SportsPosition))
  (instance ?O SportsTeam))

(subclass GovernmentOfficer Profession)
(documentation GovernmentOfficer EnglishLanguage "Any &%Profession where the position occupied 
is within a &%GovernmentOrganization.")

(=>
  (and
    (instance ?O GovernmentOfficer)
    (occupiesPosition ?P ?O ?ORG))
  (instance ?ORG GovernmentOrganization))

(instance Journalist Profession)
(documentation Journalist EnglishLanguage "The &%Profession of being a news reporter, i.e. 
investigating and reporting, in a publication or broadcast program, current 
events.")

(=>
  (attribute ?P Journalist)
  (and
    (occupation ?P (OccupationFn ContentDevelopment))
    (occupation ?P (OccupationFn Disseminating))))

(subclass ModellingPosition Position)
(documentation ModellingPosition EnglishLanguage "Any &%Position which involves posing for an 
artist or photographer.")

(instance Soldier SkilledOccupation)
(documentation Soldier EnglishLanguage "The class of &%SkilledOccupations which involve serving in 
the armed forces of a &%Nation.")

(instance Sailor SkilledOccupation)
(documentation Sailor EnglishLanguage "The class of &%Positions which involve working on a 
ship, whether a merchant ship or a navy ship.")

(subAttribute ShipCaptain Sailor)
(documentation ShipCaptain EnglishLanguage "The class of &%Positions which involve the command 
of a ship.")

(subAttribute ShipOfficer Sailor)
(documentation ShipOfficer EnglishLanguage "The class of &%Sailors which have a position of 
responsibility on a &%Ship.")

(subAttribute ShipMate Sailor)
(documentation ShipMate EnglishLanguage "The class of &%Positions that involve some responsibility 
on a ship and are lower in rank than &%ShipCaptain.")

(instance ConfederateStatesOfAmerica GeopoliticalArea)
(documentation ConfederateStatesOfAmerica EnglishLanguage "The eleven states of the &%UnitedStates 
that tried to secede from the &%UnitedStates.")

(instance UnionStatesOfAmerica GeopoliticalArea)
(documentation UnionStatesOfAmerica EnglishLanguage "The states of the &%UnitedStates that the 
&%ConfederateStatesOfAmerica seceded from.")

(subAttribute MilitaryPrivate Soldier)
(documentation MilitaryPrivate EnglishLanguage "An enlisted &%Soldier of the lowest rank.")

(subAttribute MilitaryOfficer Soldier)
(documentation MilitaryOfficer EnglishLanguage "The class of &%Soldiers who have authority or command.")

(=>
   (instance ?OFFICER MilitaryOfficer)
   (exists (?MANAGE ?PATIENT)
      (and
         (instance ?MANAGE Managing)
         (agent ?MANAGE ?OFFICER)
         (patient ?MANAGE ?PATIENT)
         (or
            (instance ?PATIENT MilitaryOrganization)
            (instance ?PATIENT Soldier)))))

(subAttribute MajorGeneral MilitaryGeneral)
(documentation MajorGeneral EnglishLanguage "A &%MilitaryGeneral that ranks above a 
&%BrigadierGeneral.")

(subAttribute BrigadierGeneral MilitaryGeneral)
(documentation BrigadierGeneral EnglishLanguage "A &%MilitaryGeneral that ranks below 
a &%MajorGeneral.")

(subAttribute Lieutenant MilitaryOfficer)
(documentation Lieutenant EnglishLanguage "A commissioned &%MilitaryOfficer.")

(subAttribute CaptainOfficer MilitaryOfficer)
(documentation CaptainOfficer EnglishLanguage "A commissioned &%MilitaryOfficer who ranks 
above a &%Lieutenant but below a &%MajorOfficer.")

(subAttribute Colonel MilitaryOfficer)
(documentation Colonel EnglishLanguage "A commissioned &%MilitaryOfficer who ranks above 
a lieutenant colonel and below a brigadier general.")

(subAttribute Corporal MilitaryOfficer)
(documentation Corporal EnglishLanguage "A noncomissioned &%MilitaryOfficer.")

(subAttribute Sergeant MilitaryOfficer)
(documentation Sergeant EnglishLanguage "A noncomissioned &%MilitaryOfficer.")

(instance CenturyDuration UnitOfDuration)
(documentation CenturyDuration EnglishLanguage "The &%TimeDuration of 100 years.")

(=>
   (equal ?NUMBER (MultiplicationFn 1 ?NUMBER))
   (equal
        (MeasureFn ?NUMBER CenturyDuration)
        (MeasureFn (MultiplicationFn ?NUMBER 100) YearDuration)))

(instance DecadeDuration UnitOfDuration)
(documentation DecadeDuration EnglishLanguage "The &%TimeDuration of 10 years.")

(=>
   (equal ?NUMBER (MultiplicationFn 1 ?NUMBER))
   (equal
        (MeasureFn ?NUMBER DecadeDuration)
        (MeasureFn (MultiplicationFn ?NUMBER 10) YearDuration)))

(instance YardLength UnitOfLength)
(documentation YardLength EnglishLanguage "English unit of length, equal to 3 &%FeetLength.")

(=>
  (equal ?NUMBER (MultiplicationFn 1 ?NUMBER))
  (equal
    (MeasureFn ?NUMBER YardLength)
    (MeasureFn (MultiplicationFn 3 ?NUMBER) FootLength)))

(subclass NightTime TimeInterval)
(documentation NightTime EnglishLanguage "The class of &%TimeIntervals that begin at &%Sunset
and end at &%Sunrise.")

(=>
   (instance ?NIGHT NightTime)
   (exists (?DAY1 ?DAY2)
      (and
         (instance ?DAY1 DayTime)
         (instance ?DAY2 DayTime)
         (meetsTemporally ?NIGHT ?DAY1)
         (meetsTemporally ?DAY2 ?NIGHT))))

(=>
   (instance ?NIGHT NightTime)
   (exists (?RISE ?SET)
      (and
         (instance ?RISE Sunrise)
         (instance ?SET Sunset)
         (starts ?SET ?NIGHT)
         (finishes ?RISE ?NIGHT))))

(subclass DayTime TimeInterval)
(documentation DayTime EnglishLanguage "The class of &%TimeIntervals that begin at &%Sunrise 
and end at &%Sunset.")

(=>
   (instance ?DAY DayTime)
   (exists (?NIGHT1 ?NIGHT2)
      (and
         (instance ?NIGHT1 NightTime)
         (instance ?NIGHT2 NightTime)
         (meetsTemporally ?DAY ?NIGHT1)
         (meetsTemporally ?NIGHT2 ?DAY))))

(=>
   (instance ?DAY DayTime)
   (exists (?RISE ?SET)
      (and
         (instance ?RISE Sunrise)
         (instance ?SET Sunset)
         (starts ?RISE ?DAY)
         (finishes ?SET ?DAY))))

(subclass Morning DayTime)
(documentation Morning EnglishLanguage "The class of &%TimeIntervals that begin at &%Sunrise 
and end at noon.")

(=>
   (instance ?MORNING Morning)
   (exists (?HOUR)
      (and
         (instance ?HOUR (HourFn 12 ?DAY))
         (finishes ?HOUR ?MORNING))))

(subclass AnteMeridiem TimeInterval)
(documentation AnteMeridiem EnglishLanguage "The class of &%TimeIntervals that begin at midnight 
and end at noon.")
(=>
   (instance ?AM AnteMeridiem)
   (exists (?D ?HOUR1 ?HOUR2)
      (and
         (instance ?HOUR1 (HourFn 24 ?D))
         (starts ?HOUR1 ?AM)
  	   (instance ?HOUR2 (HourFn 12 ?D))
         (finishes ?HOUR2 ?AM))))

(subclass PostMeridiem TimeInterval)
(documentation PostMeridiem EnglishLanguage "The class of &%TimeIntervals that begin at noon and 
end at midnight.")
(=>
   (instance ?PM PostMeridiem)
   (exists (?D ?HOUR1 ?HOUR2)
      (and
         (instance ?HOUR1 (HourFn 12 ?D))
         (starts ?HOUR1 ?PM)
  	   (instance ?HOUR2 (HourFn 24 ?D))
         (finishes ?HOUR2 ?PM))))

(subclass Afternoon DayTime)
(documentation Afternoon EnglishLanguage "The class of &%TimeIntervals that begin at noon and 
end at &%Sunset.")

(=>
   (instance ?AFTERNOON Afternoon)
   (exists (?HOUR)
      (and
         (instance ?HOUR (HourFn 12 ?DAY))
         (starts ?HOUR ?AFTERNOON))))

(subclass Sunrise TimeInterval)
(documentation Sunrise EnglishLanguage "The &%TimeInterval of each &%Day when the sun is rising 
and is partially overlapped by the horizon line.")

(subclass Sunset TimeInterval)
(documentation Sunset EnglishLanguage "The &%TimeInterval of each &%Day when the sun is setting 
and is partially overlapped by the horizon line.") 

(subclass Weekend TimeInterval)
(documentation Weekend EnglishLanguage "Any &%Saturday and &%Sunday which are contiguous.")

(=>
   (instance ?WEEKEND Weekend)
   (exists (?SATURDAY ?SUNDAY)
      (and
         (instance ?SATURDAY Saturday)
         (instance ?SUNDAY Sunday)
         (starts ?SATURDAY ?WEEKEND)
         (finishes ?SUNDAY ?WEEKEND)
         (meetsTemporally ?SATURDAY ?SUNDAY))))

(subclass SeasonOfYear TimeInterval)
(partition SeasonOfYear WinterSeason SpringSeason SummerSeason FallSeason)
(documentation SeasonOfYear EnglishLanguage "&%SeasonOfYear is the class of four 
seasons correlated with the calendar &%Year and associated with 
changes in the length of daylight and with overall temperature 
changes. Depending upon the &%GeographicArea, a &%SeasonOfYear 
may also be associated with weather patterns (e.g., rainy, dry, 
windy). The characteristics of seasons (cold vs. hot temperatures, 
long vs. short days) are reversed from the &%NorthernHemisphere 
to the &%SouthernHemisphere.")

(subclass WinterSeason SeasonOfYear)
(documentation WinterSeason EnglishLanguage "The &%SeasonOfYear that begins at the winter 
solstice and ends at the spring equinox.")

(=>
   (instance ?WINTER WinterSeason)
   (exists (?SPRING)
      (and
         (instance ?SPRING SpringSeason)
         (meetsTemporally ?WINTER ?SPRING))))

(=>
   (instance ?WINTER WinterSeason)
   (exists (?AUTUMN)
      (and
         (instance ?AUTUMN FallSeason)
         (meetsTemporally ?AUTUMN ?WINTER))))

(subclass SpringSeason SeasonOfYear)
(documentation SpringSeason EnglishLanguage "The &%SeasonOfYear that begins at the spring 
equinox and ends at the summer solstice. &%SpringSeason is the class of 
&%TimeIntervals associated with the calendar months of &%March 
through &%May.")

(=>
   (instance ?SPRING SpringSeason)
   (exists (?SUMMER)
      (and
         (instance ?SUMMER SummerSeason)
         (meetsTemporally ?SPRING ?SUMMER))))

(=>
   (instance ?SPRING SpringSeason)
   (exists (?WINTER)
      (and
         (instance ?WINTER WinterSeason)
         (meetsTemporally ?WINTER ?SPRING))))

(subclass SummerSeason SeasonOfYear)
(documentation SummerSeason EnglishLanguage "The &%SeasonOfYear that begins at the summer 
solstice and ends at the autumnal equinox.")

(=>
   (instance ?SUMMER SummerSeason)
   (exists (?SPRING)
      (and
         (instance ?SPRING SpringSeason)
         (meetsTemporally ?SPRING ?SUMMER))))

(=>
   (instance ?SUMMER SummerSeason)
   (exists (?AUTUMN)
      (and
         (instance ?AUTUMN FallSeason)
         (meetsTemporally ?SUMMER ?AUTUMN))))

(subclass FallSeason SeasonOfYear)
(documentation FallSeason EnglishLanguage "The &%SeasonOfYear that begins at the autumnal 
equinox and ends at the winter solstice.")

(=>
   (instance ?AUTUMN FallSeason)
   (exists (?SUMMER)
      (and
         (instance ?SUMMER SummerSeason)
         (meetsTemporally ?SUMMER ?AUTUMN))))

(=>
   (instance ?AUTUMN FallSeason)
   (exists (?WINTER)
      (and
         (instance ?WINTER WinterSeason)
         (meetsTemporally ?AUTUMN ?WINTER))))

(instance Upstairs PositionalAttribute)
(documentation Upstairs EnglishLanguage "A &%PositionalAttribute to indicate that one thing is 
one or more floors above a second thing in the same building.")

(=>
   (orientation ?OBJ1 ?OBJ2 Upstairs)
   (exists (?LEVEL1 ?LEVEL2 ?BUILDING)
      (and
         (instance ?LEVEL1 BuildingLevel)
         (instance ?LEVEL2 BuildingLevel)
         (instance ?BUILDING Building)
         (part ?LEVEL1 ?BUILDING)
         (part ?LEVEL2 ?BUILDING)
         (located ?OBJ1 ?LEVEL1)
         (located ?OBJ2 ?LEVEL2)
         (orientation ?LEVEL1 ?LEVEL2 Above))))

(instance Downstairs PositionalAttribute)
(documentation Downstairs EnglishLanguage "A &%PositionalAttribute to indicate that one thing is 
one or more floors below a second thing in the same building.")

(=>
   (orientation ?OBJ1 ?OBJ2 Downstairs)
   (exists (?LEVEL1 ?LEVEL2 ?BUILDING)
      (and
         (instance ?LEVEL1 BuildingLevel)
         (instance ?LEVEL2 BuildingLevel)
         (instance ?BUILDING Building)
         (part ?LEVEL1 ?BUILDING)
         (part ?LEVEL2 ?BUILDING)
         (located ?OBJ1 ?LEVEL1)
         (located ?OBJ2 ?LEVEL2)
         (orientation ?LEVEL1 ?LEVEL2 Below))))

(subclass LinguisticAttribute InternalAttribute)
(documentation LinguisticAttribute EnglishLanguage "Any &%Attribute that is 
expressed by a &%Language or class of &%Languages.")

(instance Happiness EmotionalState)
(documentation Happiness EnglishLanguage "The state of being happy, experiencing pleasure, 
joy or contentment.  Note that this &%Attribute covers both active enjoyment, 
as well as the emotional state of simply being free from anxiety or fear.")

(=>
  (attribute ?PERSON Happiness)
  (not 
    (exists (?PROP)
      (fears ?PERSON ?PROP))))

(subAttribute Satisfaction Happiness)
(documentation Satisfaction EnglishLanguage "The state of being happy about a state of
affairs that occurred in the past.  In cases where that state of affairs 
is the product of one's own doing, this is known as pride.")

(subAttribute Tranquility Happiness)
(contraryAttribute Tranquility Anxiety)
(documentation Tranquility EnglishLanguage "The state of being free from &%Anxiety.")

(instance Unhappiness EmotionalState) 
(contraryAttribute Unhappiness Happiness)
(documentation Unhappiness EnglishLanguage "The state of being unhappy, experiencing pain, 
sorrow or unease.")

(subAttribute Excitement Happiness)
(documentation Excitement EnglishLanguage "The state of being happy about a state of 
affairs that might occur in the future.")

(subAttribute Anxiety Unhappiness)
(documentation Anxiety EnglishLanguage "The state of being worried, troubled or uneasy.")

(=>
   (attribute ?PERSON Anxiety)
   (exists (?PROP)
      (fears ?PERSON ?PROP)))

(subAttribute Anger Unhappiness)
(documentation Anger EnglishLanguage "The state of being wrathful, irate or indignant.")

(=>
   (attribute ?PERSON Anger)
   (exists (?PROP)
      (disapproves ?PERSON ?PROP)))

(subAttribute Pain Unhappiness)
(documentation Pain EnglishLanguage "A physical sensation of discomfort which can vary widely 
in intensity.")

(subAttribute Headache Pain)
(documentation Headache EnglishLanguage "&%Pain that that is restricted to the &%Head.")

(=>
  (attribute ?E Headache)
  (exists (?H)
    (and
      (instance ?H Head)
      (part ?H ?E)
      (attribute ?H Pain))))

(instance Surprise EmotionalState)
(documentation Surprise EnglishLanguage "The &%EmotionalState that one experiences when something 
unexpected and of significance occurs.")

(=>
   (holdsDuring ?TIME (attribute ?AGENT Surprise))
   (exists (?PART ?PROP)
      (and
         (temporalPart ?PART (PastFn ?TIME))
         (holdsDuring ?PART (expects ?AGENT ?PROP))
         (holdsDuring ?TIME (truth ?PROP False)))))
   

(subclass SecondaryColor ColorAttribute)
(documentation SecondaryColor EnglishLanguage "A color that is the product of mixing together 
two or more &%PrimaryColors.")

(=>
   (and
      (instance ?COLOR SecondaryColor)
      (attribute ?OBJ ?COLOR))
   (exists (?PROCESS ?RESOURCE1 ?RESOURCE2 ?PRIMARY1 ?PRIMARY2)
      (and
         (result ?PROCESS ?OBJ)
         (resource ?PROCESS ?RESOURCE1)
         (resource ?PROCESS ?RESOURCE2)
         (attribute ?RESOURCE1 ?PRIMARY1)
         (attribute ?RESOURCE2 ?PRIMARY2)
         (instance ?PRIMARY1 PrimaryColor)
         (instance ?PRIMARY2 PrimaryColor)
         (not (equal ?PRIMARY1 ?PRIMARY2)))))

(instance GrayColor SecondaryColor)
(documentation GrayColor EnglishLanguage "A &%SecondaryColor that results from mixing &%Black and 
&%White.")

(instance Pink SecondaryColor)
(documentation Pink EnglishLanguage "A &%SecondaryColor that results from mixing &%Red and 
&%White.")

(instance Brown SecondaryColor)
(documentation Brown EnglishLanguage "A &%SecondaryColor that resembles the color of wood or 
of soil.")

(instance OrangeColor SecondaryColor)
(documentation OrangeColor EnglishLanguage "A &%SecondaryColor that results from mixing &%Red 
and &%Yellow and resembles the color of a ripe orange fruit.")

(instance Green SecondaryColor)
(documentation Green EnglishLanguage "A &%SecondaryColor that resembles the color of fresh 
grass.")

(instance Purple SecondaryColor)
(documentation Purple EnglishLanguage "A &%SecondaryColor that results from mixing &%Red and 
&%Blue.")

(instance LineFormation ShapeAttribute)
(documentation LineFormation EnglishLanguage "A &%ShapeAttribute that applies to 
&%Collections and indicates that all of the &%members of the &%Collection 
are arrayed in a line, i.e. each &%member (except possibly the first) is 
behind or to the side of exactly one other &%member.")

(=>
   (attribute ?COLLECTION LineFormation)
   (instance ?COLLECTION Collection))

(instance Stressed SoundAttribute)
(documentation Stressed EnglishLanguage "A &%SoundAttribute of &%Syllables.  It denotes 
the quality of being emphasized over the other &%Syllables in the same 
&%Word.")

(=>
   (attribute ?SYLLABLE Stressed)
   (instance ?SYLLABLE Syllable))

(=>
   (and
      (attribute ?SYLLABLE Stressed)
      (instance ?WORD Word)
      (part ?SYLLABLE ?WORD))
   (not (exists (?SYLLABLE2)
      (and
         (instance ?SYLLABLE2 Syllable)
         (part ?SYLLABLE2 ?WORD)
         (attribute ?SYLLABLE2 Stressed)
         (not (equal ?SYLLABLE2 ?SYLLABLE))))))

(subclass Syllable SymbolicString)
(documentation Syllable EnglishLanguage "A sequence of &%Characters from the same &%Word 
that denote a single sound.")

(=>
   (instance ?WORD Word)
   (exists (?SYLLABLE)
      (and
         (instance ?SYLLABLE Syllable)
         (part ?SYLLABLE ?WORD))))

(subclass AlphabeticCharacter Character)
(documentation AlphabeticCharacter EnglishLanguage "Any &%Character that is comprised of a single 
alphabetical character, e.g. A, B, C, D, ...")

(subclass DigitCharacter Character)
(documentation DigitCharacter EnglishLanguage "Any &%Character that is comprised of a single digit, 
i.e. one of the numerals 0, 1, 2, 3, 4, 5, 6, 7, 8, 9.")

(subclass Consonant AlphabeticCharacter)
(documentation Consonant EnglishLanguage "An &%AlphabeticCharacter that denotes a speech sound 
that results in audible friction when it is pronounced.")

(subclass Vowel AlphabeticCharacter)
(documentation Vowel EnglishLanguage "An &%AlphabeticCharacter that denotes a speech sound that 
does not result in audible friction when it is pronounced.")

(instance Lost ContestAttribute)
(contraryAttribute Lost Won)
(documentation Lost EnglishLanguage "The &%ContestAttribute that applies to a &%Contest 
participant who has lost the &%Contest.")

(instance Won ContestAttribute)
(documentation Won EnglishLanguage "The &%ContestAttribute that applies to a &%Contest 
participant who has won the &%Contest.")

(instance TieScore ContestAttribute)
(documentation TieScore EnglishLanguage "The &%ContestAttribute that applies to all &%contestParticipants 
in a &%Contest when none of them have &%Won or &%Lost the &%Contest.")

(subclass GameAttribute ContestAttribute)
(documentation GameAttribute EnglishLanguage "Any &%ContestAttribute that is specific 
to a &%Game.")

(=>
   (and
      (property ?THING ?ATTR)
      (instance ?ATTR GameAttribute))
   (exists (?GAME)
      (and
         (instance ?GAME Game)
         (or
            (agent ?GAME ?THING)
            (patient ?GAME ?THING)
            (subProcess ?THING ?GAME)))))

(subclass SportsAttribute GameAttribute)
(documentation SportsAttribute EnglishLanguage "Any &%GameAttribute that is specific to 
a &%Sport.")

(=>
   (and
      (property ?THING ?ATTR)
      (instance ?ATTR SportsAttribute))
   (exists (?SPORT)
      (and
         (instance ?SPORT Game)
         (or
            (agent ?SPORT ?THING)
            (patient ?SPORT ?THING)
            (subProcess ?THING ?SPORT)))))

(instance Foul SportsAttribute)
(documentation Foul EnglishLanguage "A &%SportsAttribute that indicates that the &%GamePiece of 
a &%Sport is no longer in play because it has gone beyond the limits of the 
sports field.")

(instance SportsOut SportsAttribute)
(documentation SportsOut EnglishLanguage "A &%SportsAttribute that indicates that a player in 
a &%Sport can no longer compete because he has been tagged with the &%GamePiece.")

(subclass PoliticoEconomicAttribute RelationalAttribute)
(partition PoliticoEconomicAttribute FormOfGovernment EconomicAttribute)
(documentation PoliticoEconomicAttribute EnglishLanguage "Any &%Attribute of a &%Government 
which specifies some aspect of the political or economic system of the 
&%Government.")

(=>
   (and
      (instance ?ATTRIBUTE PoliticoEconomicAttribute)
      (attribute ?GOVERNMENT ?ATTRIBUTE))
   (instance ?GOVERNMENT Government))


(subclass FormOfGovernment PoliticoEconomicAttribute)
(documentation FormOfGovernment EnglishLanguage "&%FormOfGovernment is a class of 
&%Attributes used to describe the characteristics of a government, 
especially a &%NationalGovernment.  The concept &%FormOfGovernment is 
interpreted broadly enough to include &%Anarchy and &%Factionalism.")

(subclass ChemicalReduction ChemicalSynthesis)
(disjoint ChemicalReduction Oxidation)
(documentation ChemicalReduction EnglishLanguage "Any &%ChemicalProcess where &%Electrons 
are added to the substance undergoing the &%ChemicalProcess.")
(=>
  (and
    (instance ?R ChemicalReduction)
    (patient ?R ?S)
    (holdsDuring 
      (BeginFn (WhenFn ?R)) 
      (electronNumber ?S ?L)))
  (and
    (greaterThan ?G ?L)
    (holdsDuring 
      (EndFn (WhenFn ?R)) 
      (electronNumber ?S ?G))))

(subclass Oxidation ChemicalDecomposition)
(documentation Oxidation EnglishLanguage "Any &%ChemicalProcess where &%Electrons 
are removed from the substance undergoing the &%ChemicalProcess.")

(=>
  (and
    (instance ?O Oxidation)
    (patient ?O ?S)
    (holdsDuring 
      (BeginFn (WhenFn ?O)) 
      (electronNumber ?S ?G)))
  (exists (?L)
    (and
      (lessThan ?L ?G)
      (holdsDuring 
        (EndFn (WhenFn ?O)) 
	(electronNumber ?S ?L)))))

(subclass Dialysis Separating)
(documentation Dialysis EnglishLanguage "Any process of &%Separating a &%Solution into two or more 
constituent &%PureSubstances by means of their unequal diffusion through membranes 
that are partially permeable.")

(=>
   (instance ?DIALYSIS Dialysis)
   (exists (?SOLUTION ?SUBSTANCE1 ?SUBSTANCE2)
      (and
         (resource ?DIALYSIS ?SOLUTION)
         (instance ?SOLUTION Solution)
         (result ?DIALYSIS ?SUBSTANCE1)
         (instance ?SUBSTANCE1 PureSubstance)
         (result ?DIALYSIS ?SUBSTANCE2)
         (instance ?SUBSTANCE2 PureSubstance)
         (not (equal ?SUBSTANCE1 ?SUBSTANCE2)))))
      
(subclass Diluting Putting)
(documentation Diluting EnglishLanguage "Adding a &%Liquid to a &%Solution to decrease 
the concentration of the &%Solution.")

(=>
  (instance ?D Diluting)
  (exists (?S)
    (and
      (patient ?D ?S)
      (instance ?S Solution)
      (attribute ?S Liquid))))

(instance ChemicalEquilibrium InternalAttribute)
(documentation ChemicalEquilibrium EnglishLanguage "The &%Attribute of being in a chemically 
stable state, i.e. the relative proportions of &%resources and &%results will 
not longer change.")

(=>
   (property ?PROCESS ChemicalEquilibrium)
   (instance ?PROCESS ChemicalProcess))

(=>
   (property ?PROCESS ChemicalEquilibrium)
   (exists (?RATIO)
      (=>
         (and
            (holdsDuring ?TIME (resource ?PROCESS ?RESOURCE))
            (holdsDuring ?TIME (result ?PROCESS ?RESULT)))
         (equal ?RATIO (DivisionFn ?RESOURCE ?RESULT)))))

(subclass Chromatography Separating)
(documentation Chromatography EnglishLanguage "&%Processes of &%Separating a &%LiquidMixture or 
a &%GasMixture into some or all of the &%PureSubstances that comprise it.")

(=>
  (instance ?C Chromatography)
  (exists (?M)
    (and
      (resource ?C ?M)
      (or
        (instance ?M LiquidMixture)
        (instance ?M GasMixture)))))

(=>
  (instance ?C Chromatography)
  (exists (?S)
    (and
      (instance ?S PureSubstance)
      (result ?C ?S))))

(subclass BreakabilityAttribute InternalAttribute)
(documentation BreakabilityAttribute EnglishLanguage "A subclass of &%Attributes for 
characterizing the breakability of &%CorpuscularObjects.")

(instance Fragile BreakabilityAttribute)
(documentation Fragile EnglishLanguage "An &%Attribute which indicates that the 
associated &%Object is very breakable.")

(instance Unbreakable BreakabilityAttribute)
(contraryAttribute Unbreakable Fragile)
(documentation Unbreakable EnglishLanguage "An &%Attribute which indicates that the 
associated &%Object cannot be broken under normal usage conditions.")

(subclass RightAngle TwoDimensionalAngle)
(documentation RightAngle EnglishLanguage "Any &%TwoDimensionalAngle that has the 
&%angularMeasure of 90 &%AngularDegrees.")

(=>
   (instance ?A RightAngle)
   (angularMeasure ?A (MeasureFn 90 AngularDegree)))

(subclass AcuteAngle TwoDimensionalAngle)
(documentation AcuteAngle EnglishLanguage "Any &%TwoDimensionalAngle that has an 
&%angularMeasure that is less than 90 &%AngularDegrees.")
(partition TwoDimensionalAngle RightAngle ObliqueAngle AcuteAngle)
(=>
  (and
    (instance ?A AcuteAngle)
    (angularMeasure ?A (MeasureFn ?N AngularDegree)))
  (lessThan ?N 90))

(subclass ObliqueAngle TwoDimensionalAngle)
(documentation ObliqueAngle EnglishLanguage "Any &%TwoDimensionalAngle that has an 
&%angularMeasure that is greater than 90 &%AngularDegrees.")

(=>
  (and
    (instance ?A ObliqueAngle)
    (angularMeasure ?A (MeasureFn ?N AngularDegree)))
  (greaterThan ?N 90))

(subclass Polygon ClosedTwoDimensionalFigure)
(documentation Polygon EnglishLanguage "A &%ClosedTwoDimensionalFigure that is composed 
exclusively of straight lines, i.e. &%OneDimensionalFigures.")

(=>
   (instance ?POLYGON Polygon)
   (=>
      (geometricPart ?PART ?POLYGON)
      (or
         (sideOfFigure ?PART ?POLYGON)
         (exists (?SIDE)
            (and
               (sideOfFigure ?SIDE ?POLYGON)
               (geometricPart ?PART ?SIDE))))))

(subclass Triangle Polygon)
(documentation Triangle EnglishLanguage "Any three-sided &%Polygon.")

(=>
   (instance ?TRIANGLE Triangle)
   (equal (CardinalityFn (KappaFn ?SIDE (sideOfFigure ?SIDE ?TRIANGLE))) 3))

(subclass RightTriangle Triangle)
(documentation RightTriangle EnglishLanguage "Any &%Triangle that contains a &%RightAngle.")

(=>
   (instance ?T RightTriangle)
   (exists (?A)
      (and
         (instance ?A RightAngle)
         (angleOfFigure ?A ?T))))

(subclass Quadrilateral Polygon)
(documentation Quadrilateral EnglishLanguage "Any four-sided &%Polygon.")

(=>
   (instance ?QUAD Quadrilateral)
   (equal (CardinalityFn (KappaFn ?SIDE (sideOfFigure ?SIDE ?QUAD))) 4))

(subclass Rectangle Quadrilateral)
(documentation Rectangle EnglishLanguage "Any &%Quadrilateral whose angles are all 
&%RightAngles.")

(=>
   (instance ?RECTANGLE Rectangle)
   (=>
      (angleOfFigure ?ANGLE ?RECTANGLE)
      (instance ?ANGLE RightAngle)))

(subclass Square Rectangle)
(documentation Square EnglishLanguage "Any &%Rectangle whose sides are all equal.")

(=>
   (instance ?SQUARE Square)
   (exists (?LENGTH)
      (forall (?SIDE)
         (=>
            (sideOfFigure ?SIDE ?SQUARE)
            (lineMeasure ?SIDE ?LENGTH)))))

(documentation DiameterFn EnglishLanguage "(DiameterFn ?CIRCLE) denotes the length of the 
diameter of the &%Circle ?CIRCLE.")
(instance DiameterFn UnaryFunction)
(instance DiameterFn TotalValuedRelation)
(domain DiameterFn 1 Circle)
(range DiameterFn LengthMeasure)

(=>
  (equal (DiameterFn ?C) ?L)
  (exists (?R)
    (and
      (equal (RadiusFn ?C) ?R)
      (equal (MultiplicationFn ?R 2) ?L))))

(documentation RadiusFn EnglishLanguage "(RadiusFn ?CIRCLE) denotes the length of the radius 
of the &%Circle ?CIRCLE.")
(instance RadiusFn UnaryFunction)
(instance RadiusFn TotalValuedRelation)
(domain RadiusFn 1 Circle)
(range RadiusFn LengthMeasure)

(=>
  (and
    (equal (CenterOfCircleFn ?C) ?P)
    (pointOfFigure ?P2 ?C)
    (geometricDistance ?P2 ?P ?R))
  (equal (RadiusFn ?C) ?R))

(subclass CircleSector ClosedTwoDimensionalFigure)
(documentation CircleSector EnglishLanguage "A slice of a &%Circle, i.e. any 
&%ClosedTwoDimensionalFigure which consists of two Radii and the arc of 
the &%Circle that they bound.")

(subrelation sideOfFigure geometricPart)
(domain sideOfFigure 1 OneDimensionalFigure)
(domain sideOfFigure 2 GeometricFigure)
(documentation sideOfFigure EnglishLanguage "(&%sideOfFigure ?SIDE ?FIGURE) means that the 
&%OneDimensionalFigure ?POINT is a side of the &%GeometricFigure ?FIGURE.")

(instance tangent BinaryPredicate)
(domain tangent 1 OneDimensionalFigure)
(domain tangent 2 TwoDimensionalObject)
(documentation tangent EnglishLanguage "(&%tangent ?LINE ?CIRCLE) means that the straight line 
?LINE is tangent to the figure ?CIRCLE, i.e. ?LINE touches ?CIRCLE without 
intersecting it.")

(=>
   (tangent ?LINE ?CIRCLE)
   (exists (?POINT1)
      (and
         (pointOfFigure ?POINT1 ?LINE)
         (pointOfFigure ?POINT1 ?CIRCLE)
         (forall (?POINT2)
            (=>
               (and
                  (pointOfFigure ?POINT2 ?LINE)
                  (pointOfFigure ?POINT2 ?CIRCLE))
               (equal ?POINT1 ?POINT2))))))

(subrelation contestParticipant agent)
(instance contestParticipant TotalValuedRelation)
(domain contestParticipant 1 Contest)
(domain contestParticipant 2 Agent)
(documentation contestParticipant EnglishLanguage "(&%contestParticipant ?CONTEST ?AGENT) 
means that ?AGENT is one of the sides in the &%Contest ?CONTEST.  For 
example, if the ?CONTEST is a football game, then ?AGENT would be one of 
the opposing teams.  For another example, if ?CONTEST is a &%Battle, then 
?AGENT would be one of the sides fighting each other.")

(subclass CivilWar War)
(documentation CivilWar EnglishLanguage "A &%War in which the fighting &%GeopoliticalAreas 
are both part of the same &%Nation.")

(=>
   (instance ?WAR CivilWar)
   (exists (?NATION)
      (and
         (instance ?NATION Nation)
         (forall (?AGENT)
            (=>
               (contestParticipant ?WAR ?AGENT)
               (geopoliticalSubdivision ?AGENT ?NATION))))))

(subclass Debating Contest)
(subclass Debating LinguisticCommunication)
(documentation Debating EnglishLanguage "A &%Contest where each participant holds a different 
view regarding some issue, and each participant attempts to prove, by 
rhetoric or evidence, that his/her own views about a particular matter are 
correct and/or that the views of the other participants are incorrect.")

(subclass Negotiating Contest)
(subclass Negotiating LinguisticCommunication)
(documentation Negotiating EnglishLanguage "A &%Contest where each participant attempts to 
maximize his self-interest in a &%Promise that marks the end of the &%Contest.")

(=>
   (instance ?NEGOTIATE Negotiating)
   (hasPurpose ?NEGOTIATE (exists (?COMMIT)
                             (and
                                (instance ?COMMIT Committing)
                                (subProcess ?COMMIT ?NEGOTIATE)
                                (finishes (WhenFn ?COMMIT) (WhenFn ?NEGOTIATE))))))

(subclass BusinessCompetition Contest)
(documentation BusinessCompetition EnglishLanguage "Any &%Contest where the &%contestParticipants 
are &%Corporations and the aim is to win as many customers as possible.")

(=>
   (and
      (instance ?CONTEST BusinessCompetition)
      (contestParticipant ?CONTEST ?AGENT))
   (instance ?AGENT Corporation))

(instance Blind BiologicalAttribute)
(documentation Blind EnglishLanguage "The &%Attribute that applies to &%Animals and &%Humans 
that are unable to see.")

(=>
   (attribute ?AGENT Blind)
   (not (capability Seeing agent ?AGENT)))

(instance Deaf BiologicalAttribute)
(documentation Deaf EnglishLanguage "The &%Attribute that applies to &%Animals and &%Humans 
that are unable to hear.")

(=>
  (attribute ?A Deaf)
  (not 
    (capability Hearing agent ?A)))

(instance Hungry BiologicalAttribute)
(documentation Hungry EnglishLanguage "The &%Attribute that applies to &%Animals and &%Humans 
when they want &%Food.")

(=>
  (attribute ?A Hungry)
  (desires ?A 
    (exists (?E)
      (and
        (instance ?E Eating)
        (agent ?E ?A)))))

(instance Pregnant BiologicalAttribute)
(documentation Pregnant EnglishLanguage "The &%Attribute that applies to &%Female &%Animals and 
&%Humans that have an embryo or fetus growing inside of them as the result of 
having one of the &%Female's &%Eggs fertilized.")

(=>
  (attribute ?H Pregnant)
  (attribute ?H Female))

(subAttribute Puberty NonFullyFormed)
(documentation Puberty EnglishLanguage "The &%DevelopmentalAttribute of having functional sex 
glands which are not fully mature.")

(instance Fist BodyPosition) 
(documentation Fist EnglishLanguage "The &%BodyPosition of having the fingers drawn into 
the palm so that the hand can be used for striking something.")

(=>
   (attribute ?HAND Fist)
   (instance ?HAND Hand))

(subclass LyingDown BodyMotion)
(subclass LyingDown MotionDownward)
(documentation LyingDown EnglishLanguage "The &%BodyMotion of moving from a &%Sitting 
to a &%Prostrate position.")

(=>
   (and
      (instance ?LIE LyingDown)
      (agent ?LIE ?AGENT))
   (and
      (holdsDuring 
        (BeginFn (WhenFn ?LIE)) 
	(attribute ?AGENT Sitting))
      (holdsDuring 
        (EndFn (WhenFn ?LIE)) 
	(attribute ?AGENT Prostrate))))

(instance Handstand BodyPosition)
(documentation Handstand EnglishLanguage "The &%BodyPosition where one rests on one's hands 
and extends one's feet in the air.")

(instance Kneeling BodyPosition)
(documentation Kneeling EnglishLanguage "The &%BodyPosition of resting one's weight on one's 
&%Knees.")

(instance Splitting BodyPosition)
(documentation Splitting EnglishLanguage "The &%BodyPosition of extending one's &%Legs at 
right angles to one's &%Torso.")

(subAttribute Squatting Sitting)
(documentation Squatting EnglishLanguage "&%Sitting on one's heels.")

(subclass SittingDown BodyMotion)
(subclass SittingDown MotionDownward)
(documentation SittingDown EnglishLanguage "The &%BodyMotion of moving from a &%Standing 
to a &%Sitting position.")

(=>
   (and
      (instance ?SIT SittingDown)
      (agent ?SIT ?AGENT))
   (and
      (holdsDuring 
        (BeginFn (WhenFn ?SIT)) 
	(attribute ?AGENT Standing))
      (holdsDuring 
        (EndFn (WhenFn ?SIT)) 
	(attribute ?AGENT Sitting))))
   
(subclass StandingUp BodyMotion)
(subclass StandingUp MotionUpward)
(documentation StandingUp EnglishLanguage "The &%BodyMotion of moving from a &%Sitting 
to a &%Standing position.")

(=>
   (and
      (instance ?STAND StandingUp)
      (agent ?STAND ?AGENT))
   (and
      (holdsDuring 
        (BeginFn (WhenFn ?STAND)) 
	(attribute ?AGENT Sitting))
      (holdsDuring 
        (EndFn (WhenFn ?STAND)) 
	(attribute ?AGENT Standing))))

(subclass EyeMotion BodyMotion)
(documentation EyeMotion EnglishLanguage "Any &%Motion where a patient is the agent's 
own &%Eyelid or &%Eyelids.")

(=>
  (and
    (instance ?EM EyeMotion)
    (agent ?EM ?A))
  (exists (?E)
    (and
      (instance ?E Eyelid)
      (part ?E ?A)
      (patient ?EM ?E))))

(subclass OpeningEyes EyeMotion)
(subclass OpeningEyes Opening)
(disjoint OpeningEyes ClosingEyes)
(documentation OpeningEyes EnglishLanguage "The &%BodyMotion of relaxing the eye lids so that 
the corneas are exposed to light.")

(subclass ClosingEyes EyeMotion)
(subclass ClosingEyes Closing)
(documentation ClosingEyes EnglishLanguage "The &%EyeMotion of tensing the eye lids so that 
the corneas are not exposed to light.")

(subclass Winking ClosingEyes)
(subclass Winking Gesture)
(documentation Winking EnglishLanguage "Any instance of &%ClosingEyes which is intended to 
express something to someone else.")

(subclass Shrugging BodyMotion)
(subclass Shrugging Gesture)
(documentation Shrugging EnglishLanguage "Moving the &%Shoulders in such a way that the motion 
is intended to express something to someone else.")

(=>
   (and
      (instance ?SHRUG Shrugging)
      (patient ?SHRUG ?SHOULDER))
   (instance ?SHOULDER Shoulder))

(subclass Trembling BodyMotion)
(subclass Trembling AutonomicProcess)
(subclass Trembling Tremor)
(disjoint Trembling IntentionalProcess)
(documentation Trembling EnglishLanguage "Any &%BodyMotion which is involuntary and which is 
repeated many times over a short time frame, e.g. a tremor in the hands,
shivering etc.") 

(subclass ReflexiveProcess AutonomicProcess)
(documentation ReflexiveProcess EnglishLanguage "Any &%Muscle reaction which is a response 
to a specific stimulus and which does not reach the level of consciousness.")

(=>
  (instance ?P ReflexiveProcess)
  (exists (?M)
    (and
      (instance ?M Muscle)
      (patient ?P ?M))))

(subclass Dismounting BodyMotion)
(documentation Dismounting EnglishLanguage "Any &%BodyMotion which results in not being &%On 
something else.")
(=>
  (and
    (instance ?D Dismounting)
    (agent ?D ?A)
    (patient ?D ?O))
  (and
    (holdsDuring 
      (BeginFn (WhenFn ?D)) 
      (orientation ?A ?O On))
    (holdsDuring 
      (EndFn (WhenFn ?D)) 
      (not 
        (orientation ?A ?O On)))))

(subclass DomesticAnimal Animal)
(disjoint DomesticAnimal Human)
(documentation DomesticAnimal EnglishLanguage "Any &%Animal that is kept by a &%Human, as 
a pet, as livestock, for exhibition, etc.")

(=>
  (instance ?ANIMAL DomesticAnimal)
  (exists (?KEEP ?PERSON)
    (and
      (instance ?KEEP Keeping)
      (agent ?KEEP ?PERSON)
      (instance ?PERSON Human)
      (patient ?KEEP ?ANIMAL))))

(subclass Fodder (FoodForFn DomesticAnimal))
(subclass Fodder OrganicObject)
(documentation Fodder EnglishLanguage "&%Fodder is the class of &%OrganicObjects that 
is used as food for &%DomesticAnimals.")
(=>
  (instance ?FEED Fodder)
  (hasPurpose ?FEED 
    (exists (?ANIMAL ?EAT)
      (and
        (instance ?ANIMAL DomesticAnimal)
        (instance ?EAT Eating)
        (agent ?EAT ?ANIMAL)
        (patient ?EAT ?FEED)))))

(subclass Herbivore Mammal)
(disjoint Herbivore Carnivore)
(documentation Herbivore EnglishLanguage "A plant-eating &%Mammal.")

(=>
  (and
    (instance ?A Herbivore)
    (agent ?E ?A)
    (instance ?E Eating)
    (patient ?E ?F))
  (instance ?F Plant))

(subclass Antelope HoofedMammal)
(documentation Antelope EnglishLanguage "A &%HoofedMammal with long legs and backward-facing horns.  
This class includes gazelles, addax, blackbucks, etc.")

(subclass Buffalo HoofedMammal)
(disjoint Buffalo DomesticAnimal)
(documentation Buffalo EnglishLanguage "A &%HoofedMammal with long hair whose habitat is the 
plains of &%NorthAmerica.")

(subclass DomesticCat DomesticAnimal)
(subclass DomesticCat Feline)
(documentation DomesticCat EnglishLanguage "A variety of &%Feline which has been domesticated 
by selective breeding.")

(subclass DomesticDog DomesticAnimal)
(subclass DomesticDog Canine)
(documentation DomesticDog EnglishLanguage "Canines which have evolved from the common 
wolf by selective breeding.")

(subclass Fox Canine)
(documentation Fox EnglishLanguage "&%Canines with a bushy tail and pointed ears and nose.  
&%Foxes tend to be much smaller than most &%DomesticDogs.")

(subclass Elephant Herbivore)
(documentation Elephant EnglishLanguage "A very large, almost hairless &%Herbivore with a long 
trunk.")

(documentation Goat EnglishLanguage "A domesticated &%HoofedMammal that is raised primarily 
for &%Milk.")

(subclass Horse HoofedMammal)
(subclass Horse Livestock)
(documentation Horse EnglishLanguage "A domesticated &%HoofedMammal that is used for 
transportation and work.")

(subclass Pig HoofedMammal)
(subclass Pig Livestock)
(documentation Pig EnglishLanguage "A domesticated &%HoofedMammal that is raised for pork.")

(subclass Donkey HoofedMammal)
(subclass Donkey Livestock)
(documentation Donkey EnglishLanguage "A domesticated &%HoofedMammal that is used for 
work.")

(subclass Gastropod Mollusk)
(documentation Gastropod EnglishLanguage "A &%Mollusk with a distinct head that has no 
shell (e.g. slugs) or a univalve shell (e.g. snails).")

(subclass Mule HoofedMammal)
(subclass Mule DomesticAnimal)
(documentation Mule EnglishLanguage "The product of a &%Male &%Donkey and a &%Female 
&%Horse.  &%Mules are always sterile.")

(=>
  (instance ?MULE Mule)
  (exists (?DONKEY ?HORSE)
    (and
      (father ?MULE ?DONKEY)
      (instance ?DONKEY Donkey)
      (mother ?MULE ?HORSE)
      (instance ?HORSE Horse))))

(=>
  (instance ?MULE Mule)
  (not 
    (capability SexualReproduction agent ?MULE)))

(subclass Sheep HoofedMammal)
(subclass Sheep DomesticAnimal)
(documentation Sheep EnglishLanguage "A domesticated &%HoofedMammal that is bred for its 
wool and for its meat (known as mutton).")

(subclass Lamb Sheep)
(documentation Lamb EnglishLanguage "A young &%Sheep, i.e. a &%Sheep that is &%NonFullyFormed.")

(<=>
  (instance ?L Lamb)
  (and
    (instance ?L Sheep)
    (attribute ?L NonFullyFormed)))

(subclass Cow HoofedMammal)
(subclass Cow DomesticAnimal)
(documentation Cow EnglishLanguage "A domesticated &%HoofedMammal that is raised for milk 
and beef, and is also used for work.")

(subclass Calf Cow)
(documentation Calf EnglishLanguage "A young &%Cow, i.e. a &%Cow that is &%NonFullyFormed.")
(<=>
  (instance ?C Calf)
  (and
    (instance ?C Cow)
    (attribute ?C NonFullyFormed)))

(subclass Bull Cow)
(documentation Bull EnglishLanguage "A &%Cow that is &%Male.")
(<=>
  (instance ?B Bull)
  (and
    (instance ?B Cow)
    (attribute ?B Male)))

(subclass FemaleCow Cow)
(documentation FemaleCow EnglishLanguage "A &%Cow that is &%Female.")

(<=>
   (instance ?COW FemaleCow)
   (and
      (instance ?COW Cow)
      (attribute ?COW Female)))

(subclass Poultry Livestock)
(subclass Poultry Bird)
(subclass Chicken Poultry)
(documentation Chicken EnglishLanguage "A subclass of &%Bird that is raised for its meat 
and for its eggs.")

(subclass Hen Chicken)
(documentation Hen EnglishLanguage "A &%Female &%Chicken.")

(<=>
  (instance ?H Hen)
  (and
    (instance ?H Chicken)
    (attribute ?H Female)))

(subclass Rooster Chicken)
(documentation Rooster EnglishLanguage "A &%Male &%Chicken.")

(<=>
  (instance ?R Rooster)
  (and
    (instance ?R Chicken)
    (attribute ?R Male)))

(documentation Duck EnglishLanguage "A subclass of &%Bird with webbed feet and a large bill.  
Some ducks live in the wild, and some are raised for meat and/or eggs.")
(subclass Duck Poultry)

(subclass Goose Poultry)
(documentation Goose EnglishLanguage "A subclass of &%Bird with webbed feet and a long neck 
and a large body.  These &%Birds are often raised for their &%Meat.")

(subclass Owl Bird)
(documentation Owl EnglishLanguage "A nocturnal bird of prey with a large head and 
forward-facing eyes.")

(subclass Pigeon Bird)
(documentation Pigeon EnglishLanguage "A subclass of &%Bird with a stout body and short legs.")

(subclass Vulture Bird)
(documentation Vulture EnglishLanguage "A diurnal bird of prey with a bald head that 
feeds on carrion.")

(subclass Feather AnimalAnatomicalStructure)
(subclass Feather BodyPart)
(documentation Feather EnglishLanguage "Each instance of this class is one of the structures 
that make up the external covering of &%Birds.")
(=>
  (instance ?F Feather)
  (exists (?B)
    (and
      (instance ?B Bird)
      (part ?F ?B))))

(instance meatOfAnimal BinaryRelation)
(documentation meatOfAnimal EnglishLanguage "(meatOfAnimal ?MEATCLASS ?ANIMALCLASS) states
that ?MEATCLASS was once part of the class of &%Animal ?ANIMALCLASS)")
(domainSubclass meatOfAnimal 1 Meat)
(domainSubclass meatOfAnimal 2 Animal)
(=>
  (meatOfAnimal ?MEAT ?ANIMAL)
  (exists (?TIME ?A ?M)
    (and
      (instance ?A ?ANIMAL)
      (instance ?M ?MEAT)
      (during ?TIME (WhenFn ?A))
      (holdsDuring ?TIME
        (part ?M ?A)))))

(subclass Beef Meat)
(documentation Beef EnglishLanguage "&%Meat that was originally part of a &%Cow.")
(meatOfAnimal Beef Cow)

(documentation ChickenMeat EnglishLanguage "&%Meat that was originally part of a &%Chicken.")
(subclass ChickenMeat Meat)
(meatOfAnimal ChickenMeat Chicken)
(subclass Pork Meat)
(documentation Pork EnglishLanguage "&%Meat that was originally part of a &%Pig.")
(meatOfAnimal Pork Pig)

(subclass FishMeat Seafood)
(documentation FishMeat EnglishLanguage "&%Meat that was originally part of a &%Fish.")
(meatOfAnimal FishMeat Fish)

(subclass Mouse Rodent)
(documentation Mouse EnglishLanguage "A &%Rodent that has a hairless tail like a rat but that 
is smaller than a &%Rat.")

(subclass Rat Rodent)
(documentation Rat EnglishLanguage "A &%Rodent that has a hairless tail like a &%Mouse 
but that is larger than a &%Mouse.")

(subclass Rabbit Rodent)
(documentation Rabbit EnglishLanguage "A burrowing &%Rodent with a short tail and long ears.")

(subclass Squirrel Rodent)
(documentation Squirrel EnglishLanguage "A tree-dwelling &%Rodent with a bushy tail.")

(subclass Hay Fodder)
(documentation Hay EnglishLanguage "&%Grass that has been cut and cured for use as &%Fodder.")

(=>
   (instance ?HAY Hay)
   (exists (?MAKE ?GRASS)
      (and
         (instance ?MAKE Making)
         (resource ?MAKE ?GRASS)
         (instance ?GRASS Grass)
         (result ?MAKE ?HAY))))

(subclass Copying Making)
(documentation Copying EnglishLanguage "&%Making a &%copy of something.")

(=>
   (and
      (instance ?COPY Copying)
      (resource ?COPY ?THING1)
      (result ?COPY ?THING2))
   (copy ?THING1 ?THING2))

(subclass Vacationing RecreationOrExercise)
(documentation Vacationing EnglishLanguage "Taking time off from &%Working.")

(=>
   (and
      (instance ?VACATION Vacationing)
      (instance ?WORK Working)
      (agent ?VACATION ?PERSON)
      (agent ?WORK ?PERSON)
      (instance ?PERSON Human))
   (not (overlapsTemporally (WhenFn ?VACATION) (WhenFn ?WORK))))

(documentation OccupationFn EnglishLanguage "(OccupationFn ?PROCESS) denotes the 
subclass of ?PROCESS where instances of ?PROCESS are performed as 
one's occupation, i.e. as part of a &%FinancialTransaction where one 
earns money for the performance of ?PROCESS.")
(instance OccupationFn UnaryFunction)
(domain OccupationFn 1 IntentionalProcess)
(rangeSubclass OccupationFn FinancialTransaction)

(documentation occupation EnglishLanguage "(occupation ?PERSON ?TYPE) means that the occupation 
of ?PERSON is ?TYPE.  This predicate is most often used in combination with the 
function &%OccupationFn, e.g. (&%occupation PoliceOfficerWilkins (&%OccupationFn 
&%LawEnforcement)).")
(instance occupation BinaryPredicate)
(domain occupation 1 Human)
(domain occupation 2 FinancialTransaction)

(subclass Working FinancialTransaction)
(documentation Working EnglishLanguage "Any &%FinancialTransaction where someone exchanges 
his/her labor for an instance of &%CurrencyMeasure.")

(subclass Farming Working)
(documentation Farming EnglishLanguage "Operating a farm, e.g. planting and harvesting crops, 
tending livestock, etc.")

(=>
   (and
      (instance ?FARMING Farming)
      (agent ?FARMING ?FARMER))
   (exists (?FARM)
      (and
         (instance ?FARM Farm)
         (holdsDuring (WhenFn ?FARMING) (located ?FARMER ?FARM)))))

(subclass Serving Working)
(documentation Serving EnglishLanguage "Working as a waiter or servant, either for an 
&%Organization (e.g. a &%Restaurant) or for a person or family.")

(subclass Sales Working)
(documentation Sales EnglishLanguage "Any instance of &%Working that involves &%Selling or 
trying to sell items.")

(subAttribute Retired Unemployed)
(documentation Retired EnglishLanguage "Voluntary unemployment toward the end of one's life.")

(subAttribute Housewife Unemployed)
(documentation Housewife EnglishLanguage "A role occupied by a wife who manages the home 
while her husband earns income for the family.")

(=>
  (attribute ?W Housewife)
  (exists (?H)
    (wife ?W ?H)))

(subclass SkilledOccupation Position)
(documentation SkilledOccupation EnglishLanguage "Any &%Position which requires 
learning a set of skills.")

(=>
   (and
      (instance ?OCCUPATION SkilledOccupation)
      (attribute ?PERSON ?OCCUPATION))
   (exists (?TRAINING)
      (and
         (instance ?TRAINING EducationalProcess)
         (destination ?TRAINING ?PERSON)))) 

(subclass ManualLabor Position)
(documentation ManualLabor EnglishLanguage "Any &%Position which involves manual 
work.")

(subclass UnskilledOccupation ManualLabor)
(disjoint UnskilledOccupation SkilledOccupation)
(documentation UnskilledOccupation EnglishLanguage "Any &%Position which does not 
require learning a set of skills.") 

(instance Waiter ServicePosition)
(documentation Waiter EnglishLanguage "&%Waiter refers to the &%ServicePosition present
in &%Restaurant that brings food from the kitchen to the customer's table")
(termFormat EnglishLanguage Waiter "waiter")

(=>
  (attribute ?X Waiter)
  (instance ?X Human))

(=>
  (and
    (attribute ?X Waiter)
    (instance ?X Human)
    (employs ?REST ?X)
    (instance ?REST Restaurant)
    (customer ?CUST ?REST)
    (located ?CUST ?TABLE)
    (instance ?TABLE Table))
  (and
    (possesses ?REST ?KITCHEN)
    (instance ?KITCHEN KitchenArea)
    (instance ?DINING DiningArea)
    (located ?TABLE ?DINING)
    (hasPurpose ?X
      (and
        (instance ?C Carrying)
        (agent ?C ?X)
        (origin ?C ?KITCHEN)
        (destination ?C ?TABLE)
        (patient ?C ?FOOD)
        (instance ?FOOD Food)))))

(instance Maid ServicePosition)
(documentation Maid EnglishLanguage "Any &%ServicePosition where various needs of a person 
or family are served over a long period of time.  Note that these &%Positions 
are, by definition, filled only by &%Women.")

(=>
  (attribute ?MAID Maid)
  (instance ?MAID Woman))

(=>
  (attribute ?MAID Maid)
  (exists (?SERVE)
    (and
      (instance ?SERVE Serving)
      (agent ?SERVE ?MAID))))       

(subclass Janitor UnskilledOccupation)
(documentation Janitor EnglishLanguage "Any &%Position which involves cleaning a &%Building 
or some of the &%Rooms within a &%Building.")

(=>
  (and
    (instance ?J Janitor)
    (attribute ?P ?J))
  (occupation ?P (OccupationFn Washing)))

(instance TheaterProfession SkilledOccupation)
(subclass TheaterProfession EntertainmentProfession)
(documentation TheaterProfession EnglishLanguage "Often know as the stage, the 
&%Position of performing live plays.")

(instance FilmDirector FilmMakingProfession)
(documentation FilmDirector EnglishLanguage "&%Positions which involve directing &%MotionPictures.")

(subclass FilmMaking ContentDevelopment)
(documentation FilmMaking EnglishLanguage "&%ContentDevelopment where the result is a 
&%MotionPicture.")
(=>
  (instance ?FM FilmMaking)
  (exists (?M ?F)
    (and
      (instance ?M MotionPicture)
      (result ?FM ?M)
      (instance ?F PhotographicFilm)
      (resource ?FM ?F))))

(subclass FilmMakingProfession EntertainmentProfession)
(documentation FilmMakingProfession EnglishLanguage "&%Positions which involve &%FilmMaking, 
i.e. acting in films, directing films, producing films, etc.")
(=>
  (and
    (instance ?P FilmMakingProfession)
    (attribute ?H ?P))
  (occupation ?H (OccupationFn FilmMaking)))

(instance FilmProducer FilmMakingProfession)
(documentation FilmProducer EnglishLanguage "&%Positions which involve producing &%MotionPictures, 
i.e arranging the financing for the &%MotionPicture and supervising its production.")

(instance Coach SkilledOccupation)
(documentation Coach EnglishLanguage "Any occupation that involves training an athlete or a 
sports team.")

(instance GameReferee SkilledOccupation)
(documentation GameReferee EnglishLanguage "A position which involves adjudicating contested 
&%Maneuvers in a &%Game, i.e. deciding whether or not the &%Maneuvers are 
permitted by the rules of the &%Game.")

(subclass OccupationalTrade SkilledOccupation)
(subclass OccupationalTrade ManualLabor)
(documentation OccupationalTrade EnglishLanguage "Any &%Position that involves skilled 
manual work.")

(instance Carpenter OccupationalTrade)
(documentation Carpenter EnglishLanguage "Any occupation that involves creating and repairing 
structural and decorative portions of &%Buildings that are made chiefly of &%Wood.
This does not include plumbing, roofing, electrical, foundation and site work.")
(=>
  (attribute ?P Carpenter)
  (hasSkill Carpentry ?P))

(subclass Carpentry Making)
(documentation Carpentry EnglishLanguage "&%Making &%Buildings out of &%Wood.")
(=>
  (instance ?C Carpentry)
  (exists (?B)
    (and
      (instance ?B Building)
      (instance ?W Wood)
      (part ?W ?B)
      (result ?C ?B))))

(instance FarmHand OccupationalTrade)
(documentation FarmHand EnglishLanguage "&%OccupationalTrades which involve &%Agriculture.")
(=>
  (attribute ?P FarmHand)
  (exists (?F)
    (and
      (instance ?F Agriculture)
      (agent ?F ?P)))) 

(instance Plumber OccupationalTrade)
(documentation Plumber EnglishLanguage "Any occupation that involves installing, repairing, and 
replacing pipes and pipe fixtures.")

(subclass SewageSystem Pipeline)
(documentation SewageSystem EnglishLanguage "A &%Pipline which is used to transport human 
waste to an area where it can be treated and/or disposed of.")

(=>
  (and
    (instance ?T Transportation)
    (patient ?T ?S)
    (instrument ?T ?SS)
    (instance ?SS SewageSystem))
  (instance ?S Sewage))

(instance Potter OccupationalTrade)
(documentation Potter EnglishLanguage "Any occupation that involves the creation of &%Pottery.")

(=>
  (attribute ?H Potter)
  (exists (?M ?P)
    (and
      (instance ?P Pottery)
      (instance ?M Making)
      (agent ?M ?H)
      (result ?M ?P))))

(subclass Profession SkilledOccupation)
(disjoint Profession OccupationalTrade)
(documentation Profession EnglishLanguage "Any occupation that requires at least a bachelor's
degree.")

(=>
   (and
      (or
         (instance ?ROLE OccupationalTrade)
         (instance ?ROLE Profession))
      (attribute ?HUMAN ?ROLE))
   (exists (?FIELD)
      (and
         (instance ?FIELD FieldOfStudy)
         (knows ?HUMAN ?FIELD))))

(subclass ReligiousPosition Position)
(documentation ReligiousPosition EnglishLanguage "Any &%Position within a &%ReligousOrganization.")

(=>
   (and
      (occupiesPosition ?H ?P ?O)
      (instance ?P ReligiousPosition))
   (instance ?O ReligiousOrganization))

(instance Cleric Profession)
(instance Cleric ReligiousPosition)
(documentation Cleric EnglishLanguage "The &%Profession of being in charge of or ministering 
to a &%ReligousOrganization.")

(<=>
   (occupiesPosition ?PERSON Cleric ?ORG)
   (and
      (leader ?ORG ?PERSON)
      (instance ?ORG ReligiousOrganization)))

(instance Deacon ReligiousPosition)
(instance Deacon PartTimePosition)
(documentation Deacon EnglishLanguage "Any &%Position within a &%ReligiousOrganization that is held 
by a layman, which is part-time, and which involves assisting a &%Cleric.")
(=>
  (instance ?D Deacon)
  (exists (?O ?C)
    (and
      (instance ?O ReligiousOrganization)
      (instance ?C Cleric)
      (subordinateInOrganization ?O ?D ?C))))

(instance PoliceOfficer Profession)
(instance PoliceOfficer GovernmentOfficer)
(documentation PoliceOfficer EnglishLanguage "The &%Profession of being a police officer, i.e. 
working for a law enforcement agency that is part of a &%Government.")

(=>
  (attribute ?P PoliceOfficer)
  (hasSkill PlacingUnderArrest ?P))

(subAttribute PoliceCaptain PoliceOfficer)
(documentation PoliceCaptain EnglishLanguage "A &%PoliceOfficer who is in charge of a precinct.")

(subAttribute PoliceSergeant PoliceOfficer)
(documentation PoliceSergeant EnglishLanguage "A &%PoliceOfficer with the rank of sergeant.")

(subAttribute Sheriff PoliceOfficer)
(documentation Sheriff EnglishLanguage "A &%PoliceOfficer whose jurisdiction is a &%County.")

(subAttribute Marshal PoliceOfficer)
(documentation Marshal EnglishLanguage "A &%PoliceOfficer whose responsibility is to enforce 
the decisions of a law court.")

(subAttribute Deputy PoliceOfficer)
(documentation Deputy EnglishLanguage "A &%Position which authorizes the holder of the position 
to act as the sheriff when the sheriff is not available.")

(instance Coroner GovernmentOfficer)
(documentation Coroner EnglishLanguage "A &%GovernmentOfficer who investigates deaths that are 
suspected of being due to something other than natural causes.")

(subAttribute PoliceDetective PoliceOfficer)
(documentation PoliceDetective EnglishLanguage "The &%Profession of being a police 
detective, i.e. being a &%PoliceOfficer whose duties include the 
investigation of crimes.") 

(instance PrivateDetective Profession)
(contraryAttribute PrivateDetective PoliceDetective)
(documentation PrivateDetective EnglishLanguage "The &%Profession of being a private detective, 
i.e. a detective who can be hired for a fee to investigate something.")

(instance Attorney Profession)
(documentation Attorney EnglishLanguage "The &%Profession of practicing law, whether as a 
judge or as a legal advocate.")
(=>
  (attribute ?P Attorney)
  (hasExpertise ?P FieldOfLaw))

(subAttribute AttorneyGeneral Attorney)
(instance AttorneyGeneral GovernmentOfficer)
(documentation AttorneyGeneral EnglishLanguage "The head law officer of a &%GeopoliticalArea, 
usually a &%Nation or &%StateOrProvince.")

(subAttribute PublicDefender Attorney)
(instance PublicDefender GovernmentOfficer)
(documentation PublicDefender EnglishLanguage "An &%Attorney who defends criminal cases for a 
&%GovernmentOrganization.")

(subAttribute PublicProsecutor Attorney)
(instance PublicProsecutor GovernmentOfficer)
(documentation PublicProsecutor EnglishLanguage "An &%Attorney who prosecutes criminal cases for 
a &%GovernmentOrganization.")

(subAttribute Judge Attorney)
(instance Judge GovernmentOfficer)
(documentation Judge EnglishLanguage "An &%Attorney who has the power of deciding legal cases.")

(=>
  (and
    (instance ?J Judge)
    (occupiesPosition ?P ?J ?O))
  (modalAttribute 
    (exists (?D) 
      (and 
        (instance ?D LegalDecision)
        (agent ?D ?P))) Permission)) 

(=>
  (and
    (instance ?J Judge)
    (occupiesPosition ?P ?J ?O))
  (instance ?O JudicialOrganization))

(subAttribute Professor Teacher)
(documentation Professor EnglishLanguage "The &%Profession of being a &%teacher at a 
&%PostSecondarySchool.")

(=>
   (and
      (instance ?PROF Professor)
      (occupiesPosition ?PERSON ?PROF ?ORG))
   (and
      (teacher ?PROF ?ORG)
      (instance ?ORG PostSecondarySchool))) 

(instance MedicalDoctor Profession)
(documentation MedicalDoctor EnglishLanguage "The &%Profession of being a medical doctor, 
i.e. having attended medical school and being licensed to practice medicine.")

(=>
   (attribute ?DOCTOR MedicalDoctor)
   (exists (?PROCESS1 ?PROCESS2)
      (and
         (subclass ?PROCESS1 DiagnosticProcess)
         (subclass ?PROCESS2 TherapeuticProcess)
         (capability ?PROCESS1 agent ?DOCTOR)
         (capability ?PROCESS2 agent ?DOCTOR))))

(subAttribute Surgeon MedicalDoctor)
(documentation Surgeon EnglishLanguage "The &%Profession of being a surgeon, i.e. being a 
medical doctor who specializes in performing surgical operations.")

(documentation Circumision EnglishLanguage "&%Removing the foreskin of the penis.  This is
usually performed on infants, but is occasionally performed on adolescents
and adults, either for medical reasons, or after religious conversion. It is
often conducted as a religious rite, since it is prescribed by both the
&%Jewish and &%Muslim religions, although it is prevalent also as a social
norm in the &%UnitedStates and other &%Nations.")
(subclass Circumision Removing)
(subclass Circumision Surgery)

(instance NewsReporter Profession)
(documentation NewsReporter EnglishLanguage "The &%Profession of being a news reporter, i.e. 
investigating and reporting, in a publication or broadcast program, current 
events.")
(=>
  (attribute ?H NewsReporter)
  (exists (?T ?EV)
    (and
      (instance ?EV Process)
      (overlapsTemporally 
        (WhenFn ?EV) 
        (WhenFn ?H))
      (authors ?H ?T)
      (refers ?T ?EV))))

(subAttribute ArtCritic Journalist)
(documentation ArtCritic EnglishLanguage "The &%Profession of reporting and critiquing current 
&%ArtWorks.")

(=>
  (attribute ?H ArtCritic)
  (exists (?T ?AW)
    (and
      (instance ?AW ArtWork)
      (authors ?H ?T)
      (refers ?T ?AW))))

(instance Accountant Profession)
(documentation Accountant EnglishLanguage "The &%Profession of being an &%Accountant.")
(=>
  (attribute ?X Accountant)
  (capability Calculating agent ?X))

(subclass PensionPlan FinancialAccount)
(documentation PensionPlan EnglishLanguage "A qualified retirement plan set up by a 
corporation, labor union, government, or other organization for its 
employees.  Examples include profit-sharing plans, stock bonus and 
employee stock ownership plans, thrift plans, target benefit plans, 
money purchase plans, and defined benefit plans.")

(=>
  (and
    (instance ?P PensionPlan)
    (accountHolder ?P ?A)
    (employs ?O ?A))
  (financialAccount ?P ?O))

(subclass ProfitSharingPlan PensionPlan)
(documentation ProfitSharingPlan EnglishLanguage "An arrangement in which an employer 
shares its profits with its employees.  The compensation can be stocks, 
bonds or cash, and can be immediate or deferred until retirement.")

(=>
  (and
    (instance ?PSP ProfitSharingPlan)
    (accountHolder ?PSP ?H)
    (employs ?O ?H)
    (profit ?A ?P)
    (agent ?A ?O))
    (exists (?C ?PAY)
      (and
        (instance ?PAY FinancialTransaction)
        (transactionAmount ?PAY ?C)
        (lessThan ?C ?P)
        (destination ?PAY ?PSP))))

(instance Literature FieldOfStudy)
(documentation Literature EnglishLanguage "The study of literature, i.e. instances of &%FictionalText 
that are regarded as having special merit.")

(instance Mathematics FieldOfStudy)
(documentation Mathematics EnglishLanguage "The &%FieldOfStudy dealing with quantities and their 
relations to one another.")

(instance InteriorDesign FieldOfStudy)
(documentation InteriorDesign EnglishLanguage "The &%FieldOfStudy of designing the interiors of 
&%Buildings.")

(subclass Science FieldOfStudy)
(documentation Science EnglishLanguage "Any &%FieldOfStudy which tests theories on the basis of 
careful observations and/or experiments and which has a cumulative body of results.") 

(subclass SocialScience Science)
(documentation SocialScience EnglishLanguage "Any &%Science which studies human behavior, either in 
the aggregate, as do, for example, &%Economics and &%Linguistics, or with respect to 
the individual, as does &%Psychology.")

(instance PoliticalScience SocialScience)
(documentation PoliticalScience EnglishLanguage "The field of political science.")

(instance Anthropology SocialScience)
(documentation Anthropology EnglishLanguage "The field of anthropology.")

(instance Architecture FieldOfStudy)
(documentation Architecture EnglishLanguage "The &%FieldOfStudy of designing &%Buildings, i.e. 
creating &%Blueprints for &%Buildings.")

(=>
  (hasExpertise ?H Architecture)
  (exists (?D ?P ?B)
    (and
      (instance ?D ContentDevelopment)
      (agent ?D ?H)
      (result ?D ?P)
      (instance ?P Blueprint)
      (represents ?P ?B)
      (instance ?B Building))))

(instance Economics SocialScience)
(documentation Economics EnglishLanguage "The field of economics.")

(instance Linguistics SocialScience)
(documentation Linguistics EnglishLanguage "The field of linguistics.")

(instance Psychology SocialScience)
(documentation Psychology EnglishLanguage "The field of psychology.")

(instance Biology Science)
(documentation Biology EnglishLanguage "The study of the classification, development, and 
functioning of &%Organisms.")

(subField Physiology Biology)
(instance Physiology Science)
(documentation Physiology EnglishLanguage "The part of &%Biology dealing with the functioning of 
&%Organisms.")

(subField MedicalScience Biology)
(instance MedicalScience Science)
(documentation MedicalScience EnglishLanguage "The field of medicine.")

(instance Chemistry Science)
(documentation Chemistry EnglishLanguage "The study of the compositions, properties, and 
reactions of &%Substances.")

(instance Physics Science)
(documentation Physics EnglishLanguage "The study of matter and energy and their relations.")

(instance Engineering Science)
(documentation Engineering EnglishLanguage "The application of instances of &%Science to the solution 
of practical problems, i.e. the creation of various forms of technology.")

(subField Electronics Physics)
(subField Electronics Engineering)
(instance Electronics FieldOfStudy)
(documentation Electronics EnglishLanguage "The branch of &%Physics that deals with the theory 
and applications of electron emissions.")

(instance Theology FieldOfStudy)
(documentation Theology EnglishLanguage "The systematic study of religion.")
(=>
  (instance ?Y ReligiousProcess)
  (refers Theology ?Y))

(instance MilitaryScience FieldOfStudy)
(documentation MilitaryScience EnglishLanguage "The study of the principles of war.")

(instance History FieldOfStudy)
(documentation History EnglishLanguage "The recording and interpretation of past events involving 
&%Humans, including political events and cultural practices.")

(instance Philosophy FieldOfStudy)
(documentation Philosophy EnglishLanguage "The study of first principles, including epistemology, 
metaphysics, and ethics.")

(instance FieldOfLaw FieldOfStudy)
(documentation FieldOfLaw EnglishLanguage "The study of legal principles and the framework of national 
and/or international laws.")

(subrelation subField subProposition)
(instance subField TransitiveRelation)
(instance subField IrreflexiveRelation)
(domain subField 1 FieldOfStudy)
(domain subField 2 FieldOfStudy)
(documentation subField EnglishLanguage "(&%subField ?FIELD1 ?FIELD2) means that ?FIELD1 is a proper 
part of the &%FieldOfStudy ?FIELD2.  For example, &%Physiology is a &%subField of 
&%Biology.")

(instance hasExpertise BinaryPredicate)
(domain hasExpertise 1 Human)
(domain hasExpertise 2 FieldOfStudy)
(documentation hasExpertise EnglishLanguage "(&%hasExpertise ?PERSON ?FIELD) means that ?PERSON has 
studied the &%FieldOfStudy ?FIELD and is regarded as an expert.")

(=>
   (hasExpertise ?PERSON ?FIELD)
   (exists (?LEARN)
      (and
         (instance ?LEARN Learning)
         (agent ?LEARN ?PERSON)
         (realization ?LEARN ?FIELD))))

(instance hasOccupation BinaryPredicate)
(domain hasOccupation 1 Human)
(domainSubclass hasOccupation 2 IntentionalProcess)
(documentation hasOccupation EnglishLanguage "(&%hasOccupation ?PERSON ?WORK) means that ?PERSON 
engages in activities of the class ?WORK as a means of earning a living.")

(=>
   (hasOccupation ?PERSON ?WORK)
   (exists (?INST)
      (and
         (instance ?INST ?WORK)
         (instance ?INST Working)
         (agent ?INST ?PERSON))))

(subclass Bleeding AutonomicProcess)
(documentation Bleeding EnglishLanguage "The release of &%Blood from an &%Animal in response 
to an &%Injuring of some sort.")

(=>
   (instance ?BLEED Bleeding)
   (exists (?INJURY)
      (and
         (instance ?INJURY Injuring)
         (causes ?INJURY ?BLEED))))

(=>
   (and
      (instance ?BLEED Bleeding)
      (experiencer ?BLEED ?ANIMAL))
   (instance ?ANIMAL Animal))

(=>
   (and
      (instance ?BLEED Bleeding)
      (experiencer ?BLEED ?ANIMAL))
   (exists (?BLOOD)
      (and
         (instance ?BLOOD Blood)
         (holdsDuring (BeginFn (WhenFn ?BLEED)) (part ?BLOOD ?ANIMAL))
         (holdsDuring (EndFn (WhenFn ?BLEED)) (not (part ?BLOOD ?ANIMAL))))))
   
(subclass Blushing AutonomicProcess)
(documentation Blushing EnglishLanguage "The process of turning red in response to a 
stimulus which has a deep emotional effect.")
(=>
  (and
    (instance ?B Blushing)
    (experiencer ?B ?A))
  (exists (?T1 ?T2 ?S1 ?S2)
    (and
      (instance ?S1 EmotionalState)
      (instance ?S2 EmotionalState)
      (not
        (equal ?S1 ?S2))
      (holdsDuring ?T1
        (attribute ?A ?S1))
      (holdsDuring ?T2
        (attribute ?A ?S2))
      (starts (WhenFn ?B) ?T2)
      (finishes (WhenFn ?B) ?T1))))

(subclass Aborting Removing)
(documentation Aborting EnglishLanguage "&%Removing a human fetus from a &%Pregnant woman 
in such a way that the fetus cannot survive.")

(=>
   (and
      (instance ?A Aborting)
      (patient ?A ?W)
      (instance ?W Woman))
   (and
      (holdsDuring 
        (BeginFn (WhenFn ?A)) 
        (attribute ?W Pregnant))
      (holdsDuring 
        (EndFn (WhenFn ?A)) 
        (not (attribute ?W Pregnant)))))

(subclass ClosingContract Committing)
(documentation ClosingContract EnglishLanguage "Completing a &%Contract of some sort, 
e.g. the purchase of a house, closing a business deal, etc.")

(=>
   (and
      (instance ?CLOSE ClosingContract)
      (agent ?CLOSE ?AGENT)
      (destination ?CLOSE ?CONTRACT))
   (agreementMember ?CONTRACT ?AGENT))

(documentation financialAccount EnglishLanguage "A formal banking, brokerage, or business 
relationship established to provide for regular services, dealings, and 
other financial transactions.  (&%financialAccount ?ACCOUNT ?ORG) means that 
?ACCOUNT is a financial account opened at the &%FinancialCompany 
?ORG.")
(domain financialAccount 1 FinancialAccount)
(domain financialAccount 2 FinancialCompany)
(instance financialAccount BinaryPredicate)
(subrelation financialAccount agreementMember)

(subclass Opening Motion) 
(relatedInternalConcept Opening Closing) 
(documentation Opening EnglishLanguage "The &%Class of &%Processes where an aperture is 
created in an &%Object.  Note that the aperture may be created intentionally, 
as when one opens a door, or unintentionally, as when the ground ruptures 
in a seismic event.") 

(subclass Closing Motion) 
(documentation Closing EnglishLanguage "The &%Class of &%Processes where an aperture is 
closed in an &%Object.")

(subclass Diet Plan)
(documentation Diet EnglishLanguage "A &%Plan regarding what one is allowed to eat.")
(=>
  (and
    (instance ?D Diet)
    (realization ?P ?D))
  (exists (?E)
    (and
      (subProcess ?E ?P)
      (instance ?E Eating))))

(subclass PreparedFood (FoodForFn Animal))
(subclass PreparedFood Object)
(documentation PreparedFood EnglishLanguage "&%Food that is the result of &%Cooking.")

(=>
   (instance ?FOOD PreparedFood)
   (exists (?COOK)
      (and
         (instance ?COOK Cooking)
         (result ?COOK ?FOOD))))

(subclass RawFood (FoodForFn Organism))
(subclass RawFood Object)
(documentation RawFood EnglishLanguage "&%Food that is not the result of &%Cooking.")

(=>
  (instance ?F RawFood)
  (not 
    (exists (?C)
      (and
        (instance ?C Cooking)
        (result ?C ?F)))))

(subclass Baking Cooking)
(subclass Baking Heating)
(documentation Baking EnglishLanguage "Any instance of &%Cooking where the instrument 
is an &%Oven.")
(=>
  (instance ?B Baking)
  (exists (?O)
    (and
      (instance ?O Oven)
      (instrument ?B ?O))))

(subclass BreadOrBiscuit PreparedFood)
(documentation BreadOrBiscuit EnglishLanguage "&%Food that consists largely of grain flour 
and water.  Note that this class covers crackers, cookies, as well as any 
self-connected instance of bread, whether it is a loaf, a slice, a chunk 
of bread, etc.")
(=>
  (instance ?B BreadOrBiscuit)
  (exists (?D ?BAKE)
    (and
      (instance ?D Dough)
      (instance ?BAKE Baking)
      (resource ?BAKE ?D)
      (result ?BAKE ?B))))

(subclass Sandwich PreparedFood)
(documentation Sandwich EnglishLanguage "Any &%Food which consists of two or more pieces 
of bread and some sort of filling between the two pieces of bread.")

(=>
  (instance ?S Sandwich)
  (exists (?B1 ?B2 ?F)
    (and
      (instance ?B1 BreadOrBiscuit)
      (instance ?B2 BreadOrBiscuit)
      (instance ?F (FoodForFn Human))
      (between ?B1 ?F ?B2)
      (not (equal ?B1 ?B2))
      (not (equal ?B1 ?F))
      (not (equal ?B2 ?F))
      (part ?B1 ?S)
      (part ?B2 ?S)
      (part ?F ?S))))

(subclass Dough PreparedFood)
(subclass Dough Mixture)
(documentation Dough EnglishLanguage "A &%Mixture of &%Flour, &%Water, and possibly 
other ingredients (such as &%Butter and &%Salt), which is used in 
making &%BreadOrBiscuits.")
(=>
  (instance ?D Dough)
  (exists (?F ?W)
    (and
      (instance ?F Flour)
      (instance ?W Water)
      (part ?F ?D)
      (part ?W ?D))))

(subclass Butter PreparedFood)
(subclass Butter DairyProduct)

(documentation Butter EnglishLanguage "An emulsion of fat which is produced by churning &%Milk.")
(=>
  (instance ?B Butter)
  (exists (?C ?M)
    (and
      (instance ?C Cooking)
      (resource ?C ?M)
      (instance ?M Milk)
      (result ?C ?B))))

(subclass CerealGrain Seed)
(subclass CerealGrain (FoodForFn Animal))
(documentation CerealGrain EnglishLanguage "Any &%Seed which is produced by the cereal grasses, 
e.g. rice, corn, wheat, etc.")

(subclass RiceGrain CerealGrain)
(documentation RiceGrain EnglishLanguage "A &%CerealGrain which has short and long grain varieties 
and which is usually prepared for eating by steaming.")

(subclass WheatGrain CerealGrain)
(documentation WheatGrain EnglishLanguage "A long &%CerealGrain which is produced by certain grasses and 
which is used to make &%BreadOrBiscuits.")

(subclass Pea Seed)
(subclass Pea (FoodForFn Animal))
(documentation Pea EnglishLanguage "The edible &%Seed of a pea plant.")

(subclass Radish RootVegetable)
(documentation Radish EnglishLanguage "A &%PlantRoot that is often used in salads.")

(subclass Flour PreparedFood)
(subclass Flour Powder)
(documentation Flour EnglishLanguage "A &%Powder that is prepared from &%CerealGrain, e.g. 
wheat flour, rice flour, etc.  Flour is most often used in the making 
of &%BreadOrBiscuits.")
(=>
  (instance ?F Flour)
  (exists (?P ?C)
    (and
      (instance ?P IntentionalProcess)
      (resource ?P ?C)
      (instance ?C CerealGrain)
      (result ?P ?F))))

(subclass Honey AnimalSubstance)
(subclass Honey (FoodForFn Animal))
(documentation Honey EnglishLanguage "An edible, sweet &%Substance produced by some species of 
&%Bees.")

(=>
  (instance ?H Honey)
  (exists (?P ?B)
    (and
      (instance ?P PhysiologicProcess)
      (agent ?P ?B)
      (instance ?B Bee)
      (result ?P ?H))))

(subclass Coconut FruitOrVegetable)
(subclass Coconut (FoodForFn Animal))
(documentation Coconut EnglishLanguage "A &%FruitOrVegetable that is produced by a type of palm 
and has an edible white meat.")

(subclass LemonFruit CitrusFruit)
(subclass LemonFruit (FoodForFn Animal))
(documentation LemonFruit EnglishLanguage "A citrus &%FruitOrVegetable that has a &%Yellow skin 
and a tart, acidic flavor.")

(subclass OrangeFruit FruitOrVegetable)
(subclass OrangeFruit (FoodForFn Animal))
(documentation OrangeFruit EnglishLanguage "A citrus &%FruitOrVegetable that has an &%OrangeColor 
skin and a sweet, acidic flavor.")

(subclass MaizeGrain CerealGrain)
(documentation MaizeGrain EnglishLanguage "A &%CerealGrain which is found on long ears of a &%Plant 
native to the Americas.")
(subclass MaizeGrain GroceryProduce)

(subclass SoupStock PreparedFood)
(documentation SoupStock EnglishLanguage "&%Food which is prepared by reducing &%Meat 
and/or &%FruitOrVegetables to a translucent broth which can be used as 
a base for soups or sauces.") 

(subclass Coffee Beverage)
(subclass Coffee PreparedFood)
(documentation Coffee EnglishLanguage "A &%Beverage which is prepared by infusing ground, 
roasted coffee beans into hot water.")

(subclass Apple FruitOrVegetable)
(subclass Apple (FoodForFn Animal))
(documentation Apple EnglishLanguage "A &%FruitOrVegetable that has a tart to sweet taste and 
a spherical shape.")

(subclass DateFruit FruitOrVegetable)
(subclass DateFruit (FoodForFn Animal))
(documentation DateFruit EnglishLanguage "A &%FruitOrVegetable that is produced by the date 
palm.")

(subclass PearFruit FruitOrVegetable)
(subclass PearFruit (FoodForFn Animal))
(documentation PearFruit EnglishLanguage "A &%FruitOrVegetable that has a thick skin and the 
shape of a tear drop.")

(subclass Avocado FruitOrVegetable)
(subclass Avocado (FoodForFn Animal))
(documentation Avocado EnglishLanguage "A &%FruitOrVegetable that is shaped like a pear and 
has a dark green skin and a rich meat.")

(subclass Ion PureSubstance)
(documentation Ion EnglishLanguage "An electronically charged &%Atom or &%Molecule.  In 
other words, a &%PureSubstance that has lost one of its &%Electrons.")

(subclass FreeAtom Atom)
(documentation FreeAtom EnglishLanguage "Any &%Atom which is not part of a &%Molecule.")
(=>
  (instance ?A FreeAtom)
  (not 
    (exists (?M)
      (and
        (instance ?M Molecule)
        (part ?A ?M)))))

(instance protonNumber BinaryPredicate)
(instance protonNumber SingleValuedRelation)
(instance protonNumber TotalValuedRelation)
(domain protonNumber 1 PureSubstance)
(domain protonNumber 2 PositiveInteger)
(documentation protonNumber EnglishLanguage "(&%protonNumber ?SUBSTANCE ?NUMBER) means that 
the &%PureSubstance ?SUBSTANCE has the number of &%Protons ?NUMBER.")

(=> 
   (protonNumber ?SUBSTANCE ?NUMBER) 
   (=> 
      (and 
         (part ?ATOM ?SUBSTANCE)
         (or 
            (instance ?ATOM Atom)
            (instance ?ATOM Molecule))) 
      (equal ?NUMBER (CardinalityFn (KappaFn ?PROTON 
                                       (and 
                                          (part ?PROTON ?ATOM) 
                                          (instance ?PROTON Proton))))))) 

(instance electronNumber BinaryPredicate)
(instance electronNumber SingleValuedRelation)
(instance electronNumber TotalValuedRelation)
(domain electronNumber 1 PureSubstance)
(domain electronNumber 2 PositiveInteger)
(documentation electronNumber EnglishLanguage "(&%electronNumber ?SUBSTANCE ?NUMBER) means that 
the &%PureSubstance ?SUBSTANCE has the number of &%Electrons ?NUMBER.")

(=> 
   (electronNumber ?TYPE ?NUMBER) 
   (=> 
      (and 
         (part ?ATOM ?SUBSTANCE)
         (or 
            (instance ?ATOM Atom)
            (instance ?ATOM Molecule))) 
      (equal ?NUMBER 
        (CardinalityFn 
          (KappaFn ?ELECTRON 
            (and 
              (part ?ELECTRON ?ATOM) 
              (instance ?ELECTRON Electron))))))) 

(subclass AtomicGroup CompoundSubstance)
(documentation AtomicGroup EnglishLanguage "Two or more &%Atoms that are bound together and 
comprise part of a &%Molecule.")
(=>
  (instance ?G AtomicGroup)
  (exists (?ATOM1 ?ATOM2)
    (and
      (instance ?ATOM1 Atom)
      (instance ?ATOM2 Atom)
      (not (equal ?ATOM1 ?ATOM2))
      (part ?ATOM1 ?G)
      (part ?ATOM2 ?G))))

(=>
  (instance ?G AtomicGroup)
  (exists (?M)
    (and
      (instance ?M Molecule)
      (part ?G ?M))))

(subclass Ambush Maneuver)
(documentation Ambush EnglishLanguage "Any &%Maneuver in a &%ViolentContest where one 
&%contestParticipant attempts to conceal himself from another 
contestParticipant so that he can &%Attack the other 
&%contestParticipant.") 

(=>
  (and
    (instance ?AMBUSH Ambush)
    (agent ?AMBUSH ?AG))
  (exists (?C)
    (and
      (instance ?C Concealing)
      (subProcess ?C ?AMBUSH)
      (hasPurpose ?C 
        (exists (?ATTACK) 
          (and 
            (instance ?ATTACK Attack) 
            (agent ?ATTACK ?AG)))))))

(subclass Blockade Maneuver)
(documentation Blockade EnglishLanguage "Any &%Maneuver in a &%Contest where one 
&%contestParticipant attempts to deny access to something that is wanted 
by another &%contestParticipant.") 

(=>
  (and
    (instance ?B Blockade)
    (instance ?C Contest)
    (subProcess ?B ?C)
    (contestParticipant ?C ?A1)
    (agent ?B ?A1))
  (exists (?C1 ?A2)
    (and
      (prevents ?C1 Translocation)
      (instance ?B ?C1)
      (contestParticipant ?C ?A2)
      (not 
        (equal ?A1 ?A2))
      (desires ?A2 
        (exists (?I) 
          (and 
            (instance ?I ?C1)
            (agent ?I ?A1) 
            (subProcess ?I ?C)))))))

(subclass MilitaryManeuver Maneuver)
(documentation MilitaryManeuver EnglishLanguage "Any &%Maneuver which takes place in a &%Battle 
and which is intended to secure a tactical advantage for one of the agents of 
the &%Battle.")

(=>
  (instance ?M MilitaryManeuver)
  (exists (?B)
    (and
      (instance ?B Battle)
      (subProcess ?M ?B))))

(subclass Arson Combustion)
(subclass Arson Destruction)
(documentation Arson EnglishLanguage "An act where an agent sets something which it does not possess 
on fire in order to destroy it or its contents.")

(subclass Bombing Destruction)
(documentation Bombing EnglishLanguage "An act of &%Destruction where the thing destroyed
is destroyed by means of an explosive device.")

(subclass Bomb Weapon)
(subclass Bomb ExplosiveDevice)
(documentation Bomb EnglishLanguage "A weapon that explodes in order to cause damage.")

(=>
  (instance ?X Bombing)
  (exists (?BOMB)
    (and
      (instance ?BOMB Bomb)
      (instrument ?X ?BOMB))))

(subclass CarBombing Bombing)
(documentation CarBombing EnglishLanguage "A car bombing is an attack in which a car is
used as the delivery mechanism for a bomb.  The car is usually destroyed
in the blast if the bomb detonates successfully.")

(=>
  (instance ?X CarBombing)
  (exists (?BOMB ?CAR)
    (and
      (instance ?BOMB Bomb)
      (instance ?CAR TransportationDevice)
      (instrument ?X ?CAR)
      (instrument ?X ?BOMB)
      (connected ?CAR ?BOMB))))

(subclass ChemicalAttack ViolentContest)
(documentation ChemicalAttack EnglishLanguage "An attack against people or property in which
a chemical agent is used as the active ingredient of the attack.")

(=>
  (instance ?X ChemicalAttack)
  (exists (?CHEM)
    (and
      (instance ?CHEM Substance)
      (instrument ?X ?CHEM))))
      
(subclass HandgrenadeAttack Bombing)
(documentation HandgrenadeAttack EnglishLanguage "An attack in which a &%HandGrenade is 
employed as an explosive device for the purpose of &%Destruction.
A case in which a hand grenade is used as a blunt striking object in
an attack would not be in this class.")

(subclass HandGrenade Bomb)
(documentation HandGrenade EnglishLanguage "A hand grenade is a small explosive device
with a simple trigger mechanism, usually lasting a few seconds, designed
to be propelled by a human to its target.")

(=>
  (instance ?X HandGrenade)
  (and
    (measure ?X (MeasureFn ?N PoundMass))
    (lessThan ?N 10)))
    
(=>
  (instance ?X HandgrenadeAttack)
  (exists (?HG)
    (and
      (instance ?HG HandGrenade)
      (instrument ?X ?HG))))
      
(subclass Hijacking Robbing)
(documentation Hijacking EnglishLanguage "&%Robbing a &%Vehicle and then driving
or piloting it away.")

(=>
  (instance ?H Hijacking)
  (exists (?V)
    (and
      (instance ?V Vehicle)
      (patient ?H ?V))))

(=>
  (instance ?H Hijacking)
  (exists (?D)
    (and
      (subProcess ?D ?H)
      (instance ?D Driving)))) 

(=>
  (instance ?X Hijacking)
  (exists (?V ?A)
    (and 
      (agent ?X ?A)
      (instance ?V TransportationDevice)
      (holdsDuring 
        (ImmediatePastFn 
          (WhenFn ?X))
        (not 
          (possesses ?A ?V)))
      (holdsDuring
        (WhenFn ?X)
        (possesses ?A ?V)))))
        
(subclass HostageTaking UnilateralGetting)
(documentation HostageTaking EnglishLanguage "An event in which one agent keeps another
against his or her will, in order to force it or a third agent to 
accede to some demand.  The hostages are typically kept in the place
where they are first taken hostage, in contrast to a &%Kidnapping.
Hostage taking often involves a number of people (although it may be
just one), again, in contrast to a &%Kidnapping.")

(=>
  (instance ?X HostageTaking)
  (exists (?A)
    (and
      (instance ?A SentientAgent)
      (patient ?X ?A))))
           
(subclass IncendiaryDeviceAttack Combustion)
(subclass IncendiaryDeviceAttack ViolentContest)
(documentation IncendiaryDeviceAttack EnglishLanguage "An attack in which an agent
uses a device which causes fire in order to destroy life or property.")

(subclass Infiltration JoiningAnOrganization)
(documentation Infiltration EnglishLanguage "An event in which an agent joins an
organization under false pretenses.  The objective of such an act
is often to gather information helpful to a rival organization.")

(subclass Kidnapping Robbing)
(documentation Kidnapping EnglishLanguage "An event in which one agent keeps another
against his or her will, in order to force it or a third agent to 
accede to some demand.  The kidnapped party is normally removed from
the location where first attacked.  In contrast to a &%HotageTaking,
a kidnapping normally involves an attack on one, or a very small 
number of people, such as a couple.")

(=>
  (and
    (instance ?X Kidnapping)
    (instance ?G GroupOfPeople)
    (memberCount ?G ?N)
    (patient ?X ?G))
  (lessThan ?N 3))
      
(=>
  (instance ?X Kidnapping)
  (exists (?A ?LOC1 ?LOC2)
    (and
      (instance ?A SentientAgent)
      (instance ?LOC1 GeographicArea)
      (instance ?LOC2 GeographicArea)
      (not
        (equal ?LOC1 ?LOC2))
      (holdsDuring
        (ImmediatePastFn
          (WhenFn ?X))
        (located ?A ?LOC1))
      (holdsDuring
        (WhenFn ?X)
        (located ?A ?LOC2)))))

(subclass KnifeAttack ViolentContest)
(documentation KnifeAttack EnglishLanguage "An attack in which a knife is used.")

(subclass Knife CuttingDevice)

(documentation Knife EnglishLanguage "A sharp object used for cutting.  The object must 
have at least single blade or major protrusion which may be sharp at its 
end as well.  It may be sharp on both sides, or just one.  Unlike an awl 
it has a sharp edge rather than a point.  Unlike scissors, it is a single 
blade without additional articulated parts.  Unlike an axe, a knife is 
well-designed for slicing rather than chopping, although a heavy knife 
such as a broadsword can also be used for chopping, whereas a non-knife is 
not well designed for slicing meat for example.  A knife can be small like 
a pocket knife, or large like a two-handed broadsword.  A knife may also 
include other protrusions such as in the split swords and trident-like 
objects (that also possess a long sharp edge) in Chinese weaponry.") 

(subclass CuttingDevice Device)
(documentation CuttingDevice EnglishLanguage "Any &%Device whose purpose is &%Cutting something 
else.  This class covers knives of all times, axes, saws, razors, chisels etc.")
(=>
  (instance ?X CuttingDevice)
  (capability Cutting ?X instrument))

(=>
  (instance ?X KnifeAttack)
  (exists (?HG)
    (and
      (instance ?HG Knife)
      (instrument ?X ?HG))))

(subclass LetterBombAttack Bombing)
(documentation LetterBombAttack EnglishLanguage "An attack with a bomb that is
concealed in a letter or package, which is typically designed to 
explode upon opening.")
                               
(subclass Lynching Killing)
(documentation Lynching EnglishLanguage "A lynching is a form of &%Killing conducted under 
a thin guise of legitimacy by a vigilante group.  It may be a purge of an 
individual from the group who is perceived to have gone astray, or it may 
be the summary execution of an individual outside the group that the group 
has determined has violated some law or code, either of the group, or the 
larger society.  A lynching is distinguished from a legal execution in 
that the laws of a government are not followed in carrying out the 
killing.  A lynching is distinguished from a murder in that there is the 
pretense of some process carried out by a group in the name of their own 
group or a higher power.") 

(subclass MortarAttack Bombing)
(documentation MortarAttack EnglishLanguage "An attack in which a &%Mortar weapon is
used.")

(subclass Mortar Weapon)
(documentation Mortar EnglishLanguage "A mortar is a bomb that is shot at a location.  A 
howitzer is a weapon that fires mortars.  The mortar may not include the 
propelling charge that provides the motive force for delivering it to its 
target, which distinguishes it from a rocket.  A mortar is distinguished 
from other bombs in that it is propelled rather than exploded in place, or 
dropped, or propelled by human power.") 
   
(=>
  (instance ?X MortarAttack)
  (exists (?MA)
    (and
      (instance ?MA Mortar)
      (instrument ?X ?MA))))
  
(subclass RocketMissileAttack Bombing)
(documentation RocketMissileAttack EnglishLanguage "An attack in which a rocket or
missile is used.")

(subclass Rocket Projectile)
                   
(subclass Stoning ViolentContest)
(documentation Stoning EnglishLanguage "An act where a victim or victims is attacked
with stones.  The stones may either be placed on top of a victim in
order to cause death by pressure and suffocation, in which case the act
is typically part of a ritualized legal sentence, or where the victim
is pelted with stones causing injury or death by trauma.  The intent
of such an act is usually the death of the victim.")
          
(=>
  (instance ?X Stoning)
  (exists (?ST)
    (and
      (instance ?ST Rock)
      (instrument ?X ?ST))))  

(subclass Marble Rock)
(documentation Marble EnglishLanguage "A metamorphic &%Rock that is used in &%Constructing 
and in creating &%Sculptures.")

(subclass SuicideBombing Bombing)
(subclass SuicideBombing Killing)
(documentation SuicideBombing EnglishLanguage "A bomb attack in which the bomber
intends to blow himself up during the course of the attack.  The bomber
is the delivery mechanism for the explosive and the bomb is typically
in close contact with the bomber, such as being strapped around his
torso.  This action is distinguished from actions in which the bomber
merely happens to be blown up by his own bomb in that the bomber
knows that he is committing suicide.")

(=>
  (instance ?X SuicideBombing)
  (exists (?A)
    (and
      (agent ?X ?A)
      (holdsDuring
        (FutureFn
          (WhenFn ?X))
        (attribute ?A Dead)))))
        
(=>
  (and
    (instance ?X SuicideBombing)
    (agent ?X ?P))
  (believes ?P
    (holdsDuring
      (FutureFn
        (WhenFn ?X))
      (attribute ?P Dead))))
          
(subclass Vandalism Destruction)
(documentation Vandalism EnglishLanguage "An event where the objective of the agent
is to damage or destroy some property of another agent.  This is
distinguished from acts where the prime objective is damage or loss
of life of another agent.")
                          
(=>
  (instance ?X Vandalism)
  (not
    (exists (?A)
      (and
        (instance ?A Agent)
        (patient ?X ?A)))))
      
(subclass VehicleAttack ViolentContest)
(documentation VehicleAttack EnglishLanguage "An event in which a vehicle is used
by an agent in order to inflict injury, death or damage to people
or property.")
               
(=>
  (instance ?X VehicleAttack)
  (exists (?V)
    (and
      (instance ?V TransportationDevice)
      (instrument ?X ?V))))

(subclass BusStop GeographicArea)
(documentation BusStop EnglishLanguage "An area, often, though not necessarily with
seats or some kind of minimal shelter, where people gather to meet and
board a bus.  It must border a road.")
(=>
  (instance ?X BusStop)
  (hasPurpose ?X
    (exists (?B ?BO ?D)
      (and
        (instance ?B Bus)
        (instance ?BO Boarding)
        (instance ?D Deboarding)
        (located ?BO ?X)
        (located ?D ?X)
        (patient ?BO ?B)
        (patient ?D ?B)))))

(=>
  (instance ?X BusStop)
  (exists (?R)
    (and
      (instance ?R Roadway)
      (meetsSpatially ?X ?R))))

(subclass Burrow Cave)
(documentation Burrow EnglishLanguage "A small &%Cave created by an &%Animal for the purpose 
of inhabiting it.")
(=>
  (instance ?B Burrow)
  (exists (?P ?A)
    (and
      (instance ?P Process)
      (agent ?P ?A)
      (instance ?A Animal)
      (not 
        (instance ?A Human))
      (result ?P ?B))))

(=>
  (instance ?B Burrow)
  (exists (?A)
    (hasPurpose ?B
      (inhabits ?A ?B))))

(instance BusinessPerson OccupationalRole)
(documentation BusinessPerson EnglishLanguage "Any of a class of people in a professional 
occupation such as banking, finance, management, or engineering.  This is 
distinguished from blue collar jobs that primarily involve manual labor 
rather than thought as the effort expended to derive remuneration.  It is 
also distinguished from professions that may be outwardly similar but are 
done in a non-profit organization.") 

(instance Celebrity SocialRole)
(documentation Celebrity EnglishLanguage "Anyone who is known by a large number of people,
either explicitly by name, or by action.")

(=>
  (attribute ?X Celebrity)
  (greaterThan 
    (CardinalityFn 
      (KappaFn ?A
        (knows ?A 
          (exists (?P)
            (equal ?P ?X)))))
    1000))
      
(instance Knight SocialRole)
(documentation Knight EnglishLanguage "A &%SocialRole that is assigned by the 
&%UnitedKingdom to persons for reasons of personal merit.")

(instance Musician SocialRole)
(documentation Musician EnglishLanguage "A Musician is someone who is capable of giving a &%MusicalPerformance.
This includes amateurs and others who don't formally make a living at the activity
or choose it as a profession.")

(=>
  (attribute ?X Musician)
  (hasSkill MusicalPerformance ?X))

(instance Veteran SocialRole)
(documentation Veteran EnglishLanguage "The &%Attribute that applies to someone who was a 
&%Soldier at one time, but is not currently a &%Soldier.")

(=>
  (holdsDuring ?T1 
    (attribute ?H Veteran))
  (exists (?T2 ?P)
    (and
      (temporalPart ?T2 
        (PastFn (WhenFn ?T1)))
      (instance ?P Soldier)
      (holdsDuring ?T2 
        (attribute ?H ?P))
      (not 
        (holdsDuring ?T1 
	  (attribute ?H ?P)))))) 

(subclass Checkpoint GeographicArea)
(documentation Checkpoint EnglishLanguage "An area on or very near a border, usually along 
a road connecting two regions, where &%MilitaryPersons or &%Police 
restrict the flow of traffic in order to extract tarrifs, deny movement to 
certain kinds of people or goods, or other enforement actions.  Because of 
the power relationship involved, such areas are often the site of illegal 
activities conducted by the officials in order to extract favors or 
bribes.") 

(=>
  (instance ?X Checkpoint)
  (or
    (exists (?ROAD)
      (and
        (instance ?ROAD Road)
        (orientation ?ROAD ?X Near)))
    (exists (?REG1 ?REG2)
      (orientation (BorderFn ?REG1 ?REG2) ?X Near)))) 
      
(subclass Road Region)
(documentation Road EnglishLanguage "A path along which vehicles travel.  It is typically,
although not necessarily, paved and intended for cars.")
(=>
  (instance ?X Road)
  (hasPurpose ?X
    (exists (?EV ?VEH)
      (and
        (instance ?EV Transportation)
        (located ?EV ?X)
        (instance ?VEH LandVehicle)
        (instrument ?EV ?VEH)))))
      
(contraryAttribute Civilian MilitaryPerson)
(subclass OccupationalRole Position)
(documentation OccupationalRole EnglishLanguage "A &%Role of a &%Human in a &%OrganizationalProcess.")

(subclass Civilian SocialRole)
(documentation Civilian EnglishLanguage "Someone who is not a member of an active
&%MilitaryOrganization.")

(=>
  (holdsDuring ?T1 (attribute ?X Civilian))
  (holdsDuring ?T1 (not (attribute ?X MilitaryPerson))))
    
(instance Widowed SocialRole)
(documentation Widowed EnglishLanguage "The &%Attribute that applies to someone who was 
married to someone who has died, and who has not remarried.")

(=>
  (holdsDuring ?NOW 
    (attribute ?P Widowed))
  (and
    (exists (?BEFORE ?SPOUSE)
      (and
        (earlier ?BEFORE ?NOW)
        (holdsDuring ?BEFORE
          (spouse ?SPOUSE ?P))      
        (holdsDuring ?NOW 
          (attribute ?SPOUSE Dead))))
    (not
      (exists (?OTHER)        
        (holdsDuring ?NOW
          (spouse ?OTHER ?P))))))

(subclass Convoy Collection)
(documentation Convoy EnglishLanguage "A group of vehicles that all are being driven
in formation (e.g., lines, rows, columns) to the same destination.
That destination may be an intermediate destination on the way to a
final destination for some of the vehicles however.  This is
distinguished from vehicles that have no common purpose, such as
traffic on a freeway.  This includes cases where some of the agents
driving the vehicles intend to reach a point but fail to do so.") 

;; Every convoy consists of at least two vehicles.  Note that, by
;; definition, the members of a Collection can change over time,
;; so a Convoy could still be considered the "same" Convoy even
;; if it has different members at different times.  The rule
;; below states that for any given Convoy, there exist at least
;; two Vehicles, and a TimeInterval during which the Vehicles are
;; members of the Convoy.

(=>
  (instance ?C Convoy)
  (exists (?V1 ?V2 ?TIME)
    (and
      (not (equal ?V1 ?V2))
      (instance ?V1 Vehicle)
      (instance ?V2 Vehicle)
      (instance ?TIME TimeInterval)
      (temporalPart ?TIME (WhenFn ?C))
      (holdsDuring ?TIME 
        (and
	  (member ?V1 ?C)
	  (member ?V2 ?C))))))
  
;; For every Vehicle that is a member of a Convoy, there exists
;; some Driving in which the Vehicle is the object driven (the
;; patient) and which temporally overlaps the Convoy. This is
;; intentionally weak, because (a) the vehicles in a convoy might
;; be parked, not moving (being driven), but the convoy would
;; nevertheless still exist with the vehicles stationary and (b)
;; a vehicle might join or leave a convoy, in which case the
;; driving event in which it is being driven only partially
;; overlaps the temporal extent of the convoy itself.
(=>
  (and 
    (instance ?C Convoy)
    (member ?VEH ?C))
  (exists (?DRIVE)
    (and
      (instance ?DRIVE Driving)
      (overlapsTemporally (WhenFn ?DRIVE) (WhenFn ?C))
      (patient ?DRIVE ?VEH))))

;; Convoy needs more work.  A Convoy is analogous to a flock
;; of birds.  When the birds are not in flight, do they still
;; constitute a flock?  (Yes, if they stay together.)  When some
;; birds leave or join the flock, is it still "the same" flock?
;; (Yes, according to the SUMO definition of Collection.)  We
;; still need to capture the notion that the members of a Convoy
;; move in formation.  The rule below needs to be expanded with
;; assertions describing what LineFormation means for the members
;; of a Convoy.  Also, we have not represented the notion that
;; the members of a Convoy, when moving, are heading toward a
;; common destination.

(=>
  (instance ?C Convoy)
  (attribute ?C LineFormation))

(instance Diplomat OccupationalRole)
(subAttribute Diplomat GovernmentPerson)
(documentation Diplomat EnglishLanguage "A person who works as a facilitator for communication
between countries, in the official employ of one of the countries.")

(instance Dissident SocialRole)
(subAttribute Dissident Civilian)
(documentation Dissident EnglishLanguage "Someone who is opposed to the leadership
of a particular country.  Typically, a dissident suffers punishment
at the hands of the country whose leadership he is opposing.")

(subclass EmbassyBuilding GovernmentBuilding)
(documentation EmbassyBuilding EnglishLanguage "An embassy is a &%Building that is owned by a
&%Government to house its diplomatic and consular staff that is in 
another country.")

(=>
  (instance ?X EmbassyBuilding)
  (exists (?EGOV ?OTHERGOV)
    (and
      (instance ?EGOV Nation)
      (instance ?OTHERGOV Nation)
      (not
        (equal ?EGOV ?OTHERGOV))
      (possesses ?EGOV ?X)
      (located ?X ?OTHERGOV))))

(subclass EuropeanNation Nation)
(documentation EuropeanNation EnglishLanguage "The class of nations that are in &%Europe.")
(=>
  (instance ?N EuropeanNation)
  (part ?N Europe))

(instance UnitedKingdom EuropeanNation)
(documentation UnitedKingdom EnglishLanguage "The &%Nation comprising 
England, Scotland, Wales, and Northern Ireland.")

(subclass EntertainmentBuilding Building)
(documentation EntertainmentBuilding EnglishLanguage "A building that has the primary 
purpose of entertaining people.")

(=>
   (instance ?BUILDING EntertainmentBuilding)
   (hasPurpose ?BUILDING 
     (exists (?REC) 
       (and 
         (instance ?REC RecreationOrExercise) 
         (located ?REC ?BUILDING)))))

(subclass GovernmentBuilding Building)
(documentation GovernmentBuilding EnglishLanguage "A building belonging to a &%Government")

(=>
  (instance ?X GovernmentBuilding)
  (exists (?G)
    (and
      (instance ?G Government)
      (possesses ?G ?X))))
      
(instance GovernmentPerson OccupationalRole)
(documentation GovernmentPerson EnglishLanguage "A person who works for a &%Government.")
(=>
  (holdsDuring ?T1 (attribute ?X GovernmentPerson))
  (exists (?GOV)
    (and
      (instance ?GOV Government)
      (holdsDuring ?T1 (employs ?GOV ?X)))))

(subclass Hotel TemporaryResidence)
(subclass Hotel Building)
(documentation Hotel EnglishLanguage "A building which servers the purpose of a temporary
home for people, either for people who are travelling, or occasionally for
those who do not have another more permanent residence.  A hotel is 
distinguished from a homeless shelter, or a residence loaned to a friend
in that the primary purpose of the building is to generate revenue in
return for the priviledge of staying there.  Many hotels contains other 
facilities such as &%Restaurants or health clubs or even miscellaneous stores
although that is not their primary business or purpose.")
        
(=>
  (instance ?X Hotel)
  (hasPurpose ?X
    (exists (?OWN ?EMP ?EV ?P ?PAY)
      (and
        (employs ?OWN ?EMP)
        (possesses ?OWN ?X)
        (instance ?P Human)
        (instance ?PAY FinancialTransaction)
        (agent ?PAY ?P)
        (patient ?PAY ?EMP)
        (confersRight ?EMP ?EV ?P)
        (agent ?EV ?P)
        (located ?EV ?X)))))

(subclass Renting FinancialTransaction)
(subclass Renting Borrowing)
(documentation Renting EnglishLanguage "Giving money to the owner of an &%Object in 
exchange for the right to use the &%Object for a fixed time period.")

(=>
  (and
    (instance ?R Renting)
    (agent ?R ?A)
    (patient ?R ?O))
  (holdsDuring 
    (WhenFn ?R) 
    (modalAttribute (uses ?A ?O) Permission)))

(subclass Marketplace GeographicArea)
(documentation Marketplace EnglishLanguage "An area, building, or set of buildings
where &%FinancialTransactions are intended to take place.  There should
be more than one owner or renter of space or buildings within the
marketplace.")

(=>
  (instance ?X Marketplace)
  (hasPurpose ?X
    (exists (?EV)
      (and
        (instance ?EV FinancialTransaction)
        (located ?EV ?X)))))
        
(instance StoreOwner OccupationalRole)
(subAttribute StoreOwner BusinessPerson)
(documentation StoreOwner EnglishLanguage "Someone who operates a store which he
either owns or rents.")

(instance Militant SocialRole)
(documentation Militant EnglishLanguage "A militant is someone who is not 
officially authorized by a government to engage in combat, but who
does so on behalf of a &%PoliticalOrganization.")

(instance MilitaryPerson OccupationalRole)
(documentation MilitaryPerson EnglishLanguage "Someone who is a member of a
&%ModernMilitaryOrganization.")

(=>
  (holdsDuring ?T1 (attribute ?X MilitaryPerson))
  (exists (?ORG)
    (and
      (instance ?ORG MilitaryOrganization)
      (holdsDuring ?T1 (member ?X ?ORG)))))
    
(=> 
  (and
    (instance ?ORG MilitaryOrganization)
    (holdsDuring ?T1 (member ?X ?ORG))
    (instance ?X Human))
  (holdsDuring ?T1 (attribute ?X MilitaryPerson)))

(subclass OfficeBuilding Building)
(documentation OfficeBuilding EnglishLanguage "A building in which work activities take
place which is not primarily designed for manufacturing or retail sales.")

(subclass PeaceKeepingMission MilitaryOrganization)
(documentation PeaceKeepingMission EnglishLanguage "A military deployment which is intended
to prevent hostilities between two other entities located in the same rough
geographic area.")

(subclass PlaceOfWorship Building)
(documentation PlaceOfWorship EnglishLanguage "Any place designed for &%ReligiousProcesses.")
(=>
  (instance ?X PlaceOfWorship)
  (hasPurpose ?X
    (exists (?EV)
      (and
        (instance ?EV ReligiousProcess)
        (located ?EV ?X)))))
        
(subclass PoliceFacility Building)              
(documentation PoliceFacility EnglishLanguage "A building designed to house &%PolicePersons.")

(instance PolicePerson OccupationalRole)
(subAttribute PolicePerson GovernmentPerson)
(documentation PolicePerson EnglishLanguage "A member of a government whose occupation
has the primary purpose of preventing crime and arresting criminals.")

(subAttribute PoliticalFigure Celebrity)
(documentation PoliticalFigure EnglishLanguage "A well known person who participates
in a PoliticalProcess.  This must either be the person's principal
occupation, or what is intended to become his principal occupation
(for example, after an election).")

(=>
  (attribute ?X PoliticalFigure)
  (exists (?EV)
    (and
      (agent ?EV ?X)
      (instance ?EV PoliticalProcess))))
      
(=>
  (attribute ?X PoliticalFigure)
  (exists (?GOV)
    (and
      (instance ?GOV Government)
      (or
        (member ?X ?GOV)
        (desires ?X
          (member ?X ?GOV))))))

(subAttribute ReligiousFigure Celebrity)
(documentation ReligiousFigure EnglishLanguage "A well-known leader of a religious group.")
(=>
  (attribute ?X ReligiousFigure)
  (exists (?ORG)
    (and
      (instance ?ORG ReligiousOrganization)
      (occupiesPosition ?X Leader ?ORG))))
  
(subclass RestaurantBuilding Building)
(documentation RestaurantBuilding EnglishLanguage "A &%Building where people pay to be served food
and eat.  Some restaurants may also offer entertainment.")

(=>
  (instance ?X RestaurantBuilding)
  (hasPurpose ?X
    (exists (?OWN ?EMP ?EV ?P ?PAY)
      (and
        (employs ?OWN ?EMP)
        (possesses ?OWN ?X)
        (instance ?P Human)
        (instance ?PAY FinancialTransaction)
        (agent ?PAY ?P)
        (patient ?PAY ?EMP)
        (confersRight ?EMP ?EV ?P)
        (instance ?EV Eating)
        (agent ?EV ?P)
        (located ?EV ?X)))))

(subclass EducationalFacility StationaryArtifact)
(documentation EducationalFacility EnglishLanguage "A building or campus, owned by an
&%EducationalOrganization, which is intended as the location for
&%EducationalProcesses.")
(=>
  (instance ?X EducationalFacility)
  (exists (?ORG)
    (and
      (instance ?ORG EducationalOrganization)
      (possesses ?ORG ?X))))
      
(=>
  (instance ?X EducationalFacility)
  (hasPurpose ?X
    (exists (?EV)
      (and
        (instance ?EV EducationalProcess)
        (located ?EV ?X)))))
        
(instance Student SocialRole)
(documentation Student EnglishLanguage "A person who participates in an
&%EducationalProcess in order to learn something.")
(=>
  (attribute ?X Student)
  (exists (?EV)
    (and
      (instance ?EV EducationalProcess)
      (patient ?EV ?X))))
      
(subclass Subway Hole)
(documentation Subway EnglishLanguage "A subway is a hollow area of the earth, typically
under large cities, designed for running trains that move people. It is
distinguished from other kinds of tunnels in that trains run through
them, primarily for the purpose of carrying people rather than ore, for
example.")
(=>
  (instance ?X Subway)
  (exists (?SURF)
    (and
      (surface ?SURF PlanetEarth)
      (orientation ?X ?SURF Below))))
      
(=>
  (instance ?X Subway)
  (hasPurpose ?X
    (exists (?EV ?P)
      (and
        (instance ?EV Transportation)
        (instance ?P Human)
        (patient ?EV ?P)
        (located ?EV ?X)))))
        
(instance Tourist SocialRole)
(documentation Tourist EnglishLanguage "A tourist is a person who is travelling to a 
place primarily for reasons of entertainment or education, rather than
business, although business transaction may take place during the trip.")

(subclass TouristSite GeographicArea)
(documentation TouristSite EnglishLanguage "A tourist site is a location that has some
feature of interest to &%Tourists, which entertains or informs them.")
   
(subclass TerroristOrganization PoliticalOrganization)

(=>
  (instance ?X TerroristOrganization)
  (exists (?EV)
    (and
      (instance ?EV ViolentContest)
      (agent ?EV ?X))))
      
(documentation TerroristOrganization EnglishLanguage "A group that uses violent means in an
  attempt to bring about their political aims.  Those violent means are
  distinguished from a war between nations, or a civil war in that the group
  is at least partially clandestine and a significant proportion of its acts
  are against non-military targets.")

(instance memberCount BinaryPredicate)
(domain memberCount 1 Collection)
(domain memberCount 2 Integer)

(documentation memberCount EnglishLanguage "(&%memberCount ?ORG ?NUMBER) means that there 
is a total ?NUMBER of &%members in the &%Collection ?ORG.") 

(=>
  (memberCount ?ORG ?NUM)
  (equal
    ?NUM
    (CardinalityFn
      (KappaFn ?MEM
        (member ?MEM ?ORG)))))

(=>
  (and
    (seatsInOrganizationCount ?ORG ?SEATS)
    (memberCount ?ORG ?MEMBERS))
  (greaterThanOrEqualTo ?SEATS ?MEMBERS))

(instance memberType BinaryPredicate)
(domain memberType 1 Collection)
(domain memberType 2 SetOrClass)

(documentation memberType EnglishLanguage "(&%memberType ?GROUP ?TYPE) means that all 
the members of the &%Collection ?GROUP belong to the &%SetOrClass 
?TYPE.")

(=>
  (and
    (memberType ?GROUP ?TYPE)
    (member ?MEMBER ?GROUP))
  (instance ?MEMBER ?TYPE))

(instance memberTypeCount TernaryPredicate)
(domain memberTypeCount 1 Collection)
(domain memberTypeCount 2 SetOrClass)
(domain memberTypeCount 3 NonnegativeInteger)

(documentation memberTypeCount EnglishLanguage 
"(&%memberTypeCount ?GROUP ?TYPE ?NUMBER) means that the 
&%Collection ?GROUP has ?NUMBER members of the kind ?TYPE.")

(=>
 (memberTypeCount ?GROUP ?TYPE ?NUMBER)
 (exists (?SUBGROUP)
   (and
     (subCollection ?SUBGROUP ?GROUP)
     (memberCount ?SUBGROUP ?NUMBER)
     (memberType ?SUBGROUP ?TYPE))))

(=>
  (and
    (memberCount ?GROUP ?NUMBER1)
    (memberTypeCount ?GROUP ?TYPE ?NUMBER2))
  (greaterThanOrEqualTo ?NUMBER1 ?NUMBER2))

(=>
  (and
    (memberTypeCount ?GROUP ?TYPE ?NUMBER)
    (equal ?NUMBER 0))
  (not
    (exists (?ITEM)
      (and
        (instance ?ITEM ?TYPE)
        (member ?ITEM ?GROUP)))))

(instance memberAtTime TernaryPredicate)
(documentation memberAtTime EnglishLanguage "(&%memberAtTime ?MEMBER ?COLLECTION
?TIME) means that during the time period denoted by ?TIME,
?MEMBER is a &%member of ?COLLECTION.")
(domain memberAtTime 1 SelfConnectedObject)
(domain memberAtTime 2 Collection)
(domain memberAtTime 3 TimePosition)

(<=>
  (memberAtTime ?OBJ ?COLLECTION ?TIME)
  (holdsDuring ?TIME (member ?OBJ ?COLLECTION)))

(=>
  (and
    (locatedAtTime ?COLLECTION ?TIME ?PLACE)
    (memberAtTime ?OBJ ?COLLECTION ?TIME))
  (locatedAtTime ?OBJ ?TIME ?PLACE))

(documentation inventory EnglishLanguage "(inventory ?CBO ?COLLECTION) means that 
the &%ContentBearingObject ?CBO contains a list or enumeration of the 
members of the &%Collection ?COLLECTION.")
(instance inventory BinaryPredicate)
(domain inventory 1 ContentBearingObject)
(domain inventory 2 Collection)

(=>
  (and
    (inventory ?O ?C)
    (member ?I ?C))
  (refers ?O ?I))

(instance subordinateInOrganization TernaryPredicate)
(domain subordinateInOrganization 1 Organization)
(domain subordinateInOrganization 2 Human)
(domain subordinateInOrganization 3 Human)

(documentation subordinateInOrganization EnglishLanguage 
"(&%subordinateInOrganization ?ORG ?PERSON1 ?PERSON2) means that 
in the &%Organization ?ORG, ?PERSON1 is subordinate to ?PERSON2.")

(=>
    (subordinateInOrganization ?ORG ?PERSON1 ?PERSON2)
    (employs ?ORG ?PERSON1))

(=>
    (subordinateInOrganization ?ORG ?PERSON1 ?PERSON2)
    (employs ?ORG ?PERSON2))

(instance subordinatePosition TernaryPredicate)
(domain subordinatePosition 1 Organization)
(domain subordinatePosition 2 Position)
(domain subordinatePosition 3 Position)

(documentation subordinatePosition EnglishLanguage 
"(&%subordinatePosition ?ORG ?SUB ?SUPER) means that in 
the &%Organization ?ORG, the holder of role ?SUB is subordinate to 
the holder of role ?SUPER.")

(=>
  (and
    (subordinatePosition ?ORG ?ROLE1 ?ROLE2)
    (occupiesPosition ?PERSON1 ?ROLE1 ?ORG))
  (exists (?PERSON2)
    (and
      (instance ?PERSON2 Human)
      (occupiesPosition ?PERSON2 ?ROLE2 ?ORG)
      (subordinateInOrganization ?ORG ?PERSON1 ?PERSON2))))

(instance operator BinaryPredicate)
(instance operator AsymmetricRelation)
(instance operator IrreflexiveRelation)
(domain operator 1 Object)	
(domain operator 2 Agent)

(documentation operator EnglishLanguage "(&%operator ?OBJECT ?AGENT) means that ?AGENT 
determines how ?OBJECT is used, either by directly or indirectly operating 
it.")

(subclass ClassificationScheme Proposition)
(documentation ClassificationScheme EnglishLanguage "A &%ClassificationScheme is
a conceptual structure, an abstract arrangement of concepts and
the relations that link them.")

(subclass Taxonomy ClassificationScheme)
(documentation Taxonomy EnglishLanguage "A &%Taxonomy is a &%ClassificationScheme
that typically includes the salient concepts of a domain of
interest, plus, minimally, a binary `broader than'/`narrower
than' relation by which the concepts are linked.  The `broader
than'/`narrower than' relation is usually conceived as set- or
class-based subsumption, but taxonomies are notorious for
conflating the set membership and set subsumption (i.e.,
subset/superset) relations into a single `IS-A' relation.  Some
taxonomies include additional binary relations, such as
`subpart'/`superpart'.")

(subclass Ontology ClassificationScheme)
(documentation Ontology EnglishLanguage "An &%Ontology is a
&%ClassificationScheme that links concepts via many different
relations.  Ontologies typically are not restricted to binary
relations and are structured by several kinds of conceptual
hierarchies, including set- or class-based subsumption, spatial
containment, mereology
(theory of parts and wholes), and logical contexts.  Thus,
an ontology typically includes multiple taxonomies.")

;; Every ontology incorporates at least one taxonomy.
(=>
  (instance ?ONT Ontology)
  (exists (?TAX)
    (and
      (instance ?TAX Taxonomy)
      (abstractPart ?TAX ?ONT))))

;; transported
(instance transported BinaryPredicate)
(instance transported AsymmetricRelation)
(documentation transported EnglishLanguage "(&%transported ?EVENT ?OBJ)
means that ?OBJ is transported (carried/moved to a
different physical location) in the &%Transportation
?EVENT.")

(domain transported 1 Transportation)
(domain transported 2 Object)
(subrelation transported patient)

;; cargo
(instance cargo BinaryPredicate)
(instance cargo AsymmetricRelation)
(documentation cargo EnglishLanguage "(&%cargo ?EVENT ?OBJ) means that
?OBJ is transported as cargo in the &%Shipping event
?EVENT.")

(domain cargo 1 Shipping)
(domain cargo 2 Object)
(subrelation cargo transported)

(instance serviceProvider BinaryPredicate)
(instance serviceProvider AsymmetricRelation)
(instance serviceProvider IntentionalRelation)
(domain serviceProvider 1 ServiceProcess)
(domain serviceProvider 2 CognitiveAgent)
(subrelation serviceProvider agent)
(documentation serviceProvider EnglishLanguage "(&%serviceProvider ?EVENT ?AGENT)
means that ?AGENT is the supplier of the service provided in
?EVENT.")

(instance serviceRecipient BinaryPredicate)
(instance serviceRecipient AsymmetricRelation)
(instance serviceRecipient IntentionalRelation)
(domain serviceRecipient 1 ServiceProcess)
(domain serviceRecipient 2 CognitiveAgent)
(subrelation serviceRecipient experiencer)
(documentation serviceRecipient EnglishLanguage 
"(&%serviceRecipient ?EVENT ?AGENT) means that ?AGENT is the
receiver of the service provided in ?EVENT.")

;; Every ServiceProcess has some provider.
(=>
  (instance ?EVENT ServiceProcess)
  (exists (?PROVIDER)
    (and
      (instance ?PROVIDER CognitiveAgent)
      (serviceProvider ?EVENT ?PROVIDER))))

;; Every ServiceProcess has some recipient.
(=>
  (instance ?EVENT ServiceProcess)
  (exists (?RECEIVER)
    (and
      (instance ?RECEIVER CognitiveAgent)
      (serviceRecipient ?EVENT ?RECEIVER))))

(instance benefits BinaryPredicate)
(instance benefits AsymmetricRelation)
(domain benefits 1 Process)
(domain benefits 2 Agent)
(documentation benefits EnglishLanguage "(&%benefits ?PROCESS ?AGENT) means that
?AGENT somehow derives benefit as a result of ?PROCESS.  This is
a very general relation, and does not entail that ?AGENT is a
participant in ?PROCESS.")

;; If a process benefits some agent, the process must begin
;; before the agent ends.  This is very weak.
(=>
  (and
    (equal ?PROCESS-START (BeginFn (WhenFn ?PROCESS)))
    (equal ?AGENT-END (EndFn (WhenFn ?AGENT)))
    (benefits ?PROCESS ?AGENT))
  (before ?PROCESS-START ?AGENT-END))

(instance areaOfResponsibility TernaryPredicate)
(domain areaOfResponsibility 1 Agent)
(domainSubclass areaOfResponsibility 2 Process)
(domain areaOfResponsibility 3 GeographicArea)
(documentation areaOfResponsibility EnglishLanguage "(&%areaOfResponsibility
?AGENT ?PROCESS-TYPE ?AREA) means that ?AGENT (typically an
instance of &%Organization) is responsible or accountable for
actions or undertakings of type ?PROCESS-TYPE in the
&%GeographicArea denoted by ?AREA.")

(instance moves CaseRole)
(domain moves 1 Motion)
(domain moves 2 Object)
(documentation moves EnglishLanguage "(&%moves ?MOTION ?OBJECT) means that during
the &%Motion event ?MOTION, ?OBJECT moves.  This does not
necessarily imply that the location of ?OBJECT changes during
?MOTION.  See also &%changesLocation and &%Translocation.")
(subrelation moves involvedInEvent)

(instance changesLocation CaseRole)
(domain changesLocation 1 Translocation)
(domain changesLocation 2 Object)
(documentation changesLocation EnglishLanguage "(&%changesLocation ?EVENT
?OBJECT) means that during the &%Translocation event ?EVENT,
?OBJECT's location changes.  ?OBJECT might also be the &%agent,
&%patient, or &%experiencer of ?EVENT.")
(subrelation changesLocation moves)

;; ------------------------------------------------------
;; Social relationships

(instance friend BinaryPredicate)
(instance friend SymmetricRelation)
(instance friend IntentionalRelation)

(documentation friend EnglishLanguage "(&%holdsDuring ?T1 (&%friend ?H1 ?H2)) means that
during time ?T1, ?H1 and ?H2 know each other, share a relationship of
mutual care and concern, and probably also share some common interests.")

(domain friend 1 Human)
(domain friend 2 Human)
(subrelation friend mutualAcquaintance)

(instance coworker BinaryPredicate)
(instance coworker SymmetricRelation)

(documentation coworker EnglishLanguage "(&%holdsDuring ?T1 (&%coworker ?H1 ?H2)) means
that during time ?T1, ?H1 and ?H2 are both employed by the same agent, are
of roughly the same job status, and come into contact at least part of the
time at the same work location.")

(domain coworker 1 Human)
(domain coworker 2 Human)
(subrelation coworker mutualAcquaintance)

(=>
 (holdsDuring ?T1 (coworker ?H1 ?H2))
 (exists (?ORG)
        (and
         (instance ?ORG Organization)
         (holdsDuring ?T1 (and
                           (employs ?ORG ?H1)
                           (employs ?ORG ?H2))))))

(instance cohabitant BinaryPredicate)
(instance cohabitant SymmetricRelation)
(instance cohabitant IntentionalRelation)

(documentation cohabitant EnglishLanguage "(&%holdsDuring ?T1 (&%cohabitant ?H1 ?H2))
means that during the time ?T1, ?H1 and ?H2 have the same home.")

(domain cohabitant 1 Human)
(domain cohabitant 2 Human)

(subrelation cohabitant mutualAcquaintance)

(=>
 (holdsDuring ?T1 (cohabitant ?H1 ?H2))
 (exists (?UNIT)
   (and
     (instance ?UNIT SocialUnit)
     (holdsDuring ?T1 (and (member ?UNIT ?H1)
                           (member ?UNIT ?H2))))))

(=>
  (and
    (instance ?UNIT SocialUnit)
    (holdsDuring ?T1 (and (member ?UNIT ?H1)
                          (member ?UNIT ?H2))))
  (holdsDuring ?T1 (cohabitant ?H1 ?H2)))

(instance grandparent BinaryPredicate)
(instance grandparent AsymmetricRelation)
(domain grandparent 1 Human)
(domain grandparent 2 Human)

(documentation grandparent EnglishLanguage "(&%grandparent ?YOUNGER ?OLDER) means that
?OLDER is a &%parent of ?YOUNGER's &%parent.")

(subrelation grandmother grandparent)
(subrelation grandfather grandparent)
(subrelation grandparent ancestor)

;; parent -> father
(=>
  (and
    (parent ?CHILD ?PARENT)
    (instance ?PARENT Man))
  (father ?CHILD ?PARENT))

;; parent -> mother
(=>
  (and
    (parent ?CHILD ?PARENT)
    (instance ?PARENT Woman))
  (mother ?CHILD ?PARENT))

;; parent -> son
(=>
  (and
    (parent ?CHILD ?PARENT)
    (instance ?CHILD Man))
  (son ?CHILD ?PARENT))

;; parent -> daughter
(=>
  (and
    (parent ?CHILD ?PARENT)
    (instance ?CHILD Woman))
  (daughter ?CHILD ?PARENT))

(documentation aunt EnglishLanguage "(aunt ?AUNT ?PERSON) means that ?AUNT is 
the sister of a parent of ?PERSON.")
(domain aunt 1 Woman)
(domain aunt 2 Human)
(subrelation aunt familyRelation)

(<=>
  (aunt ?A ?H)
  (exists (?P)
    (and
      (sister ?A ?P)
      (parent ?H ?P)))) 

(documentation cousin EnglishLanguage "(cousin ?PERSON1 ?PERSON2) means that ?PERSON1 
and ?PERSON2 are cousins, i.e. ?PERSON1 and ?PERSON2 have grandparents 
(but not parents) in common.")
(domain cousin 1 Human)
(domain cousin 2 Human)
(subrelation cousin familyRelation)

(<=>
  (cousin ?P1 ?P2)
  (and
    (exists (?G1 ?G2)
      (and
        (grandmother ?P1 ?G1)
        (grandfather ?P1 ?G2)
        (grandmother ?P2 ?G1)
        (grandfather ?P2 ?G2)))
    (not 
      (exists (?M ?F)
        (and
          (mother ?P1 ?M)
          (father ?P1 ?F)
          (mother ?P2 ?M)
          (father ?P2 ?F))))))

(documentation grandfather EnglishLanguage "(grandfather ?PERSON ?PARENT) means that 
?PARENT is the grandfather of ?PERSON.")
(domain grandfather 1 Human)
(domain grandfather 2 Man)
(subrelation grandfather familyRelation)

(=>
  (grandfather ?H ?P)
  (exists (?C)
    (and
      (father ?C ?P)
      (parent ?H ?C))))

(documentation grandmother EnglishLanguage "(grandmother ?PERSON ?PARENT) means that 
?PARENT is the grandmother of ?PERSON.")
(domain grandmother 1 Human)
(domain grandmother 2 Woman)
(subrelation grandmother familyRelation)

(=>
  (grandmother ?H ?P)
  (exists (?C)
    (and
      (mother ?C ?P)
      (parent ?H ?C))))

(documentation nephew EnglishLanguage "(nephew ?NEPHEW ?PERSON) means that ?NEPHEW is 
the &%son of a &%sibling of ?PERSON.")
(domain nephew 1 Man)
(domain nephew 2 Human)
(subrelation nephew familyRelation)

(<=>
  (nephew ?N ?H)
  (exists (?S)
    (and
      (sibling ?S ?H)
      (son ?N ?S)))) 

(documentation niece EnglishLanguage "(niece ?NIECE ?PERSON) means that ?NIECE is 
the &%daughter of a &%sibling of ?PERSON.")
(domain niece 1 Woman)
(domain niece 2 Human)
(subrelation niece familyRelation)

(<=>
   (niece ?N ?H)
   (exists (?S)
      (and
         (sibling ?S ?H)
         (daughter ?N ?S)))) 

(documentation uncle EnglishLanguage "(uncle ?UNCLE ?PERSON) means that ?UNCLE is 
the brother of a parent of ?PERSON.")
(domain uncle 1 Man)
(domain uncle 2 Human)
(subrelation uncle familyRelation)

(<=>
   (uncle ?U ?H)
   (exists (?P)
      (and
         (brother ?U ?P)
         (parent ?H ?P)))) 

(documentation stepfather EnglishLanguage "(stepfather ?PERSON ?FATHER) means that ?FATHER 
is the stepfather of ?PERSON, i.e. ?FATHER is the &%spouse of the &%mother 
of ?PERSON, without also being the &%father of ?PERSON.")
(domain stepfather 1 Human)
(domain stepfather 2 Man)
(subrelation stepfather legalRelation)

(<=>
  (stepfather ?P ?F)
  (exists (?M)
    (and
      (mother ?P ?M)
      (spouse ?F ?M)
      (not 
        (father ?P ?F)))))

(documentation stepmother EnglishLanguage "(stepmother ?PERSON ?MOTHER) means that ?MOTHER 
is the &%stepmother of ?PERSON, i.e. ?MOTHER is the spouse of the &%father 
of ?PERSON, without also being the &%mother of ?PERSON.")
(domain stepmother 1 Human)
(domain stepmother 2 Woman)
(subrelation stepmother legalRelation)

(<=>
  (stepmother ?P ?M)
  (exists (?F)
    (and
      (father ?P ?F)
      (spouse ?M ?F)
      (not 
        (mother ?P ?M)))))

(subclass NuclearFamily FamilyGroup)
(subclass NuclearFamily SocialUnit)

(documentation NuclearFamily EnglishLanguage "A &%NuclearFamily is a &%SocialUnit composed
of at least two and not more than three generations of &%familyRelations:
&%parents, their children, and possibly the parents' parents (children's
&%grandparents).")

;; TO DO: Add axiomatic support for defining and
;; concluding to NuclearFamily.

(=>
  (and
    (parent ?CHILD ?PARENT1)
    (parent ?CHILD ?PARENT2)
    (holdsDuring ?T1 
      (and 
        (spouse ?PARENT1 ?PARENT2)
        (cohabitant ?PARENT1 ?PARENT2)
	(cohabitant ?CHILD ?PARENT1))))
  (exists (?FAM)
    (and
      (instance ?FAM NuclearFamily)
      (holdsDuring ?T1
        (and
          (member ?CHILD ?FAM)
          (member ?PARENT1 ?FAM)
          (member ?PARENT2 ?FAM))))))

(instance alias BinaryPredicate)
(instance alias AsymmetricRelation)
(domain alias 1 SymbolicString)
(domain alias 2 Agent)

(documentation alias EnglishLanguage "(&%alias ?STRING ?AGENT) means that ?STRING is an
alternate identifier for ?AGENT, and is likely being used to hide or
obscure ?AGENT's true identity.")

(subrelation alias names)

(instance workAddress BinaryPredicate)
(instance workAddress AsymmetricRelation)
(subrelation workAddress postContactSite)
(documentation workAddress EnglishLanguage 
"(&%holdsDuring ?T1 (&%workAddress ?AGENT ?ADDRESS)) means that
during the time specified by ?T1, ?ADDRESS identifies one of the
locations at which ?AGENT works.")
(domain workAddress 1 CognitiveAgent)
(domain workAddress 2 PostalPlace)

(instance homeAddress BinaryPredicate)
(instance homeAddress AsymmetricRelation)
(domain homeAddress 1 PermanentResidence)
(domain homeAddress 2 Human)
(subrelation homeAddress postContactSite)
(documentation homeAddress EnglishLanguage 
"(&%holdsDuring ?T1 (&%homeAddress ?PLACE ?AGENT)) means that
during the time specified by ?T1, the &%PostalPlace ?PLACE is
?AGENT's &%home.")

(instance playsRoleInEvent TernaryPredicate)
(domain playsRoleInEvent 1 Object)
(domain playsRoleInEvent 2 CaseRole)
(domain playsRoleInEvent 3 Process)

(documentation playsRoleInEvent EnglishLanguage 
"(&%playsRoleInEvent ?OBJ ?ROLE ?EVENT) means that the &%Object
?OBJ is involved in the &%Process ?EVENT in a way specified by
the &%CaseRole predicate ?ROLE.")

(=>
  (playsRoleInEvent ?OBJ ?ROLE ?EVENT)
  (?ROLE ?EVENT ?OBJ))

(=>
  (and
    (playsRoleInEvent ?OBJ ?ROLE ?EVENT)
    (subrelation ?ROLE ?ROLE2))
  (playsRoleInEvent ?OBJ ?ROLE2 ?EVENT))

(=>
 (and
   (?ROLE ?EVENT ?OBJ)
   (instance ?ROLE CaseRole)
   (instance ?EVENT Process)
   (instance ?OBJ Object))
 (playsRoleInEvent ?OBJ ?ROLE ?EVENT))

(=>
 (and
   (playsRoleInEvent ?OBJ ?ROLE ?EVENT)
   (instance ?EVENT ?CLASS)
   (subclass ?CLASS Process)
   (time ?EVENT ?TIME)
   (located ?EVENT ?PLACE))
 (playsRoleInEventOfType ?OBJ ?ROLE ?CLASS ?TIME ?PLACE))

(instance playsRoleInEventOfType QuintaryPredicate)
(domain playsRoleInEventOfType 1 Object)
(domain playsRoleInEventOfType 2 CaseRole)
(domainSubclass playsRoleInEventOfType 3 Process)
(domain playsRoleInEventOfType 4 TimePosition)
(domain playsRoleInEventOfType 5 Object)

(documentation playsRoleInEventOfType EnglishLanguage "(&%playsRoleInEventOfType ?OBJ
?ROLE ?TYPE ?TIME ?PLACE) means that ?OBJ plays the role specified by
?ROLE in an instance of ?TYPE, which occurs at ?TIME and ?PLACE.")

(=>
 (playsRoleInEventOfType ?OBJ ?ROLE ?TYPE ?TIME ?PLACE)
 (exists (?EVENT)
   (and
     (instance ?EVENT ?TYPE)
     (time ?EVENT ?TIME)
     (located ?EVENT ?PLACE)
     (playsRoleInEvent ?OBJ ?ROLE ?EVENT))))

(=>
 (and
   (playsRoleInEvent ?OBJ ?ROLE ?EVENT)
   (instance ?EVENT ?TYPE)
   (subclass ?TYPE Process)
   (time ?EVENT ?TIME)
   (located ?EVENT ?PLACE))
 (playsRoleInEventOfType ?OBJ ?ROLE ?TYPE ?TIME ?PLACE))

(instance locatedAtTime TernaryPredicate)
(domain locatedAtTime 1 Object)
(domain locatedAtTime 2 TimePosition)
(domain locatedAtTime 3 Object)

(documentation locatedAtTime EnglishLanguage "(&%locatedAtTime ?OBJ ?TIME ?PLACE) means
that during the time specified by ?TIME, ?OBJ was in the location
specified by ?PLACE.")

(<=>
 (locatedAtTime ?OBJ ?TIME ?PLACE)
 (holdsDuring ?TIME (located ?OBJ ?PLACE)))

(=> 
  (and
    (playsRoleInEvent ?OBJ ?ROLE ?EVENT)
    (time ?EVENT ?TIME)
    (located ?EVENT ?PLACE))
  (locatedAtTime ?OBJ ?TIME ?PLACE))


;; NB: This needs more work.  The topic of ID strings, ID objects
;; (e.g., finger prints, DNA sequences) and their use for both
;; identification and deception merits an entire ontology of its own.
;; deceptiveIdentifier
(instance deceptiveIdentifier BinaryPredicate)
(instance deceptiveIdentifier AsymmetricRelation)
(domain deceptiveIdentifier 1 ContentBearingObject)
(domain deceptiveIdentifier 2 Agent)
(subrelation deceptiveIdentifier refers)
(subrelation alias deceptiveIdentifier)

(documentation deceptiveIdentifier EnglishLanguage "(&%deceptiveIdentifier ?OBJ ?AGENT)
means that ?AGENT presents ?OBJ as a representation of ?AGENT's `true'
identity, when in fact it is not.")

(instance stranger BinaryPredicate)

(documentation stranger EnglishLanguage "(&%stranger ?H1 ?H2) means that ?H1 has
not met ?H2, or, in other words, (&%not (&%acquaintance ?H1
?H2)).  Statements made with &%stranger should be temporally
specified with &%holdsDuring.  Note that &%stranger is not
symmetric, meaning that ?H2 might know ?H1.  For the symmetric
version, see &%mutualStranger.")

(domain stranger 1 Human)
(domain stranger 2 Human)

(disjointRelation stranger acquaintance)

(instance mutualStranger BinaryPredicate)
(instance mutualStranger SymmetricRelation)

(documentation mutualStranger EnglishLanguage "(&%mutualStranger ?H1 ?H2) means that ?H1
and ?H2 have not met each other and do not know each other.  Statements
made with this predicate should be temporally specified with
&%holdsDuring.  See also the weaker, non-symmetric version of this
predicate, &%stranger.")

(domain mutualStranger 1 Human)
(domain mutualStranger 2 Human)
(subrelation mutualStranger stranger)

(disjointRelation mutualStranger mutualAcquaintance)

(instance domesticPartner BinaryPredicate)
(instance domesticPartner SymmetricRelation)
(instance domesticPartner IntentionalRelation)

(documentation domesticPartner EnglishLanguage "(&%holdsDuring ?T1
(&%domesticPartner ?H1 ?H2)) means that during the time ?T1, ?H1
and ?H2 live together and share a common domestic life but are
not joined in a traditional marriage, a common-law marriage, or a
civil union.")

(domain domesticPartner 1 Human)
(domain domesticPartner 2 Human)

(disjointRelation spouse domesticPartner)
(subrelation domesticPartner cohabitant)

(instance legalGuardian BinaryPredicate)
(instance legalGuardian AntisymmetricRelation)

(documentation legalGuardian EnglishLanguage "(&%holdsDuring ?T1 (&%legalGuardian ?H1
?H2)) means that during the time ?T1, ?H2 has legal authority over
?H1, and is responsible for looking after ?H1's interests.")

(domain legalGuardian 1 Human)
(domain legalGuardian 2 Human)

(=>
  (and
    (locatedAtTime ?OBJ ?TIME ?PLACE)
    (geographicSubregion ?PLACE ?REGION))
  (locatedAtTime ?OBJ ?TIME ?REGION))

(subclass Seating Guiding)
(documentation Seating EnglishLanguage "Guiding someone to a &%Seat, e.g. as when an usher 
shows someone to a &%Seat in an &%Auditorium.")

(=>
  (instance ?S Seating)
  (exists (?P ?H)
    (and
      (experiencer ?S ?H)
      (instance ?H Human)
      (destination ?S ?P)
      (instance ?P Seat))))

(subclass Signalling Guiding)
(documentation Signalling EnglishLanguage "Signalling is the subclass of &%Guiding 
processes in which an agent, animate or inanimate, sends a signal to 
another &%Object.  In many cases, this signal will be an electrical or 
electronic one.  Some signals may directly control the behavior of 
the object signalled, while others may merely cause information to be 
presented.  Instances of electrical Signalling are typically more 
complex than simply than operation of a &%DeviceSwitch, though in some 
cases, Signalling involves remote activation of such a switch by another 
device.")

;; Process Types for arrest and transportation

(subclass AchievingControl Guiding)
(documentation AchievingControl EnglishLanguage "&%AchievingControl is the
class of all events in which an &%Agent gains physical
control over some object.")

(instance controlled CaseRole)
(documentation controlled EnglishLanguage
"(&%controlled ?EVENT ?OBJECT) means that during the
&%AchievingControl denoted by ?EVENT, ?OBJECT comes to be
physically controlled by an &%Agent.")
(subrelation controlled patient)
(domain controlled 1 AchievingControl)
(domain controlled 2 Object)

(instance gainsControl CaseRole)
(documentation gainsControl EnglishLanguage
"(&%gainsControl ?EVENT ?AGENT) means that during ?EVENT, ?AGENT
gains control of the &%patient (object).")
(subrelation gainsControl agent)
(domain gainsControl 1 AchievingControl)
(domain gainsControl 2 Agent)

(instance ChangeOfControl Class)
(subclass ChangeOfControl AchievingControl)
(documentation ChangeOfControl EnglishLanguage
"A &%ChangeOfControl is an event in which an &%agent gains
physical control over some object that was previously controlled
by a different &%agent.")

(instance losesControl CaseRole)
(subrelation losesControl experiencer)
(documentation losesControl EnglishLanguage
"(&%losesControl ?EVENT ?AGENT) means that during ?EVENT, ?AGENT
loses physical control of the &%controlled object.")
(domain losesControl 1 ChangeOfControl)
(domain losesControl 2 Agent)

(subclass Capturing ChangeOfControl)
(documentation Capturing EnglishLanguage
"A &%Capturing is a &%ChangeOfControl event in which an &%Agent
gains physical control over another &%Agent.")

(=>
  (and
    (instance ?CAPTURE Capturing)
    (controlled ?CAPTURE ?OBJ))
  (instance ?OBJ Agent))

(subclass PlacingUnderArrest Capturing)
(subclass PlacingUnderArrest LawEnforcement)
(documentation PlacingUnderArrest EnglishLanguage 
"&%PlacingUnderArrest is the class of events in which a
&%CognitiveAgent, typically a law enforcement professional,
legally takes into custody a human or group of humans.")
(subclass PlacingUnderArrest SocialInteraction)
(subclass PlacingUnderArrest LegalAction)

(instance arrested CaseRole)
(documentation arrested EnglishLanguage 
"(&%arrested ?EVENT ?AGENT) means that during ?EVENT, ?AGENT is
taken into custody, typically by a representative of a law
enforcement organization.")
(domain arrested 1 PlacingUnderArrest)
(domain arrested 2 Agent)
(subrelation arrested controlled)

(=>
  (and
    (instance ?ARREST PlacingUnderArrest)
    (agent ?ARREST ?AGENT))
  (instance ?AGENT CognitiveAgent))

(=>
  (arrested ?ARREST ?OBJ)
  (or
    (instance ?OBJ Human)
    (instance ?OBJ GroupOfPeople)))

(=>
  (and
    (instance ?ARREST PlacingUnderArrest)
    (arrested ?ARREST ?AGENT)
    (instance ?AGENT GroupOfPeople)
    (member ?MEMBER ?AGENT))
  (exists (?ARREST1)
    (and
      (instance ?ARREST1 PlacingUnderArrest)
      (arrested ?ARREST1 ?MEMBER)
      (subProcess ?ARREST1 ?ARREST))))

(=>
  (and
    (instance ?PROC Process)
    (located ?PROC ?LOC)
    (subProcess ?SUB ?PROC))
  (located ?SUB ?LOC))

(=>
  (and
    (instance ?IMPRISON Imprisoning)
    (detainee ?IMPRISON ?AGENT)
    (instance ?AGENT Human)
    (time ?IMPRISON ?INTERVAL)
    (instance ?INTERVAL TimeInterval))
  (exists (?ARREST ?TIME)
    (and
      (instance ?TIME TimeInterval)
      (time ?ARREST ?TIME)
      (earlier ?TIME ?INTERVAL)
      (instance ?ARREST PlacingUnderArrest)
      (arrested ?ARREST ?AGENT))))

(subclass TransportViaRoadVehicle Transportation)
(subclass TransportViaRoadVehicle LandTransportation)
(documentation TransportViaRoadVehicle EnglishLanguage
"&%TransportViaRoadVehicle is the class of &%Transportation
events in which the &%instrument is an instance of
&%RoadVehicle.")

(instance conveyance CaseRole)
(documentation conveyance EnglishLanguage "(&%conveyance ?EVENT ?OBJ) means that
?OBJ is the &%Vehicle or other transportation device used in
?EVENT.")
(subrelation conveyance instrument)
(domain conveyance 1 Transportation)
(domain conveyance 2 TransportationDevice)

(=>
  (and
    (instance ?TRANSPORT Transportation)
    (instrument ?TRANSPORT ?VEHICLE)
    (instance ?VEHICLE RoadVehicle))
  (instance ?TRANSPORT TransportViaRoadVehicle))

(=>
  (and
    (instance ?TRANSPORT TransportViaRoadVehicle)
    (conveyance ?TRANSPORT ?VEHICLE))
  (instance ?VEHICLE RoadVehicle))

(=>
  (instance ?TRANSPORT TransportViaRoadVehicle)
  (exists (?DRIVING)
    (and
      (instance ?DRIVING Driving)
      (subProcess ?TRANSPORT ?DRIVING))))

(=>
  (and
    (instance ?TRANSPORT Transportation)
    (subProcess ?TRANSPORT ?DRIVE)
    (instance ?DRIVE Driving)
    (instrument ?TRANSPORT ?VEHICLE)
    (instance ?VEHICLE TransportationDevice))
  (patient ?DRIVE ?VEHICLE))

(=>
  (and
    (instance ?DRIVE Driving)
    (patient ?DRIVE ?VEHICLE)
    (instance ?VEHICLE RoadVehicle))
  (exists (?TRANSPORT)
    (and 
      (instance ?TRANSPORT TransportViaRoadVehicle)
      (subProcess ?TRANSPORT ?DRIVE))))

(=>
  (and
    (instance ?DRIVING Driving)
    (subProcess ?TRANSPORT ?DRIVING)
    (instance ?TRANSPORT Transportation)
    (agent ?DRIVING ?DRIVER))
  (transported ?TRANSPORT ?DRIVER))

(=>
  (and
    (instance ?DRIVING Driving)
    (subProcess ?TRANSPORT ?DRIVING)
    (instance ?TRANSPORT Transportation)
    (destination ?DRIVING ?DEST))
  (destination ?TRANSPORT ?DEST))

(=>
  (and
    (instance ?DRIVING Driving)
    (subProcess ?TRANSPORT ?DRIVING)
    (instance ?TRANSPORT Transportation)
    (instance ?VEHICLE Vehicle)
    (patient ?DRIVING ?VEHICLE))
  (conveyance ?TRANSPORT ?VEHICLE))

(subclass GasolineEngine InternalCombustionEngine)
(documentation GasolineEngine EnglishLanguage "&%GasolineEngine is the subclass of 
&%InternalCombustionEngines that use &%Gasoline as their resource.")
(=>
  (instance ?R (OperatingFn GasolineEngine))
  (exists (?F)
    (and
      (instance ?F Gasoline)
      (resource ?R ?F))))

(subclass DieselEngine InternalCombustionEngine)
(documentation DieselEngine EnglishLanguage "&%DieselEngine is the subclass of 
&%InternalCombustionEngines that use &%DieselFuel as their resource.")
(=>
  (instance ?P (OperatingFn DieselEngine))
  (exists (?F)
    (and
      (instance ?F DieselFuel)
      (resource ?P ?F))))

(instance typicalPart BinaryRelation)
(instance typicalPart SpatialRelation)
(relatedInternalConcept typicalPart part)
(documentation typicalPart EnglishLanguage "An instance of the first argument is typically 
found as part of an instance of the second argument.  This is a 
class-level relation roughly corresponding to &%part.  Note that this does 
not imply that such wholes typically have such parts.") 
(domainSubclass typicalPart 1 Object)
(domainSubclass typicalPart 2 Object)

(=>
  (typicalPart ?PART ?WHOLE)
  (exists (?X ?Y)
    (and      
      (instance ?X ?WHOLE)
      (instance ?Y ?PART)
      (part ?Y ?X)))) 

(=>
  (and
    (typicalPart ?PART ?WHOLE)
    (instance ?X ?PART)
    (equal ?PARTPROB
      (ProbabilityFn
        (exists (?Y)
          (and
            (instance ?Y ?WHOLE)
            (part ?X ?Y)))))
    (equal ?NOTPARTPROB
      (ProbabilityFn
        (not
          (exists (?Z)
            (and
              (instance ?Z ?WHOLE)
              (part ?X ?Z)))))))
  (greaterThan ?PARTPROB ?NOTPARTPROB))

(instance typicallyContainsPart BinaryRelation)
(instance typicallyContainsPart SpatialRelation)
(relatedInternalConcept typicallyContainsPart part)
(documentation typicallyContainsPart EnglishLanguage "An instance of the second argument typically 
contains an instance of the first argument.  This is a 
class-level relation roughly corresponding to &%part.  Note that this does 
not imply that such parts typically have such wholes.") 
(domainSubclass typicallyContainsPart 1 Object)
(domainSubclass typicallyContainsPart 2 Object)

(=>
  (typicallyContainsPart ?PART ?WHOLE)
  (exists (?X ?Y)
    (and      
      (instance ?X ?WHOLE)
      (instance ?Y ?PART)
      (part ?Y ?X)))) 

(=>
  (and
    (typicallyContainsPart ?PART ?WHOLE)
    (instance ?Y ?WHOLE)
    (equal ?PARTPROB
      (ProbabilityFn
        (exists (?X1)
          (and
            (instance ?X1 ?PART)
            (part ?X1 ?Y)))))
    (equal ?NOTPARTPROB
      (ProbabilityFn
        (not
          (exists (?X2)
            (and
              (instance ?X2 ?PART)
              (part ?X2 ?Y)))))))
  (greaterThan ?PARTPROB ?NOTPARTPROB))

(instance initialPart BinaryRelation)
(instance initialPart SpatialRelation)
(relatedInternalConcept initialPart part)
(documentation initialPart EnglishLanguage "Every instance of the first argument is initially 
found as part of an instance of the second argument, even though it 
might lose that part later in its lifetime.  While the part must initially 
exist as part of a whole, this does not say that each whole necessarily initially  
contains such a part.  For example, a thumb must at some time have been 
part of a hand, but every hand need not have a thumb, even at birth.  This 
is a class-level relation roughly corresponding to &%part.") 

(domainSubclass initialPart 1 Object)
(domainSubclass initialPart 2 Object)

(=>
  (and
    (initialPart ?C2 ?C1)
    (instance ?PART ?C2))
  (exists (?WHOLE)
    (and      
      (instance ?WHOLE ?C1)
      (holdsDuring 
        (BeginFn (WhenFn ?PART))
        (part ?PART ?WHOLE)))))

(initialPart AnimalAnatomicalStructure Animal)

(instance initiallyContainsPart BinaryRelation)
(instance initiallyContainsPart SpatialRelation)
(relatedInternalConcept initiallyContainsPart part)
(documentation initiallyContainsPart EnglishLanguage "Every instance of the second argument initially 
contains an instance of the first argument, even though it 
might lose that part later in its lifetime.  Every normal human starts life with an
appendix for example.  While the whole must initially 
contain such a part, this does not say that each part necessarily initially  
exists as part of such a whole.   This is a class-level relation roughly 
corresponding to &%part.") 

(domainSubclass initiallyContainsPart 1 Object)
(domainSubclass initiallyContainsPart 2 Object)

(=>
  (and
    (initiallyContainsPart ?C2 ?C1)
    (instance ?WHOLE ?C1))
  (exists (?PART)
    (and      
      (instance ?PART ?C2)
      (holdsDuring (BeginFn (WhenFn ?WHOLE))
      (part ?PART ?WHOLE)))))

(subclass Atrophy PathologicProcess)
(subclass Atrophy Decreasing)
(documentation Atrophy EnglishLanguage "Any decrease in the size of a &%BodyPart which is due to 
disease or lack of use.")
(=>
  (instance ?A Atrophy)
  (exists (?B)
    (and
      (experiencer ?A ?B)
      (instance ?B BodyPart))))

(subclass CausingHappiness Process)
(documentation CausingHappiness EnglishLanguage "Any &%Process whose result is that the 
patient of the process is happy.")
(=>
  (and
    (instance ?C CausingHappiness)
    (patient ?C ?P))
  (and
    (holdsDuring 
      (BeginFn (WhenFn ?C)) 
    (not 
      (attribute ?P Happiness)))
    (holdsDuring 
      (EndFn (WhenFn ?C)) 
    (attribute ?P Happiness)))) 

(subclass CausingPain CausingUnhappiness)
(documentation CausingPain EnglishLanguage "Any &%Process whose result is that the 
patient of the process is in &%Pain.")

(subclass CausingUnhappiness Process)
(documentation CausingUnhappiness EnglishLanguage "Any &%Process whose result is that the 
patient of the process is unhappy.")
(=>
  (and
    (instance ?C CausingHappiness)
    (patient ?C ?P))
  (and
    (holdsDuring 
      (BeginFn (WhenFn ?C)) 
    (not 
      (attribute ?P Unhappiness)))
    (holdsDuring 
      (EndFn (WhenFn ?C)) 
    (attribute ?P Unhappiness)))) 

(subclass Bloating Increasing)
(subclass Bloating PathologicProcess)
(documentation Bloating EnglishLanguage "Any &%Process where the stomach or instestines of a 
&%Human or &%Animal become distended from excessive gas.")
(=>
  (instance ?B Bloating)
  (exists (?P ?S ?I ?G)
    (and
      (experiencer ?B ?P)
      (instance ?G Gas)
      (or
        (and
          (part ?I ?P)
          (instance ?I Intestine)
          (contains ?I ?G))
        (and
          (part ?S ?P)
          (instance ?S Stomach)
          (contains ?S ?G))))))

(subclass TakingIll PathologicProcess)
(documentation TakingIll EnglishLanguage "Any &%Process where the experiencer contracts a 
&%DiseaseOrSyndrome.")

(=>
  (and
    (instance ?T TakingIll)
    (experiencer ?T ?A))
  (exists (?D)
    (and
      (instance ?D DiseaseOrSyndrome)
      (holdsDuring 
        (BeginFn (WhenFn ?T)) 
	(not (attribute ?A ?D)))
      (holdsDuring 
        (EndFn (WhenFn ?T)) 
	(attribute ?A ?D)))))

(subclass Lengthening Increasing)
(documentation Lengthening EnglishLanguage "&%Increasing the length of something.")

(=>
  (and
    (instance ?L Lengthening)
    (patient ?L ?O))
  (exists (?L1 ?L2)
    (and
      (holdsDuring 
	(BeginFn (WhenFn ?L)) 
	(length ?O ?L1))
      (holdsDuring 
	(EndFn (WhenFn ?L)) 
	(length ?O ?L2))
      (greaterThan ?L2 ?L1))))

(subclass Saving Increasing)
(documentation Saving EnglishLanguage "Any instance of &%Increasing where the &%PhysicalQuantity 
involved is a &%CurrencyMeasure.  This includes saving in a financial account
as well as stuffing gold bars under the matress, or a squirrel saving nuts
for winter.")

(=>
  (and
    (instance ?S Saving)
    (patient ?S ?O)
    (agent ?S ?A))
  (exists (?Q1 ?Q2)
    (and
      (holdsDuring 
        (BeginFn (WhenFn ?S)) 
	(monetaryValue ?O ?Q1))
      (holdsDuring 
        (EndFn (WhenFn ?S)) 
	(monetaryValue ?O ?Q2))
      (possesses ?A ?O)
      (lessThan ?Q1 ?Q2))))

(documentation financialAsset EnglishLanguage "A predicate that relates an &%Agent to any 
item of economic value owned by the &%Agent.  Examples of financial assets 
are cash, securities, accounts receivable, inventory, office equipment, a 
house, a car, and other property.")
(domain financialAsset 1 Agent)
(domain financialAsset 2 Object)
(subrelation financialAsset possesses)

(=>
  (financialAsset ?P ?A)
  (exists (?V)
    (monetaryValue ?A ?V)))

(subclass Barking RadiatingSound)
(documentation Barking EnglishLanguage "Any instance of &%RadiatingSound which is produced 
by a &%Canine.")
(=>
  (instance ?B Barking)
  (exists (?D)
    (and
      (instance ?D Canine)
      (agent ?B ?D))))

(subclass Menstruation OrganOrTissueProcess)
(documentation Menstruation EnglishLanguage "A periodic discharge of &%Blood and other 
&%BiologicalSubstances by &%Females that is part of a readjustment of 
the uterus.")

(subclass Organification OrganOrTissueProcess)
(documentation Organification EnglishLanguage "Any &%BiologicalProcess that results in 
the formation of an &%Organ.")

(=>
   (instance ?P Organification)
   (exists (?O)
      (and
         (instance ?O Organ)
         (result ?P ?O))))

(subclass BiologicalConception OrganOrTissueProcess)
(relatedInternalConcept BiologicalConception SexualReproduction)
(relatedInternalConcept BiologicalConception Birth)
(relatedInternalConcept BiologicalConception Mating)
(documentation BiologicalConception EnglishLanguage "The production of a zygote from the fusion 
of a male and female gamete.")
(=>
  (instance ?SR SexualReproduction)
  (exists (?C)
    (and
      (instance ?C BiologicalConception)
      (subProcess ?C ?SR))))

(subclass ContraceptiveDevice Device)
(documentation ContraceptiveDevice EnglishLanguage "&%Devices which permit sexual intercourse but 
which reduce the likelihood of conception.")
(=>
  (instance ?C ContraceptiveDevice)
  (hasPurpose ?C
    (exists (?M)
      (and
        (instance ?M Mating)
        (patient ?M ?C)
        (not
          (exists (?SR)
            (and
              (instance ?SR BiologicalConception)
              (result ?M ?SR))))))))

(subclass BirdEgg Egg)
(documentation BirdEgg EnglishLanguage "Any &%Egg that is produced by a &%Bird.")
(=>
  (instance ?E BirdEgg)
  (exists (?SR ?B)
    (and
      (instance ?SR SexualReproduction)
      (agent ?SR ?B)
      (instance ?B Bird)
      (result ?SR ?E))))

(subclass Mating OrganismProcess)
(documentation Mating EnglishLanguage "Any process of sexual intercourse between two &%Humans 
or &%Animals.")

(subclass Raping Mating)
(subclass Raping CriminalAction)
(documentation Raping EnglishLanguage "Any instance of &%Mating where one participant does not 
consent.  This is limited to acts between &%Humans.")

(=>
  (instance ?R Raping)
  (exists (?A)
    (and
      (experiencer ?R ?A)
      (not 
        (wants ?A ?R)))))

(subclass LayingEggs OrganismProcess)
(documentation LayingEggs EnglishLanguage "Any &%OrganismProcess where an &%Egg is expelled 
from the body of a &%Animal, e.g. a &%Bird or &%Reptile laying eggs.")

(=>
  (and
    (instance ?L LayingEggs)
    (experiencer ?L ?A))
  (exists (?E)
    (and
      (instance ?E Egg)
      (patient ?L ?E)
      (holdsDuring 
        (BeginFn (WhenFn ?L)) 
	(part ?E ?A))
      (holdsDuring 
        (EndFn (WhenFn ?L)) 
	(not (part ?E ?A)))))) 

(=>
  (and
    (instance ?L LayingEggs)
    (experiencer ?L ?A))
  (and
    (instance ?A Animal)
    (attribute ?A Female)))

(subclass Biopsy DiagnosticProcess)
(subclass Biopsy Removing)
(documentation Biopsy EnglishLanguage "Any &%DiagnosticProcess which involves the examination of 
&%BodySubstances taken from a living &%Organism.")

(=>
  (and
    (instance ?B Biopsy)
    (patient ?B ?BS))
  (instance ?BS BodySubstance))

(instance affiliatedOrganization BinaryPredicate)
(domain affiliatedOrganization 1 Organization)
(domain affiliatedOrganization 2 Organization)

(documentation ideologicalAffiliationOfOrganization EnglishLanguage "(ideologicalAffiliationOfOrganization 
?ORG ?GOV) means that one of the goals of ?ORG is to realize the &%FormOfGovernment ?GOV.")
(instance ideologicalAffiliationOfOrganization BinaryPredicate)
(domain ideologicalAffiliationOfOrganization 1 Organization)
(domain ideologicalAffiliationOfOrganization 2 FormOfGovernment)

(documentation religiousAffiliationOfOrganization EnglishLanguage "(religiousAffiliationOfOrganization 
?ORG ?BELIEF) means that one of the goals of ?ORG is to advance the religious teachings 
of the &%BeliefGroup ?BELIEF.")
(instance religiousAffiliationOfOrganization BinaryPredicate)
(domain religiousAffiliationOfOrganization 1 Organization)
(domain religiousAffiliationOfOrganization 2 BeliefGroup)

(documentation allegiance EnglishLanguage "(allegiance ?AGENT ?ENTITY) means that the &%CognitiveAgent 
?AGENT owes its allegiance to the political entity ?ENTITY.")
(instance allegiance BinaryPredicate)
(domain allegiance 1 CognitiveAgent)
(domain allegiance 2 CognitiveAgent)

(documentation ancestorOrganization EnglishLanguage "(ancestorOrganization ?ORG1 ?ORG2) means that 
the &%Organization ?ORG1 descended from the &%Organization ?ORG2.")
(instance ancestorOrganization BinaryPredicate)
(instance ancestorOrganization TransitiveRelation)
(instance ancestorOrganization IrreflexiveRelation)
(domain ancestorOrganization 1 Organization)
(domain ancestorOrganization 2 Organization)
(disjointRelation ancestorOrganization subOrganization)

(documentation anniversary EnglishLanguage "(anniversary ?PHYSICAL ?TIME) means that 
?TIME is the class of TimeIntervals which mark the anniversary of 
?PHYSICAL.  For example, (anniversary Christmas (&%DayFn 35 &%December)) 
means that Christmas is celebrated each year on the 25th of &%December.")
(instance anniversary BinaryPredicate)
(instance anniversary TemporalRelation)
(domain anniversary 1 Physical)
(domain anniversary 2 TimeInterval)

(documentation birthday EnglishLanguage "(birthday ?PERSON ?DAY) means that ?DAY is the 
anniversary each year of the birth of ?PERSON.  For example, (birthday 
&%WilliamJeffersonClinton (&%DayFn 19 &%August)) means that Bill Clinton's 
birthday is &%August 19th.")
(domain birthday 1 Human)
(domain birthday 2 Day)
(subrelation birthday anniversary)

(documentation birthplace EnglishLanguage "(birthplace ?INDIV ?PLACE) means that the &%Animal ?INDIV was born 
at the location ?PLACE.  The location may be a geographic area or a building, such as a hospital.")
(instance birthplace BinaryPredicate)
(domain birthplace 1 Animal)
(domain birthplace 2 Object)

(=>
  (birthplace ?INDIV ?PLACE)
  (exists (?BIRTH)
    (and
      (instance ?BIRTH Birth)
      (experiencer ?BIRTH ?INDIV)
      (located ?BIRTH ?PLACE))))

(documentation birthdate EnglishLanguage "(birthdate ?PERSON ?DAY) means that ?DAY is the 
&%Day on which the ?PERSON was born.")
(instance birthdate BinaryPredicate)
(domain birthdate 1 Human)
(domain birthdate 2 Day)
(=>
  (birthdate ?P ?D)
  (exists (?B)
    (and
      (instance ?B Birth)
      (experiencer ?B ?P)
      (date ?B ?D))))

(documentation deathdate EnglishLanguage "(birthdate ?PERSON ?DAY) means that ?DAY is the 
&%Day on which the ?PERSON died.")
(domain deathdate 1 Human)
(domain deathdate 2 Day)
(instance deathdate BinaryPredicate)

(=>
  (deathdate ?P ?T)
  (exists (?D)
    (and
      (instance ?D Death)
      (experiencer ?D ?P)
      (date ?D ?T))))

(documentation deathplace EnglishLanguage "(deathplace ?INDIV ?PLACE) means that the &%Animal ?INDIV died 
at the location ?PLACE.  The location may be a geographic area or a building, such as a hospital.")
(instance deathplace BinaryPredicate)
(domain deathplace 1 Animal)
(domain deathplace 2 Object)
(=>
  (deathplace ?P ?L)
  (exists (?D)
    (and
      (instance ?D Death)
      (experiencer ?D ?P)
      (located ?D ?L))))

(instance equipmentCount TernaryPredicate)
(domain equipmentCount 1 Object)
(domainSubclass equipmentCount 2 Device)
(domain equipmentCount 3 Quantity)

(documentation equipmentCount EnglishLanguage 
"(&%equipmentCount ?OBJECT ?TYPE ?QUANTITY) means that ?OBJECT 
is equipped with devices of the type ?TYPE, in the number ?QUANTITY.  
Equipment associated with an ?OBJECT may be a &%component of ?OBJECT 
(such as the emergency oxygen system built into passenger jets) or it may 
be a device simply located on or with ?OBJECT (such as a first aid kit).")

(documentation equipmentType EnglishLanguage "(equipmentType ?THING ?TYPE) means that 
the &%Artifact ?THING has a component or attachment of &%Device ?TYPE. 
See also &%equipmentTypeCount.")
(instance equipmentType BinaryPredicate)
(domain equipmentType 1 Artifact)
(domain equipmentType 2 Device)

(=>
  (equipmentType ?O ?T)
  (exists (?D)
    (and 
      (instance ?D ?T)
      (or 
        (part ?D ?O)
        (connected ?D ?O)))))

(=>
  (equipmentCount ?O ?T ?C)
  (equipmentType ?O ?T))

(=>
  (equipmentType ?O ?T)
  (exists (?C)
    (and
      (greaterThanOrEqualTo ?C 1)
      (equipmentCount ?O ?T ?C))))

(documentation grammaticalRelation EnglishLanguage "(grammaticalRelation ?PHRASE ?SENTENCE) 
means that the &%Phrase ?PHRASE has a grammatical relation to the Sentence 
?SENTENCE, i.e. it is a subject, object, main verb, etc. of the ?SENTENCE.")
(instance grammaticalRelation BinaryPredicate)
(domain grammaticalRelation 1 Phrase)
(domain grammaticalRelation 2 Sentence)

(documentation pathInSystem EnglishLanguage "(pathInSystem ?PATH ?SYSTEM) means that 
the &%Physical thing ?PATH consists of one or more connected routes in 
the &%PhysicalSystem ?SYSTEM.")
(instance pathInSystem BinaryPredicate)
(domain pathInSystem 1 Transitway)
(domain pathInSystem 2 TransitSystem)
(subrelation pathInSystem part)

(=>
  (pathInSystem ?P ?S)
  (or
    (routeInSystem ?P ?S)
    (exists (?R1 ?R2)
      (and
        (routeInSystem ?R1 ?S)
        (routeInSystem ?R2 ?S)
        (part ?R1 ?P)
        (part ?R2 ?P)
        (connected ?R1 ?R2)))))

(instance distanceOnPath BinaryRelation)
(documentation distanceOnPath EnglishLanguage
"(distanceOnPath ?DIST ?PATH) means that for a given path (which is a
pathInSystem) that the distance of the route is the measurement ?DIST.")
(domain distanceOnPath 1 ConstantQuantity)
(domain distanceOnPath 2 Transitway)

(documentation routeInSystem EnglishLanguage "(routeInSystem ?PART ?SYSTEM) means that 
the &%Transitway ?PART is an established route of the 
&%TransportationSystem ?SYSTEM.")
(instance routeInSystem BinaryPredicate)
(domain routeInSystem 1 Transitway)
(domain routeInSystem 2 TransitSystem)
(subrelation routeInSystem pathInSystem)

(=>
  (and
    (subSystem ?S ?T)
    (routeInSystem ?P ?S))
  (routeInSystem ?P ?T))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Start: General relations on SymbolicStrings ;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(instance inString AsymmetricRelation)
(instance inString BinaryPredicate)
(domain inString 1 Character)
(domain inString 2 SymbolicString)
(documentation inString EnglishLanguage "(&%inString ?&%Character
?&%SymbolicString) means that ?&%Character is &%part of
?&%SymbolicString.  See also &%subString.")
(subrelation inString part)

;; NS: Narrowing part -> inString
(=>
  (and
    (instance ?STRING SymbolicString)
    (part ?CHAR ?STRING)
    (instance ?CHAR Character))
  (inString ?CHAR ?STRING))

;; NS: TO DO: create *NullString, by anaolgy with NullList.  A
;; (or, the) NullString is a SymbolicString comprising no
;; Characters.  The following rule in Merge.kif, which relates
;; SymbolicString to Character, will have to be changed if
;; NullString is to be a subclass of SymbolicString:
;;
;; (=>
;;     (instance ?STRING SymbolicString)
;;     (exists (?PART)
;;         (and
;;             (part ?PART ?STRING)
;;             (instance ?PART Character))))

(instance subString AntisymmetricRelation)
(instance subString ReflexiveRelation)
(instance subString BinaryPredicate)
(domain subString 1 SymbolicString)
(domain subString 2 SymbolicString)
(documentation subString EnglishLanguage "(&%subString
?&%SymbolicString-1 ?&%SymbolicString-2) means that
?&%SymbolicString-1 is &%part of ?&%SymbolicString-2.
?&%SymbolicString-2 includes all the same &%Characters as
?&%SymbolicString-1 and in the same order, but
?&%SymbolicString-2 may include more &%Characters than
?&%SymbolicString-1.  See also &%inString.")
(subrelation subString part)

(=>
  (and
    (subString ?S1 ?S2)
    (equal ?L1 (StringLengthFn ?S1))
    (equal ?L2 (StringLengthFn ?S2)))
  (lessThanOrEqualTo ?L1 ?L2))

(=>
  (and
    (subString ?S1 ?S2)
    (inString ?CH ?S1))
  (inString ?CH ?S2))
;; NS: Note that character and string equality vs. identity might
;; be implementation issues.

(instance SubstringFn TernaryFunction)
(instance SubstringFn PartialValuedRelation)
(instance SubstringFn SingleValuedRelation)
(domain SubstringFn 1 SymbolicString)
(domain SubstringFn 2 NonnegativeInteger)
(domain SubstringFn 3 NonnegativeInteger)
(range SubstringFn SymbolicString)
(documentation SubstringFn EnglishLanguage "(&%SubstringFn
?&%SymbolicString ?&%NonnegativeInteger-1
?&%NonnegativeInteger-2) denotes a string that is a &%subString
of ?&%SymbolicString, begins with the &%Character at the index
?&%NonnegativeInteger-1, and ends with the &%Character at the
index (&%SubtractionFn ?&%NonnegativeInteger-2 1).  The first
&%Character in ?&%SymbolicString is denoted by the index 0.  If
the value of ?&%NonnegativeInteger-1 or ?&%NonnegativeInteger-2
is greater than the value of (&%StringLengthFn
?&%SymbolicString), or if ?&%NonnegativeInteger-1 is greater than
?&%NonnegativeInteger-2, the result of evaluating the
&%SubstringFn expression is undefined.  Example: (&%equal
&quot;Charle&quot; (&%SubstringFn &quot;Charles Darwin&quot; 0 6)).")

(=>
  (and
    (equal ?SUB (SubstringFn ?STRING ?START ?END))
    (equal ?LEN (StringLengthFn ?STRING)))
  (and
    (lessThanOrEqualTo ?START ?END)
    (lessThan ?START ?LEN)
    (lessThanOrEqualTo ?END ?LEN)))

(instance StringLengthFn UnaryFunction)
(instance StringLengthFn TotalValuedRelation)
(instance StringLengthFn SingleValuedRelation)
(domain StringLengthFn 1 SymbolicString)
(range StringLengthFn NonnegativeInteger)
(documentation StringLengthFn EnglishLanguage "This
&%UnaryFunction takes a &%SymbolicString as its sole argument.
The result of evaluating a term formed with &%StringLengthFn is a
&%NonnegativeInteger that denotes the number of &%Characters in
the &%SymbolicString.  Example: (&%equal 14 (&%StringLengthFn
&quot;Charles Darwin&quot;)).")

(instance stringLength BinaryPredicate)
(instance stringLength TotalValuedRelation)
(instance stringLength SingleValuedRelation)
(instance stringLength AsymmetricRelation)
(domain stringLength 1 NonnegativeInteger)
(domain stringLength 2 SymbolicString)
(documentation stringLength EnglishLanguage "(&%stringLength
?&%NonnegativeInteger ?&%SymbolicString) means that the number of
&%Characters in ?&%SymbolicString is ?&%NonnegativeInteger.
Example: (&%stringLength 14 &quot;Charles Darwin&quot;), which is a
&%True &%Sentence.")

(<=>
  (stringLength ?INT ?STRING)
  (equal ?INT (StringLengthFn ?STRING)))

(instance StringConcatenateFn BinaryFunction)
(instance StringConcatenateFn TotalValuedRelation)
(instance StringConcatenateFn SingleValuedRelation)
(domain StringConcatenateFn 1 SymbolicString)
(domain StringConcatenateFn 2 SymbolicString)
(range StringConcatenateFn SymbolicString)
(documentation StringConcatenateFn EnglishLanguage "The result of
applying this &%Function to two &%SymbolicString arguments is a
third &%SymbolicString that is the concatenation of the two
arguments.  Example: (&%equal &quot;Charles
Darwin&quot; (&%StringConcatenateFn &quot;Charles &quot; &quot;Darwin&quot;)).")

(instance stringConcatenation TernaryPredicate)
(instance stringConcatenation TotalValuedRelation)
(instance stringConcatenation SingleValuedRelation)
(domain stringConcatenation 1 SymbolicString)
(domain stringConcatenation 2 SymbolicString)
(domain stringConcatenation 3 SymbolicString)
(documentation stringConcatenation
EnglishLanguage "(&%stringConcatenation ?&%SymbolicString-1
?&%SymbolicString-2 ?&%SymbolicString-3) means that
?&%SymbolicString-3 can be formed by appending
?&%SymbolicString-1 and ?&%SymbolicString-2, in order.
Example: (&%stringConcatenation &quot;Charles &quot; &quot;Darwin&quot; &quot;Charles
Darwin&quot;) is a &%True &%Sentence.")

(<=>
  (stringConcatenation ?S1 ?S1 ?S3)
  (equal ?S3 (StringConcatenateFn ?S1 ?S2)))

(instance precedesInString TernaryPredicate)
(domain precedesInString 1 SymbolicString)
(domain precedesInString 2 SymbolicString)
(domain precedesInString 3 SymbolicString)
(documentation precedesInString
EnglishLanguage "(&%precedesInString ?&%SymbolicString-1
?&%SymbolicString-2 ?&%SymbolicString-3) means that
?&%SymbolicString-1 and ?&%SymbolicString-2 are &%subStrings of
?&%SymbolicString-3, and ?&%SymbolicString-1 occurs sequentially
before ?&%SymbolicString-2 in ?&%SymbolicString-3.
Example: (&%precedesInString &quot;Johann&quot; &quot;Bach&quot; &quot;Johann
Sebastian Bach&quot;).")
(subrelation stringConcatenation precedesInString)

(=>
  (precedesInString ?S1 ?S2 ?S3)
  (and
    (subString ?S1 ?S3)
    (subString ?S2 ?S3)))

(=>
  (and
    (precedesInString ?S1 ?S2 ?S4)
    (precedesInString ?S2 ?S3 ?S4))
  (precedesInString ?S1 ?S3 ?S4))  
  
  
;; --------------------------------------------------------------------------------
;; Address and Telephone details adapted from Media.kif
;; --------------------------------------------------------------------------------
 
 ;;;;;;;;;;;;;;;;;;;;;;;;
;; Start: Identifiers ;;
;;;;;;;;;;;;;;;;;;;;;;;;

;; "IdentifierComposite"
;; "IdentifierSyntaxMismatch"
;; "IdentifierType"

(subclass Descriptor ContentBearingObject)

;; "DescriptorComposite"
;; "DescriptorDesignation"
;; "DescriptorExtension"
;; "DescriptorSyntax"
;; "DescriptorType"

(subclass Name Descriptor)
(documentation Name EnglishLanguage "&%Name denotes the &%Class
of &%ContentBearingObjects that identify an &%Entity, typically
in some linguistic form such as an epithet, given name, street
address, etc.")

(subclass Identifier Name)
(documentation Identifier EnglishLanguage "&%Identifier is the
&%Class of &%ContentBearingObjects that identify some entity,
such as a bank account, a person, or a location (e.g., as
identified by a specific street address or GPS coordinates),
perhaps uniquely under some circumstances.")

(subclass PlaceDescriptor Descriptor)
(subclass DescriptorPart ContentBearingObject)
(subclass NamePart DescriptorPart)
(subclass IdentifierPart NamePart)

(subclass PlaceID PlaceDescriptor)

(subclass LocalizablePlaceID PlaceID)

(subclass PlaceAddress PlaceID)

(subclass ContactSite Object)
(documentation ContactSite EnglishLanguage "A &%ContactSite is an
&%Object, typically a &%Place or a &%Residence or a
&%CommunicationDevice such as a &%Telephone, that has some kind
of address identifier and can serve as a point of contact for a
&%Human or &%Organization.")

(subclass TelephonyDevice ContactSite)
(subclass TelephonyDevice CommunicationDevice)
(subclass TelephonyDevice ElectricDevice)
(documentation TelephonyDevice EnglishLanguage "A
&%TelephonyDevice is a &%CommunicationDevice that, when connected
to a &%TelephoneSystem, functions as an access point allowing end
users (&%Humans or other &%Agents, such as autonomous/scheduled
&%ComputerPrograms) to send or receive signals via the system.
&%Telephones and &%FaxMachines are &%TelephonyDevices.  A given
&%Computer may or may not function as a &%TelephonyDevice,
depending on the &%ComputerPrograms it runs and the
&%ComputerComponents that are part of it.")

(subclass Telephone TelephonyDevice)
(subclass FaxMachine TelephonyDevice)

(subclass LocalizablePlace GeographicArea)

;; Address Elements (as collected from http://www.bitboost.com/ref/international-address-formats.html)
;; a.) Street Number (can contain letters) --> postStreetNumber from Media.kif
;; b.) Floor Code (can contain letters) --> floorCode
;; c.) Unit Number (can contain letters) --> unitNumber
;; d.) Street Name --> postStreet
;; e.) Subdivision / Neighborhood / Quarter --> postNeighborhood
;; f.) City --> postCity
;; g.) State / Territory --> postDistrict
;; h.) Country --> postCountry
;; i.) Post Office Box Number --> postPostOfficeBox
;; j.) Building Name --> names
;; k.) Province Code --> codeMapping
;; l.) Postal Code (can contain letters) --> postPostcodeArea

;; changed canonicalPostPlaceName to canonicalPlaceName to denote that this can be 
;; applied to different types of areas (countries, continents, etc) and not only to 
;; LocalizablePlace.  Also, canonicalPostPlaceName has domain LocalizablePlace but 
;; it is applied to geographic areas that aren't subclassed or instantiated from 
;; LocalizablePlace
(instance canonicalPlaceName TernaryPredicate)
(documentation canonicalPlaceName EnglishLanguage "(&%canonicalPlaceName ?PLACE 
?LANG ?STRING) means that in the &%Language ?LANG, the &%SymbolicString ?STRING 
is a canonical name for the &%GeographicArea ?PLACE")
(domain canonicalPlaceName 1 Region)
(domain canonicalPlaceName 2 Language)
(domain canonicalPlaceName 3 SymbolicString)

(=>
  (canonicalPlaceName ?PLACE ?LANG ?STR)
  (names ?STR ?PLACE))

(subclass PostalPlace StationaryArtifact)
(subclass StationaryArtifact LocalizablePlace)
(subclass PostalPlace ContactSite)
(documentation PostalPlace EnglishLanguage "A &%PostalPlace is
any &%LocalizablePlace that can have a postal address, and to
which hardcopy mail (letters, packages, etc.) can be delivered.")

(subclass MilitaryInstallation PostalPlace)
(subclass Residence PostalPlace)
(subclass PlaceOfCommerce PostalPlace)

(instance siteForContact AsymmetricRelation)
(instance siteForContact BinaryPredicate)
(domain siteForContact 1 ContactSite)
(domain siteForContact 2 Agent)
(documentation siteForContact EnglishLanguage "(&%siteForContact
?&%ContactSite ?&%Agent) means that the &%Human or &%Organization
?&%Agent can be reached at or via the point of contact
?&%ContactSite.")

(instance postContactSite AsymmetricRelation)
(instance postContactSite BinaryPredicate)
(domain postContactSite 1 PostalPlace)
(domain postContactSite 2 Agent)
(documentation postContactSite
EnglishLanguage "(&%postContactSite ?&%PostalPlace ?&%Agent)
means that the &%Human or &%Organization ?&%Agent can be
contacted by post (mail) at the location ?&%PostalPlace.")
(subrelation postContactSite siteForContact)

(instance unitNumber BinaryPredicate)
(documentation unitNumber EnglishLanguage "(&%unitNumber ?STR ?PLACE) means that 
?STR represents the particular unit or space within the building that a 
&%PostalPlace is located")
(domain unitNumber 1 SymbolicString)
(domain unitNumber 2 PostalPlace)

(instance floorCode BinaryPredicate)
(documentation floorCode EnglishLanguage "(&%floorCode ?STR ?PLACE) means that ?STR 
represents the &%BuildingLevel that a particular &%PostalPlace can be found at")
(domain floorCode 1 SymbolicString)
(domain floorCode 2 PostalPlace)

(=>
  (and
    (unitNumber ?UNITSTR ?PLACE)
    (floorCode ?FLRCODE ?PLACE))
  (exists (?UNIT ?FLOOR)
    (and
      (instance ?FLOOR BuildingLevel)
      (instance ?UNIT Room)
      (located ?UNIT ?FLOOR)
      (part ?FLOOR ?PLACE)
      (names ?UNITSTR ?UNIT)
      (names ?FLRCODE ?FLOOR))))

(=>
  (and
    (unitNumber ?UNITSTR ?PLACE)
    (not 
      (exists (?FLR)
        (floorCode ?FLR ?PLACE))))
  (exists (?UNIT)
    (and
      (instance ?UNIT Room)
      (names ?UNITSTR ?UNIT)
      (located ?UNIT ?BLDG))))

(subclass Neighborhood GeographicArea)
(documentation Neighborhood EnglishLanguage "&%Neighborhood refers to a 
&%GeographicArea that is a subregion of a larger geographic area, such as a 
&%City, normally with a distinctive feature")
(termFormat EnglishLanguage Neighborhood "neighborhood")

(=>
  (instance ?PLACE Neighborhood)
  (exists (?AREA)
    (geographicSubregion ?PLACE ?AREA)))
      
(instance postNeighborhood BinaryPredicate)
(instance postNeighborhood AsymmetricRelation)
(subrelation postNeighborhood located)
(documentation postNeighborhood EnglishLanguage "(&%postNeighborhood ?PLACE 
?NEIGHBORHOOD) means that ?PLACE is located in ?NEIGHBORHOOD")
(domain postNeighborhood 1 PostalPlace)
(domain postNeighborhood 2 Neighborhood)

(<=>
  (and
    (located ?PLACE ?GEO)
    (instance ?PLACE PostalPlace)
    (instance ?GEO Neighborhood))
  (postNeighborhood ?PLACE ?GEO))

(=>
  (and
    (postNeighborhood ?PLACE ?NEIGHBORHOOD)
    (geographicSubregion ?NEIGHBORHOOD ?CITY)
    (instance ?CITY City))
  (postCity ?PLACE ?CITY))
  
(=>
  (and
    (postNeighborhood ?PLACE ?NEIGHBORHOOD)
    (geographicSubregion ?NEIGHBORHOOD ?STATE)
    (instance ?STATE StateOrProvince))
  (postDistrict ?PLACE ?STATE))    

(instance postCountry AsymmetricRelation)
(instance postCountry BinaryPredicate)
(domain postCountry 1 PostalPlace)
(domain postCountry 2 GeopoliticalArea)
(subrelation postCountry located)
(documentation postCountry EnglishLanguage "(&%postCountry
?&%PostalPlace ?&%GeopoliticalArea) means that ?&%PostalPlace is
located in the &%Nation or &%DependencyOrSpecialSovereigntyArea
?&%GeopoliticalArea.")

(=>
  (postCountry ?PLACE ?COUNTRY)
  (or
    (instance ?COUNTRY Nation)
    (instance ?COUNTRY DependencyOrSpecialSovereigntyArea)))

(<=>
  (and
    (located ?PLACE ?GEO)
    (instance ?PLACE PostalPlace)
    (or
      (instance ?GEO Nation)
      (instance ?GEO DependencyOrSpecialSovereigntyArea)))
  (postCountry ?PLACE ?GEO))

(=>
  (and
    (postCity ?PLACE ?CITY)
    (geographicSubregion ?CITY ?AREA)
    (instance ?AREA StateOrProvince))
  (postDistrict ?PLACE ?AREA))

(=>
  (and
    (postCity ?PLACE ?CITY)
    (geographicSubregion ?CITY ?AREA)
    (or
      (instance ?AREA Nation)
      (instance ?AREA DependencyOrSpecialSovereigntyArea)))
  (postCountry ?PLACE ?AREA))
  
(instance postDistrict AsymmetricRelation)
(instance postDistrict BinaryPredicate)
(domain postDistrict 1 PostalPlace)
(domain postDistrict 2 StateOrProvince)
(subrelation postDistrict located)
(documentation postDistrict EnglishLanguage "(&%postDistrict
?&%PostalPlace ?&%StateOrProvince) means that ?&%PostalPlace is
located in ?&%StateOrProvince.")

(<=>
  (and
    (located ?PLACE ?GEO)
    (instance ?PLACE PostalPlace)
    (instance ?GEO StateOrProvince))
  (postDistrict ?PLACE ?GEO))

(instance postCity AsymmetricRelation)
(instance postCity BinaryPredicate)
(domain postCity 1 PostalPlace)
(domain postCity 2 City)
(subrelation postCity located)
(documentation postCity EnglishLanguage "(&%postCity
?&%PostalPlace ?&%City) means that ?&%PostalPlace is located in
?&%City.")

(<=>
  (and
    (located ?PLACE ?GEO)
    (instance ?PLACE PostalPlace)
    (instance ?GEO City))
  (postCity ?PLACE ?GEO))
  
(subclass PostcodeArea LocalizablePlace)
(documentation PostcodeArea EnglishLanguage "An &%instance of
&%PostcodeArea is a &%LocalizablePlace that is identified by a
&%Postcode.")

(=>
  (instance ?AREA PostcodeArea)
  (exists (?GEO)
    (geographicSubregion ?AREA ?GEO)))

(instance postPostcodeArea AsymmetricRelation)
(instance postPostcodeArea BinaryPredicate)
(domain postPostcodeArea 1 PostalPlace)
(domain postPostcodeArea 2 PostcodeArea)
(subrelation postPostcodeArea located)
(documentation postPostcodeArea
EnglishLanguage "(&%postPostcodeArea ?&%PostalPlace
?&%PostcodeArea) means that ?&%PostalPlace is located in
?&%PostcodeArea.")

(<=>
  (and
    (located ?PLACE ?AREA)
    (instance ?PLACE PostalPlace)
    (instance ?AREA PostcodeArea))
  (postPostcodeArea ?PLACE ?AREA))

(=>
  (and
    (postPostcodeArea ?PLACE ?AREA)
    (geographicSubregion ?AREA ?CITY)
    (instance ?CITY City))
  (postCity ?PLACE ?CITY))

(=>
  (and
    (postPostcodeArea ?PLACE ?AREA)
    (geographicSubregion ?AREA ?GEO)
    (instance ?GEO StateOrProvince))
  (postDistrict ?PLACE ?GEO))
  
(instance postStreet AsymmetricRelation)
(instance postStreet BinaryPredicate)
(domain postStreet 1 PostalPlace)
(domain postStreet 2 Roadway)
(documentation postStreet EnglishLanguage "(&%postStreet
?&%PostalPlace ?&%Roadway) means that ?&%PostalPlace is
&%Adjacent to ?&%Roadway.")

(=>
  (postStreet ?POSTPLACE ?ROADWAY)
  (orientation ?POSTPLACE ?ROADWAY Adjacent))

(instance postStreetNumber AsymmetricRelation)
(instance postStreetNumber BinaryPredicate)
(domain postStreetNumber 1 SymbolicString)
(domain postStreetNumber 2 PostalPlace)
(documentation postStreetNumber
EnglishLanguage "(&%postStreetNumber ?&%SymbolicString
?&%PostalPlace) means that ?&%SymbolicString identifies the
sub-city block or building level location of a ?&%PostalPlace.
?&%SymbolicString need not consist exclusively of
&%DigitCharacters.")

(instance postPostOfficeBox AsymmetricRelation)
(instance postPostOfficeBox BinaryPredicate)
(domain postPostOfficeBox 1 SymbolicString)
(domain postPostOfficeBox 2 PostalPlace)
(documentation postPostOfficeBox
EnglishLanguage "(&%postPostOfficeBox ?&%SymbolicString
?&%PostalPlace) means that ?&%SymbolicString identifies the post
office box of a ?&%PostalPlace.  ?&%SymbolicString need not
consist exclusively of &%DigitCharacters.")

(subclass PostalAddressText PlaceAddress)
(subclass PostalAddressText LocalizablePlaceID)
(subclass PostalAddressText SymbolicString)
(subclass PostalAddressText LinguisticExpression)

(=>
  (and
    (postCountry ?PLACE UnitedStates)
    (canonicalPlaceName UnitedStates EnglishLanguage ?COUNTRYSTR)
    (postPostcodeArea ?PLACE ?POST)
    (names ?POST ?POSTSTR)
    (postDistrict ?PLACE ?STATE)
    (canonicalPlaceName ?STATE EnglishLanguage ?STATESTR)
    (postCity ?PLACE ?CITY)
    (canonicalPlaceName ?CITY EnglishLanguage ?CITYSTR)
    (postStreet ?PLACE ?STREET)
    (canonicalPlaceName ?STREET EnglishLanguage ?STREETSTR)
    (postStreetNumber ?STREETNUM ?PLACE)
    (not (exists (?FC ?UN)
      (and
        (floorCode ?FC ?PLACE)
        (unitNumber ?UN ?PLACE)))))
  (and
    (postAddressText ?TEXT ?PLACE)
    (precedesInString ?STREETNUM ?STREETSTR ?TEXT)
    (precedesInString ?STREETSTR ?CITYSTR ?TEXT)
    (precedesInString ?CITYSTR ?STATESTR ?TEXT)
    (precedesInString ?STATESTR ?POSTSTR ?TEXT)
    (precedesInString ?POSTSTR ?COUNTRYSTR ?TEXT)))
    
(=>
  (and
    (postCountry ?PLACE UnitedStates)
    (canonicalPlaceName UnitedStates EnglishLanguage ?COUNTRYSTR)
    (postPostcodeArea ?PLACE ?POST)
    (names ?POST ?POSTSTR)
    (postDistrict ?PLACE ?STATE)
    (canonicalPlaceName ?STATE EnglishLanguage ?STATESTR)
    (postCity ?PLACE ?CITY)
    (canonicalPlaceName ?CITY EnglishLanguage ?CITYSTR)
    (postStreet ?PLACE ?STREET)
    (canonicalPlaceName ?STREET EnglishLanguage ?STREETSTR)
    (postStreetNumber ?STREETNUM ?PLACE)
    (unitNumber ?UN ?PLACE)
    (not (exists (?FC)
      (floorCode ?FC ?PLACE))))
  (and
    (postAddressText ?TEXT ?PLACE)
    (precedesInString ?UN ?STREETNUM ?TEXT)
    (precedesInString ?STREETNUM ?STREETSTR ?TEXT)
    (precedesInString ?STREETSTR ?CITYSTR ?TEXT)
    (precedesInString ?CITYSTR ?STATESTR ?TEXT)
    (precedesInString ?STATESTR ?POSTSTR ?TEXT)
    (precedesInString ?POSTSTR ?COUNTRYSTR ?TEXT)))
    
(=>
  (and
    (postCountry ?PLACE UnitedStates)
    (canonicalPlaceName UnitedStates EnglishLanguage ?COUNTRYSTR)
    (postPostcodeArea ?PLACE ?POST)
    (names ?POST ?POSTSTR)
    (postDistrict ?PLACE ?STATE)
    (canonicalPlaceName ?STATE EnglishLanguage ?STATESTR)
    (postCity ?PLACE ?CITY)
    (canonicalPlaceName ?CITY EnglishLanguage ?CITYSTR)
    (postStreet ?PLACE ?STREET)
    (canonicalPlaceName ?STREET EnglishLanguage ?STREETSTR)
    (postStreetNumber ?STREETNUM ?PLACE)
    (unitNumber ?UN ?PLACE)
    (floorCode ?FC ?PLACE))
  (and
    (postAddressText ?TEXT ?PLACE)
    (precedesInString ?UN ?FC ?TEXT)
    (precedesInString ?FC ?STREETNUM ?TEXT)
    (precedesInString ?STREETNUM ?STREETSTR ?TEXT)
    (precedesInString ?STREETSTR ?CITYSTR ?TEXT)
    (precedesInString ?CITYSTR ?STATESTR ?TEXT)
    (precedesInString ?STATESTR ?POSTSTR ?TEXT)
    (precedesInString ?POSTSTR ?COUNTRYSTR ?TEXT)))

(=>
  (and
    (postCountry ?PLACE UnitedStates)
    (canonicalPlaceName UnitedStates EnglishLanguage ?COUNTRYSTR)
    (postPostcodeArea ?PLACE ?POST)
    (names ?POST ?POSTSTR)
    (postPostOfficeBox ?POBOX ?PLACE)
    (postPostcodeArea ?PLACE ?POST)
    (names ?POST ?POSTSTR)
    (postDistrict ?PLACE ?STATE)
    (canonicalPlaceName ?STATE EnglishLanguage ?STATESTR)
    (postCity ?PLACE ?CITY)
    (canonicalPlaceName ?CITY EnglishLanguage ?CITYSTR))
  (and
    (postAddressText ?TEXT ?PLACE)
    (precedesInString ?POBOX ?CITYSTR ?TEXT)
    (precedesInString ?CITYSTR ?STATESTR ?TEXT)
    (precedesInString ?STATESTR ?POSTSTR ?TEXT)
    (precedesInString ?POSTSTR ?COUNTRYSTR ?TEXT)))
    
(instance postAddressText AsymmetricRelation)
(instance postAddressText BinaryPredicate)
(domain postAddressText 1 PostalAddressText)
(domain postAddressText 2 PostalPlace)
(documentation postAddressText
EnglishLanguage "(&%postAddressText ?&%PostalAddressText
?&%PostalPlace) means that the &%ContactSite ?&%PostalPlace is
identified by ?&%PostalAddressText.")

;; --------------------------------------------------------------------------------
;; Telephone number structure adapted from Media.kif
;; --------------------------------------------------------------------------------

(instance telecomContactDevice AsymmetricRelation)
(instance telecomContactDevice BinaryPredicate)
(domain telecomContactDevice 1 TelephonyDevice)
(domain telecomContactDevice 2 Agent)
(documentation telecomContactDevice
EnglishLanguage "(&%telecomContactDevice ?&%TelephonyDevice
?&%Agent) means that ?&%TelephonyDevice is a &%ContactSite for
the &%Human or &%Organization ?&%Agent.")
(subrelation telecomContactDevice siteForContact)

;; NS: Actually, this is more complicated in the case of "cord
;; phones", where multiple &%FixedPhones may share the same line
;; and, so, the same telephone number.  The phone number uniquely
;; identifies the line (termination or connection location), not
;; the phone (hand set).

(subclass VirtualPlaceID PlaceID)

(subclass VirtualAddress PlaceAddress)
(subclass VirtualAddress VirtualPlaceID)

(subclass TelecomNumber VirtualAddress)
(subclass TelecomNumber SymbolicString)
(subclass TelecomNumber ContactSite)

(instance deviceTelecomNumber BinaryPredicate)
(instance deviceTelecomNumber AsymmetricRelation)
(domain deviceTelecomNumber 1 TelecomNumber)
(domain deviceTelecomNumber 2 TelephonyDevice)
(documentation deviceTelecomNumber
EnglishLanguage "(&%deviceTelecomNumber ?&%TelecomNumber
?&%TelephonyDevice) means that ?&%TelephonyDevice is identified,
and can be accessed, by ?&%TelecomNumber.")

;; (subclass TelecomNumberPart IdentifierPart)
;; (subclass TelecomNumberPart VirtualPlaceID)
;; (subclass TelecomNumberPart SymbolicString)

(instance telecomCoreNumber BinaryPredicate)
(instance telecomCoreNumber AsymmetricRelation)
(domain telecomCoreNumber 1 SymbolicString)
(domain telecomCoreNumber 2 TelecomNumber)
(subrelation telecomCoreNumber subString)
(documentation telecomCoreNumber
EnglishLanguage "(&%telecomCoreNumber ?&%SymbolicString
?&%TelecomNumber) means that ?&%SymbolicString is the &%part of a
&%TelecomNumber that identifies an individual subscriber line,
not including the area code and any possible extensions.")

(instance telecomCountryCode BinaryPredicate)
(instance telecomCountryCode AsymmetricRelation)
(domain telecomCountryCode 1 SymbolicString)
(domain telecomCountryCode 2 TelecomNumber)
(documentation telecomCountryCode
EnglishLanguage "(&%telecomCountryCode ?&%SymbolicString
&%TelecomNumber) means that ?&%SymbolicString is the &%part of
?&%TelecomNumber that identifies the &%Nation in which the device
identified by ?&%TelecomNumber is located (or registered).
?&%SymbolicString will be standard code conforming to, and
assigned by, ITU-T Recommendation E.164.")
(subrelation telecomCountryCode subString)

(instance telecomAreaCode BinaryPredicate)
(instance telecomAreaCode AsymmetricRelation)
(domain telecomAreaCode 1 SymbolicString)
(domain telecomAreaCode 2 TelecomNumber)
(documentation telecomAreaCode
EnglishLanguage "(&%telecomAreaCode ?&%SymbolicString
&%TelecomNumber) means that ?&%SymbolicString is the &%part of
?&%TelecomNumber that follows the country code.
?&%SymbolicString may denote a &%GeographicArea in which the
&%TelephonyDevice identified by ?&%TelecomNumber is
located (registered), but it may also denote a call billing plan
or status, as for 800 numbers in the North American Numbering
Plan.")
(subrelation telecomAreaCode subString)

(instance telecomCode2 BinaryPredicate)
(instance telecomCode2 AsymmetricRelation)
(domain telecomCode2 1 SymbolicString)
(domain telecomCode2 2 TelecomNumber)
(documentation telecomCode2 EnglishLanguage "(&%telecomCode2
?&%SymbolicString ?&%TelecomNumber) means that ?&%SymbolicString
is the second code (if there is one, such as a sub-area code)
following the part of ?&%TelecomNumber denoted by
&%telecomCountryCode.")
(subrelation telecomCode2 subString)

(instance telecomExtension BinaryPredicate)
(instance telecomExtension AsymmetricRelation)
(domain telecomExtension 1 SymbolicString)
(domain telecomExtension 2 TelecomNumber)
(documentation telecomExtension
EnglishLanguage "(&%telecomExtension ?&%SymbolicString
?&%TelecomNumber) means that ?&%SymbolicString is a &%part of
?&%TelecomNumber that identifies a &%TelephonyDevice within a
group of such &%Devices, the members of which share a local
subscriber line.")
;; NS: TO DO: create &%SubscriberTelephoneLine, relate it to
;; &%MainTelephoneLine and &%TelephoneSystem, which are defined
;; in Communications.kif.

(=>
  (and
    (telecomCoreNumber ?CN ?WHOLE)
    (telecomAreaCode ?AC ?WHOLE))
  (precedesInString ?AC ?CN ?WHOLE))

(=>
  (and
    (telecomCoreNumber ?CN ?WHOLE)
    (telecomCountryCode ?CC ?WHOLE))
  (precedesInString ?CC ?CN ?WHOLE))

(=>
  (and
    (telecomAreaCode ?AC ?WHOLE)
    (telecomCountryCode ?CC ?WHOLE))
  (precedesInString ?CC ?AC ?WHOLE))

(=>
  (and
    (telecomCoreNumber ?CN ?WHOLE)
    (telecomExtension ?EX ?WHOLE))
  (precedesInString ?CN ?EX ?WHOLE))

(=>
  (and
    (telecomCode2 ?C2 ?WHOLE)
    (telecomAreaCode ?AC ?WHOLE))
  (precedesInString ?AC ?C2 ?WHOLE))

(=>
  (and
    (telecomCode2 ?C2 ?WHOLE)
    (telecomCountryCode ?CC ?WHOLE))
  (precedesInString ?CC ?C2 ?WHOLE))

;; NS: It's not clear from the documentation for "TelecomCode2",
;; nor from an otherwise good explanation of telecom number
;; fields (http://en.wikipedia.org/wiki/Area_code), if the
;; portion of a telecom number denoted by "TelecomCode2" can be
;; part of the "CoreTelecomNumber" (e.g., designating the local
;; exchange, as in the xxx part of a US number conforming to the
;; pattern xxx-yyyy), or is something else, different from both
;; the "CoreTelecomNumber" and the area code.  Therefore, the
;; rules above are agnostic on this point, and less complete than
;; they might otherwise be.

(instance telephoneNumber BinaryPredicate)
(instance telephoneNumber AsymmetricRelation)
(domain telephoneNumber 1 TelecomNumber)
(domain telephoneNumber 2 Agent)
(documentation telephoneNumber EnglishLanguage "(%&telephoneNumber ?NUMBER ?AGENT) 
means that ?&%Agent identifies a &%TelecomeNumber at which ?&%Agent can be contacted")

(=>
  (telephoneNumber ?NUMBER ?AGENT)
  (siteForContact ?NUMBER ?AGENT))


(instance personalPhoneNumber BinaryPredicate)
(instance personalPhoneNumber AsymmetricRelation)
(domain personalPhoneNumber 1 TelecomNumber)
(domain personalPhoneNumber 2 Human)
(subrelation personalPhoneNumber telephoneNumber)
(documentation personalPhoneNumber
EnglishLanguage "(&%personalPhoneNumber ?&%TelecomNumber
?&%Human) means that ?&%TelecomNumber identifies a &%Telephone at
which ?&%Human can be contacted when not a work, or for purposes
other than work.")

(=>
  (and
    (instance ?DEVICE Telephone)
    (holdsDuring ?TIME 
      (and
        (possesses ?AGENT ?DEVICE)
        (deviceTelecomNumber ?NUMBER ?DEVICE))))
  (holdsDuring ?TIME (telephoneNumber ?NUMBER ?AGENT)))

(instance homePhoneNumber BinaryPredicate)
(instance homePhoneNumber AsymmetricRelation)
(domain homePhoneNumber 1 TelecomNumber)
(domain homePhoneNumber 2 Human)
(subrelation homePhoneNumber telephoneNumber)
(documentation homePhoneNumber
EnglishLanguage "(&%homePhoneNumber ?&%TelecomNumber ?&%Human)
means that ?&%TelecomNumber identifies a &%Telephone located at
?&%Human's &%PermanentResidence.")

;; NS: This is too liberal; a person could have multiple "home"
;; addresses.  Use the telecom area code to match the phone's location
;; to the person's address.
;; (=>
;;   (holdsDuring ?TIME
;;     (and
;;       (deviceTelecomNumber ?NUMBER ?DEVICE)
;;       (homePhoneNumber ?NUMBER ?PERSON)
;;       (home ?PERSON ?RESIDENCE)))
;;   (holdsDuring ?TIME (located ?DEVICE ?RESIDENCE)))

(=>
  (holdsDuring ?TIME 
    (and
      (deviceTelecomNumber ?NUMBER ?DEVICE)
      (homePhoneNumber ?NUMBER ?AGENT)
      (located ?DEVICE ?PLACE)
      (instance ?PLACE PermanentResidence)))
  (holdsDuring ?TIME (home ?AGENT ?PLACE)))

;; This is a weaker version of the axiom immediately above.
(=>
  (and
    (instance ?AGENT Human)
    (holdsDuring ?TIME 
      (and
        (deviceTelecomNumber ?NUMBER ?DEVICE)
        (telephoneNumber ?NUMBER ?AGENT)
        (located ?DEVICE ?PLACE)
        (instance ?PLACE Residence))))
  (holdsDuring ?TIME (inhabits ?AGENT ?PLACE)))

(instance workPhoneNumber BinaryPredicate)
(instance workPhoneNumber AsymmetricRelation)
(domain workPhoneNumber 1 TelecomNumber)
(domain workPhoneNumber 2 Agent)
(subrelation workPhoneNumber telephoneNumber)
(documentation workPhoneNumber
EnglishLanguage "(&%workPhoneNumber ?&%TelecomNumber ?&%Agent)
means that ?&%TelecomNumber identifies a &%Telephone at which
?&%Agent can be contacted when at work, or for business
purposes.")

(instance mobilePhoneNumber BinaryPredicate)
(instance mobilePhoneNumber AsymmetricRelation)
(domain mobilePhoneNumber 1 TelecomNumber)
(domain mobilePhoneNumber 2 Agent)
(subrelation mobilePhoneNumber telephoneNumber)
(documentation mobilePhoneNumber
EnglishLanguage "(&%mobilePhoneNumber ?&%TelecomNumber ?&%Agent)
means that ?&%TelecomNumber identifies a &%MobileCellPhone at
which ?&%Agent can be contacted.")

(=>
  (holdsDuring ?TIME
    (and
      (deviceTelecomNumber ?NUMBER ?DEVICE)
      (mobilePhoneNumber ?NUMBER ?AGENT)))
  (instance ?DEVICE MobileCellPhone))

;; NS: Narrowing telephoneNumber -> mobilePhoneNumber.
(=>
  (and
    (instance ?DEVICE MobileCellPhone)
    (holdsDuring ?TIME
      (and
        (deviceTelecomNumber ?NUMBER ?DEVICE)
        (telephoneNumber ?NUMBER ?AGENT))))
  (holdsDuring ?TIME (mobilePhoneNumber ?NUMBER ?AGENT)))

;; NS: This isn't reliably true for Telephones in general, given
;; the rather strict "ownership" sense of possesses.  But it is
;; generally true for cell phones.
(=>
  (holdsDuring ?TIME
    (and
      (deviceTelecomNumber ?NUMBER ?DEVICE)
      (mobilePhoneNumber ?NUMBER ?AGENT)))
  (holdsDuring ?TIME (possesses ?AGENT ?DEVICE)))

(instance faxNumber BinaryPredicate)
(instance faxNumber AsymmetricRelation)
(domain faxNumber 1 TelecomNumber)
(domain faxNumber 2 Agent)
(documentation faxNumber EnglishLanguage "(&%faxNumber
?&%TelecomNumber ?&%Agent) means that ?&%TelecomNumber identifies
a &%FaxMachine at which ?&%Agent can be contacted.")

(=>
  (and
    (instance ?DEVICE FaxMachine)
    (holdsDuring ?TIME 
      (and
        (deviceTelecomNumber ?NUMBER ?DEVICE)
        (possesses ?AGENT ?DEVICE))))
  (holdsDuring ?TIME (faxNumber ?NUMBER ?AGENT)))

;; NS: temporally qualified this way only because telecom numbers
;; can be reassigned to different types of TelephonyDevices.
(=>
  (holdsDuring ?TIME
    (and
      (deviceTelecomNumber ?NUMBER ?DEVICE)
      (faxNumber ?NUMBER ?AGENT)))
  (instance ?DEVICE FaxMachine))      
