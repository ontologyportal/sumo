% time and temporal reasoning

tff(worlds_tp,type,
    w: $tType ).

tff(modals_tp,type,
    m: $tType ).

tff(desires_tp,type,
    desires: m ).

tff(knows_tp,type,
    knows: m ).
        
tff(holdsDuring_tp,type,
    holdsDuring: m ).
   
tff(confersObligation_tp,type,
    confersObligation: m ).
   
tff(employs_tp,type,
    employs: ($i * $i * w ) > $o ).
                
tff(agent_tp,type,
    agent: ($i * $i * w) > $o ).

tff(patient_tp,type,
    patient: ($i * $i * w) > $o ).

tff(destination_tp,type,
    destination: ($i * $i * w) > $o ).

tff(plaintiff_tp,type,
    plaintiff: ($i * $i * w) > $o ).
    
tff(defendant_tp,type,
    defendant: ($i * $i * w) > $o ).
        
tff(victim_tp,type,
    victim: ($i * $i * w) > $o ).

tff(suffers_tp,type,
    suffers: ($i * $i * w) > $o ).
    
tff(earlier_tp,type,    % Doesn't need a world.  Assume basic physics in all worlds. Time can't go backwards.
    earlier: ($i * $i) > $o ).
        
tff(meetsTemporally_tp,type,    % Doesn't need a world.  Assume basic physics in all worlds. Time can't go backwards.
    meetsTemporally: ($i * $i) > $o ).
            
tff(during_tp,type,     % Doesn't need a world.  Assume basic physics in all worlds.
    during: ($i * $i) > $o ).
            
tff(whenFn_tp,type,
    whenFn: ($i * w) > $i ).

tff(pastFn_tp,type,    % Doesn't need a world.  Assume basic physics in all worlds.
    pastFn: ($i) > $i ).
    
tff(futureFn_tp,type,  % Doesn't need a world.  Assume basic physics in all worlds.
    futureFn: ($i) > $i ).

tff(immediatePastFn_tp,type,    % Doesn't need a world.  Assume basic physics in all worlds.
    immediatePastFn: ($i) > $i ).
    
tff(immediateFutureFn_tp,type,  % Doesn't need a world.  Assume basic physics in all worlds.
    immediateFutureFn: ($i) > $i ).
            
tff(dayFn_tp,type,
    dayFn: ($int * $i) > $i ).
    
tff(monthFn_tp,type,
    monthFn: ($i * $i) > $i ).
        
tff(yearFn_tp,type,
    yearFn: ($int) > $i ).

tff(governmentFn_tp,type,   % definitional, doesn't need a world
    governmentFn: ($i) > $i ).
                    
tff(attribute_tp,type,
    attribute: ($i * $i * w) > $o ).

tff(subOrganization_tp,type,
    subOrganization: ($i * $i * w) > $o ).
    
tff(instance_tp,type,       % constant domains, doesn't need a world
    instance: ($i * $i) > $o ).

tff(mIBUnit_tp,type,
    mIBUnit: $i ).
    
tff(organization_tp,type,
    organization: $i ).

tff(uSGovernment_tp,type,
    uSGovernment: $i ).
            
tff(unitedStates_tp,type,
    unitedStates: $i ).
                
tff(human_tp,type,
    human: $i ).

tff(agentSmith_tp,type,
    agentSmith: $i ).

tff(facility_tp,type,
    facility: $i ).
        
tff(area51_tp,type,
    area51: $i ).
        
tff(legalAction_tp,type,
    legalAction: $i ).
     
tff(entering_tp,type,
    entering: $i ).
    
tff(terminatingEmployment_tp,type,
    terminatingEmployment: $i ).    
                
tff(accreln_tp,type,
    accreln: (m * $i * w * w) > $o ).

tff(accreln2_tp,type,
    accreln2: (m * $i * $i * w * w) > $o ).
    
tff(currentworld_tp,type,
    cw: w ).

tff(month_tp,type,
  month: $i).
  
tff(succClass_tp,type,
  successorClass: ($i * $i) > $o).  
  
tff(jan_tp,type,
  january: $i).
tff(feb_tp,type,
  february: $i).  
tff(mar_tp,type,
  march: $i).
tff(apr_tp,type,
  april: $i).
tff(may_tp,type,
  may: $i).
tff(jun_tp,type,
  june: $i).
tff(july_tp,type,
  july: $i).
tff(aug_tp,type,
  august: $i).
tff(sep_tp,type,
  september: $i).
tff(oct_tp,type,
  october: $i).
tff(nov_tp,type,
  november: $i).
tff(dec_tp,type,
  december: $i).

tff(succJan,axiom,
  successorClass(january, february)).
tff(succFeb,axiom,
  successorClass(february, march)).
tff(succMar,axiom,
  successorClass(march, april)).
tff(succApr,axiom,
  successorClass(april, may)).
tff(succMay,axiom,
  successorClass(may, june)).
tff(succJun,axiom,
  successorClass(june, july)).
tff(succJul,axiom,
  successorClass(july, august)).
tff(succAug,axiom,
  successorClass(august, september)).
tff(succSep,axiom,
  successorClass(september, october)).
tff(succOct,axiom,
  successorClass(october, november)).
tff(succNov,axiom,
  successorClass(november, december)).

tff(succTrans,axiom,
  (![C1:$i,C2:$i,C3:$i]:(
    (successorClass(C1, C2) &
     successorClass(C2, C3)) =>
       successorClass(C1, C3))) ).
             
tff(def1,axiom,
  instance(agentSmith, human)).
  
tff(def2,axiom,
  instance(area51, facility)).
  
% time stuff.  Temporal relations between times are true in all worlds

tff(duringDay,axiom,
  (![D:$int,M:$i,T1:$i,T2:$i,Y:$int]:(
    ((T1 = dayFn(D, monthFn(M, yearFn(Y)))) &
     (T2 = monthFn(M, yearFn(Y)))) =>
       during( T1, T2)))).

tff(duringDayYear,axiom,
  (![D:$int,M:$i,T1:$i,T2:$i,Y:$int]:(
    ((T1 = dayFn( D, monthFn( M, yearFn( Y)))) &
     (T2 = yearFn( Y))) =>
       during( T1, T2)))).
       
tff(duringMonth,axiom,
  (![M:$i,T1:$i,T2:$i,Y:$int]:(
    ((T1 = monthFn( M, yearFn( Y))) &
     (T2 = yearFn( Y))) =>
       during( T1, T2)))).
              
tff(earlierMonth,axiom,
  (![M1:$i,M2:$i,T1:$i,T2:$i,Y:$int]:(
    (successorClass( M1, M2) &
     (T1 = monthFn( M1, yearFn( Y))) &
     (T2 = monthFn( M2, yearFn( Y)))) =>
       earlier( T1, T2)))).

tff(earlierMonthDay,axiom,
  (![D1:$int,D2:$int,M1:$i,M2:$i,T1:$i,T2:$i,Y:$int]:(
    (successorClass( M1, M2) &
     (T1 = dayFn( D1, monthFn( M1, yearFn( Y)))) &
     (T2 = dayFn( D2, monthFn( M2, yearFn( Y))))) =>
       earlier( T1, T2)))).
       
tff(earlierYear,axiom,
  (![T1:$i,T2:$i,Y1:$int,Y2:$int]:(
    (($less(Y1, Y2) &
     (T1 = yearFn(Y1)) &
     (T2 = yearFn(Y2))) =>
       earlier(T1, T2))))).
              
tff(earlierYearMonth,axiom,
  (![M1:$i,M2:$i,T1:$i,T2:$i,Y1:$int,Y2:$int]:(
    (($less(Y1, Y2) &
     (T1 = monthFn( M1, yearFn( Y1))) &
     (T2 = monthFn( M2, yearFn( Y2)))) =>
       earlier(T1, T2))))).

tff(earlierYearDay,axiom,
  (![D1:$int,D2:$int,M1:$i,M2:$i,T1:$i,T2:$i,Y1:$int,Y2:$int]:(
    (($less(Y1, Y2) &
     (T1 = dayFn( D1, monthFn(M1, yearFn( Y1)))) &
     (T2 = dayFn( D2, monthFn( M2, yearFn(Y2))))) =>
       earlier(T1, T2))))).

tff(earlierDay,axiom,
  (![D1:$int,D2:$int,M:$i,T1:$i,T2:$i,Y:$int]:(
    (($less(D1, D2) &
     (T1 = dayFn( D1, monthFn(M, yearFn( Y)))) &
     (T2 = dayFn( D2, monthFn(M, yearFn( Y))))) =>
       earlier(T1, T2))))).
                       
 % Mary will be done with her project in two weeks.  It's March 7th. What's the first business day on which her project will be ready?           

      

     
