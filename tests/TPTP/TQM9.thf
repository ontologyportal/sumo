thf(worlds_tp,type,
    w: $tType ).

thf(modals_tp,type,
    m: $tType ).

thf(desires_tp,type,
    desires: m ).
    
thf(holdsDuring_tp,type,
    holdsDuring: m ).
        
thf(agent_tp,type,
    agent: $i > $i > w > $o ).

thf(patient_tp,type,
    patient: $i > $i > w > $o ).

thf(victim_tp,type,
    victim: $i > $i > w > $o ).

thf(earlier_tp,type,
    earlier: $i > $i > w > $o ).
        
thf(whenFn_tp,type,
    whenFn: $i > w > $i ).

thf(yearFn_tp,type,
    yearFn: $i > $i ).
                
thf(attribute_tp,type,
    attribute: $i > $i > w > $o ).

thf(instance_tp,type,
    instance: $i > $i > $o ).

thf(human_tp,type,
    human: $i ).

thf(vito_tp,type,
    vito: $i ).
    
thf(paulie_tp,type,
    paulie: $i ).
        
thf(dead_tp,type,
    dead: $i ).

thf(n1980_tp,type,
    n1980: $i ).
    
thf(n1982_tp,type,
    n1982: $i ).
    
        
thf(murder_tp,type,
    murder: $i ).
    
thf(execution_tp,type,
    execution: $i ).
                
thf(premeditated_tp,type,
    premeditated: $i ).
                
thf(accreln_tp,type,
    accreln: m > $i > w > w > $o ).

thf(currentworld_tp,type,
    cw: w ).

% from SUMO
% A death that one wanted before the murder is premeditated
    
thf(ax1,axiom,(
  ![T:$i,M:$i,V:$i,MURDER:$i,W:w,W2:w]:(   
     (((accreln @ holdsDuring @ T @ cw @ W) =>
        ((accreln @ desires @ M @ W @ W2) =>
          (attribute @ V @ dead @ W2))) & 
      (instance @ MURDER @ murder) &
      (agent @ MURDER @ M @ W2) &
      (patient @ MURDER @ V @ W2) &
      (earlier @ T @
        (whenFn @ MURDER @ W2) @ W2)) =>
    (attribute @ MURDER @ premeditated @ W2)))).
    
% Vito wanted Paulie dead in 1980

thf(ax2,axiom,
  ![W:w,W2:w]:(
    (((accreln @ holdsDuring @ (yearFn @ n1980) @ cw @ W) =>
       ((accreln @ desires @ vito @ W @ W2) =>
         (attribute @ paulie @ dead @ W2)))))).  
                
% Vito killed Paulie in 1982

thf(ax3,axiom,
  ![W:w, MVP:$i]:(
    ((instance @ MVP @ murder) &
     (agent @ MVP @ vito @ W) &
     (patient @ MVP @ paulie @ W) &
     ((yearFn @ n1982) = (whenFn @ MVP @ W))))).
    
% since we don't have numbers and arithmetic:
% 1980 is earlier than 1982

thf(axNum,axiom,
  ![W:w]:(
    (earlier @ (yearFn @ n1980) @ (yearFn @ n1982) @ W))).
              
% If murder is premeditated, the punishment (result) is execution

thf(ax4,axiom,
  ![M:$i,V:$i,MURDER:$i,W:w]:(
    ((instance @ MURDER @ murder) &
     (agent @ MURDER @ M @ W) &
     (patient @ MURDER @ V @ W) &
     (attribute @ MURDER @ premeditated @ W)) =>
       (?[E:$i]:(
         ((instance @ E @ execution) &
          (victim @ E @ M @ W)))))).
      
% Does somebody [the government] execute Vito?

thf(conj,conjecture,(
  ?[E:$i]:(
    ((instance @ E @ execution) &
     (victim @ E @ vito @ cw))))).

    
