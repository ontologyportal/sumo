;;;         Ontology of Geography          ;;;

;; Access to and use of these products is governed by the GNU General Public
;; License <http://www.gnu.org/copyleft/gpl.html>.
;; By using these products, you agree to be bound by the terms
;; of the GPL.

;; Contact Adam Pease (apease [at] articulatesoftware.com).
;; We ask that people using or referencing this work cite our primary paper:

;; Niles, I., and Pease, A.  2001.  Towards a Standard Upper Ontology.  In
;; Proceedings of the 2nd International Conference on Formal Ontology in
;; Information Systems (FOIS-2001), Chris Welty and Barry Smith, eds,
;; Ogunquit, Maine, October 17-19, 2001.  See also http://www.ontologyportal.org


;; Authors:  Deborah Nichols
;;     Part I incorporates some material by Doug Wulf.
;;     Part II incorporates some material from earlier Terrain.kif
;;      by Olga Babko_Malaya.

;;-----------------------------------------------------------------------

;; Ontology dependencies for Geography.kif: Merge.txt, elements.kif

;;-----------------------------------------------------------------------

;; Outline
;; I. Geography Terms for the CIA World Fact Book
;;   A. Location
;;   B. Geographic coordinates
;;   C. Map references
;;   D. Area
;;   E. Area - comparative
;;   F. Land boundaries
;;   G. Coastline
;;   H. Maritime claims
;;   I. Climate
;;   J. Terrain
;;   K. Elevation extremes
;;   L. Natural resources
;;   M. Land use
;;   N. Irrigated land
;;   O. Natural hazards
;;   P. Environment - current issues
;;   Q. Environment - international agreements
;;   R. Geography - note
;;
;; II. General Geography Terms and Background
;;  A. Planet Geography & Astronomical Bodies
;;  B. Directions and Distances
;;  C. Land Forms
;;  D. Water Areas
;;     1. Oceans & Seas
;;     2. Tides & Currents
;;     3. Water Subregions
;;     4. Fresh Water Areas
;;  E. Coastal and Shoreline Areas
;;  F. Air and Atmosphere
;;  G. Weather & Climate
;;  H. Vegetation and Biomes
;;  I. Natural Disasters
;;  J. Environmental Areas of Concern

;;-----------------------------------------------------------------------

;; GEOGRAPHY ONTOLOGY

;;-----------------------------------------------------------------------

;; I. Geography Terms for the CIA World Fact Book

;; A. Location
;;  Identifies regional location of a country, neighbors & adjacent waters.

; Regions used by 2002 CIA WFB to describe country locations:

(instance CentralAfrica GeographicArea)
(instance EasternAfrica GeographicArea)
(instance NorthernAfrica GeographicArea)
(instance SouthernAfrica GeographicArea)
(instance WesternAfrica GeographicArea)
(instance MiddleEastRegion GeographicArea)
(instance CentralEurope GeographicArea)
(instance EasternEurope GeographicArea)
(instance NorthernEurope GeographicArea)
(instance SouthernEurope GeographicArea)
(instance WesternEurope GeographicArea)
(instance SoutheasternEurope GeographicArea)
(instance SouthwesternEurope GeographicArea)
(instance NorthAmerica GeographicArea)
(instance NorthernNorthAmerica GeographicArea)
(instance CaribbeanRegion GeographicArea)
(instance MiddleAmerica GeographicArea)
(instance CentralSouthAmerica GeographicArea)
(instance EasternSouthAmerica GeographicArea)
(instance NorthernSouthAmerica GeographicArea)
(instance SouthernSouthAmerica GeographicArea)
(instance WesternSouthAmerica GeographicArea)
(instance CentralAsia GeographicArea)
(instance EasternAsia GeographicArea)
(instance NorthernAsia GeographicArea)
(instance SouthernAsia GeographicArea)
(instance SoutheasternAsia GeographicArea)
(instance SouthwesternAsia GeographicArea)
(instance Oceania GeographicArea)

(names "Central Africa" CentralAfrica)
(names "Eastern Africa" EasternAfrica)
(names "Northern Africa" NorthernAfrica)
(names "Southern Africa" SouthernAfrica)
(names "Western Africa" WesternAfrica)
(names "Middle East" MiddleEastRegion)
(names "Central Europe" CentralEurope)
(names "Eastern Europe" EasternEurope)
(names "Northern Europe" NorthernEurope)
(names "Southern Europe" SouthernEurope)
(names "Western Europe" WesternEurope)
(names "Southeastern Europe" SoutheasternEurope)
(names "Southwestern Europe" SouthwesternEurope)
(names "North America" NorthAmerica)
(names "Northern North America" NorthernNorthAmerica)
(names "Caribbean Region" CaribbeanRegion)
(names "Caribbean" CaribbeanRegion)
(names "Middle America" MiddleAmerica)
(names "Central South America" CentralSouthAmerica)
(names "Eastern South America" EasternSouthAmerica)
(names "Northern South America" NorthernSouthAmerica)
(names "Southern South America" SouthernSouthAmerica)
(names "Western South America" WesternSouthAmerica)
(names "Central Asia" CentralAsia)
(names "Eastern Asia" EasternAsia)
(names "Northern Asia" NorthernAsia)
(names "Southern Asia" SouthernAsia)
(names "Southeastern Asia" SoutheasternAsia)
(names "Southwestern Asia" SouthwesternAsia)
(names "Oceania" Oceania)

(geographicSubregion CentralAfrica Africa)
(geographicSubregion EasternAfrica Africa)
(geographicSubregion NorthernAfrica Africa)
(geographicSubregion SouthernAfrica Africa)
(geographicSubregion WesternAfrica Africa)

(instance MiddleEastRegion GeopoliticalArea)
(overlapsSpatially MiddleEastRegion Asia)

(documentation MiddleEastRegion EnglishLanguage "&%MiddleEastRegion is a
&%GeopoliticalArea that comprises countries of Southwestern &%Asia
and (in some definitions) Northwestern &%Africa.  Here this term is
defined as in the CIA World Fact Book, in which the Middle East
includes: Bahrain, Cyprus, the Gaza Strip, Iran, Iraq, Israel,
Jordan, Kuwait, Lebanon, Oman, Qatar, Saudi Arabia, Syria, the United
Arab Emirates, the West Bank, and Yemen.")

(geographicSubregion CentralEurope Europe)
(geographicSubregion EasternEurope Europe)
(geographicSubregion NorthernEurope Europe)
(geographicSubregion SouthernEurope Europe)
(geographicSubregion WesternEurope Europe)
(geographicSubregion SoutheasternEurope Europe)
(geographicSubregion SouthwesternEurope Europe)

(geographicSubregion NorthernNorthAmerica NorthAmerica)

(overlapsSpatially CaribbeanRegion SouthAmerica)
(overlapsSpatially CaribbeanRegion NorthAmerica)
(overlapsSpatially MiddleAmerica NorthAmerica)
(overlapsSpatially MiddleAmerica SouthAmerica)

(geographicSubregion CentralSouthAmerica SouthAmerica)
(geographicSubregion EasternSouthAmerica SouthAmerica)
(geographicSubregion NorthernSouthAmerica SouthAmerica)
(geographicSubregion SouthernSouthAmerica SouthAmerica)
(geographicSubregion WesternSouthAmerica SouthAmerica)

(geographicSubregion CentralAsia Asia)
(geographicSubregion EasternAsia Asia)
(geographicSubregion NorthernAsia Asia)
(geographicSubregion NorthernAsia NorthernHemisphere)

(geographicSubregion SouthernAsia Asia)
(geographicSubregion SoutheasternAsia Asia)
(geographicSubregion SouthwesternAsia Asia)

(geographicSubregion CentralAfrica EasternHemisphere)
(overlapsSpatially CentralAfrica NorthernHemisphere)
(overlapsSpatially CentralAfrica SouthernHemisphere)

(geographicSubregion EasternAfrica EasternHemisphere)
(overlapsSpatially EasternAfrica NorthernHemisphere)
(overlapsSpatially EasternAfrica SouthernHemisphere)

(overlapsSpatially NorthernAfrica EasternHemisphere)
(overlapsSpatially NorthernAfrica WesternHemisphere)
(geographicSubregion NorthernAfrica NorthernHemisphere)

(geographicSubregion SouthernAfrica EasternHemisphere)
(geographicSubregion SouthernAfrica SouthernHemisphere)

(overlapsSpatially WesternAfrica EasternHemisphere)
(overlapsSpatially WesternAfrica WesternHemisphere)
(overlapsSpatially WesternAfrica NorthernHemisphere)
(overlapsSpatially WesternAfrica SouthernHemisphere)

(geographicSubregion MiddleEastRegion EasternHemisphere)
(geographicSubregion MiddleEastRegion WesternHemisphere)

(geographicSubregion EasternEurope EasternHemisphere)
(geographicSubregion EasternEurope NorthernHemisphere)

(geographicSubregion CentralEurope EasternHemisphere)
(geographicSubregion CentralEurope NorthernHemisphere)

(geographicSubregion NorthernEurope EasternHemisphere)
(geographicSubregion NorthernEurope NorthernHemisphere)

(geographicSubregion SouthernEurope EasternHemisphere)
(geographicSubregion SouthernEurope NorthernHemisphere)

(overlapsSpatially WesternEurope EasternHemisphere)
(overlapsSpatially WesternEurope WesternHemisphere)
(geographicSubregion WesternEurope NorthernHemisphere)

(geographicSubregion SoutheasternEurope EasternHemisphere)
(geographicSubregion SoutheasternEurope NorthernHemisphere)

(overlapsSpatially SouthwesternEurope EasternHemisphere)
(overlapsSpatially SouthwesternEurope WesternHemisphere)
(geographicSubregion SouthwesternEurope NorthernHemisphere)

(geographicSubregion CaribbeanRegion NorthernHemisphere)
(geographicSubregion CaribbeanRegion WesternHemisphere)

(geographicSubregion MiddleAmerica NorthernHemisphere)
(geographicSubregion MiddleAmerica WesternHemisphere)

(geographicSubregion CentralSouthAmerica WesternHemisphere)
(geographicSubregion CentralSouthAmerica SouthernHemisphere)

(geographicSubregion EasternSouthAmerica WesternHemisphere)
(overlapsSpatially EasternSouthAmerica NorthernHemisphere)
(overlapsSpatially EasternSouthAmerica SouthernHemisphere)

(geographicSubregion NorthernSouthAmerica WesternHemisphere)
(overlapsSpatially NorthernSouthAmerica NorthernHemisphere)
(overlapsSpatially NorthernSouthAmerica SouthernHemisphere)

(geographicSubregion SouthernSouthAmerica WesternHemisphere)
(geographicSubregion SouthernSouthAmerica SouthernHemisphere)

(geographicSubregion WesternSouthAmerica WesternHemisphere)
(overlapsSpatially WesternSouthAmerica NorthernHemisphere)
(overlapsSpatially WesternSouthAmerica SouthernHemisphere)

(geographicSubregion CentralAsia EasternHemisphere)
(geographicSubregion CentralAsia NorthernHemisphere)

(geographicSubregion EasternAsia EasternHemisphere)
(geographicSubregion EasternAsia NorthernHemisphere)

(geographicSubregion NorthernAsia EasternHemisphere)

(geographicSubregion SouthernAsia EasternHemisphere)
(overlapsSpatially SouthernAsia NorthernHemisphere)
(overlapsSpatially SouthernAsia SouthernHemisphere)

(geographicSubregion SoutheasternAsia EasternHemisphere)
(overlapsSpatially SoutheasternAsia NorthernHemisphere)
(overlapsSpatially SoutheasternAsia SouthernHemisphere)

(geographicSubregion SouthwesternAsia EasternHemisphere)
(geographicSubregion SouthwesternAsia NorthernHemisphere)

(orientation Europe Africa North)
(orientation Europe Asia West)
(orientation Europe NorthAmerica East)
(orientation NorthAmerica SouthAmerica North)
(orientation NorthAmerica Asia East)
(orientation Africa SouthAmerica Northeast)
(orientation Asia Oceania Northwest)
(orientation Africa NorthAmerica Southeast)
(orientation Europe SouthAmerica Northeast)
(orientation Oceania NorthAmerica Southwest)
(orientation MiddleEastRegion Asia Southwest)
(orientation MiddleEastRegion Africa Northeast)
(orientation MiddleAmerica SouthAmerica North)
(orientation MiddleAmerica NorthAmerica South)
(orientation CaribbeanRegion SouthAmerica North)
(orientation CaribbeanRegion NorthAmerica South)

(=>
  (and
    (instance ?LAND Continent)
    (not (equal ?LAND Antarctica)))
  (orientation Antarctica ?LAND South))

(instance LevantRegion GeographicArea)
(documentation LevantRegion EnglishLanguage "&%LevantRegion is the subregion that borders
the Eastern Mediterranean sea to the west, and forms the core of West Asia and the political
term, Middle East.  In its narrowest sense, which is in use today in archaeology and other
cultural contexts, it is equivalent to Cyprus and a stretch of land bordering the Mediterranean
Sea in Western Asia that is, the historical region of Syria (Greater Syria), which
includes present-day Syria, as well as Lebanon, Jordan, Palestine, Israel, and the southern part of
Cilicia (modern-day Turkey). Its overwhelming characteristic is that it represents the land bridge
between Africa and Eurasia.[Wikipedia]")
(termFormat EnglishLanguage LevantRegion "Levant Region")
(geographicSubregion LevantRegion MiddleEastRegion)
(orientation LevantRegion MediterraneanRegion West)
(climateTypeInArea LevantRegion AridClimateZone)
(geographicSubregion Israel LevantRegion)
(geographicSubregion Syria LevantRegion)
(geographicSubregion Lebanon LevantRegion)
(geographicSubregion Jordan LevantRegion)
(geographicSubregion Palestine LevantRegion)
(geographicSubregion AntakyaTurkey LevantRegion)

(instance MediterraneanRegion GeographicArea)
(documentation MediterraneanRegion EnglishLanguage "&%MediterraneanRegion is the region of lands around
the &%MediterraneanSea that have mostly a Mediterranean climate, with mild to cool, rainy winters
and warm to hot, dry summers, which supports characteristic Mediterranean forests, woodlands, and
scrub vegetation. The Mediterranean basin covers portions of three continents: Africa, Asia, and Europe.
[Wikipedia]")
(termFormat EnglishLanguage MediterraneanRegion "Mediterranean Region")
(climateTypeInArea MediterraneanRegion MediterraneanClimateZone)
(geographicSubregion MediterraneanRegion Africa)
(geographicSubregion MediterraneanRegion Europe)
(geographicSubregion MediterraneanRegion Asia)

(instance SouthernFrance GeographicArea)
(documentation SouthernFrance EnglishLanguage "&%SouthernFrance is a geographical area consisting of
the regions of &%France that border the &%AtlanticOcean south of the Marais Poitevin, &%Spain,
&%MediterraneanSea and &%Italy.[Wikipedia]")
(termFormat EnglishLanguage SouthernFrance "Southern France")
(names "south of France" SouthernFrance)
(names "le Midi" SouthernFrance)
(geographicSubregion SouthernFrance France)
(orientation SouthernFrance GulfOfLion North)
(climateTypeInArea SouthernFrance MediterraneanClimateZone)
(geographicSubregion MarseillesFrance SouthernFrance)
(geographicSubregion ToulonFrance SouthernFrance)
(orientation AtlanticOcean SouthernFrance West)
(orientation Spain SouthernFrance South)
(orientation Italy SouthernFrance East)

(instance RockyMountains MountainRange)
(documentation RockyMountains EnglishLanguage "The &%RockyMountains, also known as the Rockies,
are a major mountain range and the largest mountain system in &%NorthAmerica. The Rocky Mountains
stretch 3,000 miles (4,800 kilometers) in straight-line distance from the northernmost part of
Western Canada, to New Mexico in the Southwestern United States.The Rockies vary in width from 110 
to 480 kilometres (70 to 300 miles).  [Wikipedia]")
(termFormat EnglishLanguage RockyMountains "Rocky Mountains")
(names "Rockies" RockyMountains)
(geographicSubregion RockyMountains NorthAmerica)
(traverses RockyMountains BritishColumbia)
(traverses RockyMountains Alberta)
(traverses RockyMountains Washington)
(traverses RockyMountains Idaho)
(traverses RockyMountains Montana)
(traverses RockyMountains Wyoming)
(traverses RockyMountains Utah)
(traverses RockyMountains Colorado)
(traverses RockyMountains NewMexico)
(geographicSubregion RockyMountains Canada)
(geographicSubregion RockyMountains UnitedStates)
(orientation Oregon RockyMountains West)
(orientation Nevada RockyMountains West)
(orientation California RockyMountains West)
(orientation Arizona RockyMountains West)
(orientation RockyMountains Texas East)
(orientation RockyMountains Oklahoma East)
(orientation RockyMountains Kansas East)
(orientation RockyMountains Nebraska East)
(orientation RockyMountains SouthDakota East)
(orientation RockyMountains NorthDakota East)

(=>
  (and
    (instance ?A PointInSpace)
    (instance ?B PointInSpace)
    (located ?A RockyMountains)
    (located ?B RockyMountains)
    (orientation ?A ?B North)    
    (not
      (exists (?A1 ?B1)
        (and
          (instance ?A1 PointInSpace)
          (located ?A1 RockyMountains)
          (orientation ?A1 ?A North)
          (instance ?B1 PointInSpace)
          (located ?B1 RockyMountains)
          (orientation ?B1 ?B South))))
    (distance ?A ?B
      (MeasureFn ?X Mile)))
 (approximateValue ?X 3000))
 
(=>
  (and
    (instance ?A PointInSpace)
    (instance ?B PointInSpace)
    (located ?A RockyMountains)
    (located ?B RockyMountains)
    (orientation ?A ?B East)    
    (not
      (exists (?A1 ?B1)
        (and
          (instance ?A1 PointInSpace)
          (located ?A1 RockyMountains)
          (orientation ?A1 ?A East)
          (instance ?B1 PointInSpace)
          (located ?B1 RockyMountains)
          (orientation ?B1 ?B West))))
    (distance ?A ?B
      (MeasureFn ?X Mile)))
 (and
   (greaterThan ?X 70)
   (greaterThan 300 ?X)))
   
(instance KlamathMountains MountainRange)
(documentation KlamathMountains EnglishLanguage "The &%KlamathMountains are a rugged and 
lightly populated mountain range in northwestern California and southwestern Oregon in 
the western United States. It is a mountain system within both the greater Pacific Coast 
Ranges and the California Coast Ranges.[Wikipedia]")
(termFormat EnglishLanguage KlamathMountains "Klamath Mountains")
(geographicSubregion KlamathMountains Oregon)
(geographicSubregion KlamathMountains California)
(orientation Oregon KlamathMountains West)
(orientation KlamathMountains CaliforniaCoastRanges North)
(overlapsSpatially KlamathMountains CaliforniaCoastRanges)
   
(instance CaliforniaCoastRanges MountainRange)
(documentation CaliforniaCoastRanges EnglishLanguage "The &%CaliforniaCoastRanges span 
400 miles (644 km) from Del Norte or Humboldt County, California, south to Santa Barbara 
County.The other three coastal California mountain ranges are the Transverse Ranges, 
Peninsular Ranges and the Klamath Mountains. The northern end of the California Coast 
Ranges overlap the southern end of the Klamath Mountains for approximately 80 miles 
(130 km) on the west. [Wikipedia]")
(termFormat EnglishLanguage CaliforniaCoastRanges "California Coast Ranges")
(geographicSubregion CaliforniaCoastRanges California)
(orientation
  (SeacoastFn California PacificOcean) CaliforniaCoastRanges West)

(=>
  (and
    (instance ?A PointInSpace)
    (instance ?B PointInSpace)
    (located ?A CaliforniaCoastRanges)
    (located ?B CaliforniaCoastRanges)
    (orientation ?A ?B North)    
    (not
      (exists (?A1 ?B1)
        (and
          (instance ?A1 PointInSpace)
          (located ?A1 CaliforniaCoastRanges)
          (orientation ?A1 ?A North)
          (instance ?B1 PointInSpace)
          (located ?B1 CaliforniaCoastRanges)
          (orientation ?B1 ?B South))))
    (distance ?A ?B
      (MeasureFn ?X Mile)))
 (approximateValue ?X 400))

(subclass EndorheicBasin Basin)
(documentation EndorheicBasin EnglishLanguage "An &%EndorheicBasin is a drainage &%Basin 
that normally retains water and allows no outflow to other external &%BodyOfWater 
(such as &%Rivers and &%Oceans). Instead, the water drainage &%flows into permanent and seasonal 
lakes and swamps that equilibrate through evaporation.[Wikipedia]")
(termFormat EnglishLanguage EndorheicBasin "endorheic basin")

(=>
  (and
    (instance ?B EndorheicBasin)
    (instance ?R River)
    (located ?R ?B))
  (not
    (exists (?OUT)
      (and
        (instance ?OUT BodyOfWater)
        (flows ?R ?OUT)))))
 
(instance GreatBasin EndorheicBasin)
(documentation GreatBasin EnglishLanguage "The &%GreatBasin is the largest area of 
contiguous endorheic watersheds, those with no outlets to the ocean, in &%NorthAmerica. 
It spans nearly all of Nevada, much of Utah, and portions of California, Idaho, Oregon, 
Wyoming, and Baja California. It is noted for both its arid climate and the basin and 
range topography that varies from the North American low point at Badwater Basin in 
Death Valley to the highest point of the contiguous United States, less than 100 miles 
(160 km) away at the summit of Mount Whitney. [Wikipedia]")
(termFormat EnglishLanguage GreatBasin "Great Basin")
(geographicSubregion GreatBasin Nevada)
(geographicSubregion GreatBasin Utah)
(geographicSubregion GreatBasin Oregon)
(geographicSubregion GreatBasin California)
(geographicSubregion GreatBasin Idaho)
(geographicSubregion GreatBasin Wyoming)
(geographicSubregion GreatBasin Mexico)
(climateTypeInArea GreatBasin AridClimateZone)

(instance MojaveDesert Desert)
(documentation MojaveDesert EnglishLanguage "The &%MojaveDesert is a desert in the rain shadow 
of the southern Sierra Nevada mountains and &%TransverseRanges in the Southwestern United States.
Named after the indigenous Mohave people, it is located primarily in southeastern California and 
southwestern Nevada, with small portions extending into Arizona and Utah.[Wikipedia]")
(termFormat EnglishLanguage MojaveDesert "Mojave Desert")
(geographicSubregion MojaveDesert Nevada)
(geographicSubregion MojaveDesert Utah)
(geographicSubregion MojaveDesert Arizona)
(geographicSubregion MojaveDesert California)
(orientation MojaveDesert SierraNevada Downwind)
(orientation MojaveDesert TransverseRanges Downwind)

(instance DeathValley Valley)
(documentation DeathValley EnglishLanguage "&%DeathValley is a &%Desert valley in Eastern 
&%California, in the northern &%MojaveDesert, bordering the &%GreatBasin Desert. It is thought 
to be the hottest place on Earth during summer.[Wikipedia]")
(termFormat EnglishLanguage DeathValley "Death Valley")
(climateTypeInArea DeathValley SubtropicalDesertClimateZone)
(geographicSubregion DeathValley MojaveDesert)
(geographicSubregion DeathValley SouthernCalifornia)

(instance BadwaterBasin EndorheicBasin)
(documentation BadwaterBasin EnglishLanguage "&%BadwaterBasin is an endorheic basin in Death 
Valley National Park, &%DeathValley, Inyo County, &%California, noted as the lowest point in 
&%NorthAmerica and the United States, with a depth of 282 ft (86 m) below sea level. 
&%MountWhitney, the highest point in the contiguous United States, is only 84.6 miles (136 km) 
to the northwest.[Wikipedia]")
(termFormat EnglishLanguage BadwaterBasin "Badwater Basin")
(geographicSubregion BadwaterBasin DeathValley)
(geographicSubregion BadwaterBasin SouthernCalifornia)
(distance BadwaterBasin SeaLevel
  (MeasureFn -86 Meter))
(distance BadwaterBasin MountWhitney
 (MeasureFn 136 Kilometer))
(orientation MountWhitney BadwaterBasin Northwest)

(instance SouthernCalifornia GeographicArea)
(documentation SouthernCalifornia EnglishLanguage "&%SouthernCalifornia is a geographic and 
cultural region that generally comprises the southern portion of the U.S. state of &%California. 
Its densely populated coastal region includes Greater Los Angeles (the second-most populous 
urban agglomeration in the United States) and San Diego County (the second-most 
populous county in California). The region generally contains ten of California's 58 
counties: Los Angeles, San Diego, Orange, Riverside, San Bernardino, Kern, Ventura, 
Santa Barbara, San Luis Obispo, and Imperial counties.[Wikipedia]")
(termFormat EnglishLanguage SouthernCalifornia "Southern California")
(names "SoCal" SouthernCalifornia)
(geographicSubregion SouthernCalifornia California)
(located LosAngelesCalifornia SouthernCalifornia)
(externallyConnected SouthernCalifornia NorthernCalifornia)

(instance NorthernCalifornia GeographicArea)
(documentation NorthernCalifornia EnglishLanguage "&%NorthernCalifornia is a geocultural region 
that comprises the northern portion of the U.S. state of California, spanning the northernmost 
48 of the state's 58 counties. &%NorthernCalifornia in its largest definition is determined 
by dividing the state into two regions, the other being &%SouthernCalifornia.[Wikipedia]")
(termFormat EnglishLanguage NorthernCalifornia "Northern California")
(names "NorCal" NorthernCalifornia)
(geographicSubregion NorthernCalifornia California)
(located SanFranciscoCalifornia NorthernCalifornia)

;; ?R is defined using TPP pf the RCC-8 (Region Connection Calculus)
  
(=>
  (and
    (instance ?R GeographicArea)
    (tangentialProperPart ?R KlamathMountains)
    (tangentialProperPart ?R CaliforniaCoastRanges) 
    (instance ?N PointInSpace)
    (located ?N ?R)
    (instance ?S PointInSpace)
    (located ?S ?R)
    (orientation ?N ?S North)
    (not
      (exists (?N1 ?S1)
        (and
          (instance ?N1 PointInSpace)
          (located ?N1 ?R)
          (orientation ?N1 ?N North)
          (instance ?S1 PointInSpace)
          (located ?S1 ?R)
          (orientation ?S1 ?S South))))
    (distance ?N ?S
      (MeasureFn ?X Mile)))
  (approximateValue ?X 80))

;;
;; Jennie Pease July 2025
;; Adding Tangetial proper part (TPP) and non-tangential proper part to complete the
;; impliment of RCC-8
;;
;;

(instance connectedRegions BinaryPredicate)
(instance connectedRegions SpatialRelation)
(instance connectedRegions ReflexiveRelation)
(instance connectedRegions SymmetricRelation)
(domain connectedRegions 1 Region)
(domain connectedRegions 2 Region)
(relatedInternalConcept connectedRegions connected)
(format EnglishLanguage connectedRegions "%1 and %2 are &%connectedRegions")
(documentation connectedRegions EnglishLanguage "(connectedRegions ?R1 ?R2) means that ?R1
and ?R2 are &%connected.")
(termFormat EnglishLanguage connectedRegions "connected regions")

(=>
  (and
    (instance ?R1 Region)
    (instance ?R2 Region)
    (connected ?R1 ?R2))
  (connectedRegions ?R1 ?R2))

(subclass RCC8Relation SpatialRelation)
(documentation RCC8Relation EnglishLanguage "The &%RCC8Relation is intended to serve for qualitative 
spatial representation and reasoning. RCC abstractly describes regions (in Euclidean space, or in a 
topological space) by their possible relations to each other. RCC8 consists of 8 basic relations that 
are possible between two regions: disconnected (DC), externally connected (EC), equal (EQ), partially 
overlapping (PO), tangential proper part (TPP), tangential proper part inverse (TPPi), non-tangential 
proper part (NTPP), non-tangential proper part inverse (NTPPi). From these basic relations, combinations 
can be built, for spatial reasoning,for example via a composition table.[Wikipedia]")
(termFormat EnglishLanguage RCC8Relation "region connection calculus 8")
(names "RCC8" RCC8Relation)

(instance disconnected RCC8Relation)
(instance disconnected SymmetricRelation)
(documentation disconnected EnglishLanguage "&%disconnected is a &%RCC8Relation.
(disconnected ?A ?B) means that ?A is a &%disconnected from ?B, meaning ?A and ?B 
shares no point.")
(termFormat EnglishLanguage disconnected "disconnected")
(domain disconnected 1 Region)
(domain disconnected 2 Region)
(relatedInternalConcept disconnected connected)
(format EnglishLanguage disconnected "%1 and %2 are &%disconnected")
(names "DC" disconnected)

(=>
  (disconnected ?A ?B)
  (not
    (connectedRegions ?A ?B)))
    
(instance externallyConnected RCC8Relation)
(instance externallyConnected SymmetricRelation)
(documentation externallyConnected EnglishLanguage "&%externallyConnected is a &%RCC8Relation.
(externallyConnected ?A ?B) means that ?A and ?B are &%externallyConnected, meaning ?A and ?B 
touch at the boundary but don’t overlap.")
(termFormat EnglishLanguage externallyConnected "externally connected")
(domain externallyConnected 1 Region)
(domain externallyConnected 2 Region)
(relatedInternalConcept externallyConnected meetsSpatially)
(format EnglishLanguage externallyConnected "%1 and %2 are &%externallyConnected")
(names "EC" externallyConnected)

(=>
  (and
    (instance ?A Region)
    (instance ?B Region)
    (meetsSpatially ?A ?B))
  (externallyConnected ?A ?B))

(instance partiallyOverlapping RCC8Relation)
(instance partiallyOverlapping SymmetricRelation)
(documentation partiallyOverlapping EnglishLanguage "&%partiallyOverlapping is a &%RCC8Relation.
(partiallyOverlapping ?A ?B) means that ?A and ?B are &%partiallyOverlapping, meaning ?A and ?B 
share some, but not all, interior points.")
(termFormat EnglishLanguage partiallyOverlapping "partially overlapping")
(domain partiallyOverlapping 1 Region)
(domain partiallyOverlapping 2 Region)
(relatedInternalConcept partiallyOverlapping overlapsSpatially)
(format EnglishLanguage partiallyOverlapping "%1 is &%partiallyOverlapping with %2")
(names "PO" partiallyOverlapping)

(=>
  (and
    (instance ?A Region)
    (instance ?B Region)
    (overlapsSpatially ?A ?B))   
  (partiallyOverlapping ?A ?B))

(instance equalRegions RCC8Relation)
(instance equalRegions SymmetricRelation)
(documentation equalRegions EnglishLanguage "&%equalRegions is a &%RCC8Relation.
(equalRegions ?A ?B) means that ?A and ?B are &%equalRegions, meaning ?A and ?B 
are identical regions.")
(termFormat EnglishLanguage equalRegions "partially overlapping")
(domain equalRegions 1 Region)
(domain equalRegions 2 Region)
(format EnglishLanguage equalRegions "%1 and %2 are &%equalRegions")
(names "EQ" equalRegions)

(=>
  (equalRegions ?A ?B)
  (equal ?A ?B))

(instance tangentialProperPart RCC8Relation)
(instance tangentialProperPart AsymmetricRelation)
(documentation tangentialProperPart EnglishLanguage "&%tangentialProperPart is a &%RCC8Relation.
(tangentialProperPart ?A ?B) means that ?A is a &%tangentialProperPart of ?B, meaning ?A is inside ?B 
and touches its boundary.To invoke the inverse, TPPi, one would swap the order of the two arguments 
specified.That is (tangentialProperPart ?B ?A).")
(termFormat EnglishLanguage tangentialProperPart "tangential proper part")
(domain tangentialProperPart 1 Region)
(domain tangentialProperPart 2 Region)
(relatedInternalConcept tangentialProperPart contains)
(format EnglishLanguage tangentialProperPart "%1 is a &%tangentialProperPart of %2")
(names "TPP" tangentialProperPart)

(=>
  (and
    (instance ?A Region)
    (instance ?B Region)
    (contains ?B ?A)
    (orientation ?A ?B Inside)    
    (tangentialProperPart ?A ?B))
  (exists (?X ?Y)
    (and
      (instance ?X PointInSpace)
      (superficialPart ?X ?B)
      (instance ?Y PointInSpace)
      (superficialPart ?Y ?A)
      (meetsSpatially ?X ?Y))))
                               
(instance nonTangentialProperPart RCC8Relation)
(instance nonTangentialProperPart AsymmetricRelation)
(documentation nonTangentialProperPart EnglishLanguage "&%nonTangentialProperPart is a &%RCC8Relation.
(nonTangentialProperPart ?A ?B) means that ?A is a &%nonTangentialProperPart of ?B, meaning ?A is entirely 
inside ?B and without touching its boundary.To invoke the inverse, NTPPi, one would swap the order of the two arguments 
specified.That is (nonTangentialProperPart ?B ?A).")
(termFormat EnglishLanguage nonTangentialProperPart "non tangential proper part")
(names "NTPP" nonTangentialProperPart)
(domain nonTangentialProperPart 1 Region)
(domain nonTangentialProperPart 2 Region)
(format EnglishLanguage nonTangentialProperPart "%1 is a &%nonTangentialProperPart of %2")

(=>
  (and
    (instance ?A Region)
    (instance ?B Region)
    (contains ?B ?A)
    (orientation ?A ?B Inside)    
    (nonTangentialProperPart ?A ?B))
  (exists (?X ?Y)
    (and
      (instance ?X PointInSpace)
      (superficialPart ?X ?B)
      (instance ?Y PointInSpace)
      (superficialPart ?Y ?A)
      (not
        (meetsSpatially ?X ?Y)))))  
;;
;; Jennie Pease August 2025
;; Composition table for RCC8
;;

;; DC

(=>
  (and
    (disconnected ?A ?B)
    (externallyConnected ?B ?C))
  (or
    (disconnected ?A ?C)
    (externallyConnected ?A ?C)
    (partiallyOverlapping ?A ?C)
    (tangentialProperPart ?A ?C)
    (nonTangentialProperPart ?A ?C)))
    
(=>
  (and
    (disconnected ?A ?B)
    (partiallyOverlapping ?B ?C))
  (or
    (disconnected ?A ?C)
    (externallyConnected ?A ?C)
    (partiallyOverlapping ?A ?C)
    (tangentialProperPart ?A ?C)
    (nonTangentialProperPart ?A ?C)))
    
(=>
  (and
    (disconnected ?A ?B)
    (tangentialProperPart ?B ?C))
  (or
    (disconnected ?A ?C)
    (externallyConnected ?A ?C)
    (partiallyOverlapping ?A ?C)
    (tangentialProperPart ?A ?C)
    (nonTangentialProperPart ?A ?C)))

(=>
  (and
    (disconnected ?A ?B)
    (nonTangentialProperPart ?B ?C))
  (or
    (disconnected ?A ?C)
    (externallyConnected ?A ?C)
    (partiallyOverlapping ?A ?C)
    (tangentialProperPart ?A ?C)
    (nonTangentialProperPart ?A ?C)))
    
(=>
  (and
    (disconnected ?A ?B)
    (tangentialProperPart ?C ?B))
  (disconnected ?A ?C))
  
(=>
  (and
    (disconnected ?A ?B)
    (nonTangentialProperPart ?C ?B))
  (disconnected ?A ?C))
  
(=>
  (and
    (disconnected ?A ?B)
    (equalRegions ?B ?C))
  (disconnected ?A ?C))
  
;; EC

(=>
  (and
    (externallyConnected ?A ?B)
    (disconnected ?B ?C))
  (or
    (disconnected ?A ?C)
    (externallyConnected ?A ?C)
    (partiallyOverlapping ?A ?C)
    (tangentialProperPart ?C ?A)
    (nonTangentialProperPart ?C ?A)))

(=>
  (and
    (externallyConnected ?A ?B)
    (externallyConnected ?B ?C))
  (or
    (disconnected ?A ?C)
    (externallyConnected ?A ?C)
    (partiallyOverlapping ?A ?C)
    (tangentialProperPart ?A ?C)
    (nonTangentialProperPart ?C ?A)
    (equalRegions ?A ?C)))
    
(=>
  (and
    (externallyConnected ?A ?B)
    (partiallyOverlapping ?B ?C))
  (or
    (disconnected ?A ?C)
    (externallyConnected ?A ?C)
    (partiallyOverlapping ?A ?C)
    (tangentialProperPart ?A ?C)
    (nonTangentialProperPart ?A ?C)))
    
(=>
  (and
    (externallyConnected ?A ?B)
    (tangentialProperPart ?B ?C))
  (or
    (externallyConnected ?A ?C)
    (partiallyOverlapping ?A ?C)
    (tangentialProperPart ?A ?C)
    (nonTangentialProperPart ?A ?C)))

(=>
  (and
    (externallyConnected ?A ?B)
    (nonTangentialProperPart ?B ?C))
  (or
    (partiallyOverlapping ?A ?C)
    (tangentialProperPart ?A ?C)
    (nonTangentialProperPart ?A ?C)))
    
(=>
  (and
    (externallyConnected ?A ?B)
    (tangentialProperPart ?C ?B))
  (or
    (disconnected ?A ?C)
    (externallyConnected ?A ?C)))
  
(=>
  (and
    (externallyConnected ?A ?B)
    (nonTangentialProperPart ?C ?B))
  (disconnected ?A ?C))
  
(=>
  (and
    (externallyConnected ?A ?B)
    (equalRegions ?B ?C))
  (externallyConnected ?A ?C)) 

;; PO

(=>
  (and
    (partiallyOverlapping ?A ?B)
    (disconnected ?B ?C))
  (or
    (disconnected ?A ?C)
    (externallyConnected ?A ?C)
    (partiallyOverlapping ?A ?C)
    (tangentialProperPart ?C ?A)
    (nonTangentialProperPart ?C ?A)))

(=>
  (and
    (partiallyOverlapping ?A ?B)
    (externallyConnected ?B ?C))
  (or
    (disconnected ?A ?C)
    (externallyConnected ?A ?C)
    (partiallyOverlapping ?A ?C)
    (tangentialProperPart ?C ?A)
    (nonTangentialProperPart ?C ?A)))
    
(=>
  (and
    (partiallyOverlapping ?A ?B)
    (tangentialProperPart ?B ?C))
  (or
    (partiallyOverlapping ?A ?C)
    (tangentialProperPart ?A ?C)
    (nonTangentialProperPart ?A ?C)))

(=>
  (and
    (partiallyOverlapping ?A ?B)
    (nonTangentialProperPart ?B ?C))
  (or
    (partiallyOverlapping ?A ?C)
    (tangentialProperPart ?A ?C)
    (nonTangentialProperPart ?A ?C)))
    
(=>
  (and
    (partiallyOverlapping ?A ?B)
    (tangentialProperPart ?C ?B))
  (or
    (disconnected ?A ?C)
    (externallyConnected ?A ?C)
    (partiallyOverlapping ?A ?C)    
    (tangentialProperPart ?C ?A)
    (nonTangentialProperPart ?C ?A)))   
  
(=>
  (and
    (partiallyOverlapping ?A ?B)
    (nonTangentialProperPart ?C ?B))
  (or
    (disconnected ?A ?C)
    (externallyConnected ?A ?C)
    (partiallyOverlapping ?A ?C)    
    (tangentialProperPart ?C ?A)
    (nonTangentialProperPart ?C ?A)))
  
(=>
  (and
    (partiallyOverlapping ?A ?B)
    (equalRegions ?B ?C))
  (partiallyOverlapping ?A ?C)) 

;; TPP

(=>
  (and
    (tangentialProperPart ?A ?B)
    (disconnected ?B ?C))
  (disconnected ?A ?C))


(=>
  (and
    (tangentialProperPart ?A  ?B)
    (externallyConnected ?B ?C))
  (or
    (disconnected ?A ?C)
    (externallyConnected ?A ?C)))
    
(=>
  (and
    (tangentialProperPart ?A ?B)
    (partiallyOverlapping ?B ?C))
  (or
    (disconnected ?A ?C)
    (externallyConnected ?A ?C)
    (partiallyOverlapping ?A ?C)
    (tangentialProperPart ?A ?C)
    (nonTangentialProperPart ?A ?C)))
    
(=>
  (and
    (tangentialProperPart ?A ?B)
    (tangentialProperPart ?B ?C))
  (or
    (tangentialProperPart ?A ?C)
    (nonTangentialProperPart ?A ?C)))

(=>
  (and
    (tangentialProperPart ?A ?B)
    (nonTangentialProperPart ?B ?C))
  (nonTangentialProperPart ?A ?C))
    
(=>
  (and
    (tangentialProperPart ?A ?B)
    (tangentialProperPart ?C ?B))
  (or
    (disconnected ?A ?C)
    (externallyConnected ?A ?C)
    (partiallyOverlapping ?A ?C)    
    (tangentialProperPart ?A ?C)
    (nonTangentialProperPart ?C ?A)
    (equalRegions ?A ?C)))   
  
(=>
  (and
    (tangentialProperPart ?A ?B)
    (nonTangentialProperPart ?C ?B))
  (or
    (disconnected ?A ?C)
    (externallyConnected ?A ?C)
    (partiallyOverlapping ?A ?C)    
    (tangentialProperPart ?C ?A)
    (nonTangentialProperPart ?C ?A)))
  
(=>
  (and
    (tangentialProperPart ?A ?B)
    (equalRegions ?B ?C))
  (tangentialProperPart ?A ?C)) 

;; NTPP

(=>
  (and
    (nonTangentialProperPart ?A ?B)
    (disconnected ?B ?C))
  (disconnected ?A ?C))


(=>
  (and
    (nonTangentialProperPart ?A  ?B)
    (externallyConnected ?B ?C))
  (disconnected ?A ?C))
    
(=>
  (and
    (nonTangentialProperPart ?A ?B)
    (partiallyOverlapping ?B ?C))
  (or
    (disconnected ?A ?C)
    (externallyConnected ?A ?C)
    (partiallyOverlapping ?A ?C)
    (tangentialProperPart ?A ?C)
    (nonTangentialProperPart ?A ?C)))
    
(=>
  (and
    (nonTangentialProperPart ?A ?B)
    (tangentialProperPart ?B ?C))
  (nonTangentialProperPart ?A ?C))

(=>
  (and
    (nonTangentialProperPart ?A ?B)
    (nonTangentialProperPart ?B ?C))
  (nonTangentialProperPart ?A ?C))
    
(=>
  (and
    (nonTangentialProperPart ?A ?B)
    (tangentialProperPart ?C ?B))
  (or
    (disconnected ?A ?C)
    (externallyConnected ?A ?C)
    (partiallyOverlapping ?A ?C)    
    (tangentialProperPart ?A ?C)
    (nonTangentialProperPart ?A ?C)))
    
(=>
  (and
    (nonTangentialProperPart ?A ?B)
    (equalRegions ?B ?C))
  (nonTangentialProperPart ?A ?C))

;; TPPi

(=>
  (and
    (tangentialProperPart ?B ?A)
    (disconnected ?B ?C))
  (or
    (disconnected ?A ?C)
    (externallyConnected ?A ?C)
    (partiallyOverlapping ?A ?C)
    (tangentialProperPart ?C ?A)
    (nonTangentialProperPart ?C ?A)))

(=>
  (and
    (tangentialProperPart ?B ?A)
    (externallyConnected ?B ?C))
  (or
    (externallyConnected ?A ?C)
    (partiallyOverlapping ?A ?C)
    (tangentialProperPart ?C ?A)
    (nonTangentialProperPart ?C ?A)))
    
(=>
  (and
    (tangentialProperPart ?B ?A)
    (partiallyOverlapping ?B ?C))
  (or
    (partiallyOverlapping ?A ?C)
    (tangentialProperPart ?C ?A)
    (nonTangentialProperPart ?C ?A)))
    
(=>
  (and
    (tangentialProperPart ?B ?A)
    (tangentialProperPart ?B ?C))
  (or
    (partiallyOverlapping ?A ?C)
    (tangentialProperPart ?C ?A)
    (nonTangentialProperPart ?C ?A)
    (equalRegions ?A ?C)))  

(=>
  (and
    (tangentialProperPart ?B ?A)
    (nonTangentialProperPart ?B ?C))
  (or
    (partiallyOverlapping ?A ?C)
    (tangentialProperPart ?A ?C)
    (nonTangentialProperPart ?A ?C)))
    
(=>
  (and
    (tangentialProperPart ?B ?A)
    (tangentialProperPart ?C ?B))
  (or
    (tangentialProperPart ?C ?A)
    (nonTangentialProperPart ?C ?A)))
  
(=>
  (and
    (tangentialProperPart ?B ?A)
    (nonTangentialProperPart ?C ?B))
  (nonTangentialProperPart ?C ?A))
  
(=>
  (and
    (tangentialProperPart ?B ?A)
    (equalRegions ?B ?C))
  (tangentialProperPart ?C ?A))

;; NTTPi

(=>
  (and
    (nonTangentialProperPart ?B ?A)
    (disconnected ?B ?C))
  (or
    (disconnected ?A ?C)
    (externallyConnected ?A ?C)
    (partiallyOverlapping ?A ?C)
    (tangentialProperPart ?C ?A)
    (nonTangentialProperPart ?C ?A)))

(=>
  (and
    (nonTangentialProperPart ?B ?A)
    (externallyConnected ?B ?C))
  (or
    (partiallyOverlapping ?A ?C)
    (tangentialProperPart ?C ?A)
    (nonTangentialProperPart ?C ?A)))
    
(=>
  (and
    (nonTangentialProperPart ?B ?A)
    (partiallyOverlapping ?B ?C))
  (or
    (partiallyOverlapping ?A ?C)
    (tangentialProperPart ?C ?A)
    (nonTangentialProperPart ?C ?A)))
    
(=>
  (and
    (nonTangentialProperPart ?B ?A)
    (tangentialProperPart ?B ?C))
  (or
    (partiallyOverlapping ?A ?C)
    (tangentialProperPart ?C ?A)
    (nonTangentialProperPart ?C ?A)))    

(=>
  (and
    (nonTangentialProperPart ?B ?A)
    (nonTangentialProperPart ?B ?C))
  (or
    (partiallyOverlapping ?A ?C)
    (tangentialProperPart ?A ?C)
    (nonTangentialProperPart ?A ?C)
    (tangentialProperPart ?C ?A)
    (nonTangentialProperPart ?C ?A)     
    (equalRegions ?A ?C))) 
    
(=>
  (and
    (nonTangentialProperPart ?B ?A)
    (tangentialProperPart ?C ?B))
  (nonTangentialProperPart ?C ?A))
  
(=>
  (and
    (nonTangentialProperPart ?B ?A)
    (nonTangentialProperPart ?C ?B))
  (nonTangentialProperPart ?C ?A))
  
(=>
  (and
    (nonTangentialProperPart ?B ?A)
    (equalRegions ?B ?C))
  (nonTangentialProperPart ?C ?A))

;; EQ

(=>
  (and
    (equalRegions ?A ?B)
    (disconnected ?B ?C))
  (disconnected ?A ?C))

(=>
  (and
    (equalRegions ?A  ?B)
    (externallyConnected ?B ?C))
  (externallyConnected ?A ?C))
    
(=>
  (and
    (equalRegions ?A ?B)
    (partiallyOverlapping ?B ?C))
  (partiallyOverlapping ?A ?C))
    
(=>
  (and
    (equalRegions ?A ?B)
    (tangentialProperPart ?B ?C))
  (tangentialProperPart ?A ?C))
  
(=>
  (and
    (equalRegions ?A ?B)
    (nonTangentialProperPart ?B ?C))
  (nonTangentialProperPart ?A ?C))    
  
(=>
  (and
    (equalRegions ?A ?B)
    (tangentialProperPart ?C ?B))
  (tangentialProperPart ?C ?A))
  
(=>
  (and
    (equalRegions ?A ?B)
    (nonTangentialProperPart ?C ?B))
  (nonTangentialProperPart ?C ?A))
  
(=>
  (and
    (equalRegions ?A ?B)
    (equalRegions ?B ?C))
  (equalRegions ?A ?C))
  
;;;;;;;;;
    
(instance TransverseRanges MountainRange)
(documentation TransverseRanges EnglishLanguage "The &%TransverseRanges are a group of 
mountain ranges of Southern California, in the Pacific Coast Ranges physiographic region 
in North America. The Transverse Ranges begin at the southern end of the &%CaliforniaCoastRanges 
and lie within Santa Barbara, Ventura, Los Angeles, San Bernardino, Riverside and Kern counties.
The &%PeninsularRanges lie to the &%South. The name is due to the ranges' east–west orientation, 
making them transverse to the general northwest–southeast orientation of most of California's 
coastal mountains. [Wikipedia]")
(termFormat EnglishLanguage TransverseRanges "Transverse Ranges")
(geographicSubregion TransverseRanges California)
(connected TransverseRanges CaliforniaCoastRanges)
(orientation TransverseRanges CaliforniaCoastRanges South)

(externallyConnected CaliforniaCoastRanges TransverseRanges)

(located TransverseRanges
  (GeographicSouthFn California))

(instance PeninsularRanges MountainRange)
(documentation PeninsularRanges EnglishLanguage "The &%PeninsularRanges are a group of mountain 
ranges that stretch 930 mi (1,500 km) from Southern California to the southern tip of the Baja 
California peninsula; they are part of the North American Pacific Coast Ranges, which run along 
the Pacific coast from Alaska to Mexico. Elevations range from 500 to 10,834 ft (150 to 3,300 m). 
[Wikipedia]")
(termFormat EnglishLanguage PeninsularRanges "Peninsular Ranges")
(geographicSubregion PeninsularRanges California)
(orientation PeninsularRanges TransverseRanges South)

(=>
  (and
    (instance ?A PointInSpace)
    (instance ?B PointInSpace)
    (located ?A PeninsularRanges)
    (located ?B PeninsularRanges)
    (orientation ?A ?B North)    
    (not
      (exists (?A1 ?B1)
        (and
          (instance ?A1 PointInSpace)
          (located ?A1 PeninsularRanges)
          (orientation ?A1 ?A North)
          (instance ?B1 PointInSpace)
          (located ?B1 PeninsularRanges)
          (orientation ?B1 ?B South))))
    (distance ?A ?B
      (MeasureFn ?X Mile)))
 (approximateValue ?X 930))

(instance SierraNevada MountainRange)
(documentation SierraNevada EnglishLanguage "The &%SierraNevada is a mountain range in the 
Western United States, between the Central Valley of California and the Great Basin. The 
vast majority of the range lies in the state of California, although the Carson Range spur 
lies primarily in Nevada. The Sierra runs 400 mi (640 km) north-south, and its width ranges
from 50 mi (80 km) to 80 mi (130 km) across east–west.[Wikipedia]")
(termFormat EnglishLanguage SierraNevada "Sierra Nevada")
(geographicSubregion SierraNevada California)
(geographicSubregion SierraNevada Nevada)

(=>
  (and
    (instance ?A PointInSpace)
    (instance ?B PointInSpace)
    (located ?A SierraNevada)
    (located ?B SierraNevada)
    (orientation ?A ?B North)    
    (not
      (exists (?A1 ?B1)
        (and
          (instance ?A1 PointInSpace)
          (located ?A1 SierraNevada)
          (orientation ?A1 ?A North)
          (instance ?B1 PointInSpace)
          (located ?B1 SierraNevada)
          (orientation ?B1 ?B South))))
    (distance ?A ?B
      (MeasureFn ?X Mile)))
 (approximateValue ?X 400))
 
(=>
  (and
    (instance ?A PointInSpace)
    (instance ?B PointInSpace)
    (located ?A SierraNevada)
    (located ?B SierraNevada)
    (orientation ?A ?B East)    
    (not
      (exists (?A1 ?B1)
        (and
          (instance ?A1 PointInSpace)
          (located ?A1 SierraNevada)
          (orientation ?A1 ?A East)
          (instance ?B1 PointInSpace)
          (located ?B1 SierraNevada)
          (orientation ?B1 ?B West))))
    (distance ?A ?B
      (MeasureFn ?X Mile)))
 (and
   (greaterThan ?X 50)
   (greaterThan 80 ?X)))
   
(instance MountWhitney Mountain)
(documentation MountWhitney EnglishLanguage "&%MountWhitney  is a mountain in the 
&%SierraNevada mountain range of &%California, and the highest point in the contiguous 
United States, with an elevation of 14,505 feet (4,421 m).[Wikipedia]")
(termFormat EnglishLanguage MountWhitney "Mount Whitney")
(geographicSubregion MountWhitney SierraNevada)
(geographicSubregion MountWhitney California)
(elevation MountWhitney
  (MeasureFn 4421 Meter))
   
(instance CentralValleyCalifornia MountainRange)
(documentation CentralValleyCalifornia EnglishLanguage "The &%CentralValleyCalifornia 
is a broad, elongated, flat valley that dominates the interior of California, United 
States. It is 40–60 mi (60–100 km) wide and runs approximately 450 mi (720 km) from 
north-northwest to south-southeast, inland from and parallel to the Pacific coast. 
It covers approximately 18,000 sq mi (47,000 km2),[1] about 11% of California's 
land area. The valley is bounded by the Coast Ranges to the west and the &%SierraNevada 
to the east. [Wikipedia]")
(termFormat EnglishLanguage CentralValleyCalifornia "Central Valley California")
(geographicSubregion CentralValleyCalifornia California)
(orientation
 (SeacoastFn California PacificOcean)
   CentralValleyCalifornia West)
(meetsSpatially CentralValleyCalifornia CaliforniaCoastRanges)
(orientation  CentralValleyCalifornia CaliforniaCoastRanges East)
(meetsSpatially CentralValleyCalifornia SierraNevada)
(orientation  CentralValleyCalifornia SierraNevada West)

(=>
  (totalArea CentralValleyCalifornia
    (MeasureFn ?X SquareMile))
  (approximateValue ?X 18000))

(instance AndesMountains MountainRange)
(documentation AndesMountains EnglishLanguage "The &%AndesMountains, Andean
Mountain Range are the longest continental mountain range in the world, forming
a continuous highland along the western edge of &%SouthAmerica. The range is 8,900 km
(5,500 mi) long and 200 to 700 km (120 to 430 mi) wide (widest between 18°S and
20°S latitude) and has an average height of about 4,000 m (13,000 ft). The Andes
extend from south to north through seven South American countries: Argentina,
Chile, Bolivia, Peru, Ecuador, Colombia, and Venezuela.[Wikipedia]")
(termFormat EnglishLanguage AndesMountains "Andes Mountains")
(names "Andes" AndesMountains)
(geographicSubregion AndesMountains SouthAmerica)
(traverses AndesMountains Argentina)
(traverses AndesMountains Chile)
(traverses AndesMountains Bolivia)
(traverses AndesMountains Peru)
(traverses AndesMountains Ecuador)
(traverses AndesMountains Colombia)
(traverses AndesMountains Venezuela)
(orientation Brazil AndesMountains East)
(orientation Paraguay AndesMountains East)
(orientation
  (SeacoastFn SouthAmerica PacificOcean) AndesMountains West)
  
(=>
  (and
    (instance ?A PointInSpace)
    (instance ?B PointInSpace)
    (located ?A AndesMountains)
    (located ?B AndesMountains)
    (orientation ?A ?B North)    
    (not
      (exists (?A1 ?B1)
        (and
          (instance ?A1 PointInSpace)
          (located ?A1 AndesMountains)
          (orientation ?A1 ?A North)
          (instance ?B1 PointInSpace)
          (located ?B1 AndesMountains)
          (orientation ?B1 ?B South))))
    (distance ?A ?B
      (MeasureFn ?X Mile)))
 (approximateValue ?X 5500))
 
(=>
  (and
    (instance ?A PointInSpace)
    (instance ?B PointInSpace)
    (located ?A AndesMountains)
    (located ?B AndesMountains)
    (orientation ?A ?B East)    
    (not
      (exists (?A1 ?B1)
        (and
          (instance ?A1 PointInSpace)
          (located ?A1 AndesMountains)
          (orientation ?A1 ?A East)
          (instance ?B1 PointInSpace)
          (located ?B1 AndesMountains)
          (orientation ?B1 ?B West))))
    (distance ?A ?B
      (MeasureFn ?X Mile)))
 (and
   (greaterThan ?X 120)
   (greaterThan 430 ?X)))

(instance PacificNorthwest GeographicArea)
(documentation PacificNorthwest EnglishLanguage "The &%PacificNorthwest is a &%GeographicArea in
&%WesternNorthAmerica bounded by its coastal waters of the &%PacificOcean to the &%West and,
loosely, by the &%RockyMountains to the east. Though no official boundary exists, the most common
conception includes the U.S. states of &%Oregon, &%Washington, &%Washington, and the Canadian province of
&%BritishColumbia.[Wikipedia]")
(termFormat EnglishLanguage PacificNorthwest "Pacific Northwest")
(geographicSubregion PacificNorthwest WesternNorthAmerica)
(orientation PacificOcean PacificNorthwest West)
(orientation RockyMountains PacificNorthwest West)
(geographicSubregion Oregon PacificNorthwest)
(geographicSubregion Washington PacificNorthwest)
(geographicSubregion Idaho PacificNorthwest)

(instance WesternNorthAmerica GeographicArea)
(documentation WesternNorthAmerica EnglishLanguage "The &%WesternNorthAmerica is a &%GeographicArea
&%located on the western edge of the &%NorthAmerican continent that borders the &%PacificOcean. It
consists of &%Alaska at the farthest north, down through the western Canadian province of &%BritishColumbia,
the western U.S. states of &%Washington, &%Oregon, and &%California, and then &%Mexico farthest south. [Wikipedia]")
(termFormat EnglishLanguage WesternNorthAmerica "Western NorthAmerica")
(geographicSubregion WesternNorthAmerica NorthAmerica)
(orientation PacificOcean WesternNorthAmerica West)
(orientation RockyMountains WesternNorthAmerica West)
(geographicSubregion Alaska WesternNorthAmerica)
(geographicSubregion Oregon WesternNorthAmerica)
(geographicSubregion Washington WesternNorthAmerica)
(geographicSubregion California WesternNorthAmerica)
(geographicSubregion BritishColumbia WesternNorthAmerica)
(geographicSubregion Mexico WesternNorthAmerica)
(overlapsSpatially WesternNorthAmerica PacificNorthwest)
(meetsSpatially WesternNorthAmerica PacificOcean)

(subclass CanadianProvinces StateOrProvince)
(documentation CanadianProvinces EnglishLanguage "The class of 10 provinces that make up the &%Canada.")
(termFormat EnglishLanguage CanadianProvinces "Canadian provinces")
(equal CanadianProvinces
    (GeographicPartTypeFn StateOrProvince Canada))

(instance BritishColumbia CanadianProvinces)
(documentation BritishColumbia EnglishLanguage "The &%BritishColumbia
is the westernmost province of &%Canada. It is &%located in the &%PacificNorthwest between
the &%PacificOcean and the &%RockyMountains. British Columbia borders the province of
Alberta to the east; the territories of Yukon and Northwest Territories to the north;
the U.S. states of Washington, Idaho and Montana to the south, and Alaska to the northwest. [Wikipedia]")
(termFormat EnglishLanguage BritishColumbia "British Columbia")
(geographicSubregion BritishColumbia PacificNorthwest)
(meetsSpatially BritishColumbia PacificOcean)
(orientation PacificOcean BritishColumbia West)
(meetsSpatially BritishColumbia RockyMountains)
(orientation RockyMountains BritishColumbia East)
(meetsSpatially BritishColumbia Washington)
(orientation Washington BritishColumbia South)
(meetsSpatially BritishColumbia Idaho)
(orientation Idaho BritishColumbia South)
(meetsSpatially BritishColumbia Montana)
(orientation Montana BritishColumbia South)
(meetsSpatially BritishColumbia Alaska)
(orientation Alaska BritishColumbia Northwest)

(instance Ontario CanadianProvinces)
(instance Quebec CanadianProvinces)
(instance NovaScotia CanadianProvinces)
(instance NewBrunswick CanadianProvinces)
(instance Manitoab CanadianProvinces)
(instance PrinceEdwardIsland CanadianProvinces)
(instance Saskatchewan CanadianProvinces)
(instance Alberta CanadianProvinces)
(instance NewfoundlandAndLabrador CanadianProvinces)

;;-----------------------------------------------------------------------

;; B. Geographic coordinates

(subclass Latitude Region)
(names "latitude" Latitude)
(names "parallel" Latitude)
(termFormat EnglishLanguage Latitude "latitude")
(relatedInternalConcept Latitude Longitude)

(documentation Latitude EnglishLanguage "&%Latitude is the class of &%Regions,
associated with areas on the Earth's surface, which are parallels
measured in plane &%AngularDegrees from the &%Equator.")


(instance Equator Latitude)
(termFormat EnglishLanguage Equator "equator")
(documentation Equator EnglishLanguage "The &%Equator is the circle of &%Latitude that 
divides Earth into the Northern and Southern hemispheres. It is an imaginary line 
located at 0 degrees latitude, about 40,075 km (24,901 mi) in circumference, 
halfway between the North and South poles. [Wikipedia]")

(=>
  (equal Equator
    (LatitudeFn ?DIR 
      (MeasureFn 0 AngularDegree)))
  (or
    (equal ?DIR North)
    (equal ?DIR South)))

(subclass Longitude Region)
(names "longitude" Longitude)
(names "meridian" Longitude)
(termFormat EnglishLanguage Longitude "longitude")

(documentation Longitude EnglishLanguage "&%Longitude is the class of &%Regions,
associated with areas on the Earth's surface, which are meridians
measured in &%PlaneAngleDegrees from the &%PrimeMeridian through
&%GreenwichEnglandUK.")

(instance GreenwichEnglandUK City)

(instance objectGeographicCoordinates TernaryPredicate)
(domain objectGeographicCoordinates 1 Object)
(domain objectGeographicCoordinates 2 Latitude)
(domain objectGeographicCoordinates 3 Longitude)

(documentation objectGeographicCoordinates EnglishLanguage
"(&%objectGeographicCoordinates ?OBJECT ?LAT ?LONG) means that
the &%Object ?OBJECT is found at the geographic coordinates
?LAT and ?LONG.")

(instance GeographicCenterFn UnaryFunction)
(names "geographic center" GeographicCenterFn)
(domain GeographicCenterFn 1 GeographicArea)
(range GeographicCenterFn GeographicArea)

(documentation GeographicCenterFn EnglishLanguage
"(&%GeographicCenterFn ?REGION) denotes the geographical center
of the &%GeographicArea ?REGION.")

(=>
  (and
    (instance ?REGION GeographicArea)
    (instance (GeographicCenterFn ?REGION) GeographicArea))
  (geographicSubregion (GeographicCenterFn ?REGION) ?REGION))

(=>
  (and
    (instance ?REGION GeographicArea)
    (objectGeographicCoordinates
      (GeographicCenterFn ?REGION) ?LAT ?LONG))
  (objectGeographicCoordinates ?REGION ?LAT ?LONG))

(instance GeographicNorthFn UnaryFunction)
(names "geographic north" GeographicNorthFn)
(domain GeographicNorthFn 1 GeographicArea)
(range GeographicNorthFn GeographicArea)
(documentation GeographicNorthFn EnglishLanguage
"(&%GeographicNorthFn ?REGION) denotes the northern half of the &%geographicSubregion
of &%GeographicArea ?REGION.")
(termFormat EnglishLanguage GeographicNorthFn "geographic North")
(relatedInternalConcept GeographicNorthFn GeographicEastFn)

(=>
  (and
    (instance ?REGION GeographicArea)
    (geographicSubregion (GeographicNorthFn ?REGION) ?REGION)
    (geographicSubregion (GeographicSouthFn ?REGION) ?REGION))
  (orientation (GeographicNorthFn ?REGION) (GeographicSouthFn ?REGION) North))

(=>
  (and
    (instance ?REGION GeographicArea)
    (geographicSubregion (GeographicNorthFn ?REGION) ?REGION)
    (geographicSubregion (GeographicSouthFn ?REGION) ?REGION))
  (not
    (overlapsSpatially (GeographicNorthFn ?REGION) (GeographicSouthFn ?REGION))))

(=>
  (and
    (instance ?REGION GeographicArea)
    (geographicSubregion (GeographicNorthFn ?REGION) ?REGION)
    (geographicSubregion (GeographicEastFn ?REGION) ?REGION))
  (overlapsSpatially (GeographicNorthFn ?REGION) (GeographicEastFn ?REGION)))

(=>
  (and
    (instance ?REGION GeographicArea)
    (geographicSubregion (GeographicNorthFn ?REGION) ?REGION)
    (geographicSubregion (GeographicWestFn ?REGION) ?REGION))
  (overlapsSpatially (GeographicNorthFn ?REGION) (GeographicWestFn ?REGION)))

(=>
  (and
    (instance ?REGION GeographicArea)
    (geographicSubregion (GeographicNorthFn ?REGION) ?REGION)
    (geographicSubregion (GeographicCenterFn ?REGION) ?REGION))
  (overlapsSpatially (GeographicNorthFn ?REGION) (GeographicCenterFn ?REGION)))

(instance GeographicSouthFn UnaryFunction)
(names "geographic South" GeographicSouthFn)
(domain GeographicSouthFn 1 GeographicArea)
(range GeographicSouthFn GeographicArea)
(documentation GeographicSouthFn EnglishLanguage
"(&%GeographicSouthFn ?REGION) denotes the southern half of the &%geographicSubregion
of &%GeographicArea ?REGION.")
(termFormat EnglishLanguage GeographicSouthFn "geographic South")
(relatedInternalConcept GeographicSouthFn GeographicWestFn)

(=>
  (and
    (instance ?REGION GeographicArea)
    (geographicSubregion (GeographicSouthFn ?REGION) ?REGION)
    (geographicSubregion (GeographicNorthFn ?REGION) ?REGION))
  (orientation (GeographicSouthFn ?REGION) (GeographicNorthFn ?REGION) South))

(=>
  (and
    (instance ?REGION GeographicArea)
    (geographicSubregion (GeographicSouthFn ?REGION) ?REGION)
    (geographicSubregion (GeographicEastFn ?REGION) ?REGION))
  (overlapsSpatially (GeographicSouthFn ?REGION) (GeographicEastFn ?REGION)))

(=>
  (and
    (instance ?REGION GeographicArea)
    (geographicSubregion (GeographicSouthFn ?REGION) ?REGION)
    (geographicSubregion (GeographicWestFn ?REGION) ?REGION))
  (overlapsSpatially (GeographicSouthFn ?REGION) (GeographicWestFn ?REGION)))

(=>
  (and
    (instance ?REGION GeographicArea)
    (geographicSubregion (GeographicSouthFn ?REGION) ?REGION)
    (geographicSubregion (GeographicCenterFn ?REGION) ?REGION))
  (overlapsSpatially (GeographicSouthFn ?REGION) (GeographicCenterFn ?REGION)))

(instance GeographicEastFn UnaryFunction)
(names "geographic east" GeographicEastFn)
(domain GeographicEastFn 1 GeographicArea)
(range GeographicEastFn GeographicArea)
(documentation GeographicEastFn EnglishLanguage
"(&%GeographicEastFn ?REGION) denotes the eastern half of the &%geographicSubregion
of &%GeographicArea ?REGION.")
(termFormat EnglishLanguage GeographicEastFn "geographic east")

(=>
  (and
    (instance ?REGION GeographicArea)
    (geographicSubregion (GeographicEastFn ?REGION) ?REGION)
    (geographicSubregion (GeographicWestFn ?REGION) ?REGION))
  (orientation (GeographicEastFn ?REGION) (GeographicWestFn ?REGION) East))

(=>
  (and
    (instance ?REGION GeographicArea)
    (geographicSubregion (GeographicEastFn ?REGION) ?REGION)
    (geographicSubregion (GeographicWestFn ?REGION) ?REGION))
  (not
    (overlapsSpatially (GeographicEastFn ?REGION) (GeographicWestFn ?REGION))))

(=>
  (and
    (instance ?REGION GeographicArea)
    (geographicSubregion (GeographicEastFn ?REGION) ?REGION)
    (geographicSubregion (GeographicCenterFn ?REGION) ?REGION))
  (overlapsSpatially (GeographicEastFn ?REGION) (GeographicCenterFn ?REGION)))

(instance GeographicWestFn UnaryFunction)
(names "geographic west" GeographicWestFn)
(domain GeographicWestFn 1 GeographicArea)
(range GeographicWestFn GeographicArea)
(documentation GeographicWestFn EnglishLanguage
"(&%GeographicWestFn ?REGION) denotes the western half of the &%geographicSubregion
of &%GeographicArea ?REGION.")
(termFormat EnglishLanguage GeographicWestFn "geographic west")

(=>
  (and
    (instance ?REGION GeographicArea)
    (geographicSubregion (GeographicWestFn ?REGION) ?REGION)
    (geographicSubregion (GeographicEastFn ?REGION) ?REGION))
  (orientation (GeographicWestFn ?REGION) (GeographicEastFn ?REGION) West))

(=>
  (and
    (instance ?REGION GeographicArea)
    (geographicSubregion (GeographicWestFn ?REGION) ?REGION)
    (geographicSubregion (GeographicCenterFn ?REGION) ?REGION))
  (overlapsSpatially (GeographicWestFn ?REGION) (GeographicCenterFn ?REGION)))

;; See Merge.kif for definitions of &%PlaneAngleMeasure and &%AngularDegree

;; Additional assertions for &%AngularDegree:
(=>
  (measure ?ANGLE (MeasureFn ?NUMBER AngularDegree))
  (greaterThanOrEqualTo ?NUMBER 0.0))

(=>
  (measure ?ANGLE (MeasureFn ?NUMBER AngularDegree))
  (lessThanOrEqualTo ?NUMBER 360.0))

(instance ArcMinute UnitOfAngularMeasure)
(documentation ArcMinute EnglishLanguage "&%ArcMinute represents a &%UnitOfMeasure
equivalent to 1/60th of an &%AngularDegree.")

(=>
  (measure ?ANGLE (MeasureFn ?NUMBER ArcMinute))
  (greaterThanOrEqualTo ?NUMBER 0.0))

(=>
  (measure ?ANGLE (MeasureFn ?NUMBER ArcMinute))
  (lessThanOrEqualTo ?NUMBER 60.0))

(<=>
  (measure ?OBJ (MeasureFn ?DEG AngularDegree))
  (measure ?OBJ (MeasureFn (MultiplicationFn 60.0 ?DEG) ArcMinute)))

(equal (MeasureFn 1.0 AngularDegree) (MeasureFn 60.0 ArcMinute))
(equal (MeasureFn ?NUM AngularDegree) (MeasureFn (MultiplicationFn 60.0 ?NUM) ArcMinute))

(instance ArcSecond UnitOfAngularMeasure)
(documentation ArcSecond EnglishLanguage "&%ArcSecond represents a &%UnitOfMeasure
equivalent to 1/60th of an &%ArcMinute.")

(=>
  (measure ?ANGLE (MeasureFn ?NUMBER ArcSecond))
  (greaterThanOrEqualTo ?NUMBER 0.0))

(=>
  (measure ?ANGLE (MeasureFn ?NUMBER ArcSecond))
  (lessThanOrEqualTo ?NUMBER 60.0))

(<=>
  (measure ?OBJ (MeasureFn ?DEG ArcMinute))
  (measure ?OBJ (MeasureFn (MultiplicationFn 60.0 ?DEG) ArcSecond)))

(equal (MeasureFn 1.0 ArcMinute) (MeasureFn 60.0 ArcSecond))
(equal (MeasureFn ?DEG ArcMinute) (MeasureFn (MultiplicationFn 60.0 ?DEG) ArcSecond))

(instance LatitudeFn VariableArityRelation)
(instance LatitudeFn Function)
(domain LatitudeFn 1 DirectionalAttribute)
(domain LatitudeFn 2 AngleMeasure)
(domain LatitudeFn 3 AngleMeasure)
(domain LatitudeFn 4 AngleMeasure)
(range LatitudeFn Region)
(termFormat EnglishLanguage LatitudeFn "Latitude")

(documentation LatitudeFn EnglishLanguage "&%LatitudeFn is a &%VariableArityRelation
used to denote a parallel of latitude.  Examples:
(&%LatitudeFn &%North (&%MeasureFn 38.0 &%AngularDegree)), (&%LatitudeFn
&%South (&%MeasureFn 23.0 &%AngularDegree) (&%MeasureFn 30.0 &%ArcMinute)),
(&%LatitudeFn &%South (&%MeasureFn 60.0 &%AngularDegree) (&%MeasureFn 0.0 &%ArcMinute)
(&%MeasureFn 0.0 &%ArcSecond)), (&%LatitudeFn &%North
(&%MeasureFn 42.0 &%AngularDegree) (&%MeasureFn 7.89 &%ArcMinute)).")

(=>
  (instance (LatitudeFn ?DIRECTION @ROW) Region)
  (or
    (equal ?DIRECTION North)
    (equal ?DIRECTION South)))

(=>
  (and
    (instance (LatitudeFn ?DIRECTION @ROW) Region)
    (equal (ListOrderFn (ListFn @ROW) 1) (MeasureFn ?NUM AngularDegree)))
  (lessThanOrEqualTo ?NUM 90.0))

(=>
  (or
    (equal ?DIRECTION North)
    (equal ?DIRECTION South))
  (length
     (LatitudeFn ?DIRECTION
    (MeasureFn 0.0 AngularDegree) (MeasureFn 1.0 ArcMinute) (MeasureFn 0.0 ArcSecond))
     (MeasureFn 1.0 NauticalMile)))

(instance LongitudeFn Function)
(instance LongitudeFn VariableArityRelation)
(domain LongitudeFn 1 DirectionalAttribute)
(domain LongitudeFn 2 AngleMeasure)
(domain LongitudeFn 3 AngleMeasure)
(domain LongitudeFn 4 AngleMeasure)
(range LongitudeFn GeographicArea)
(termFormat EnglishLanguage LongitudeFn "Longitude")

(documentation LongitudeFn EnglishLanguage "(&%LongitudeFn ?DIRECTION @ROW)
denotes a meridian of longitude.  Note that &%LongitudeFn is
a &%VariableArityRelation.  Examples:
(&%LongitudeFn &%East (&%MeasureFn 180.0 &%AngularDegree)), (&%LongitudeFn
&%West (&%MeasureFn 122.0 &%AngularDegree) (&%MeasureFn 24.0 &%ArcMinute)),
(&%LongitudeFn &%East (&%MeasureFn 121.0 &%AngularDegree)
(&%MeasureFn 0.0 &%ArcMinute) (&%MeasureFn 15.0 &%ArcSecond)),
(&%LongitudeFn &%West (&%MeasureFn 80.0 &%AngularDegree)
(&%MeasureFn 6.78 &%ArcMinute)).")

(=>
  (instance (LongitudeFn ?DIRECTION @ROW) Region)
  (or
    (equal ?DIRECTION East)
    (equal ?DIRECTION West)))

(=>
  (and
    (instance (LongitudeFn ?DIRECTION @ROW) Region)
    (equal
      (ListOrderFn
        (ListFn @ROW) 1)
      (MeasureFn ?NUM AngularDegree)))
  (lessThanOrEqualTo ?NUM 180.0))

(instance DirectionalSubregionFn BinaryFunction)
(domain DirectionalSubregionFn 1 DirectionalAttribute)
(domain DirectionalSubregionFn 2 GeographicArea)
(range DirectionalSubregionFn GeographicArea)

(documentation DirectionalSubregionFn EnglishLanguage
"(&%DirectionalSubregionFn ?DIRECTION ?AREA) denotes the part
of &%GeographicArea ?AREA that lies in ?DIRECTION from the
geographic center of ?AREA. For example,
(&%DirectionalSubregionFn &%Iraq &%North) denotes the Northern
part of &%Iraq.  Such subregions are defined purely by geographical
points of reference, not by sociological ones.  For example,
(&%DirectionalSubregionFn &%UnitedStatesOfAmerica &%South) denotes
the Southern half of the United States, it does not denote the
American South as distinguished for historical, literary, or
cultural purposes.")

(=>
  (and
    (instance ?DIRECTION DirectionalAttribute)
    (instance ?AREA GeographicArea)
    (instance (DirectionalSubregionFn ?DIRECTION ?AREA) GeographicArea))
  (geographicSubregion (DirectionalSubregionFn ?DIRECTION ?AREA) ?AREA))

(=>
  (and
    (instance ?DIRECTION DirectionalAttribute)
    (instance ?AREA GeographicArea)
    (instance (DirectionalSubregionFn ?DIRECTION ?AREA) GeographicArea))
  (orientation (DirectionalSubregionFn ?DIRECTION ?AREA) (GeographicCenterFn ?AREA) ?DIRECTION))

(=>
  (and
    (partlyLocated ?PLACE ?SUBAREA)
    (instance ?SUBAREA GeographicArea)
    (geographicSubregion ?SUBAREA ?AREA))
  (partlyLocated ?PLACE ?AREA))

(=>
  (and
    (connected ?X ?Y)
    (part ?Y ?Z))
  (connected ?X ?Z))

;;-----------------------------------------------------------------------

;; C. Map references
;; This section simply indicates on which specific maps the country
;; may be found.

(instance mapOfArea BinaryPredicate)
(domain mapOfArea 1 Region)
(domain mapOfArea 2 SymbolicString)

(documentation mapOfArea EnglishLanguage "(&%mapOfArea ?AREA ?POINTER) means that a map
of the &%Region ?AREA can be found at the location ?POINTER.")

;;-----------------------------------------------------------------------

;; D. Area

(instance landAreaOnly BinaryPredicate)
(domain landAreaOnly 1 GeographicArea)
(domain landAreaOnly 2 AreaMeasure)

(documentation landAreaOnly EnglishLanguage "(&%landAreaOnly ?REGION ?MEASURE) means
that the total area(s) of solid ground within the &%GeographicArea
?REGION has the &%AreaMeasure ?AMOUNT. The pieces of solid ground need
not be continuous within the region.")

(instance waterAreaOnly BinaryPredicate)
(domain waterAreaOnly 1 GeographicArea)
(domain waterAreaOnly 2 AreaMeasure)

(documentation waterAreaOnly EnglishLanguage "(&%waterAreaOnly ?REGION ?MEASURE) means
that the total area(s) of surface water within the &%GeographicArea
?REGION has the &%AreaMeasure ?AMOUNT. The pieces of water need not be
continuous within the region.")

(=>
  (and
    (instance ?UNIT UnitOfArea)
    (landAreaOnly ?AREA (MeasureFn ?LAND ?UNIT))
    (waterAreaOnly ?AREA (MeasureFn ?WATER ?UNIT)))
  (totalArea ?AREA (MeasureFn (AdditionFn ?LAND ?WATER) ?UNIT)))

(subclass SurfaceGroundArea GeographicArea)

(documentation SurfaceGroundArea EnglishLanguage "&%SurfaceGroundArea is a subclass of
&%GeographicArea that is restricted to regions whose surface is solid
ground.  A &%SurfaceGroundArea may be a discontinuous region overlapping
a larger, continuous &%GeographicArea but excluding any &%WaterAreas
enclosed therein. Rivers, lakes, reservoirs and other surface water areas
are not part of any &%SurfaceGroundArea.")

(=>
  (instance ?EXCLUSIVELANDAREA SurfaceGroundArea)
  (not
    (exists (?WATERAREA)
      (and
        (instance ?WATERAREA WaterArea)
      (part ?WATERAREA ?EXCLUSIVELANDAREA)))))

(=>
  (landAreaOnly ?AREA ?MEASURE)
  (exists (?LAND)
    (and
      (instance ?LAND SurfaceGroundArea)
      (part ?LAND ?AREA)
      (totalArea ?LAND ?MEASURE))))

(subclass WaterOnlyArea GeographicArea)

(documentation WaterOnlyArea EnglishLanguage "&%WaterOnlyArea is a subclass of
&%GeographicArea that is restricted to regions whose surface is water.
A &%WaterOnlyArea may be a discontinuous region overlapping a larger,
continuous &%GeographicArea but excluding any &%LandAreas enclosed therein.
Dry land areas, including islands, are not part of any &%WaterOnlyArea.")

(=>
  (instance ?EXCLUSIVEWATERAREA WaterOnlyArea)
  (not
    (exists (?LANDAREA)
      (and
        (instance ?LANDAREA LandArea)
        (part ?LANDAREA ?EXCLUSIVEWATERAREA)))))

(=>
  (waterAreaOnly ?AREA ?MEASURE)
  (exists (?WATER)
    (and
      (instance ?WATER WaterOnlyArea)
      (part ?WATER ?AREA)
      (totalArea ?WATER ?MEASURE))))

;; TERM

(instance SquareKilometer UnitOfArea)
(documentation SquareKilometer EnglishLanguage "&%SquareKilometer represents a
&%UnitOfMeasure equal to one square kilometer.")

(equal SquareKilometer (SquareUnitFn (KiloFn Meter)))
(equal SquareKilometer (SquareUnitFn Kilometer))

; the following codes are redundant
;(=>
;  (equal ?NUM
;    (MultiplicationFn 1.0 ?NUM))
;  (equal
;    (MeasureFn ?NUM SquareKilometer)
;    (MeasureFn
;      (MultiplicationFn ?NUM 1000000.0) SquareMeter)))
;
;;-----------------------------------------------------------------------

;; E. Area - comparative
;; This section compares the area of a country to some state or other
;; part of the United States.

;; This is a bit of a hack, but the easiest solution since IntersectionFn
;; is only used here in a domain statement

(subclass BinaryRelationExtendedToQuantities RelationExtendedToQuantities)
(subclass BinaryRelationExtendedToQuantities BinaryPredicate)

(instance comparativeArea QuaternaryPredicate)
(domain comparativeArea 1 GeographicArea)
; (domain comparativeArea 2 (IntersectionFn RelationExtendedToQuantities BinaryPredicate))
(domain comparativeArea 2 BinaryRelationExtendedToQuantities)
(domain comparativeArea 3 PositiveRealNumber)
(domain comparativeArea 4 GeographicArea)

(documentation comparativeArea EnglishLanguage
"(&%comparativeArea ?REGION1 ?RELATION ?FACTOR ?REGION2) means that
the total area of &%GeographicArea ?REGION1 has the relation ?RELATION
to the total area of &%GeographicArea ?REGION2 times ?FACTOR.  For
example, that France is (slightly) less than twice the size of Colorado
is represented by (&%comparativeArea &%France &%lessThan 2.0 &%Colorado).")

(=>
  (and
    (comparativeArea ?REGION1 ?RELATION ?FACTOR ?REGION2)
    (instance ?UNIT UnitOfArea)
    (measure ?REGION1 (MeasureFn ?NUM1 ?UNIT))
    (measure ?REGION2 (MeasureFn ?NUM2 ?UNIT)))
  (?RELATION
    (MeasureFn ?NUM1 ?UNIT)
    (MeasureFn
      (MultiplicationFn ?FACTOR ?NUM2)
      ?UNIT)))

;;-----------------------------------------------------------------------

;; F. Land boundaries

(instance totalLandBoundary BinaryPredicate)
(domain totalLandBoundary 1 GeographicArea)
(domain totalLandBoundary 2 LengthMeasure)

(documentation totalLandBoundary EnglishLanguage "(&%totalLandBoundary ?REGION ?LENGTH)
means that the &%GeographicArea ?REGION has a total &%LengthMeasure
?LENGTH of land boundaries between it and other countries.  Note that
the boundaries included in the total length may be discontinuous, as when
interrupted by stretches of &%Seacoast.")

(instance BorderFn BinaryFunction)
(names "border" BorderFn)
(domain BorderFn 1 GeographicArea)
(domain BorderFn 2 GeographicArea)
(range BorderFn GeographicArea)

(documentation BorderFn EnglishLanguage "(&%BorderFn ?AREA1 ?AREA2) denotes the
border area where the &%GeographicAreas ?AREA1 and ?AREA2 meet.")

(=>
  (instance (BorderFn ?AREA1 ?AREA2) GeographicArea)
  (meetsSpatially ?AREA1 ?AREA2))

(=>
  (and
    (instance ?OBJ1 GeographicArea)
    (instance ?OBJ2 GeographicArea))
  (equal (BorderFn ?OBJ1 ?OBJ2) (BorderFn ?OBJ2 ?OBJ1)))

(=>
  (and
    (instance (BorderFn ?COUNTRY1 ?COUNTRY2) GeographicArea)
    (instance ?COUNTRY1 Nation)
    (instance ?COUNTRY2 Nation))
  (instance (BorderFn ?COUNTRY1 ?COUNTRY2) InternationalBorder))

(=>
  (instance (BorderFn ?AREA1 ?AREA2) GeographicArea)
  (orientation ?AREA1 ?AREA2 Adjacent))

(=>
  (orientation ?ONE ?TWO Adjacent)
  (orientation ?TWO ?ONE Adjacent))

(=>
  (orientation ?ONE ?TWO Near)
  (orientation ?TWO ?ONE Near))

(=>
  (meetsSpatially ?AREA1 ?AREA2)
  (not (overlapsSpatially ?AREA1 ?AREA2)))

(subclass InternationalBorder GeographicArea)

(documentation InternationalBorder EnglishLanguage "&%InternationalBorder is the
subclass of &%GeographicAreas where the areas of two &%Nations meet.")

(subclass Checkpoint GeographicArea)
(documentation Checkpoint EnglishLanguage "An area on or very near a border, usually along
a road connecting two regions, where &%MilitaryPersons or &%PoliceOfficers
restrict the flow of traffic in order to extract tarrifs, deny movement to
certain kinds of people or goods, or other enforement actions.  Because of
the power relationship involved, such areas are often the site of &%Illegal
activities conducted by the officials in order to extract favors or
bribes.")

(=>
  (instance ?X Checkpoint)
  (or
    (exists (?ROAD)
      (and
        (instance ?ROAD Road)
        (orientation ?ROAD ?X Near)))
    (exists (?REG1 ?REG2)
      (orientation (BorderFn ?REG1 ?REG2) ?X Near))))

(documentation sharedBorderLength EnglishLanguage "The &%length of the boundary
between two &%GeographicRegions.")
(instance sharedBorderLength TernaryPredicate)
(domain sharedBorderLength 1 GeographicArea)
(domain sharedBorderLength 2 GeographicArea)
(domain sharedBorderLength 3 LengthMeasure)
(=>
  (sharedBorderLength ?N1 ?N2 ?M)
  (length (BorderFn ?N1 ?N2) ?M))

(subclass PerimeterArea Region)
(documentation PerimeterArea EnglishLanguage "A &%PerimeterArea is a region that
extends outward from a boundary with another region, surrounding or
partially surrounding it, but which is not part of that other region.
See &%BorderFn and &%PerimeterAreaFn.")

(instance PerimeterAreaFn UnaryFunction)
(domain PerimeterAreaFn 1 Region)
(rangeSubclass PerimeterAreaFn PerimeterArea)

(documentation PerimeterAreaFn EnglishLanguage "(&%PerimeterAreaFn ?REGION) denotes a
class including any peripheral zones extending outward from the &%Region
?REGION.")

(=>
  (instance ?zone (PerimeterAreaFn ?region))
  (meetsSpatially ?zone ?region))

(instance MaritimeClaimsTerritorialSeaFn UnaryFunction)
(domain MaritimeClaimsTerritorialSeaFn 1 Region)
(range MaritimeClaimsTerritorialSeaFn PerimeterArea)
(documentation MaritimeClaimsTerritorialSeaFn EnglishLanguage "(&%MaritimeClaimsTerritorialSeaFn ?REGION) denotes a
peripheral zones of maritime control extending outward from the &%Region
?REGION.")

(=>
  (instance ?region GeopoliticalArea)
  (instance (MaritimeClaimsTerritorialSeaFn ?region) (PerimeterAreaFn ?region)))

(=>
  (instance ?region GeopoliticalArea)
  (instance (MaritimeClaimsTerritorialSeaFn ?region) UniformPerimeterArea))

(instance InnerBoundaryFn UnaryFunction)
(domain InnerBoundaryFn 1 Region)  ;; a PerimeterArea or general class of oriented areas?
(range InnerBoundaryFn Region)

(documentation InnerBoundaryFn EnglishLanguage "(&%InnerBoundaryFn ?REGION) denotes the
inner boundary of the &%Region ?REGION, where ?REGION has an inner and
outer &%orientation with respect to another object.")

(=>
  (instance ?REGION Region)
  (superficialPart (InnerBoundaryFn ?REGION) ?REGION))

(instance OuterBoundaryFn UnaryFunction)
(domain OuterBoundaryFn 1 Region)  ;; a PerimeterArea or general class of oriented areas
(range OuterBoundaryFn Region)

(documentation OuterBoundaryFn EnglishLanguage "(&%OuterBoundaryFn ?REGION) denotes the
outer boundary of the &%Region ?REGION, where ?REGION has an inner and
outer &%orientation with respect to another object.")

(=>
  (instance ?REGION Region)
  (superficialPart (OuterBoundaryFn ?REGION) ?REGION))


(subclass UniformPerimeterArea PerimeterArea)

(documentation UniformPerimeterArea EnglishLanguage "A &%UniformPerimeterArea is one
that has a defined uniform &%width.")

(=>
  (instance ?AREA UniformPerimeterArea)
  (exists (?WIDTH)
    (distance (InnerBoundaryFn ?AREA) (OuterBoundaryFn ?AREA) ?WIDTH)))

(=>
  (instance ?ZONE UniformPerimeterArea)
  (exists (?WIDTH)
    (and
      (instance ?WIDTH LengthMeasure)
      (width ?ZONE ?WIDTH))))

(=>
  (and
    (instance ?ZONE UniformPerimeterArea)
    (part ?INNER1
      (InnerBoundaryFn ?ZONE))
    (part ?OUTER1
      (OuterBoundaryFn ?ZONE))
    (distance ?INNER1 ?OUTER1 ?WIDTH1)
    (part ?INNER2
      (InnerBoundaryFn ?ZONE))
    (part ?OUTER2
      (OuterBoundaryFn ?ZONE))
    (distance ?INNER2 ?OUTER2 ?WIDTH2))
  (equal ?WIDTH1 ?WIDTH2))

(=>
  (and
    (orientation ?OBJ ?REGION Outside)
    (instance ?ZONE (PerimeterAreaFn ?REGION))
    (instance ?ZONE UniformPerimeterArea)
    (width ?ZONE (MeasureFn ?LIMIT ?UNIT))
    (distance ?OBJ ?REGION (MeasureFn ?FAR ?UNIT))
    (instance ?UNIT UnitOfMeasure)
    (lessThanOrEqualTo (SubtractionFn ?FAR ?LIMIT) 0.0))
  (located ?OBJ ?ZONE))

(=>
  (and
    (orientation ?OBJ ?REGION Outside)
    (instance ?ZONE (PerimeterAreaFn ?REGION))
    (instance ?ZONE UniformPerimeterArea)
    (width ?ZONE (MeasureFn ?LIMIT ?UNIT))
    (distance ?OBJ ?REGION (MeasureFn ?FAR ?UNIT))
    (instance ?UNIT UnitOfMeasure)
    (greaterThan (SubtractionFn ?FAR ?LIMIT) 0.0))
  (not (located ?OBJ ?ZONE)))

(=>
  (and
    (instance ?AREA (MaritimeClaimsTerritorialSeaFn ?PLACE))
    (width ?AREA (MeasureFn ?WIDTH ?UNIT))
    (distance ?OBJ ?PLACE (MeasureFn ?DISTANCE ?UNIT))
    (lessThanOrEqualTo ?DISTANCE ?WIDTH))
  (located ?OBJ ?AREA))

;;-----------------------------------------------------------------------

;; G. Coastline

(instance totalCoastline BinaryPredicate)
(domain totalCoastline 1 GeographicArea)
(domain totalCoastline 2 LengthMeasure)

(documentation totalCoastline EnglishLanguage "(&%totalCoastline ?AREA ?COASTLENGTH)
means that the total length of all boundaries between the &%GeographicArea
?AREA and the ocean is the &%LengthMeasure ?COASTLENGTH.  The coastline
counted may be discontinuous and may even be along different oceans (as
in &%Columbia).  A &%totalCoastline of zero indicates a &%LandlockedArea.
Note that only &%Ocean coastlines are counted, not any shores with inland
waters.")

(subclass LandlockedArea LandArea)

(documentation LandlockedArea EnglishLanguage "&%LandlockedArea is the class of
&%LandAreas that lack access to an &%Ocean or to a &%Waterway
providing a link to the ocean.")

(=>
  (instance ?AREA LandlockedArea)
  (not
    (exists (?COAST)
      (and
        (instance ?COAST Seacoast)
        (part ?COAST ?AREA)))))

(=>
  (and
    (instance ?AREA LandlockedArea)
    (instance ?UNIT UnitOfLength))
  (totalCoastline ?AREA (MeasureFn 0.0 ?UNIT)))

(=>
  (and
    (instance ?UNIT UnitOfLength)
    (totalCoastline ?AREA (MeasureFn 0.0 ?UNIT)))
  (instance ?AREA LandlockedArea))

(=>
  (and
    (instance ?AREA LandlockedArea)
    (instance ?AREA GeopoliticalArea))
  (not
    (exists (?CLAIM)
      (and
        (instance ?CLAIM MaritimeClaimArea)
        (claimedTerritory ?CLAIM ?AREA)))))

(=>
  (instance ?AREA LandlockedArea)
  (not
    (exists (?WATER)
      (and
        (instance ?WATER Ocean)
        (meetsSpatially ?AREA ?WATER)))))

(=>
  (instance ?AREA LandlockedArea)
  (not
    (exists (?WATER ?SEA)
      (and
        (instance ?WATER SaltWaterArea)
        (part ?WATER ?SEA)
        (instance ?SEA Ocean)
        (meetsSpatially ?AREA ?WATER)))))

(=>
  (instance ?AREA LandlockedArea)
  (exists (?LAND)
    (and
      (instance ?LAND LandArea)
      (meetsSpatially ?AREA ?LAND))))

;;-----------------------------------------------------------------------

;; H. Maritime claims

(instance claimedTerritory BinaryPredicate)
(domain claimedTerritory 1 GeographicArea)
(domain claimedTerritory 2 AutonomousAgent)
(subrelation dependentGeopoliticalArea claimedTerritory)

(documentation claimedTerritory EnglishLanguage "(&%claimedTerritory ?AREA ?POLITY)
means that some right over the &%GeographicArea ?AREA is claimed by
the &%AutonomousAgent or &%GeopoliticalArea ?POLITY.  If two politically independent
states or agents claim the same area, that area is a 'disputed
territory'.")

(instance maritimeClaimType BinaryPredicate)
(domain maritimeClaimType 1 GeopoliticalArea)
(domainSubclass maritimeClaimType 2 MaritimeClaimArea)

(documentation maritimeClaimType EnglishLanguage "(&%maritimeClaimType ?POLITY ?TYPE)
means that the &%GeopoliticalArea ?POLITY claims rights over a
&%MaritimeClaimArea of ?TYPE.")

(=>
  (and
    (instance ?AREA LandlockedArea)
    (subclass ?CLAIM MaritimeClaimArea))
  (not (maritimeClaimType ?AREA ?CLAIM)))

(=>
  (and
    (maritimeClaimType ?AREA ?TYPE)
    (subclass ?TYPE MaritimeClaimArea))
  (exists (?CLAIM)
    (and
      (instance ?CLAIM ?TYPE)
      (claimedTerritory ?CLAIM ?AREA))))

(=>
  (and
    (instance ?AREA ?TYPE)
    (subclass ?TYPE MaritimeClaimArea)
    (claimedTerritory ?AREA ?POLITY))
  (maritimeClaimType ?POLITY ?TYPE))

;; KJN: Moving this to MILO.
;;(instance waterDepth BinaryPredicate)
;;(domain waterDepth 1 WaterArea)
;;(domain waterDepth 2 LengthMeasure)
;;(subrelation waterDepth length)
;;(documentation waterDepth EnglishLanguage "(&%waterDepth ?AREA ?LENGTH) means that the
;;depth of water at the &%Region ?AREA is ?LENGTH.")

;;(=>
;;  (waterDepth ?AREA ?DEPTH)
;;  (exists (?BOTTOM ?TOP)
;;    (and
;;      (depth ?TOP ?BOTTOM ?DEPTH)
;;      (surface ?TOP ?AREA)
;;      (bottom ?BOTTOM ?AREA))))

(subclass MaritimeClaimArea GeographicArea)

(documentation MaritimeClaimArea EnglishLanguage "A &%MaritimeClaimArea is a &%GeographicArea
delimited by a geopolitical state's claim, under the &%LawOfTheSea, of rights
to certain resources, activities, or jurisdiction in the claimed area.
&%MaritimeClaimAreas can pertain to &%WaterAreas, &%SubmergedLandAreas, and &%Airspace.")

;; Types of MaritimeClaimArea:
;; (i) contiguous zone
(subclass MaritimeContiguousZone MaritimeClaimArea)
(subclass MaritimeContiguousZone SaltWaterArea)

(documentation MaritimeContiguousZone EnglishLanguage "&%MaritimeContiguousZone
is the subclass of &%MaritimeClaimArea that includes areas over
which a geopolitical state may exercise some control of activities
beyond the 12-mile zone of its &%TerritorialSea.  In general,
the authorized control is for preventing or punishing activities
that would violate laws applying within the 12-mile zone (that is,
authorization to chase and intercept).  According to the &%LawOfTheSea,
a &%MaritimeContiguousZone may extend up to 24 nautical miles from the coast.")

(instance MaritimeContiguousZoneFn UnaryFunction)
(domain MaritimeContiguousZoneFn 1 GeopoliticalArea)
(range MaritimeContiguousZoneFn MaritimeContiguousZone)

(documentation MaritimeContiguousZoneFn EnglishLanguage
"(&%MaritimeContiguousZoneFn ?POLITY) denotes the &%MaritimeContiguousZone
that is claimed by the &%GeopoliticalArea ?POLITY.")

(=>
  (and
    (instance ?AREA GeopoliticalArea)
    (instance (MaritimeContiguousZoneFn ?AREA) MaritimeContiguousZone))
  (claimedTerritory (MaritimeContiguousZoneFn ?AREA) ?AREA))

;; (ii) continental shelf
(subclass MaritimeShelfArea MaritimeClaimArea)
(subclass MaritimeShelfArea SubmergedLandArea)

(documentation MaritimeShelfArea EnglishLanguage "&%MaritimeShelfArea
is a subclass of &%MaritimeClaimArea that covers &%SubmergedLandAreas
claimed by an adjacent geopolitical area.  The &%LawOfTheSea defines
the continental shelf as extending up to 200 miles (NM) offshore and
including the resources found therein.  The defined claim area does not
correspond exactly to a geological &%ContinentalShelf, but if there is one,
the two areas will overlap spatially.")

(=>
  (and
    (instance ?ZONE MaritimeShelfArea)
    (linearExtent ?ZONE
      (MeasureFn ?WIDTH NauticalMile)))
  (lessThanOrEqualTo ?WIDTH 200.0))

(=>
  (and
    (instance ?zone MaritimeShelfArea)
    (instance ?shelf ContinentalShelf)
    (instance ?country Nation)
    (meetsSpatially ?shelf ?country)
    (claimedTerritory ?zone ?country))
  (overlapsSpatially ?zone ?shelf))

(instance MaritimeShelfAreaFn UnaryFunction)
(domain MaritimeShelfAreaFn 1 GeopoliticalArea)
(range MaritimeShelfAreaFn MaritimeShelfArea)

(documentation MaritimeShelfAreaFn EnglishLanguage
"(&%MaritimeShelfAreaFn ?POLITY) denotes the &%MaritimeShelfArea
that is claimed by the &%GeopoliticalArea ?POLITY.")

(=>
  (and
    (instance ?AREA GeopoliticalArea)
    (instance (MaritimeShelfAreaFn ?AREA) MaritimeShelfArea))
  (claimedTerritory  (MaritimeShelfAreaFn ?AREA) ?AREA))

;; (iii) exclusive economic zone
(subclass MaritimeExclusiveEconomicZone MaritimeClaimArea)
(subclass MaritimeExclusiveEconomicZone SaltWaterArea)

(documentation MaritimeExclusiveEconomicZone EnglishLanguage
"&%MaritimeExclusiveEconomicZone is the subclass of &%MaritimeClaimArea
that represents the offshore area that coastal nations can claim for
fishing and other uses of the ocean water and seabed found there.")



(=>
  (and
    (instance ?ZONE MaritimeExclusiveEconomicZone)
    (linearExtent ?ZONE
      (MeasureFn ?WIDTH NauticalMile)))
  (lessThanOrEqualTo ?WIDTH 200.0))

(instance MaritimeExclusiveEconomicZoneFn UnaryFunction)
(domain MaritimeExclusiveEconomicZoneFn 1 GeopoliticalArea)
(range MaritimeExclusiveEconomicZoneFn MaritimeExclusiveEconomicZone)

(documentation MaritimeExclusiveEconomicZoneFn EnglishLanguage
"(&%MaritimeExclusiveEconomicZoneFn ?POLITY) denotes the
&%MaritimeExclusiveEconomicZone that is claimed by the
&%GeopoliticalArea ?POLITY.")

(=>
  (and
    (instance ?AREA GeopoliticalArea)
    (instance (MaritimeExclusiveEconomicZoneFn ?AREA) MaritimeExclusiveEconomicZone))
  (claimedTerritory  (MaritimeExclusiveEconomicZoneFn ?AREA) ?AREA))

;; (iv) exclusive fishing zone
(subclass ExclusiveFishingZone MaritimeClaimArea)
(subclass ExclusiveFishingZone SaltWaterArea)

(documentation ExclusiveFishingZone EnglishLanguage "&%ExclusiveFishingZone is the
subclass of &%MaritimeClaimArea including offshore areas over which a
nation claims exclusive jurisdiction only for fishing purposes (cf.
&%MaritimeExclusiveEconomicZone).  Zone widths vary up to 200 miles
(NM).")

(=>
  (and
    (instance ?ZONE ExclusiveFishingZone)
    (linearExtent ?ZONE
      (MeasureFn ?WIDTH NauticalMile)))
  (lessThanOrEqualTo ?WIDTH 200.0))

(instance ExclusiveFishingZoneFn UnaryFunction)
(domain ExclusiveFishingZoneFn 1 GeopoliticalArea)
(range ExclusiveFishingZoneFn ExclusiveFishingZone)

(documentation ExclusiveFishingZoneFn EnglishLanguage
"(&%ExclusiveFishingZoneFn ?POLITY) denotes the
&%ExclusiveFishingZone that is claimed by the
&%GeopoliticalArea ?POLITY.")

(=>
  (and
    (instance ?AREA GeopoliticalArea)
    (instance (ExclusiveFishingZoneFn ?AREA) ExclusiveFishingZone))
  (claimedTerritory  (ExclusiveFishingZoneFn ?AREA) ?AREA))

;; (v) extended fishing zone
(subclass ExtendedFishingZone MaritimeClaimArea)
(subclass ExtendedFishingZone SaltWaterArea)

(documentation ExtendedFishingZone EnglishLanguage
"&%ExtendedFishingZone is the subclass of &%MaritimeClaimArea
that includes offshore areas over which a nation claims fishing rights,
beyond that nation's &%ExclusiveFishingZone.  Zone widths vary,
from as little as 12 miles (NM) up to a width of 200 miles (NM).")

(=>
  (and
    (instance ?ZONE ExtendedFishingZone)
    (linearExtent ?ZONE
      (MeasureFn ?WIDTH NauticalMile)))
  (lessThanOrEqualTo ?WIDTH 200.0))

(instance ExtendedFishingZoneFn UnaryFunction)
(domain ExtendedFishingZoneFn 1 GeopoliticalArea)
(range ExtendedFishingZoneFn ExtendedFishingZone)

(documentation ExtendedFishingZoneFn EnglishLanguage
"(&%ExtendedFishingZoneFn ?POLITY) denotes the
ExtendedFishingZoneFn that is claimed by the
&%GeopoliticalArea ?POLITY, beyond its &%ExclusiveFishingZone.")

(=>
  (and
    (instance ?AREA GeopoliticalArea)
    (instance (ExtendedFishingZoneFn ?AREA) ExtendedFishingZone))
  (claimedTerritory  (ExtendedFishingZoneFn ?AREA) ?AREA))

;; (vi) territorial sea
(subclass TerritorialSea MaritimeClaimArea)
(subclass TerritorialSea SaltWaterArea)

(documentation TerritorialSea EnglishLanguage "&%TerritorialSea is the class
of contiguous waters over which a &%GeopoliticalArea claims
jurisdiction in accordance with the United Nations Convention
on the &%LawOfTheSea (LOS), Part II.  A territorial sea may be
up to 12 miles (&%NauticalMiles) in breadth.  A subclass of
&%MaritimeClaimArea.")

(=>
  (and
    (instance ?ZONE MaritimeContiguousZone)
    (claimedTerritory ?ZONE ?AREA)
    (instance ?WATER TerritorialSea)
    (claimedTerritory ?WATER ?AREA))
  (between ?AREA ?WATER ?ZONE))

(=>
  (and
    (instance ?ZONE TerritorialSea)
    (linearExtent ?ZONE
      (MeasureFn ?WIDTH NauticalMile)))
  (lessThanOrEqualTo ?WIDTH 12.0))

(instance TerritorialSeaFn UnaryFunction)
(domain TerritorialSeaFn 1 GeopoliticalArea)
(range TerritorialSeaFn TerritorialSea)

(documentation TerritorialSeaFn EnglishLanguage "(&%TerritorialSeaFn ?POLITY) denotes
the &%TerritorialSea that is claimed by the &%GeopoliticalArea ?POLITY.")

(=>
  (and
    (instance ?AREA GeopoliticalArea)
    (instance (TerritorialSeaFn ?AREA) TerritorialSea))
  (claimedTerritory  (TerritorialSeaFn ?AREA) ?AREA))

;; (vi) special maritime claims
;; [create instances of &%MaritimeClaimArea as needed for individual countries]

;;-----------------------------------------------------------------------

;; I. Climate

;; WFB data interpreted using Koeppen Climate Classification system summary at
;; http://www.fao.org/WAICENT/FAOINFO/sustdev/EIdirect/climate/EIsp0002.htm

(instance climateTypeInArea BinaryPredicate)
(domain climateTypeInArea 1 GeographicArea)
(domainSubclass climateTypeInArea 2 ClimateZone)

(documentation climateTypeInArea EnglishLanguage "(&%climateTypeInArea ?REGION ?TYPE)
means that all or &%part of the &%GeographicArea ?REGION is of the &%Attribute of
&%ClimateZone ?TYPE.")

;; changed ClimateZone to subClass of Attribute
;; Jennie 1st December 2021
(=>
  (climateTypeInArea ?AREA ?CLASS)
  (exists (?REGION ?TYPE)
    (and
      (instance ?REGION GeographicArea)
      (instance ?TYPE ?CLASS)
      (attribute  ?REGION ?TYPE)
      (part ?REGION ?AREA))))

(=>
  (and
    (instance ?AREA ?TYPE)
    (subclass ?TYPE ClimateZone))
  (climateTypeInArea ?AREA ?TYPE))

(subclass ClimateZone Attribute)

(documentation ClimateZone EnglishLanguage
"&ClimateZone is a subclass of &%Attribute used by the &%BinaryPredicate
&%climateTypeInArea to classify a &GeographicArea according to its long_term
weather conditions. The subclasses of &%ClimateZone are based on the Koeppen Climate
Classification system.  In the Koeppen system, climate zones are
distinguished based on temperatures and rainfall.")

(subclass TropicalClimateZone ClimateZone)

(documentation TropicalClimateZone EnglishLanguage "&%TropicalClimateZone is a
subclass of &%ClimateZone in which the average temperature of the
coldest month is greater than 18 degrees Celsius.  This is Class
'A' in the Koeppen climate classification system.")

(=>
  (climateTypeInArea ?AREA TropicalClimateZone)
  (exists (?MO ?TEMP)
    (and
      (instance ?MO Month)
      (averageTemperatureForPeriod ?AREA ?MO
        (MeasureFn ?TEMP CelsiusDegree))
      (greaterThan ?TEMP 18.0)
      (not
        (exists (?M2 ?MT2)
          (and
            (averageTemperatureForPeriod ?AREA ?M2
              (MeasureFn ?MT2 CelsiusDegree))
            (lessThan ?MT2 ?TEMP)))))))

(instance averageRainfallForPeriod TernaryPredicate)
(documentation averageRainfallForPeriod EnglishLanguage "(averageRainfallForPeriod ?AREA ?MO ?AMOUNT)
means that ?AREA receives ?AMOUNT of rain in month ?MO in an average
year.  Note that ?AMOUNT is a linear measure indicating the depth of water that
would accumulate over ?AREA if all water were captured.")
(domain averageRainfallForPeriod 1 GeographicArea)
(domain averageRainfallForPeriod 2 Month)
(domain averageRainfallForPeriod 3 LengthMeasure)

(subclass WetTropicalClimateZone TropicalClimateZone)

(documentation WetTropicalClimateZone EnglishLanguage "&%WetTropicalClimateZone is
the subclass of &%TropicalClimateZone that is characterized by having
no dry months.  At least 60 mm of rainfall occur in the driest month.
This is subtype 'Af' in the Koeppen climate system.")

(=>
  (and
    (climateTypeInArea ?AREA TropicalClimateZone)
    (instance ?MO Month)
    (averageRainfallForPeriod ?AREA ?MO
      (MeasureFn ?AMOUNT (MilliFn Meter)))
    (greaterThanOrEqualTo ?AMOUNT 60.0))
  (climateTypeInArea ?AREA WetTropicalClimateZone))

(=>
  (climateTypeInArea ?AREA WetTropicalClimateZone)
  (forall (?MO ?AMOUNT)
    (and
      (instance ?MO Month)
      (averageRainfallForPeriod ?AREA ?MO
        (MeasureFn ?AMOUNT (MilliFn Meter)))
      (greaterThanOrEqualTo ?AMOUNT 60.0))))

(subclass MonsoonClimateZone TropicalClimateZone)

(documentation MonsoonClimateZone EnglishLanguage "&%MonsoonClimateZone is the class
of &%TropicalClimateZones in which there is a short dry season between
rains heavy enough to keep the ground wet all through the year.  This
is Koeppen climate system subtype 'Aw'.")

(=>
  (climateTypeInArea ?AREA MonsoonClimateZone)
  (exists (?INTERVAL) (rainySeasonInArea ?AREA ?INTERVAL)))

(subclass AridClimateZone ClimateZone)
(documentation AridClimateZone EnglishLanguage "&%AridClimateZone is the
class of regions in which the climate is characterized by
a distinct dry season.  Annual rate of moisture evaporation
exceeds annual rate of precipitation. This is Class B in the
Koeppen climate system.")

(subclass SteppeClimateZone AridClimateZone)
(documentation SteppeClimateZone EnglishLanguage "&%SteppeClimateZone
is the class of &%AridClimateZones where the dominant
vegetation type is grasses, with no trees.  Koeppen
system 'BS'.")

(subclass DesertClimateZone AridClimateZone)
(documentation DesertClimateZone EnglishLanguage "DesertClimateZone
is the class of &%AridClimateZones characterized by
sparse, desert vegetation.  Koeppen system 'BW'.")

(subclass SubtropicalDesertClimateZone DesertClimateZone)
(documentation SubtropicalDesertClimateZone EnglishLanguage
"&%SubtropicalDesertClimateZone is a subclass of &%DesertClimateZone
that is characterized by an average temperature greater than 18
degrees Celsius, as well as very low rainfall.  This is Koeppen
system 'BWh'.")

(=>
  (and
    (climateTypeInArea ?AREA DesertClimateZone)
    (instance ?MO Month)
    (averageTemperatureForPeriod ?AREA ?MO
      (MeasureFn ?TEMP CelsiusDegree))
    (greaterThan ?TEMP 18.0))
  (climateTypeInArea ?AREA SubtropicalDesertClimateZone))

(=>
  (climateTypeInArea ?AREA SubtropicalDesertClimateZone)
  (exists (?MO ?TEMP)
    (and
      (instance ?MO Month)
      (averageTemperatureForPeriod ?AREA ?MO
        (MeasureFn ?TEMP CelsiusDegree))
      (greaterThan ?TEMP 18.0))))

(subclass MiddleLatitudeDesertClimateZone DesertClimateZone)

(documentation MiddleLatitudeDesertClimateZone EnglishLanguage
"&%MiddleLatitudeDesertClimateZone is the subclass of
&%DesertClimateZone characterized by a cool dry climate
typical of middle latitude deserts.  This is subtype 'BWk'
in the Keoppen climate system.")

(subclass SemiaridClimateZone ClimateZone)

(subclass TemperateClimateZone ClimateZone)
;;
;; double_checked Koeppen classification on wikipedia and revised
;; documentation 15th November 2021 Jennie
;;
(documentation TemperateClimateZone EnglishLanguage "&%TemperateClimateZone is the
subclass of &%ClimateZone which the coldest month averaging between
18 degrees and -3 degrees Celsius, and at least one month averaging above
10 degrees Celsius. This is Class C in the Koeppen climate system.")

;; at least one month with average temperature above 10 C
(=>
  (climateTypeInArea ?AREA TemperateClimateZone)
  (exists (?MO ?AMOUNT)
    (and
      (instance ?MO Month)
      (averageTemperatureForPeriod ?AREA ?MO
        (MeasureFn ?AMOUNT CelsiusDegree))
      (greaterThan ?AMOUNT 10.0))))

;; average monthly temperature above - 3C
(=>
  (and
    (climateTypeInArea ?AREA TemperateClimateZone)
    (instance ?MO Month)
    (averageTemperatureForPeriod ?AREA ?MO
      (MeasureFn ?TEMP CelsiusDegree)))
  (greaterThanOrEqualTo ?TEMP -3.0))

;; coldest monthly average temperature -3 C < and > 18 C
(=>
  (climateTypeInArea ?AREA TemperateClimateZone)
  (exists (?MO ?AMOUNT)
    (and
      (instance ?MO Month)
      (averageTemperatureForPeriod ?AREA ?MO
        (MeasureFn ?AMOUNT CelsiusDegree))
      (greaterThanOrEqualTo ?AMOUNT -3.0)
      (lessThanOrEqualTo ?AMOUNT 18.0)
      (not
        (exists (?M2 ?MT2)
          (and
            (instance ?M2 Month)
            (averageTemperatureForPeriod ?AREA ?M2
              (MeasureFn ?MT2 CelsiusDegree))
            (lessThan ?MT2 ?AMOUNT)))))))

(subclass MediterraneanClimateZone TemperateClimateZone)

(documentation MediterraneanClimateZone EnglishLanguage "&%MediterraneanClimateZone
is a subclass of &%TemperateClimateZone that is characterized by
mild, cool, wet winters and warm dry summers.  Mediterranean shores
are the archetype, but the Mediterranean climate is also found elsewhere.")

(=>
  (climateTypeInArea ?AREA MediterraneanClimateZone)
  (coolSeasonInArea ?AREA WinterSeason))

(=>
  (climateTypeInArea ?AREA MediterraneanClimateZone)
  (warmSeasonInArea ?AREA SummerSeason))

(=>
  (climateTypeInArea ?AREA MediterraneanClimateZone)
  (rainySeasonInArea ?AREA WinterSeason))

(=>
  (climateTypeInArea ?AREA MediterraneanClimateZone)
  (drySeasonInArea ?AREA SummerSeason))

(subclass ContinentalClimateZone TemperateClimateZone)

(documentation ContinentalClimateZone EnglishLanguage
"&%ContinentalClimateZone is a subclass of &%TemperateClimateZone that is
characterized by cold winters and hot summers.")

(=>
  (climateTypeInArea ?AREA ContinentalClimateZone)
  (coldSeasonInArea ?AREA WinterSeason))

(=>
  (climateTypeInArea ?AREA ContinentalClimateZone)
  (hotSeasonInArea ?AREA SummerSeason))

(subclass MidlatitudeContinentalClimateZone ClimateZone)

(documentation MidlatitudeContinentalClimateZone EnglishLanguage
"&%MidlatitudeContinentalClimateZone is a subclass of
&%TemperateClimateZone that is characterized by cool winters
and hot summers.")

(=>
  (climateTypeInArea ?AREA MidlatitudeContinentalClimateZone)
  (coolSeasonInArea ?AREA WinterSeason))

(=>
  (climateTypeInArea ?AREA MidlatitudeContinentalClimateZone)
  (hotSeasonInArea ?AREA SummerSeason))

(subclass TemperateWinterDryClimateZone TemperateClimateZone)

(documentation TemperateWinterDryClimateZone EnglishLanguage
"&%TemperateWinterDryClimateZone is a subclass of &%TemperateClimateZone
characterized by having at least 10 times as much precipitation in the
wettest summer month as in the driest winter month. Koeppen climate
system type 'Cw'.")

(subclass TemperateSummerDryClimateZone TemperateClimateZone)

(documentation TemperateSummerDryClimateZone EnglishLanguage
"&%TemperateSummerDryClimateZone is the subclass of &%TemperateClimateZone
that is characterized by dry summers, and in which the wettest winter month
has at least three times the moisture of the driest summer month.  The
driest month has less than an average of 30 mm of precipitation.  Koeppen
climate system type 'Cs'.")

(subclass ColdClimateZone ClimateZone)

(documentation ColdClimateZone EnglishLanguage "&%ColdClimateZone is the subclass
of &%ClimateZone that is characterized by a warmest month with average
temperature less than 10 degrees Celsius and a coldest month with
average temperature less than -3 degrees Celsius.  This is Koeppen climate
system Type D.")

(=>
  (and
    (climateTypeInArea ?AREA ColdClimateZone)
    (forall (?MO)
      (and
        (instance ?MO Month)
        (averageTemperatureForPeriod ?AREA ?MO
          (MeasureFn ?TEMP CelsiusDegree)))))
  (greaterThan 10.0 ?TEMP))

;; coldest months
(=>
  (climateTypeInArea ?AREA ColdClimateZone)
  (exists (?MO ?AMOUNT)
    (and
      (instance ?MO Month)
      (temporalPart ?MO WinterSeason)
      (averageTemperatureForPeriod ?AREA ?MO
        (MeasureFn ?AMOUNT CelsiusDegree))
      (lessThan ?AMOUNT -3.0))))

(subclass ColdDampClimateZone ColdClimateZone)

(documentation ColdDampClimateZone EnglishLanguage "&%ColdDampClimateZone is the
subclass of &%ColdClimateZone that is characterized by having more
than 30 mm of precipitation in the driest month.  This is subtype
'Df' in the Koeppen climate system.")

(subclass DryWinterColdClimateZone ColdClimateZone)

(documentation DryWinterColdClimateZone EnglishLanguage "&%DryWinterColdClimateZone
is a subclass of &%ColdClimateZone that is characterized by having
at least ten times as much precipitation in the wettest summer month
as in the driest winter month.  This is Koeppen climate system subtype
'Dw'.")

(subclass PolarClimateZone ClimateZone)

(documentation PolarClimateZone EnglishLanguage "&%PolarClimateZone is a subclass of
&%ClimateZone characterized by having an average temperature less than
10 degrees Celsius in the warmest month.  This is Koeppen climate system
Type E.  No agriculture is supported in polar climates.")


(=>
  (climateTypeInArea ?AREA PolarClimateZone)
  (exists (?MO ?TEMP)
    (and
      (instance ?MO Month)
      (averageTemperatureForPeriod ?AREA ?MO
        (MeasureFn ?TEMP CelsiusDegree))
          (greaterThan 10.0 ?TEMP))))

(=>
  (and
    (instance ?AREA GeographicArea)
    (instance ?MO Month)
    (averageTemperatureForPeriod ?AREA ?MO
      (MeasureFn ?TEMP CelsiusDegree))
    (greaterThan 10.0 ?TEMP))
  (climateTypeInArea ?AREA PolarClimateZone))

(subclass TundraClimateZone PolarClimateZone)

(documentation TundraClimateZone EnglishLanguage "TundraClimateZone is a subclass of
&%PolarClimateZone characterized by having an average temperature
above zero degrees Celsius (but below 10 degrees) in the warmest month.
Koeppen subtype 'ET'.")

(subclass PolarTypeFClimateZone PolarClimateZone)

(documentation PolarTypeFClimateZone EnglishLanguage "&%PolarTypeFClimateZone is a
subclass of &%PolarClimateZone characterized by having no month in
which the temperature rises to 10 degrees Celsius or above.")

(=>
  (and
    (climateTypeInArea ?AREA PolarTypeFClimateZone)
    (instance ?TIME TimePosition)
    (holdsDuring ?TIME (airTemperature ?AREA
      (MeasureFn ?TEMP CelsiusDegree))))
  (holdsDuring ?TIME (greaterThan 10.0 ?TEMP)))

(instance rainySeasonInArea BinaryPredicate)
(domain rainySeasonInArea 1 GeographicArea)
(domainSubclass rainySeasonInArea 2 TimeInterval)

(documentation rainySeasonInArea EnglishLanguage "(&%rainySeasonInArea ?AREA ?INTERVAL)
means that in the &%GeographicArea ?AREA, the rainy or wet season occurs
during the &%TimeInterval ?INTERVAL.  For example, (&%rainySeasonInArea
&%Angola (&%RecurringTimeIntervalFn &%November &%April)).")

(instance warmSeasonInArea BinaryPredicate)
(domain warmSeasonInArea 1 GeographicArea)
(domainSubclass warmSeasonInArea 2 TimeInterval)

(documentation warmSeasonInArea EnglishLanguage "(&%warmSeasonInArea ?AREA ?INTERVAL)
means that in the &%GeographicArea ?AREA, the warm season occurs
during the &%TimeInterval ?INTERVAL.")

(instance drySeasonInArea BinaryPredicate)
(domain drySeasonInArea 1 GeographicArea)
(domainSubclass drySeasonInArea 2 TimeInterval)

(documentation drySeasonInArea EnglishLanguage "(&%drySeasonInArea ?AREA ?INTERVAL)
means that in the &%GeographicArea ?AREA, the dry season occurs
during the &%TimeInterval ?INTERVAL.  For example, (&%drySeasonInArea
&%Angola (&%RecurringTimeIntervalFn &%May &%October)).")

(instance coolSeasonInArea BinaryPredicate)

(domain coolSeasonInArea 1 GeographicArea)
(domainSubclass coolSeasonInArea 2 TimeInterval)

(documentation coolSeasonInArea EnglishLanguage "(&%coolSeasonInArea ?AREA ?INTERVAL)
means that in the &%GeographicArea ?AREA, the cool season occurs
during the &%TimeInterval ?INTERVAL.  For example, (&%coolSeasonInArea
&%Angola (&%RecurringTimeIntervalFn &%May &%October")

(instance hotSeasonInArea BinaryPredicate)
(domain hotSeasonInArea 1 GeographicArea)
(domainSubclass hotSeasonInArea 2 TimeInterval)

(documentation hotSeasonInArea EnglishLanguage "(&%hotSeasonInArea ?AREA ?INTERVAL)
means that in the &%GeographicArea ?AREA, the hot season occurs
during the &%TimeInterval ?INTERVAL.  For example, (&%hotSeasonInArea
&%Angola (&%RecurringTimeIntervalFn &%November &%April)).")

(instance coldSeasonInArea BinaryPredicate)
(domain coldSeasonInArea 1 GeographicArea)
(domainSubclass coldSeasonInArea 2 TimeInterval)

(documentation coldSeasonInArea EnglishLanguage "(&%coldSeasonInArea ?AREA ?INTERVAL)
means that in the &%GeographicArea ?AREA, the cold season occurs
during the &%TimeInterval ?INTERVAL.")

(equal SpringSeason (RecurrentTimeIntervalFn March May))
(equal SummerSeason (RecurrentTimeIntervalFn June August))
(equal FallSeason (RecurrentTimeIntervalFn September November))
(equal WinterSeason (RecurrentTimeIntervalFn December February))

;;-----------------------------------------------------------------------

;; J. Terrain

(instance slopeGradient BinaryPredicate)
(domain slopeGradient 1 LandArea)
(domain slopeGradient 2 NonnegativeRealNumber)

(documentation slopeGradient EnglishLanguage "(&%slopeGradient ?AREA ?SLOPE) means that
in the &%LandArea ?AREA there is an incline of ?slope, where ?SLOPE is
the percent of vertical rise over horizontal distance. &%slopeGradient
gives an approximate value for nonzero slope over ?AREA, without regard
to orientation. For example, (&%slopeGradient ?AREA 0.10) means that
there is a 10% incline across the area, without specifying the
orientation in which the land rises or falls.  Also see
&%slopeGradientTowardsOrientation.")

(instance slopeGradientTowardsOrientation TernaryPredicate)
(domain slopeGradientTowardsOrientation 1 LandArea)
(domain slopeGradientTowardsOrientation 2 DirectionalAttribute)
(domain slopeGradientTowardsOrientation 3 RealNumber)

(documentation slopeGradientTowardsOrientation EnglishLanguage
"(&%slopeGradientTowardsOrientation ?AREA ?DIRECTION ?SLOPE) means that
in the &%LandArea ?AREA, there is an incline of ?SLOPE towards the
&%DirectionalAttribute ?DIRECTION.  ?SLOPE is a &%RealNumber
representing the percent of vertical rise over horizontal distance.")

(instance Uphill PositionalAttribute)

(documentation Uphill EnglishLanguage "&%Uphill is a &%PositionalAttribute that
describes the relation between two things, one of which is located
up a slope from the other.")

(instance Downhill PositionalAttribute)
(contraryAttribute Uphill Downhill)

(documentation Downhill EnglishLanguage "&%Downhill is a &%PositionalAttribute that
describes the relation between two things, one of which is located
down a slope from the other.")

(instance terrainInArea BinaryPredicate)
(instance terrainInArea AsymmetricRelation)
(domain terrainInArea 1 GeographicArea)
(domain terrainInArea 2 TerrainAttribute)

(documentation terrainInArea EnglishLanguage "(&%terrainInArea ?AREA ?ATTRIBUTE)
means that the &%GeographicArea ?AREA is or includes a region with
the features of &%TerrainArea ?ATTRIBUTE.")

(=>
  (terrainInArea ?AREA ?ATTRIBUTE)
  (exists (?REGION)
    (and
      (instance ?REGION GeographicArea)
      (attribute ?REGION ?ATTRIBUTE)
      (partlyLocated ?REGION ?AREA))))

(=>
  (and
    (instance ?AREA GeographicArea)
    (attribute ?REGION ?ATTRIBUTE)
    (instance ?ATTRIBUTE TerrainAttribute)
    (partlyLocated ?REGION ?AREA))
  (terrainInArea ?AREA ?ATTRIBUTE))

;; Terrain attributes:

(subclass TerrainAttribute InternalAttribute)

(documentation TerrainAttribute EnglishLanguage "&%TerrainAttribute is a class of
&%Attributes that describe terrain.")

(=>
  (and
    (instance ?ATT TerrainAttribute)
    (attribute ?OBJECT ?ATT))
  (instance ?OBJECT LandArea))

(instance FlatTerrain TerrainAttribute)

(documentation FlatTerrain EnglishLanguage "&%FlatTerrain is a &%TerrainAttribute
describing regions within which there is very little variation in
&%altitude.")

(=>
  (and
    (attribute ?AREA FlatTerrain)
    (part ?ZONE ?AREA)
    (slopeGradient ?ZONE ?SLOPE))
  (greaterThan 0.005 ?SLOPE))

(instance LowTerrain TerrainAttribute)

(documentation LowTerrain EnglishLanguage "&%LowTerrain is terrain in which the slope
is less than 3%.")

(=>
  (and
    (attribute ?AREA LowTerrain)
    (part ?ZONE ?AREA)
    (slopeGradient ?ZONE ?SLOPE))
  (greaterThan 0.03 ?SLOPE))

(instance SteepTerrain TerrainAttribute)

(documentation SteepTerrain EnglishLanguage "A &%TerrainAttribute of a region in
which there is a wide variation in &%elevation and &%slopeGradients
of more than 10%.")

(=>
  (attribute ?AREA SteepTerrain)
  (exists (?ZONE ?SLOPE)
    (and
      (part ?ZONE ?AREA)
      (slopeGradient ?ZONE ?SLOPE)
      (greaterThan ?SLOPE 0.1))))

(instance MountainousTerrain TerrainAttribute)
(subAttribute MountainousTerrain SteepTerrain)

(documentation MountainousTerrain EnglishLanguage "An area of &%MountainousTerrain
is an area of rugged terrain in which there are many mountains.")

(=>
  (attribute ?AREA MountainousTerrain)
  (instance ?AREA LandArea))

(=>
  (attribute ?AREA MountainousTerrain)
  (instance ?AREA UplandArea))

(=>
  (attribute ?AREA MountainousTerrain)
  (exists (?MTN)
    (and
      (instance ?MTN Mountain)
  (part ?MTN ?AREA))))

(=>
  (attribute ?AREA MountainousTerrain)
  (exists (?MTN)
    (and
      (instance ?MTN Mountain)
      (located ?MTN ?AREA))))

(=>
  (instance ?AREA MountainRange)
  (attribute ?AREA MountainousTerrain))

(instance FertileTerrain TerrainAttribute)
(documentation FertileTerrain EnglishLanguage "&%FertileTerrain describes an area
that has the type of soil and climate conditions needed to produce
good quality crops.")

(=>
  (and
    (instance ?area GeographicArea)
    (groundSurfaceType ?area Rock))
  (not (attribute ?area FertileTerrain)))


(=>
  (attribute ?AREA FertileTerrain)
  (capability Agriculture eventLocated ?AREA)) ;; located not a CaseRole

(=>
  (and
    (instance ?SOIL Soil)
    (attribute ?SOIL Yellow)
    (instance ?AREA LandArea)
    (component ?SOIL ?AREA))
  (not (attribute ?AREA FertileTerrain)))

(=>
  (and
    (instance ?SOIL Soil)
    (attribute ?SOIL Red)
    (instance ?AREA LandArea)
    (component ?SOIL ?AREA))
  (attribute ?AREA FertileTerrain))

(instance GeologicallyStable TerrainAttribute)

(documentation GeologicallyStable EnglishLanguage "&%GeologicallyStable is a
&%TerrainAttribute of a &%GeographicArea in which the geological
substructure is stable, i.e., there are no active &%Volcanoes,
no major fault lines, no blasting or other destabilizing activity.")

(=>
  (and
    (instance ?AREA GeographicArea)
    (instance ?CONE Volcano)
    (attribute ?CONE VolcanicallyActive))
  (not (attribute ?AREA GeologicallyStable)))

(=>
  (and
    (instance ?AREA GeographicArea)
    (instance ?BLOW VolcanicEruption)
    (eventLocated ?BLOW ?AREA))
  (not (attribute ?AREA GeologicallyStable)))

(=>
  (and
    (instance ?AREA GeographicArea)
    (instance ?SHAKING EarthTremor)
    (eventLocated ?SHAKING ?AREA))
  (not (attribute ?AREA GeologicallyStable)))

(=>
  (and
    (instance ?AREA GeographicArea)
    (instance ?FAULT GeologicalFault)
    (located ?FAULT ?AREA))
  (not (attribute ?AREA GeologicallyStable)))

;;-----------------------------------------------------------------------

;; K. Elevation extremes

(instance elevation BinaryPredicate)
(domain elevation 1 Object)
(domain elevation 2 LengthMeasure)

(documentation elevation EnglishLanguage "(&%elevation ?OBJECT ?HEIGHT) means that the
physical &%Object ?OBJECT is located on the surface of &%PlanetEarth
at the vertical distance ?HEIGHT above (or below, for a negative
quantity) &%SeaLevel.  ?OBJECT may be a &%superficialPart of Earth's
surface, such as a &%GeographicArea.  Elevation is measured from
&%SeaLevel to the vertical top of the object.")

(=>
  (elevation ?OBJECT ?HEIGHT)
  (exists (?PLACE)
    (and
      (instance ?PLACE GeographicArea)
      (located ?OBJECT ?PLACE))))
(=>
  (and
    (elevation ?OBJECT ?HEIGHT)
    (located ?OBJECT ?PLACE)
    (instance ?PLACE GeographicArea))
  (superficialPart ?PLACE PlanetEarth))

(=>
  (elevation ?OBJECT ?HEIGHT)
  (exists (?DATUM)
    (and
      (properPart ?DATUM SeaLevel)
      (orientation ?OBJECT ?DATUM Vertical)
      (distance ?OBJECT ?DATUM ?HEIGHT))))

(=>
  (and
    (elevation ?OBJECT (MeasureFn ?NUM ?UNIT))
    (instance ?UNIT UnitOfLength)
    (instance ?OBJECT LandForm))
  (exists (?HIGHPOINT)
    (and
      (top ?HIGHPOINT ?OBJECT)
      (distance ?HIGHPOINT SeaLevel (MeasureFn ?NUM ?UNIT)))))

(instance ElevationLowPointFn UnaryFunction)
(domain ElevationLowPointFn 1 GeographicArea)
(range ElevationLowPointFn GeographicArea)

(documentation ElevationLowPointFn EnglishLanguage "(&%ElevationLowPointFn ?AREA)
denotes the area within the &%GeographicArea ?AREA that has the
lowest &%elevation.")

(=>
  (and
    (instance ?AREA GeographicArea)
    (geographicSubregion (ElevationLowPointFn ?AREA) ?AREA)
    (instance ?U UnitOfLength)
    (elevation (ElevationLowPointFn ?AREA)
      (MeasureFn ?ELEV1 ?U)))
  (not
    (exists (?OTHER ?ELEV2)
      (and
        (geographicSubregion ?OTHER ?AREA)
        (not (equal ?OTHER (ElevationLowPointFn ?AREA)))
        (elevation ?OTHER
          (MeasureFn ?ELEV2 ?U))
        (lessThan ?ELEV2 ?ELEV1)))))

(instance ElevationHighPointFn UnaryFunction)
(domain ElevationHighPointFn 1 GeographicArea)
(range ElevationHighPointFn GeographicArea)

(documentation ElevationHighPointFn EnglishLanguage "(&%ElevationHighPointFn ?AREA)
denotes the area within the &%GeographicArea ?AREA that has the
highest &%elevation.")

(=>
  (and
    (instance ?AREA GeographicArea)
    (geographicSubregion (ElevationHighPointFn ?AREA) ?AREA)
    (elevation (ElevationHighPointFn ?AREA) (MeasureFn ?ELEV1 ?UNIT)))
  (not
    (exists (?OTHER ?ELEV2)
      (and
        (geographicSubregion ?OTHER ?AREA)
        (not (equal ?OTHER (ElevationHighPointFn ?AREA)))
        (elevation ?OTHER (MeasureFn ?ELEV2 ?UNIT))
        (greaterThan ?ELEV2 ?ELEV1)))))

;;-----------------------------------------------------------------------

;; L. Natural resources

(instance naturalResourceTypeInArea BinaryPredicate)
(domain naturalResourceTypeInArea 1 GeographicArea)
(domainSubclass naturalResourceTypeInArea 2 Object)

(documentation naturalResourceTypeInArea EnglishLanguage
"(&%naturalResourceTypeInArea ?REGION ?TYPE) means that the
&%GeographicArea ?REGION has resources of the kind ?TYPE.")

(=>
  (naturalResourceTypeInArea ?AREA ?TYPE)
  (exists (?ITEM)
    (and
      (instance ?ITEM ?TYPE)
      (located ?ITEM ?AREA))))

(subclass Barite CompoundSubstance)
(subclass Barite Mineral)
(names "barite" Barite)

(documentation Barite EnglishLanguage
"&%Barite is Barium Sulfate occurring as a mineral.")

(subclass BrownCoal Coal)
(names "brown coal" BrownCoal)
(names "lignite" BrownCoal)

(documentation BrownCoal EnglishLanguage "&%BrownCoal is a soft &%Coal harder than
peat and softer than bituminous coal.  Also called lignite, it is
brownish in color.")

(documentation Chromite EnglishLanguage "A mineral that consists of an oxide of
iron and chromium.")
(subclass Chromite CompoundSubstance)
(subclass Chromite Mineral)
(names "chromite" Chromite)

(subclass Coal Mineral)
(subclass Coal FossilFuel)
(subclass Coal Rock)
(names "coal" Coal)

(documentation Coal EnglishLanguage "&%Coal is a black or brownish black solid combustible
substance formed by the partial decomposition of vegetable matter without
free access of air and under the influence of moisture and often increased
pressure and temperature.  &%Coal is a sedimentary rock containing a high
proportion of carbon.")

(subclass HydropowerWaterArea WaterArea)

(documentation HydropowerWaterArea EnglishLanguage "&%HydropowerWaterArea
is the class of &%WaterAreas with waterflow strength adequate
for the production of hydropower.")

(subclass NaturalGas CompoundSubstance)
(names "natural gas" NaturalGas)

(documentation NaturalGas EnglishLanguage "&%NaturalGas is a combustible mixture
of methane and higher hydrocarbons.")

(=>
  (instance ?GAS NaturalGas)
  (attribute ?GAS Gas))

(subclass Phosphate CompoundSubstance)
(names "phosphate" Phosphate)

(documentation Phosphate EnglishLanguage "&%Phospate is a salt or ester of a
phosphoric acid.")

(subclass Potash CompoundSubstance)
(names "potash" Potash)

(documentation Potash EnglishLanguage "&%Potash is a Potassium carbonate from
wood ashes or a potassium compound.")

(subclass Talc Mineral)
(names "talc" Talc)
(documentation Talc EnglishLanguage "&%Talc is a very soft &%Mineral that is
a basic silicate of magnesium.")

;;-----------------------------------------------------------------------

;; M. Land use

(subclass ArableLand LandArea)

(documentation ArableLand EnglishLanguage "&%ArableLand is the subclass of
&%LandArea that represents land in cultivation with crops that
are replanted after each harvest, e.g., wheat and rice.")

(instance arableLandArea BinaryPredicate)
(domain arableLandArea 1 GeographicArea)
(domain arableLandArea 2 ConstantQuantity)

(documentation arableLandArea EnglishLanguage "(&%arableLandArea ?REGION ?AMOUNT)
means that the &%GeographicArea ?REGION has ?AMOUNT of land under
cultivation with crops that are replanted after each harvest.
Cf. &%permanentCropLandArea.")

(=>
  (and
    (arableLandArea ?REGION
      (MeasureFn ?FRACTION ?UNIT))
    (greaterThanOrEqualTo ?FRACTION 0.0)
    (totalArea ?REGION (MeasureFn ?TOTAL ?UNIT))
    (instance ?TOTAL AreaMeasure)
    (equal ?AMOUNT
      (MultiplicationFn ?FRACTION ?TOTAL)))
  (arableLandArea ?REGION
    (MeasureFn ?AMOUNT ?UNIT)))

(=>
  (and
    (arableLandArea ?REGION
      (MeasureFn ?FRACTION ?UNIT))
    (greaterThanOrEqualTo ?FRACTION 0.0)
    (totalArea ?REGION
      (MeasureFn ?TOTAL ?UNIT))
    (instance ?UNIT UnitOfArea))
  (exists (?ARABLE)
    (and
      (instance ?ARABLE ArableLand)
      (geographicSubregion ?ARABLE ?REGION)
      (measure ?ARABLE
        (MeasureFn
          (MultiplicationFn ?FRACTION ?TOTAL) ?UNIT)))))

(subclass PermanentCropLand LandArea)

(documentation PermanentCropLand EnglishLanguage "&%PermanentCropLand is the subclass
of &%LandArea that represents land cultivated for crops that are produced
without replanting after every harvest, such as fruit trees, coffee, rubber,
nuts, and vineyards.  Does not include timberland.  Cf. &%arableLandArea.")

(instance permanentCropLandArea BinaryPredicate)
(domain permanentCropLandArea 1 GeographicArea)
(domain permanentCropLandArea 2 ConstantQuantity)

(documentation permanentCropLandArea EnglishLanguage
"(&%permanentCropLandArea ?REGION ?AMOUNT) means that the &%GeographicArea
?REGION has ?AMOUNT of land under cultivation with crops that are not
replanted after each harvest.  This includes orchards, vineyards, coffee
and rubber plantations. Cf. &%permanentCropLandArea.")

(=>
  (and
    (permanentCropLandArea ?REGION
      (MeasureFn ?FRACTION ?UNIT))
    (greaterThanOrEqualTo ?FRACTION 0.0)
    (totalArea ?REGION
      (MeasureFn ?TOTAL ?UNIT))
    (instance ?UNIT UnitOfArea)
    (equal ?AMOUNT
      (MultiplicationFn ?FRACTION ?TOTAL)))
  (permanentCropLandArea ?REGION
    (MeasureFn ?AMOUNT ?UNIT)))

(=>
  (and
    (permanentCropLandArea ?REGION
      (MeasureFn ?FRACTION ?UNIT))
    (greaterThanOrEqualTo ?FRACTION 0.0)
    (totalArea ?REGION
      (MeasureFn ?TOTAL ?UNIT))
    (instance ?UNIT UnitOfArea))
  (exists (?PERMCROP)
    (and
      (instance ?PERMCROP PermanentCropLand)
      (geographicSubregion ?PERMCROP ?REGION)
      (measure ?PERMCROP
        (MeasureFn
          (MultiplicationFn ?FRACTION ?TOTAL) ?UNIT)))))

(instance otherLandUseArea BinaryPredicate)
(domain otherLandUseArea 1 GeographicArea)
(domain otherLandUseArea 2 ConstantQuantity)

(documentation otherLandUseArea EnglishLanguage
"This predicate is used to represent information from the CIA
World Fact Book. (&%otherLandUseArea ?REGION ?AMOUNT) means that
in the &%GeographicArea ?REGION, the ?AMOUNT of land has some
use other than planting seasonal or permanent crops.  See also
&%arableLandArea and &%permanentLandArea.")

(=>
  (and
    (otherLandUseArea ?REGION
      (MeasureFn ?FRACTION ?UNIT))
    (greaterThanOrEqualTo ?FRACTION 0.0)
    (totalArea ?REGION
      (MeasureFn ?TOTAL ?UNIT))
    (instance ?UNIT UnitOfArea)
    (equal ?AMOUNT
      (MultiplicationFn ?FRACTION ?TOTAL)))
  (otherLandUseArea ?REGION
    (MeasureFn ?AMOUNT ?UNIT)))

(=>
  (and
    (otherLandUseArea ?REGION
      (MeasureFn ?FRACTION ?UNIT))
    (greaterThanOrEqualTo ?FRACTION 0.0)
    (totalArea ?REGION
      (MeasureFn ?TOTAL ?UNIT))
    (instance ?UNIT UnitOfArea))
  (exists (?OTHER)
    (and
      (instance ?OTHER LandArea)
      (not
        (instance ?OTHER ArableLand))
      (not
        (instance ?OTHER PermanentCropLand))
      (geographicSubregion ?OTHER ?REGION)
      (measure ?OTHER
        (MeasureFn
          (MultiplicationFn ?FRACTION ?TOTAL) ?UNIT)))))

;;-----------------------------------------------------------------------

;; N. Irrigated land

(subclass IrrigatedLand LandArea)

(documentation IrrigatedLand EnglishLanguage "&%IrrigatedLand is the subclass of
&%LandArea representing land whose water supply is artificially
supplied or supplemented.")

(instance irrigatedLandArea BinaryPredicate)
(domain irrigatedLandArea 1 GeographicArea)
(domain irrigatedLandArea 2 ConstantQuantity)

(documentation irrigatedLandArea EnglishLanguage "(&%irrigatedLandArea ?REGION ?AMOUNT)
means that the &%GeographicArea ?REGION has the ?AMOUNT of irrigated land.")

(=>
  (and
    (irrigatedLandArea ?REGION
      (MeasureFn ?AMOUNT ?UNIT))
    (instance ?UNIT UnitOfArea)
    (totalArea ?REGION
      (MeasureFn ?TOTAL ?UNIT))
    (equal ?FRACTION
      (DivisionFn ?AMOUNT ?TOTAL)))
  (irrigatedLandArea ?REGION
    (MeasureFn ?FRACTION ?UNIT)))

(=>
  (and
    (irrigatedLandArea ?REGION
      (MeasureFn ?AMOUNT ?UNIT))
    (totalArea ?REGION
      (MeasureFn ?TOTAL ?UNIT))
    (instance ?UNIT UnitOfArea)
    (equal ?FRACTION
      (DivisionFn ?AMOUNT ?TOTAL)))
  (irrigatedLandArea ?REGION
    (MeasureFn ?FRACTION ?UNIT)))

(=>
  (and
    (irrigatedLandArea ?REGION
      (MeasureFn ?AMOUNT ?UNIT))
    (instance ?UNIT UnitOfArea))
  (exists (?IRRLAND)
    (and
      (instance ?IRRLAND IrrigatedLand)
      (geographicSubregion ?IRRLAND ?REGION)
      (measure ?IRRLAND
        (MeasureFn ?AMOUNT ?UNIT)))))

;;-----------------------------------------------------------------------

;; O. Natural hazards

(instance naturalHazardTypeInArea BinaryPredicate)
(domain naturalHazardTypeInArea 1 GeographicArea)
(domainSubclass naturalHazardTypeInArea 2 Physical)

(documentation naturalHazardTypeInArea EnglishLanguage
"(&%naturalHazardTypeInArea ?AREA ?TYPE) means that in the
&%GeographicArea ?AREA, &%Processes of ?TYPE occur with some
frequency and may cause damage or danger to human lives and property.")

(=>
  (and
    (naturalHazardTypeInArea ?AREA ?TYPE)
    (geographicSubregion ?AREA ?COUNTRY)
    (instance ?COUNTRY Nation))
  (naturalHazardTypeInArea ?COUNTRY ?TYPE))

(=>
  (and
    (naturalHazardTypeInArea ?AREA ?TYPE)
    (subclass ?TYPE Object))
  (exists (?ITEM)
    (and
      (instance ?ITEM ?TYPE)
      (located ?ITEM ?AREA))))

(=>
  (and
    (naturalHazardTypeInArea ?AREA ?TYPE)
    (subclass ?TYPE Process))
  (exists (?ITEM)
    (and
      (instance ?ITEM ?TYPE)
      (located ?ITEM ?AREA)
      (overlapsTemporally
        (WhenFn ?ITEM)
        (WhenFn ?AREA)))))

(subclass AvalancheProcess Impelling)

(documentation AvalancheProcess EnglishLanguage "&%AvalancheProcess is a subclass of
&%Motion that represents events in which a loosened mass of snow, ice,
rock, or earth rapidly descends a steep slope, with a destructive force.")

(subclass Landslide Impelling)

(documentation Landslide EnglishLanguage "&%Landslide is a subclass
of &%Motion that represents events in which a loosened mass of mud,
dirt, or rock slides down a slope, by the force of gravity.")

(subclass Earthquake GeologicalProcess)

(documentation Earthquake EnglishLanguage "&%Earthquake is the class of events in
which the earth shakes while its layers readjust due to tensional
stresses in the surface of the earth.  A single earthquake may consist
of one or more &%EarthTremors.")

(=>
  (instance ?QUAKE Earthquake)
  (exists (?TREMOR)
    (and
      (instance ?TREMOR EarthTremor)
      (subProcess ?TREMOR ?QUAKE))))

(subclass EarthTremor GeologicalProcess)
(subclass EarthTremor Tremor)
(documentation EarthTremor EnglishLanguage "An &%EarthTremor is an individual seismic
event in which the earth shakes due to release of seismic pressures.")

(subclass Aftershock EarthTremor)

(documentation Aftershock EnglishLanguage "&%Aftershock is the subclass of &%EarthTremors
that occur after the main tremor(s) of an &%Earthquake.")

(=>
  (and
    (instance ?SHOCK Aftershock)
    (eventLocated ?SHOCK ?PLACE))
  (exists (?TREMOR)
    (and
      (instance ?TREMOR EarthTremor)
      (eventLocated ?TREMOR ?PLACE)
      (before
        (WhenFn ?TREMOR)
        (WhenFn ?SHOCK)))))

(names "Richter magnitude" RichterMagnitude)
(names "Richter scale" RichterMagnitude)

(instance RichterMagnitude CompositeUnitOfMeasure)
(documentation RichterMagnitude EnglishLanguage "A &%RichterMagnitude is a measure
of the severity of an &%EarthTremor.  For example,
(&%MeasureFn 6.5 RichterMagnitude) denotes the value of 6.5 on the
Richter scale.")

(=>
  (measure ?QUAKE (MeasureFn ?VALUE RichterMagnitude))
  (instance ?VALUE PositiveRealNumber))

(=>
  (measure ?QUAKE (MeasureFn ?VALUE RichterMagnitude))
  (or
    (instance ?QUAKE EarthTremor)
    (instance ?QUAKE Earthquake)))

(=>
  (and
    (measure ?QUAKE
      (MeasureFn ?VALUE RichterMagnitude))
    (instance ?QUAKE Earthquake))
  (exists (?TREMOR)
    (and
      (instance ?TREMOR EarthTremor)
      (subProcess ?TREMOR ?QUAKE)
      (measure ?TREMOR
        (MeasureFn ?VALUE RichterMagnitude)))))

(subclass GeologicalFault GeographicArea)

(documentation GeologicalFault EnglishLanguage "&%GeologicalFault is the subclass of
&%GeographicAreas in which there is a fracture in the Earth's crust
and differential movement can occur on the two sides of the fault.
Such movement results in &%EarthTremors and is the cause of
&%Earthquakes.")

(=>
  (instance ?QUAKE EarthTremor)
  (exists (?FAULT)
    (and
      (instance ?FAULT GeologicalFault)
      (origin ?QUAKE ?FAULT))))

(=>
  (instance ?QUAKE Earthquake)
  (exists (?FAULT)
    (and
      (instance ?FAULT GeologicalFault)
      (origin ?QUAKE ?FAULT))))

(subclass DipSlipFault GeologicalFault)
(subclass NormalFault DipSlipFault)
(subclass ThrustFault DipSlipFault)
(subclass StrikeSlipFault GeologicalFault)

;; KJN: Moving this to MILO as it is a very general idea
;;(subclass Fire Combustion)

;;(documentation Fire EnglishLanguage "&%Fire is the subclass of &%Combustion events in
;;which flames are present.  Fires are slower combustion processes than
;;explosions, though some fires may include explosive episodes.")

(subclass ForestFire Combustion)

(=>
  (instance ?FIRE ForestFire)
  (exists (?FOREST)
    (and
      (instance ?FOREST Forest)
      (eventLocated ?FIRE ?FOREST)
      (patient ?FIRE ?FOREST))))

(subclass GrassFire Combustion)

(=>
  (instance ?FIRE GrassFire)
  (exists (?GRASS)
    (and
      (instance ?GRASS Grass)
      (eventLocated ?FIRE ?GRASS)
      (patient ?FIRE ?GRASS))))

;; KJN: Removing this as Flooding, while it could be a , is not necessarily so.
;; e.g. an area can be deliberately flooded by an agent for some purpose
;;(subclass Flooding )

(instance Permafrost CompoundSubstance)

(subclass VolcanicEruption GeologicalProcess)

(documentation VolcanicEruption EnglishLanguage "&%VolcanicEruption is the subclass of
&%GeologicalProcesses in which &%Volcanoes erupt.")

(=>
  (instance ?ERUPTING VolcanicEruption)
  (exists (?VOLCANO)
    (and
      (instance ?VOLCANO Volcano)
      (eventLocated ?ERUPTING ?VOLCANO))))

(=>
  (instance ?ERUPTING VolcanicEruption)
  (exists (?HEATING)
    (and
      (instance ?HEATING Heating)
      (subProcess ?HEATING ?ERUPTING))))

;;-----------------------------------------------------------------------

;; P. Environment - current issues

(instance environmentalProblemTypeInArea BinaryPredicate)
(domain environmentalProblemTypeInArea 1 GeographicArea)
(domainSubclass environmentalProblemTypeInArea 2 Physical)

(documentation environmentalProblemTypeInArea EnglishLanguage
"(&%environmentalProblemTypeInArea ?AREA ?PROBLEM) means that
objects or processes of type ?PROBLEM is an environmental
problem in the &%GeographicArea ?AREA.")

(=>
  (environmentalProblemTypeInArea ?AREA ?PROBLEM)
  (exists (?EVENT)
    (and
      (instance ?EVENT ?PROBLEM)
      (eventLocated ?EVENT ?AREA))))

;; Environmental phenomena

(subclass Acidification Combining)

(documentation Acidification EnglishLanguage "&%Acidification is the process of
lowering soil and water pH due to acid rain or other types of
acid deposition.  Potential harmful effects include killing
freshwater fish and plants.")

(subclass AirPollution Pollution)

(documentation AirPollution EnglishLanguage "&%AirPollution is the subclass of
&%Pollution processes in which air is contaminated.")

(documentation AcidRainfall EnglishLanguage "&%AcidRainfall is the subclass of &%Raining
in which the precipitate contains harmful amounts of &%SulphurDioxide, &%NitricOxide or
&%NitrogenDioxide. The standard for acid rain is below 5pH.")

(documentation AcidRainfall ChineseLanguage "&%AcidRainfall 是 &%Raining 的子类别， 这是当落下
的雨含有足够致害分量的 &%SulphurDioxide、 &%nitricOxide 或 &%NitrogenDioxide。酸雨的标准值是低于
5pH。")

(termFormat ChineseLanguage AcidRainfall "酸雨")
(subclass AcidRainfall Raining)

(=>
  (instance ?AR AcidRainfall)
  (exists (?RAIN ?SO2 ?NO ?NO2)
    (and
      (instance ?RAIN Water)
      (patient ?AR ?RAIN)
      (instance ?SO2 SulphurDioxide)
      (instance ?NO NitricOxide)
      (instance ?NO2 NitrogenDioxide)
      (or
        (part ?SO2 ?RAIN)
        (part ?NO ?RAIN)
        (part ?NO2 ?RAIN)))))

(=>
  (instance ?AR AcidRainfall)
  (exists (?RAIN ?PH)
    (and
      (instance ?RAIN Water)
      (patient ?AR ?RAIN)
      (measure ?RAIN
        (MeasureFn ?PH PHValue))
      (lessThan ?PH 5.0))))

(=>
  (patient ?RAIN AcidRainfall)
  (modalAttribute
    (exists (?D ?O ?X ?L)
      (and
        (instance ?D Damaging)
        (causes ?RAIN ?D)
        (instrument ?D ?RAIN)
        (instance ?O Organism)
        (instance ?X StationaryArtifact)
        (instance ?L Outdoors)
        (located ?X ?L)
        (located ?O ?L)
        (or
          (patient ?D ?O)
          (patient ?D ?X)))) Possibility))

(causesSubclass AirPollution AcidRainfall)
(causesSubclass AcidRainfall Acidification)

(names "pH" PHValue)

(instance PHValue NonCompositeUnitOfMeasure)
(documentation PHValue EnglishLanguage "&%PHValue ('pH') is a &%UnitOfMeasure
used to measure the acidity or alkalinity of a solution.  The pH
scale ranges from 0 to 14.  The pH of a  neutral solution is 7.
Acid solutions have a pH value less than seven.  Alkaline solutions
have a value greater than seven.")

(<=>
    (measure ?SOLUTION
        (MeasureFn ?PH PHValue))
    (potentialOfHydrogen ?SOLUTION ?PH))

(=>
  (measure ?SOLUTION (MeasureFn ?PH PHValue))
  (lessThanOrEqualTo ?PH 14.0))

(=>
  (measure ?SOLUTION (MeasureFn ?PH PHValue))
  (greaterThanOrEqualTo ?PH 0.0))

(=>
  (and
    (instance ?RAINFALL Process)
    (patient ?RAINFALL ?RAIN)
    (instance ?RAIN Water)
    (measure ?RAIN (MeasureFn ?PH PHValue)))
  (lessThan ?PH 5.6))

(subclass CarbonDioxideEmission Separating)

(subclass Afforestation Planting)

(documentation Afforestation EnglishLanguage "&%Afforestation is the process of
planting trees and plants on spaces that are either empty or in
agricultural use.")

(subclass Planting Putting)

(documentation Planting EnglishLanguage "&%Planting is the class of processes in
which botanical &%Plants are planted or transplanted, whether as
seeds, seedlings, or mature plants.")

(subclass Reforestation Planting)

(documentation Reforestation EnglishLanguage "&%Reforestation is the process of
replanting trees on land where they were cut or burned.")

(=>
  (instance ?RESTORE Reforestation)
  (exists (?TREE)
    (and
      (instance ?TREE BotanicalTree)
      (attribute ?TREE NonFullyFormed)
      (patient ?RESTORE ?TREE))))

(subclass Asbestos Mineral)

(documentation Asbestos EnglishLanguage "&%Asbestos is a &%Mineral.")

(subclass BiodiversityAttribute InternalAttribute)

(documentation BiodiversityAttribute EnglishLanguage "&%BiodiversityAttribute is the
class of &%Attributes that describe the level of biodiversity present
in a &%GeographicArea or &%Ecosystem.")

(instance LowBiodiversity BiodiversityAttribute)
(instance MediumBiodiversity BiodiversityAttribute)
(instance HighBiodiversity BiodiversityAttribute)

(instance bioindicatorForHabitat BinaryPredicate)
(domain bioindicatorForHabitat 1 GeographicArea)
(domainSubclass bioindicatorForHabitat 2 OrganicObject)

(documentation bioindicatorForHabitat EnglishLanguage
"(&%bioindicatorForHabitat ?AREA ?SPECIES) means that the health
of the individuals in ?SPECIES indicates the condition of their
habitat in the &%GeographicArea ?AREA.")

(instance totalBiomass BinaryPredicate)
(domain totalBiomass 1 GeographicArea)
(domain totalBiomass 2 PhysicalQuantity)

(documentation totalBiomass EnglishLanguage "(&%totalBiomass ?PLACE ?MASS) means that the
total amount (in weight or volume) of living matter in the &%Region
?PLACE is ?MASS.")

(subclass CarbonCycle GeologicalProcess)

(documentation CarbonCycle EnglishLanguage "&%CarbonCycle is the class of
&%GeologicalProcesses in which carbon in various forms is passed
between air, water, earth, and the biosphere.")

(subclass WaterCatchment Artifact)

(documentation WaterCatchment EnglishLanguage "&%WaterCatchment is the subclass of
&%Artifacts used to capture rainwater or runoff as a source
of &%FreshWater.")

(subclass DDT CompoundSubstance)
(names "dichoro_diphenyl_trichloro_ethane" DDT)
(abbreviation "DDT" DDT)

(documentation DDT EnglishLanguage "&%DDT (dichoro_diphenyl_trichloro_ethane)
is a highly toxic insecticide also harmful to most other animal
species.  DDT was banned in the &%UnitedStates in 1972.")

(subclass Defoliant PureSubstance)

(documentation Defoliant EnglishLanguage "&%Defoliant is the class of substances
that are used to make plants lose their leaves, typically used
in agriculture or warfare.  Defoliants may have detrimental
environmental side effects.")

(subclass Deforestation Removing)

(subclass Dredging Removing)

(subclass Ecosystem GeographicArea)

(documentation Ecosystem EnglishLanguage "&%Ecosystem is a subclass of &%GeographicAreas
considered together with their organisms and environment as a functioning
whole.")

(subclass Effluent Substance)

(documentation Effluent EnglishLanguage "&%Effluent is a &%Substance
that generically covers any waste matter that is released into
the environment, including sewage and industrial pollutants.")

(=>
  (instance ?STUFF Effluent)
  (attribute ?STUFF Fluid))

(subclass Erosion Removing)
(subclass Erosion GeologicalProcess)

(documentation Erosion EnglishLanguage "&%Erosion is a wearing process on
&%LandForms by wind, running water, ice, heat, and other processes,
in which rock and soil material are removed from one area and
deposited elsewhere.")

(=>
  (instance ?WEARING Erosion)
  (exists (?LAND)
    (and
      (patient ?WEARING ?LAND)
      (instance ?LAND LandForm))))

(=>
  (and
    (instance ?WEARING Erosion)
    (instance ?LAND LandForm)
    (patient ?WEARING ?LAND)
    (instance ?U UnitOfVolume)
    (measure ?LAND
      (MeasureFn ?SIZE1 ?U)))
  (holdsDuring (ImmediateFutureFn (WhenFn ?WEARING))
    (exists (?SIZE2)
      (and
        (measure ?LAND
          (MeasureFn ?SIZE2 ?U))
        (lessThan ?SIZE2 ?SIZE1)))))

(subclass Fishing Hunting)
(documentation Fishing EnglishLanguage "&%Fishing is the class of &%Processes in which
&%Fish are hunted.")

(=>
  (and
    (instance ?FISHING Fishing)
    (patient ?FISHING ?TARGET)
    (instance ?TARGET Animal))
  (instance ?TARGET Fish))

(subclass ForestDamage Damaging)
(subclass ForestFire ForestDamage)
(subclass Deforestation ForestDamage)

(=>
  (instance ?HARMING ForestDamage)
  (exists (?TREE)
    (and
      (instance ?TREE BotanicalTree)
      (patient ?HARMING ?TREE))))

(subclass DriftnetFishing Fishing)

(subclass FreshWaterOverutilization SocialInteraction)

(subclass FreshWater Water)

(documentation FreshWater EnglishLanguage "&%FreshWater is the subclass of &%Water
that has low soluble mineral content.  See also &%DrinkingWater.")

(subclass GreenhouseGas PureSubstance)
(subclass GreenhouseGas Gas)
(documentation GreenhouseGas EnglishLanguage "&%GreenhouseGas is the subclass of
substances that, when present in the atmosphere, trap infrared
radiation and cause global warming.  Greenhouse gases include
carbon dioxide, hydrofluorocarbons, methane, nitrous oxide, ozone,
and water vapor.")

(subclass GroundWater Water)

(documentation GroundWater EnglishLanguage "&%GroundWater is the subclass of
&%Water that is found in deposits in the earth.")

(subclass IndustrialPollution Pollution)

(documentation IndustrialPollution EnglishLanguage "&%IndustrialPollution is
the subclass of &%Pollution characterized by pollutants that
originate in industrial processes.")

(subclass MetallurgicalPlant Organization)

(documentation MetallurgicalPlant EnglishLanguage "&%MetallurgicalPlant is the
subclass of &%Organization that includes metal refining and
manufacturing plants.  Such plants typically release extremely
toxic waste which can pollute air and groundwater if not
properly treated.")

(subclass NoxiousSubstance Substance)

(documentation NoxiousSubstance EnglishLanguage "&%NoxiousSubstance is
the class of &%Substances that are harmful to &%Humans.")

(subclass Overgrazing Eating)

(documentation Overgrazing EnglishLanguage "&%Overgrazing is the class of processes
in which grazing animals consume vegetation faster than it can regrow.")

(instance OzoneShield AtmosphericRegion)
(instance OzoneShield CompoundSubstance)

(documentation earthAltitude EnglishLanguage "The altitude of an object
above the sea level of &%PlanetEarth")
(instance earthAltitude BinaryPredicate)
(domain earthAltitude 1 Object)
(domain earthAltitude 2 PhysicalQuantity)

(<=>
  (earthAltitude ?OBJ ?ALT)
  (exists (?SEA)
    (and
      (altitude ?OBJ ?SEA ?ALT)
      (surface ?SEA PlanetEarth))))

(documentation OzoneShield EnglishLanguage "The &%OzoneShield is a layer in
&%EarthsAtmosphere, located about 25 miles above Earth's surface,
composed of ozone gas that absorbs ultraviolent radiation from
the Sun that can be damaging to living things.")

(earthAltitude OzoneShield (MeasureFn 25.0 Mile))

(subclass PesticidePollution Pollution)

(documentation PesticidePollution EnglishLanguage "&%PesticidePollution is
the subclass of &%Pollution in which the pollutant is a pesticide.")

(subclass Poaching Killing)

(documentation Poaching EnglishLanguage "&%Poaching is the illegal killing
of non_human animals.")

(subclass Pollution SocialInteraction)

(documentation Pollution EnglishLanguage "&%Pollution is the contamination of an
environment by man_made wastes.")

(subclass DrinkingWater FreshWater)

(documentation DrinkingWater EnglishLanguage "&%DrinkingWater is the subclass
of &%Water that is safe to be consumed by &%Human.")

(=>
  (subclass ?W DrinkingWater)
  (subclass ?W
    (FoodForFn Human)))

(subclass SeaWater Water)
(subclass SeaWater SalineSolution)
(documentation SeaWater EnglishLanguage "&%SeaWater is the subclass
of &%Water from a &%Sea or &%Ocean. On average, seawater in the world's ocean
has a salinity of about 3.5% or 35g/L. This means that every &%Liter of seawater
has approximately 35 &%Gram of dissolved salt (predominately sodium and cholride ion)
.[Wikipedia]")
(termFormat EnglishLanguage SeaWater "sea water")

(=>
  (instance ?SW SeaWater)
  (exists (?S)
    (and
      (located ?SW ?S)
      (instance ?S SaltWaterArea))))

(=>
  (and
    (instance ?SW SeaWater)
    (measure ?SW
      (MeasureFn 1.0 Liter))
    (part ?SALT ?SW)
    (instance ?SALT SodiumChloride)
    (measure ?SALT
      (MeasureFn ?R Gram)))
  (approximateValue ?R 35.0))

(subclass BrackishWater Water)
(subclass BrackishWater SalineSolution)
(documentation BrackishWater EnglishLanguage "&%BrackishWater is the subclass
of &%Water occuring in a natural environment that has more salinity than &%FreshWater,
but not as much as &%SeaWater. Technically, it contains between 0.5 and 30 &%Gram of salt
per liter oy &%BrackishWater.[Wikipedia]")
(termFormat EnglishLanguage BrackishWater "brackish water")

(=>
  (and
    (instance ?SW BrackishWater)
    (measure ?SW
      (MeasureFn 1.0 Liter))
    (part ?SALT ?SW)
    (instance ?SALT SodiumChloride)
    (measure ?SALT
      (MeasureFn ?R Gram)))
  (and
    (greaterThan ?R 0.5)
    (lessThanOrEqualTo ?R 30.0)))

(subclass  Brine SalineSolution)
(documentation Brine EnglishLanguage "&%Brine is the subclass of &%SalineSolution.
Technically, it contains between 0.5 and 30 &%Gram of salt per liter of &%Brine.[Wikipedia]")
(termFormat EnglishLanguage Brine "brine")

(=>
  (and
    (instance ?B Brine)
    (measure ?B
      (MeasureFn 1.0 Liter))
    (part ?SALT ?B)
    (instance ?SALT SodiumChloride)
    (measure ?SALT
      (MeasureFn ?R Gram)))
  (and
    (greaterThan ?R 35.0)
    (lessThanOrEqualTo ?R 260.0)))

(instance ShortageFn UnaryFunction)
(domainSubclass ShortageFn 1 Object)
(range ShortageFn Entity)

(documentation ShortageFn EnglishLanguage "(&%ShortageFn ?TYPE) means that there
is a shortage of objects of ?TYPE, which are used as &%resources.")

(subclass Salination Combining)
(subclass Salination NaturalProcess)

(documentation Salination EnglishLanguage "&%Salination is the class of processes
in which either &%FreshWater or &%Topsoil becomes imbued with
&%SodiumChloride from sea water or from the evaporation of irrigation water.")

(=>
  (instance ?PROCESS Salination)
  (exists (?SALT)
    (and
      (instance ?SALT SodiumChloride)
      (resource ?PROCESS ?SALT))))

(=>
  (instance ?S Water)
  (capability Salination patient ?S))

(=>
  (instance ?S Soil)
  (capability Salination patient ?S))

(subclass Sanitation Removing)

(documentation Sanitation EnglishLanguage "&%Sanitation is the class of processes
by which human waste and garbage are disposed of.")

(subclass SewageDisposal Sanitation)
(subclass SolidWasteDisposal Sanitation)

(subclass Siltation Putting)

(documentation Siltation EnglishLanguage "&%Siltation is the class of processes
in which &%WaterAreas become filled in with silt or mud, due to
soil &%Erosion.")

(subclass SlashAndBurnAgriculture Agriculture)

(documentation SlashAndBurnAgriculture EnglishLanguage "&%SlashAndBurnAgriculture
is the subclass of &%Agriculture processes in which land is
cleared by cutting and burning trees, and crops are farmed until
the soil is depleted, at which point the plot is abandoned to
regrowth.  This rotational technique is viable only with low
populations and infrequent re_use.")

(subclass SoilDegradation SocialInteraction)

(documentation SoilDegradation EnglishLanguage "&%SoilDegradation is a class of
processes in which the productive capacity of soil is lowered by
over_fertilization, overuse of pesticides, erosion, or soil
compaction.")

(subclass SoilErosion Erosion)

(documentation SoilErosion EnglishLanguage "&%SoilErosion is the subclass of
&%Erosion in which the matter removed by wind or water is topsoil.")

(causesSubclass SoilErosion Siltation)

(subclass SoilSalination Salination)

(=>
  (instance ?PROCESS SoilSalination)
  (exists (?SOIL)
    (and
      (instance ?SOIL Soil)
      (resource ?PROCESS ?SOIL))))

(subclass VehicularPollution Pollution)

(documentation VehicularPollution EnglishLanguage "&%VehicularPollution is the
subclass of &%Pollution in which the pollutants are vehicle emissions.")

(relatedInternalConcept CarbonDioxideEmission VehicularPollution)

(subclass WaterOverutilization SocialInteraction)

(subclass WaterPollution Pollution)

(documentation WaterPollution EnglishLanguage "&%WaterPollution is the subclass
of &%Pollution processes in which &%Water is the polluted substance.")

;;-----------------------------------------------------------------------

;; Q. Environment - international agreements

;;(subclass Agreement Proposition)

;; KJN: Moving contracts and agreements to Mid_level_ontology.kif
;;(documentation Agreement EnglishLanguage "&%Agreement is the class of
;;&%Propositions that express the contents of agreements
;;entered into by &%CognitiveAgents. &%Agreement includes
;;treaties, contracts, purchase orders, pledges, marriage
;;vows, etc.  An &%Agreement may be written down in a document
;;or other &%ContentBearingObject.")

;;(=>
;;  (instance ?COMPACT Agreement)
;;  (exists (?COMM)
;;    (and
;;      (instance ?COMM Committing)
;;      (represents ?COMM ?COMPACT))))

(subclass InternationalAgreement Agreement)

(documentation InternationalAgreement EnglishLanguage "&%InternationalAgreement is the
subclass of &%Agreements which are made by and between &%Nations.")

(=>
  (and
    (instance ?AGREEMENT InternationalAgreement)
    (instance ?COMMITTING Committing)
    (represents ?COMMITTING ?AGREEMENT))
  (exists (?AGENT)
    (and
      (agent ?COMMITTING ?AGENT)
      (instance ?AGENT Nation))))

(instance DocumentFn UnaryFunction)
(domain DocumentFn 1 Proposition)
(rangeSubclass DocumentFn Text)

(documentation DocumentFn EnglishLanguage "(&%DocumentFn ?PROP) denotes a class
of &%Text objects that contain the information ?PROP.")

(=>
  (and
    (instance ?PROP Proposition)
    (instance ?TEXT (DocumentFn ?PROP)))
  (exists (?DOC)
    (containsInformation ?DOC ?TEXT)))

(=>
  (and
    (instance ?PROP Proposition)
    (instance ?CBO ContentBearingObject)
    (containsInformation ?CBO ?PROP)
    (instance ?COMM Communication)
    (patient ?COMM ?CBO))
  (represents ?COMM ?PROP))

(subclass TreatyDocument Text)

(documentation TreatyDocument EnglishLanguage "&%TreatyDocument is the subclass of
&%Texts that represent written agreements between &%Nations.")

(=>
  (instance ?TREATY TreatyDocument)
  (exists (?COMM ?COUNTRY1 ?COUNTRY2)
     (and
    (instance ?COMM Committing)
    (patient ?COMM ?TREATY)
    (instance ?COUNTRY1 GeopoliticalArea)
    (agent ?COMM ?COUNTRY1)
    (instance ?COUNTRY2 GeopoliticalArea)
    (agent ?COMM ?COUNTRY2)
    (not (equal ?COUNTRY1 ?COUNTRY2)))))

(=>
  (instance ?T TreatyDocument)
  (attribute ?T Treaty))

(instance AgreementOrganizationFn UnaryFunction)
(domain AgreementOrganizationFn 1 Agreement)
(range AgreementOrganizationFn Organization)

(documentation AgreementOrganizationFn EnglishLanguage "(&%AgreementOrganizationFn ?AGR)
denotes the official &%Organization established by the &%Agreement ?AGR
to administer or enforce the terms of that agreement.")

(=>
  (and
    (instance ?AGR InternationalAgreement)
    (instance (AgreementOrganizationFn ?AGR) Organization))
  (instance (AgreementOrganizationFn ?AGR) OrganizationOfNations))

(instance dateOpenedForSignature BinaryPredicate)
(domain dateOpenedForSignature 1 Proposition)
(domainSubclass dateOpenedForSignature 2 TimePosition)

(documentation dateOpenedForSignature EnglishLanguage
"(&%dateOpenedForSignature ?PROP ?DATE) means that as of a date
indicated by ?DATE, document(s) may officially be signed (by the
appropriate agents) to effect commitments to the &%Agreement ?PROP.")

;; For "date entered into force" use (from Government.kif):
;; &%agreementEffectiveDate: Proposition x (subclass of) TimePosition

;; Moved this to Mid_level_ontology.kif
;;(instance partyToAgreement BinaryPredicate)
;;(domain partyToAgreement 1 AutonomousAgent)
;;(domain partyToAgreement 2 Proposition)

;;(documentation partyToAgreement EnglishLanguage "(&%partyToAgreement ?AGENT ?PROP)
;;means that the &%AutonomousAgent ?AGENT has committed to the agreement ?PROP.")

;;(=>
;;  (partyToAgreement ?AGENT ?CONTENT)
;;  (instance ?CONTENT Agreement))

;;(=>
;;  (partyToAgreement ?AGENT ?CONTENT)
;;  (exists (?COMMITTING ?CBO)
;;    (and
;;      (instance ?COMMITTING Committing)
;;      (instance ?CBO ContentBearingObject)
;;      (patient ?COMMITTING ?CBO)
;;      (containsInformation ?CBO ?CONTENT)
;;      (agent ?COMMITTING ?AGENT))))

(instance unratifiedSignatoryToAgreement BinaryPredicate)
(domain unratifiedSignatoryToAgreement 1 AutonomousAgent)
(domain unratifiedSignatoryToAgreement 2 Agreement)

(documentation unratifiedSignatoryToAgreement EnglishLanguage
"(&%unratifiedSignatoryToAgreement ?AGENT ?PROP) means that
the &%AutonomousAgent ?AGENT has signed a document to begin the process of
committing to the &%Agreement ?PROP, but that a ratification
process required to complete the commitment has not yet occurred.")

(=>
  (unratifiedSignatoryToAgreement ?AGENT ?PROP)
  (not (partyToAgreement ?AGENT ?PROP)))

(subclass SigningAnAgreement Committing)

(documentation SigningAnAgreement EnglishLanguage "&%SigningAnAgreement is the class of
actions in which an agent affixes a signature, stamp, or other evidence
of authorization or attestation to a document.  The document and signature
may be electronic.  Signings count as &%SocialInteractions even if done in
private, because their significance derives from a social context.")

(=>
  (instance ?SIGNING SigningAnAgreement)
  (exists (?DOC ?TERMS)
    (and
      (instance ?DOC LinguisticExpression)
      (instance ?TERMS Agreement)
      (containsInformation ?DOC ?TERMS)
      (patient ?SIGNING ?DOC))))

;;-----------------------------------

;; Environmental Agreements covered by the CIA World Fact Book:

(instance AirPollutionConvention Agreement)

(conventionalLongName "Convention on Long_Range Transboundary Air Pollution" AirPollutionConvention)

(conventionalShortName "Air Pollution" AirPollutionConvention)

(instance AirPollution_NitrogenOxidesProtocol Agreement)

(conventionalLongName "Protocol to the 1979 Convention on Long_Range Transboundary
Air Pollution Concerning the Control of Emissions of Nitrogen Oxides or Their
Transboundary Fluxes" AirPollution_NitrogenOxidesProtocol)

(instance AirPollution_PeristentOrganicPollutantsProtocol Agreement)

(conventionalLongName "Protocol to the 1979 Convention on Long_Range Transboundary
Air Pollution on Persistent Organic Pollutants" AirPollution_PeristentOrganicPollutantsProtocol)

(instance AirPollution_Sulphur85Protocol Agreement)

(conventionalLongName "Protocol to the 1979 Convention on Long_Range Transboundary
Air Pollution on the Reduction of Sulphur Emissions or Their Transboundary Fluxes
by at least 30%" AirPollution_Sulphur85Protocol)

(instance AirPollution_Sulphur94Protocol Agreement)

(conventionalLongName "Protocol to the 1979 Convention on Long_Range Transboundary
Air Pollution on Further Reduction of Sulphur Emissions"  AirPollution_Sulphur94Protocol)

(instance AirPollution_VolatileOrganicCompoundsProtocol Agreement)

(conventionalLongName "Protocol to the 1979 Convention on Long_Range Transboundary
Air Pollution Concerning the Control of Emissions of Volatile Organic Compounds or
Their Transboundary Fluxes" AirPollution_VolatileOrganicCompoundsProtocol)

(instance Antarctic_EnvironmentalProtocol Agreement)

(conventionalLongName "Protocol on Environmental Protection to the Antarctic Treaty"
  Antarctic_EnvironmentalProtocol)

(instance Antarctic_MarineLivingResourcesConvention Agreement)

(conventionalLongName "Convention on the Conservation of Antarctic Marine Living Resources"
  Antarctic_MarineLivingResourcesConvention)

(conventionalShortName "Antarctic_Marine Living Resources" Antarctic_MarineLivingResourcesConvention)

(instance AntarcticSealsConvention Agreement)

(conventionalLongName "Convention for the Conservation of Antarctic Seals" AntarcticSealsConvention)

(conventionalShortName "Antarctic Seals" AntarcticSealsConvention)

(instance AntarcticTreaty Agreement)
(conventionalLongName "Antarctic Treaty" AntarcticTreaty)
(agreementAdoptionDate AntarcticTreaty (DayFn 1 (MonthFn December (YearFn 1959))))
(agreementEffectiveDate AntarcticTreaty (instance ?D (DayFn 23 (MonthFn June (YearFn 1961)))))

;; consultative (voting) members: (claimants) Argentina, Australia,
;; Chile, France, NewZealand, Norway, UnitedKingdom; (nonclaimants)
;; Belgium, Brazil, Bulgaria, China, Ecuador, Finland, Germany, India,
;; Italy, Japan, SouthKorea, Netherlands, Peru, Poland, Russia,
;; SouthAfrica, Spain, Sweden, Uruguay, UnitedStates.  includes under
;; the treaty all land and ice shelves south of 60 degrees 00 minutes
;; south and reserves high seas rights.


(instance BiodiversityConvention Agreement)

(conventionalLongName "Convention on Biological Diversity" BiodiversityConvention)

(instance ClimateChangeConvention Agreement)

(conventionalLongName "United Nations Framework Convention on Climate Change" ClimateChangeConvention)

(conventionalShortName "Climate Change" ClimateChangeConvention)

(instance ClimateChange_KyotoProtocol Agreement)

(conventionalLongName "Kyoto Protocol to the United Nations Framework Convention
on Climate Change" ClimateChange_KyotoProtocol)

(conventionalShortName "Kyoto Protocol" ClimateChange_KyotoProtocol)
(conventionalShortName "Climate Change Kyoto Protocol" ClimateChange_KyotoProtocol)

(instance DesertificationConvention Agreement)

(conventionalLongName "United Nations Convention to Combat Desertification in those
Countries Experiencing Serious Drought or Desertification, Particularly in Africa" DesertificationConvention)

(conventionalShortName "Desertification" DesertificationConvention)

(instance EndangeredSpeciesConvention Agreement)

(conventionalLongName "Convention on the International Trade in Endangered Species of
Wild Flora and Fauna" EndangeredSpeciesConvention)

(conventionalShortName "EndangeredSpecies" EndangeredSpeciesConvention)
(abbreviation "CITES" EndangeredSpeciesConvention)

(instance EnvironmentalModificationConvention Agreement)

(conventionalLongName "Convention on the Prohibition of Military or Any Other Hostile
Use of Environmental Modification Techniques" EnvironmentalModificationConvention)

(conventionalShortName "Environmental Modification" EnvironmentalModificationConvention)

(instance HazardousWastesConvention Agreement)

(conventionalLongName "Basel Convention on the Control of Transboundary Movements of
Hazardous Wastes and Their Disposal" HazardousWastesConvention )

(conventionalShortName "Basel Convention" HazardousWastesConvention)
(conventionalShortName "Hazardous Wastes" HazardousWastesConvention)

(instance LawOfTheSeaConvention Agreement)

(conventionalLongName "United Nations Convention on the Law of the Sea" LawOfTheSeaConvention)

(conventionalShortName "Law of the Sea" LawOfTheSeaConvention)
(abbreviation "LOS" LawOfTheSeaConvention)

(instance MarineDumpingConvention Agreement)

(conventionalLongName "Convention on the Prevention of Marine Pollution by Dumping
Wastes and Other Matter" MarineDumpingConvention)

(conventionalShortName "Marine Dumping" MarineDumpingConvention)
(conventionalShortName "London Convention" MarineDumpingConvention)

(instance MarineLifeConservationConvention Agreement)

(conventionalLongName "Convention on Fishing and Conservation of Living Resources of
the High Seas" MarineLifeConservationConvention)

(conventionalShortName "Marine Life Conservation" MarineLifeConservationConvention)

(instance NuclearTestBanTreaty Agreement)

(conventionalLongName "Treaty Banning Nuclear Weapons Tests in the Atmosphere, in Outer
Space, and Under Water" NuclearTestBanTreaty)

(conventionalShortName "Nuclear Test Ban" NuclearTestBanTreaty)

(instance OzoneLayerProtectionProtocol Agreement)

(conventionalLongName "Montreal Protocol on Substances That Deplete the Ozone Layer" OzoneLayerProtectionProtocol)

(conventionalShortName "Ozone Layer Protection" OzoneLayerProtectionProtocol)

(conventionalShortName "Montreal Protocol" OzoneLayerProtectionProtocol)

(instance ShipPollutionProtocol Agreement)

(conventionalLongName "Protocol of 1978 Relating to the International Convention for
the Prevention of Pollution From Ships, 1973" ShipPollutionProtocol)

(conventionalShortName "Ship Pollution" ShipPollutionProtocol)
(conventionalShortName "MARPOL" ShipPollutionProtocol)

(instance TropicalTimber83Agreement Agreement)

(conventionalLongName "International Tropical Timber Agreement, 1983" TropicalTimber83Agreement)

(conventionalShortName "Tropical Timber 83" TropicalTimber83Agreement)

(instance TropicalTimber94Agreement Agreement)

(conventionalLongName "International Tropical Timber Agreement, 1994" TropicalTimber94Agreement)

(conventionalShortName "Tropical Timber 94" TropicalTimber94Agreement)

(instance WetlandsConvention Agreement)

(conventionalLongName "Convention on Wetlands of International Importance Especially
as Waterfowl Habitat" WetlandsConvention)

(conventionalShortName "Wetlands" WetlandsConvention)
(conventionalShortName "Ramsar Convention" WetlandsConvention)

(instance WhalingConvention Agreement)

(conventionalLongName "International Convention for the Regulation of Whaling" WhalingConvention)

(conventionalShortName "Whaling" WhalingConvention)

;;-----------------------------------------------------------------------

;; R. Geography - note
;; [Specific notes made in KIF Country files as needed.]

;;========================================================================


;; II. General Geography Terms and Background
;;   A. Planet geography & astronomical bodies
;;  B. Directions and distances
;;  C. Land forms
;;   D. Water areas
;;     1. Oceans & Seas
;;     2. Tides & Currents
;;     3. Water subregions
;;     4. Fresh water areas
;;   E. Coastal and shoreline areas
;;   F. Air and atmosphere
;;   G. Weather & Climate
;;   H. Vegetation and biomes
;;  I. Natural disasters

;;-------------------------------------------------------------------------

; A. Planet Geography and Astronomical Bodies

(subclass Star AstronomicalBody)
(documentation Star EnglishLanguage
"&%Star is the class of hot gaseous astronomical bodies.")

(instance Sol Star)
(documentation Sol EnglishLanguage "&%Sol is the nearest &%Star to &%PlanetEarth and
the focus of its &%SolarSystem.")

(=>
  (instance ?L Sunlight)
  (origin ?L Sol))

(subclass SolarSystem Collection)
(documentation SolarSystem EnglishLanguage "&%SolarSystem is the class of systems that
consist of a star or stars and any encircling astronomical bodies.")

(subclass Satellite AstronomicalBody)
(documentation Satellite EnglishLanguage "&%Satellite is the collection of bodies that
revolve around some  astronomical body, e.g., planets around a star.
This class includes both artificial and &%NaturalSatellites.")

(instance orbits BinaryPredicate)
(instance orbits AsymmetricRelation)
(domain orbits 1 Object)
(domain orbits 2 AstronomicalBody)

(documentation orbits EnglishLanguage "(&%orbits ?SATELLITE ?FOCUS) means that the &%Object
?SATELLITE revolves around the &%AstronomicalBody ?FOCUS.")

(=>
  (instance ?SAT Satellite)
  (exists (?BODY)
    (and
      (instance ?BODY AstronomicalBody)
      (orbits ?SAT ?BODY))))

(subclass NaturalSatellite Satellite)
(subclass NaturalSatellite AstronomicalBody)
(disjoint NaturalSatellite Artifact)

(documentation NaturalSatellite EnglishLanguage "&%NaturalSatellite is the class of
large, naturally occurring astronomical bodies orbiting some other
&%AstronomicalBody.")

(subclass Moon NaturalSatellite)
(documentation Moon EnglishLanguage "&%Moon is the class of &%NaturalSatellites that
orbit planets or large asteroids.")

(instance EarthsMoon Moon)
(orbits EarthsMoon PlanetEarth)
(documentation EarthsMoon EnglishLanguage "EarthsMoon is the Moon of PlanetEarth.")

(subclass Planet NaturalSatellite)
(documentation Planet EnglishLanguage "&%Planet is the class of large
&%NaturalSatellites that revolve around a star.")

(instance PlanetEarth Planet)
(orbits PlanetEarth Sol)
(instance PlanetMercury Planet)
(orbits PlanetMercury Sol)
(instance PlanetVenus Planet)
(orbits PlanetVenus Sol)
(instance PlanetMars Planet)
(orbits PlanetMars Sol)
(instance PlanetJupiter Planet)
(orbits PlanetJupiter Sol)
(instance PlanetSaturn Planet)
(orbits PlanetSaturn Sol)
(instance PlanetNeptune Planet)
(orbits PlanetNeptune Sol)
(instance PlanetUranus Planet)
(orbits PlanetUranus Sol)
(instance PlanetPluto Planet)
(orbits PlanetPluto Sol)

(subclass Meteorite Meteoroid)
(documentation Meteorite EnglishLanguage "Any &%Meteoroid that leaves traces on the surface
of &%Earth.")

(=>
  (instance ?M Meteorite)
  (exists (?T)
    (holdsDuring ?T
      (meetsSpatially ?M PlanetEarth))))

;; KJN: changing the definition of this based on Wikipedia
(subclass Meteoroid AstronomicalBody)
;;(documentation Meteoroid EnglishLanguage "Any &%AstronomicalBody that breaks through the
;;&%Atmosphere of &%Earth.")
(documentation Meteoroid EnglishLanguage "According to the International Astronomical
Union, a &%Meteoroid is a solid object moving in interplanetary space, of a size
considerably smaller than an asteroid and considerably larger than an atom.")

;; KJN: Changing this rule based on new linguistic definition
;;(=>
;;  (instance ?M Meteoroid)
;;  (exists (?T)
;;    (holdsDuring ?T
;;      (meetsSpatially ?M EarthsAtmosphere))))

(=>
  (instance ?M Meteoroid)
  (exists (?R)
    (and
      (instance ?R OuterSpaceRegion)
      (located ?M ?R))))


;;---------------------------------

;; Earth location terms

(subclass Hemisphere GeographicArea)
(documentation Hemisphere EnglishLanguage "&%Hemisphere is the class of &%GeographicAreas
that are halves of the Earth, as traditionally divided into the Northern
and Southern Hemispheres along the equator and into the Eastern and Western
Hemispheres along a north_south line running 20�W and 160�E.")

(=>
  (instance ?AREA Hemisphere)
  (geographicSubregion ?AREA PlanetEarth))

(instance NorthernHemisphere Hemisphere)
(documentation NorthernHemisphere EnglishLanguage "The half of the Earth that lies above the
equator.")

(instance SouthernHemisphere Hemisphere)
(documentation SouthernHemisphere EnglishLanguage "The half of the Earth that lies below the
equator.")

(instance EasternHemisphere Hemisphere)
(documentation EasternHemisphere EnglishLanguage "The half of the Earth that includes Europe,
Asia, Africa, and Australia.")

(instance WesternHemisphere Hemisphere)
(documentation WesternHemisphere EnglishLanguage "The half of the Earth that includes North
and South America.")

(<=>
   (instance ?HEMISPHERE Hemisphere)
   (or
      (equal ?HEMISPHERE NorthernHemisphere)
      (equal ?HEMISPHERE SouthernHemisphere)
      (equal ?HEMISPHERE EasternHemisphere)
      (equal ?HEMISPHERE WesternHemisphere)))

(=>
  (and
    (overlapsSpatially ?ONE ?TWO)
    (instance ?TWO Region)
    (not (equal ?ONE ?TWO)))
  (partlyLocated ?ONE ?TWO))

;; AXIOMS for &%Continent (defined in Merge.txt):
(<=>
   (instance ?CONTINENT Continent)
   (or
      (equal Africa ?CONTINENT)
      (equal NorthAmerica ?CONTINENT)
      (equal SouthAmerica ?CONTINENT)
      (equal Antarctica ?CONTINENT)
      (equal Europe ?CONTINENT)
      (equal Asia ?CONTINENT)
      (equal Oceania ?CONTINENT)))

(=>
  (instance ?AREA Continent)
  (geographicSubregion ?AREA PlanetEarth))

(disjoint Nation Continent)

(instance Africa Continent)
(names "Africa" Africa)
(overlapsSpatially Africa NorthernHemisphere)
(overlapsSpatially Africa SouthernHemisphere)
(geographicSubregion Africa EasternHemisphere)

(instance NorthAmerica Continent)
(geographicSubregion NorthAmerica NorthernHemisphere)
(geographicSubregion NorthAmerica WesternHemisphere)

(instance SouthAmerica Continent)
(names "South America" SouthAmerica)
(overlapsSpatially SouthAmerica NorthernHemisphere)
(overlapsSpatially SouthAmerica SouthernHemisphere)
(geographicSubregion SouthAmerica WesternHemisphere)

(instance Antarctica Continent)
(names "Antarctica" Antarctica)
(geographicSubregion Antarctica SouthernHemisphere)
(overlapsSpatially Antarctica EasternHemisphere)
(overlapsSpatially Antarctica WesternHemisphere)

(names "Europe" Europe)
(geographicSubregion Europe NorthernHemisphere)
(overlapsSpatially Europe EasternHemisphere)
(overlapsSpatially Europe WesternHemisphere)

(instance Asia Continent)
(names "Asia" Asia)
(overlapsSpatially Asia NorthernHemisphere)
(overlapsSpatially Asia SouthernHemisphere)
(geographicSubregion Asia EasternHemisphere)

(instance Oceania Continent)
; (names "Oceania" Oceania)
(overlapsSpatially Oceania NorthernHemisphere)
(overlapsSpatially Oceania SouthernHemisphere)
(overlapsSpatially Oceania EasternHemisphere)
(overlapsSpatially Oceania WesternHemisphere)

(instance ArcticRegion GeographicArea)
(names "Arctic" ArcticRegion)
(names "the Arctic" ArcticRegion)
(documentation ArcticRegion EnglishLanguage "The area above the Artic Circle, which is
66 degrees 33 minutes and 38 seconds north latitude.  It is dark
for at least 24 hours at some point during the year, with an
increasing period of yearly continuous darkness as one gets closer
to the North Pole.")

(=>
  (and
    (instance ?REGION GeographicArea)
    (located ?REGION ArcticRegion))
  (exists (?DAY)
    (and
      (instance ?DAY Day)
      (holdsDuring ?DAY
        (not
          (attribute ?REGION Illuminated))))))

(geographicSubregion ArcticRegion NorthernHemisphere)
(overlapsSpatially ArcticRegion EasternHemisphere)
(overlapsSpatially ArcticRegion WesternHemisphere)
(overlapsSpatially ArcticOcean ArcticRegion)
(not (vegetationType ArcticRegion BotanicalTree))

(instance Tropics GeographicArea)
(names "Tropics" Tropics)
(names "the Tropics" Tropics)

(documentation Tropics EnglishLanguage "The &%Tropics is the region of
&%PlanetEarth that lies between 23°26′11.5″(or 23.43652°)&%North latitude
and 23°26′11.5″(or 23.43652°) &%South latitude, encircling the globe.")

(=>
  (and
    (instance ?PLACE GeographicArea)
    (located ?PLACE Tropics))
  (exists (?NUM1 ?LONG ?DIR ?NUM2 ?U)
    (and
      (objectGeographicCoordinates ?PLACE
        (MeasureFn ?NUM1 ?U) ?LONG)
      (instance ?U UnitOfAngularMeasure)
      (lessThan ?NUM1 ?NUM2)
      (equal
        (MeasureFn ?NUM2 ?U)
        (LatitudeFn ?DIR
          (MeasureFn 23.0 AngularDegree)
          (MeasureFn 26.0 ArcMinute)
          (MeasureFn 11.5 ArcSecond)))
      (instance ?DIR DirectionalAttribute)
      (or
        (equal ?DIR North)
        (equal ?DIR South)))))

(overlapsSpatially Tropics NorthernHemisphere)
(overlapsSpatially Tropics SouthernHemisphere)
(overlapsSpatially Tropics EasternHemisphere)
(overlapsSpatially Tropics WesternHemisphere)

(geographicSubregion Antarctica AntarcticArea)
(instance AntarcticArea GeographicArea)
(instance AntarcticArea LandArea)
(documentation AntarcticArea EnglishLanguage "The area below the AntarcticCircle, which is
66 degrees 33 minutes and 38 seconds south latitude.  It is dark
for at least 24 hours at some point during the year, with an
increasing period of yearly continuous darkness as one gets closer
to the South Pole.")

(=>
  (and
    (instance ?REGION GeographicArea)
    (located ?REGION AntarcticArea))
  (exists (?DAY)
    (and
      (instance ?DAY Day)
      (holdsDuring ?DAY
        (not
          (attribute ?REGION Illuminated))))))

;;-------------------------------------------------------------------------

;; B. Directions and coordinates

;;   Additional Compass Directions:

(instance Northeast DirectionalAttribute)

(documentation Northeast EnglishLanguage
"&%Northeast represents the compass direction of Northeast.")

(<=>
  (orientation ?OBJ1 ?OBJ2 Northeast)
  (and
    (orientation ?OBJ1 ?OBJ2 North)
    (orientation ?OBJ1 ?OBJ2 East)))

(instance Southeast DirectionalAttribute)

(documentation Southeast EnglishLanguage
"&%Southeast represents the compass direction of Southeast.")

(<=>
  (orientation ?OBJ1 ?OBJ2 Southeast)
  (and
    (orientation ?OBJ1 ?OBJ2 South)
    (orientation ?OBJ1 ?OBJ2 East)))

(instance Southwest DirectionalAttribute)
(oppositeDirection Northeast Southwest)

(documentation Southwest EnglishLanguage
"&%Southwest represents the compass direction of Southwest.")

(<=>
  (orientation ?OBJ1 ?OBJ2 Southwest)
  (and
    (orientation ?OBJ1 ?OBJ2 South)
    (orientation ?OBJ1 ?OBJ2 West)))

(instance Northwest DirectionalAttribute)
(oppositeDirection Northwest Southeast)

(documentation Northwest EnglishLanguage
"&%Northwest represents the compass direction of Northwest.")

(<=>
  (orientation ?OBJ1 ?OBJ2 Northwest)
  (and
    (orientation ?OBJ1 ?OBJ2 North)
    (orientation ?OBJ1 ?OBJ2 West)))

(instance headingWRTTrueNorth BinaryPredicate)
(instance headingWRTTrueNorth AsymmetricRelation)
(domain headingWRTTrueNorth 1 Object)
(domain headingWRTTrueNorth 2 PlaneAngleMeasure)

(documentation headingWRTTrueNorth EnglishLanguage "(&%headingWRTTrueNorth ?OBJ ?DEGREE)
means that the front_to_back axis of the &%Object ?OBJ points in
the direction ?DEGREE, with respect to true North.")

(instance headingWRTMagneticNorth BinaryPredicate)
(instance headingWRTMagneticNorth AsymmetricRelation)
(domain headingWRTMagneticNorth 1 Object)
(domain headingWRTMagneticNorth 2 PlaneAngleMeasure)

(documentation headingWRTMagneticNorth EnglishLanguage "(&%headingWRTMagneticNorth ?OBJ ?DEGREE)
means that the front_to_back axis of the &%Object ?OBJ points in
the direction ?DEGREE, with respect to magnetic North.")

(instance headingWRTCompassNorth BinaryPredicate)
(instance headingWRTCompassNorth AsymmetricRelation)
(domain headingWRTCompassNorth 1 Object)
(domain headingWRTCompassNorth 2 PlaneAngleMeasure)

(documentation headingWRTCompassNorth EnglishLanguage "(&%headingWRTCompassNorth ?OBJ ?DEGREE)
means that the front_to_back axis of the &%Object ?OBJ points in
the direction ?DEGREE, according to the compass carried by ?OBJ.")

(instance courseWRTTrueNorth TernaryPredicate)
(domain courseWRTTrueNorth 1 Physical)
(domain courseWRTTrueNorth 2 Physical)
(domain courseWRTTrueNorth 3 PlaneAngleMeasure)

(documentation courseWRTTrueNorth EnglishLanguage
"(&%courseWRTTrueNorth ?OBJ1 ?OBJ2 ?HEADING) means that the course
heading or track from ?OBJ1 to ?OBJ2 is ?HEADING, in &%AngularDegrees,
with the reference point of 000 degrees being true North.")

(<=>
  (courseWRTTrueNorth ?OBJ1 ?OBJ2 (MeasureFn 0.0 AngularDegree))
  (orientation ?OBJ1 ?OBJ2 North))

(<=>
  (courseWRTTrueNorth ?OBJ1 ?OBJ2 (MeasureFn 90.0 AngularDegree))
  (orientation ?OBJ1 ?OBJ2 East))

(<=>
  (courseWRTTrueNorth ?OBJ1 ?OBJ2 (MeasureFn 180.0 AngularDegree))
  (orientation ?OBJ1 ?OBJ2 South))

(<=>
  (courseWRTTrueNorth ?OBJ1 ?OBJ2 (MeasureFn 270.0 AngularDegree))
  (orientation ?OBJ1 ?OBJ2 West))

(<=>
  (courseWRTTrueNorth ?OBJ1 ?OBJ2 (MeasureFn 360.0 AngularDegree))
  (orientation ?OBJ1 ?OBJ2 North))

(<=>
  (courseWRTTrueNorth ?OBJ1 ?OBJ2 (MeasureFn 45.0 AngularDegree))
  (orientation ?OBJ1 ?OBJ2 Northeast))

(<=>
  (courseWRTTrueNorth ?OBJ1 ?OBJ2 (MeasureFn 135.0 AngularDegree))
  (orientation ?OBJ1 ?OBJ2 Southeast))

(<=>
  (courseWRTTrueNorth ?OBJ1 ?OBJ2 (MeasureFn 225.0 AngularDegree))
  (orientation ?OBJ1 ?OBJ2 Southwest))

(<=>
  (courseWRTTrueNorth ?OBJ1 ?OBJ2 (MeasureFn 315.0 AngularDegree))
  (orientation ?OBJ1 ?OBJ2 Northwest))

(instance MagneticDeclinationFn BinaryFunction)
(documentation MagneticDeclinationFn EnglishLanguage "&%MagneticDeclinationFn takes in
the latitude and longitude in AngleMeasure and returns the magnetic declination as an angle
measure for that location. Magnetic declination is the angle between magnetic north and
true north at a particular location on the Earth's surface, expressed in degrees and
minutes east or west to indicate the direction of magnetic north from &%headingWRTTrueNorth.
The angle can change over time due to polar wandering. By convention, declination is
positive when magnetic north is east of the true north, and negative when it is to
the west.[Wikipedia]")
(domain MagneticDeclinationFn 1 Latitude)
(domain MagneticDeclinationFn 2 Longitude)
(range MagneticDeclinationFn PlaneAngleMeasure)
(names "magnetic variation" MagneticDeclinationFn)
(relatedInternalConcept MagneticDeclinationFn courseWRTCompassNorth)
(relatedInternalConcept MagneticDeclinationFn courseWRTMagneticNorth)
(relatedInternalConcept MagneticDeclinationFn courseWRTTrueNorth)
(relatedInternalConcept MagneticDeclinationFn NorthPole)
(relatedInternalConcept headingWRTMagneticNorth courseWRTMagneticNorth)
(relatedInternalConcept headingWRTTrueNorth courseWRTTrueNorth)

;; assuming (+) value denotes an easterly declination and (-) a westerly declination
;; and 0 value on the agonic line

;; the following converts magnetic north to true north by adding the declination value

(=>
  (and
    (instance ?AREA GeographicArea)
    (objectGeographicCoordinates ?AREA ?LAT ?LONG)
    (equal
      (MeasureFn ?DEG AngularDegree)
      (MagneticDeclinationFn ?LAT ?LONG)))
  (exists (?MN)
    (and
      (headingWRTMagneticNorth ?AREA
        (MeasureFn ?MN AngularDegree))
      (headingWRTTrueNorth ?AREA
        (MeasureFn (AdditionFn ?MN ?DEG) AngularDegree)))))

;; the following subracts the declination from true north to get magentic north

(=>
  (and
    (instance ?AREA GeographicArea)
    (objectGeographicCoordinates ?AREA ?LAT ?LONG)
    (equal
      (MeasureFn ?DEG AngularDegree)
      (MagneticDeclinationFn ?LAT ?LONG)))
  (exists (?TN)
    (and
      (headingWRTTrueNorth ?AREA
        (MeasureFn ?TN AngularDegree))
      (headingWRTMagneticNorth ?AREA
        (MeasureFn (SubtractionFn ?TN ?DEG) AngularDegree)))))

(instance NorthPole GeographicArea)
(documentation NorthPole EnglishLanguage "The &%NorthPole is the point in the Northern
Hemisphere where the Earth's axis of rotation meets its surface.The North Pole is by
definition the northernmost point on the Earth, lying antipodally to the South Pole.
It defines geodetic latitude 90° North, as well as the direction of true north. At the
North Pole all directions point south; all lines of longitude converge there, so its
longitude can be defined as any degree value. No time zone has been assigned to the
North Pole, so any time can be used as the local time. Along tight latitude circles,
counterclockwise is east and clockwise is west. The North Pole is at the center of
the Northern Hemisphere. [Wikipedia]")
(termFormat EnglishLanguage NorthPole "North Pole")
(relatedInternalConcept NorthPole  SouthPole)
(relatedInternalConcept NorthPole headingWRTTrueNorth)

(=>
  (and
    (equal ?N NorthPole)
    (located ?N ArcticRegion))
  (exists (?A)
    (and
      (axis ?A PlanetEarth)
      (traverses ?A ?N))))

(equal
  NorthPole
  (LatitudeFn North
    (MeasureFn 90 AngularDegree)))

(names "geographic north" courseWRTTrueNorth)
(names "geodetic north" courseWRTTrueNorth)
(names "true north" courseWRTTrueNorth)

;; South Pole
(instance SouthPole  GeographicArea)
(documentation SouthPole EnglishLanguage "The &%SouthPole is the  southernmost point
on Earth and lies antipodally on the opposite side of Earth from the &%NorthPole, at a
distance of 20,004 km (12,430 miles) in all directions. It is one of the two points where
Earth's axis of rotation intersects its surface.The South Pole is the center of the
Southern Hemisphere.[Wikipedia]")
(termFormat EnglishLanguage SouthPole "South Pole")

(=>
  (and
    (equal ?S SouthPole)
    (located ?S Antarctica))
  (exists (?A)
    (and
      (axis ?A PlanetEarth)
      (traverses ?A ?S))))

(equal
  SouthPole
  (LatitudeFn South
    (MeasureFn 90 AngularDegree)))

(instance courseWRTMagneticNorth TernaryPredicate)
(domain courseWRTMagneticNorth 1 Physical)
(domain courseWRTMagneticNorth 2 Physical)
(domain courseWRTMagneticNorth 3 PlaneAngleMeasure)

(documentation courseWRTMagneticNorth EnglishLanguage
"(&%courseWRTMagneticNorth ?OBJ1 ?OBJ2 ?HEADING) means that the course
heading from ?OBJ1 to ?OBJ2 is ?HEADING in &%AngularDegrees, with
the reference point of 000 degrees being the magnetic North pole.")

;TODO: should ?TRUEDEGREE be ?DIFFEGREE ?
; (=>
;  (and
;    (courseWRTMagneticNorth ?OBJ1 ?OBJ2
;      (MeasureFn ?MAGDEGREE AngularDegree))
;    (partlyLocated ?OBJ1 ?AREA)
;    (partlyLocated ?OBJ2 ?AREA)
;    (magneticVariation ?AREA
;      (MeasureFn ?DEGREE AngularDegree) ?DIRECTION))
;  (exists (?DIFFDEGREE ?TRUEDEGREE)
;    (and
;      (=>
;        (and
;          (equal ?DIRECTION East)
;          (equal ?DIFFDEGREE (AdditionFn ?MAGDEGREE ?DEGREE)))
;        (courseWRTTrueNorth ?OBJ1 ?OBJ2 ?TRUEDEGREE))
;      (=>
;        (and
;          (equal ?DIRECTION West)
;          (equal ?DIFFDEGREE (SubtractionFn ?MAGDEGREE ?DEGREE)))
;        (courseWRTTrueNorth ?OBJ1 ?OBJ2 ?TRUEDEGREE)))))

(instance courseWRTCompassNorth TernaryPredicate)
(domain courseWRTCompassNorth 1 Physical)
(domain courseWRTCompassNorth 2 Physical)
(domain courseWRTCompassNorth 3 PlaneAngleMeasure)

(documentation courseWRTCompassNorth EnglishLanguage
"(&%courseWRTCompassNorth ?OBJ1 ?OBJ2 ?HEADING) means that the course
heading from ?OBJ1 to ?OBJ2 is ?HEADING in &%AngularDegrees, with
the reference point of 000 degrees North as measured by compass on
or at ?OBJ1.")

(instance relativeBearing TernaryPredicate)
(domain relativeBearing 1 Object)
(domain relativeBearing 2 Object)
(domain relativeBearing 3 PlaneAngleMeasure)

(documentation relativeBearing EnglishLanguage "(&%relativeBearing ?OBJ1?OBJ2 ?DEGREES)
means that ?OBJ1 and ?OBJ2 are separated by the amount ?DEGREES of
&%PlaneAngleMeasure.")

;; Nautical units of measure

(names "fathom" Fathom)

(instance Fathom UnitOfLength)

(equal (MeasureFn 1.0 Fathom) (MeasureFn 6.0 FootLength))

(=>
  (equal ?NUMBER (MultiplicationFn 1.0 ?NUMBER))
  (equal
    (MeasureFn ?NUMBER Fathom)
    (MeasureFn (MultiplicationFn ?NUMBER 6.0) FootLength)))

(documentation Fathom EnglishLanguage "&%Fathom is a &%UnitOfMeasure used for measuring
water depth.  One fathom is equal to six feet.")

(names "nautical mile" NauticalMile)

(instance NauticalMile UnitOfLength)
(documentation NauticalMile EnglishLanguage "&%NauticalMile represents the international
unit used for measuring distance in sea and air navigation.  The unit is
based on the length of a minute of arc of a great circle of
&%PlanetEarth.")

(equal
  (MeasureFn 1.0 NauticalMile)
  (MeasureFn 6076.1 FootLength))

(equal
  (MeasureFn 1.0 NauticalMile)
  (MeasureFn 1852.0 Meter))

(=>
  (equal ?AMOUNT
    (MeasureFn ?NUM NauticalMile))
  (equal ?AMOUNT
    (MeasureFn
      (MultiplicationFn ?NUM 1.852)
      (KiloFn Meter))))

(=>
  (equal ?AMOUNT
    (MeasureFn ?NUM NauticalMile))
  (equal ?AMOUNT
    (MeasureFn
      (MultiplicationFn ?NUM 1.151)
      Mile)))

(=>
  (equal ?NUM
    (MultiplicationFn 1.0 ?NUM))
  (equal
    (MeasureFn ?NUM NauticalMile)
    (MeasureFn
      (MultiplicationFn ?NUM 1852.0)
      Meter)))

(=>
  (equal ?NUM
    (MultiplicationFn 1.0 ?NUM))
  (equal
    (MeasureFn ?NUM NauticalMile)
    (MeasureFn
      (MultiplicationFn ?NUM 1.852)
      (KiloFn Meter))))

(=>
  (equal ?NUM
    (MultiplicationFn 1.0 ?NUM))
  (equal
    (MeasureFn ?NUM NauticalMile)
    (MeasureFn
      (MultiplicationFn ?NUM 6076.1)
      FootLength)))

(=>
  (equal ?NUM
    (MultiplicationFn 1.0 ?NUM))
  (equal
    (MeasureFn ?NUM NauticalMile)
    (MeasureFn
      (MultiplicationFn ?NUM 1.151)
      Mile)))

(names "knot" KnotUnitOfSpeed)
(abbreviation "kt" KnotUnitOfSpeed)

(instance KnotUnitOfSpeed CompositeUnitOfMeasure)
(documentation KnotUnitOfSpeed EnglishLanguage "&%KnotUnitOfSpeed is a unit for measuring
speed.  One &%KnotUnitOfSpeed is equal to one &%NauticalMile per one
&%HourDuration.")

(=>
  (equal ?SPEED
    (SpeedFn
      (MeasureFn ?DISTANCE NauticalMile)
      (MeasureFn ?TIME HourDuration)))
  (equal ?SPEED
    (MeasureFn
      (DivisionFn ?DISTANCE ?TIME)
      KnotUnitOfSpeed)))

(=>
  (equal ?SPEED
    (MeasureFn ?NUM KnotUnitOfSpeed))
  (equal ?SPEED
    (SpeedFn
      (MeasureFn ?NUM NauticalMile)
      (MeasureFn 1.0 HourDuration))))

;;-------------------------------------------------------------------------

;; C. Land Forms

;; Area measures

(instance SquareMeter UnitOfArea)
(documentation SquareMeter EnglishLanguage "&%SquareMeter represents a &%UnitOfMeasure
equal to one square &%Meter.")
(equal SquareMeter (SquareUnitFn Meter))

(instance SquareMillimeter UnitOfArea)
(documentation SquareMillimeter EnglishLanguage "&%SquareMillimeter represents a &%UnitOfMeasure
equal to one square &%Millimeter.")
(termFormat EnglishLanguage SquareMillimeter "square millimeter")
(equal SquareMillimeter (SquareUnitFn Millimeter))

(subclass LandForm GeographicArea)
(documentation LandForm EnglishLanguage "A &%LandForm is the class of geographically and/or geologically
distinct areas that occur on Earth's surface, including mountains, hills, plains, valleys,
deltas, and features of submerged land areas such as the ocean floor.")

(subclass UplandArea LandForm)
(documentation UplandArea EnglishLanguage "An &%UplandArea is a &%LandArea elevated above the surrounding
terrain.")

(subclass LowlandArea LandForm)
(documentation LowlandArea EnglishLanguage "A &%LowlandArea is a land area lower than the surrounding region,
and usually level land.")

(subclass UndergroundArea GeographicArea)
(documentation UndergroundArea EnglishLanguage "The class of regions &%located &%Below the surface of the earth.")

(=>
  (instance ?place UndergroundArea)
  (exists (?area ?ground)
    (and
      (surface ?ground PlanetEarth)
      (part ?area ?ground)
      (orientation ?place ?area Below))))

(=>
  (and
    (instance ?place UndergroundArea)
    (instance ?U UnitOfLength)
    (earthAltitude ?place
      (MeasureFn ?alt1 ?U))
    (surface ?ground PlanetEarth)
    (part ?area ?ground)
    (orientation ?place ?area Below)
    (earthAltitude ?area
      (MeasureFn ?alt2 ?U)))
  (greaterThan ?alt2 ?alt1))

(subclass MountainRange LandForm)
(subclass MountainRange UplandArea)
(documentation MountainRange EnglishLanguage "A &%MountainRange is a row or chain of connected mountains.")

(=>
  (and
    (instance ?Range MountainRange)
    (part ?Mountain1 ?Range))
  (exists (?Mountain2)
    (and
      (component ?Mountain2  ?Range)
      (instance ?Mountain2 Mountain)
      (meetsSpatially ?Mountain1 ?Mountain2))))

(subclass Mountain LandForm)
(subclass Mountain UplandArea)

(documentation Mountain EnglishLanguage "A &%Mountain is a high, rocky &%LandForm, usually
with steep sides and a pointed or rounded top, and higher than a &%Hill.")

(=>
  (instance ?Mountain Mountain)
  (exists (?Incline)
    (and
      (instance ?Incline SlopedArea)
      (attribute ?Incline SteepTerrain)
      (part ?Incline ?Mountain))))

(groundSurfaceType Mountain Rock)
(groundSubsurfaceType Mountain Rock)

(subclass Hill LandForm)
(subclass Hill UplandArea)

(documentation Hill EnglishLanguage "A &%Hill is a raised part of the earth's surface with
sloping sides - an old mountain which because of erosion has become shorter
and more rounded.")

(=>
  (instance ?Hill Hill)
  (exists (?Incline)
    (and
      (instance ?Incline SlopedArea)
      (part ?Incline ?Hill))))

(subclass Volcano LandForm)

(documentation Volcano EnglishLanguage "A &%Volcano in the broadest sense, i.e., a region
containing a vent through which magmous and/or pyroclastic materials are
passed from the interior of the Earth to its surface (atmospheric or
underwater).")

(subclass VolcanoStatus InternalAttribute)
(instance VolcanicallyActive VolcanoStatus)
(instance VolcanicallyDormant VolcanoStatus)
(instance VolcanicallyExtinct VolcanoStatus)
(contraryAttribute VolcanicallyActive VolcanicallyDormant VolcanicallyExtinct)
(exhaustiveAttribute VolcanoStatus VolcanicallyActive VolcanicallyDormant VolcanicallyExtinct)

(=>
  (instance ?event VolcanicEruption)
  (exists (?volcano)
    (and
      (instance ?volcano Volcano)
      (agent ?event ?volcano))))

(subclass VolcanicMountain Mountain)
(subclass VolcanicMountain Volcano)

(documentation VolcanicMountain EnglishLanguage "A &%VolcanicMountain is a cone_shaped
mountain formed out of rock or ash thrown up from inside the earth,
frequently with an opening or depression at the top.")

(subclass VolcanicCone Hill)
(subclass VolcanicCone Volcano)

(documentation VolcanicCone EnglishLanguage "A &%VolcanicCone is a hill of lava or
pyroclastics surrounding a volcanic vent.  Not as high as a
&%VolcanicMountain.")

(subclass WatershedDivide LandArea)
(subclass WatershedDivide UplandArea)

(documentation WatershedDivide EnglishLanguage "A &%WatershedDivide is a summit area, or
narrow tract of higher ground that constitutes the watershed boundary
between two adjacent drainage basins, it divides the surface waters that
flow naturally in one direction from those that flow in the opposite
direction.")

(subclass SlopedArea LandForm)

(documentation SlopedArea EnglishLanguage "A &%SlopedArea is a land surface which lies at
an angle to the horizontal so that some points on it are higher than
others, a slope.")

(=>
  (instance ?incline SlopedArea)
  (exists (?top ?bottom ?height1 ?height2 ?U)
    (and
      (top ?top ?incline)
      (bottom ?bottom ?incline)
      (instance ?U UnitOfLength)
      (earthAltitude ?top
        (MeasureFn ?height1 ?U))
      (earthAltitude ?bottom
        (MeasureFn ?height2 ?U))
      (greaterThan ?height1 ?height2))))

; (successorAttributeClosure ?height2 ?height1) ;; needed?
; deleted the above line because ?height2 and ?height1 are not Attributes

(subclass Cliff SlopedArea)
(documentation Cliff EnglishLanguage "A &%Cliff is any high, very_steep_to_perpendicular
or overhanging face of rock or earth, a precipice.")

(=>
  (instance ?cliff Cliff)
  (exists (?slope)
    (and
      (slopeGradient ?cliff ?slope)
      (greaterThan ?slope 0.60)    ;; or more?
      (greaterThan 1.20 ?slope))))  ;; or less?

(subclass Plateau LandForm)
(subclass Plateau UplandArea)
(documentation Plateau EnglishLanguage "A &%Plateau is a flat upland area with one steep
face, elevated plain.")

(=>
  (and
    (instance ?Plateau Plateau)
    (top ?Top ?Plateau))
  (attribute ?Top FlatTerrain))

(=>
  (instance ?plateau Plateau)
  (exists (?slope)
    (and
      (instance ?slope SlopedArea)
      (attribute ?slope SteepTerrain)
      (overlapsSpatially ?slope ?plateau))))

(subclass Mesa LandForm)
(subclass Mesa UplandArea)
(documentation Mesa EnglishLanguage "A &%Mesa is a land formation having a relatively flat
top and steep rock walls.")

(=>
  (and
    (instance ?Mesa Mesa)
    (top ?Top ?Mesa))
  (attribute ?Top FlatTerrain))

(=>
  (and
    (instance ?Mesa Mesa)
    (side ?Side ?Mesa))
  (attribute ?Side SteepTerrain))

(subclass Butte UplandArea)
(subclass Butte LandForm)
(documentation Butte EnglishLanguage "A &%Butte is an &%Upland raised sharply from the
surrounding region.  Smaller in area than a &%Mesa.")

(=>
  (and
    (instance ?butte Butte)
    (side ?side ?butte))
  (attribute ?side SteepTerrain))

(=>
  (and
    (instance ?butte Butte)
    (top ?top ?butte))
  (attribute ?top FlatTerrain))

(=>
  (and
    (instance ?butte Butte)
    (instance ?mesa Mesa)
    (linearExtent ?butte (MeasureFn ?size1 ?unit))
    (linearExtent ?mesa (MeasureFn ?size2 ?unit)))
  (greaterThan ?size2 ?size1))

(subclass Piedmont LandArea)
(documentation Piedmont EnglishLanguage "A &%Piedmont is a &%LandArea at the foot of mountains.")

(=>
    (instance ?piedmont Piedmont)
    (exists (?mountains)
        (and
            (instance ?mountains MountainousTerrain)
            (orientation ?piedmont ?mountains Adjacent))))

(=>
    (and
        (instance ?piedmont Piedmont)
        (attribute ?mountains MountainousTerrain)
        (orientation ?piedmont ?mountains Adjacent)
        (instance ?U UnitOfLength)
        (height ?piedmont
          (MeasureFn ?height1 ?U))
        (height ?mountains
          (MeasureFn ?height2 ?U)))
    (greaterThan ?height2 ?height1))

(subclass Plain LowlandArea)
(documentation Plain EnglishLanguage "A &%Plain is a broad, flat or gently rolling area,
usually low in elevation.")

(=>
  (instance ?plain Plain)
  (attribute ?plain FlatTerrain))

(subclass Valley LandForm)
(subclass Valley LowlandArea)
(documentation Valley EnglishLanguage "A &%Valley is an area of low_lying land flanked by
higher ground. Valleys typically contain a stream or river flowing along the valley floor.")

(=>
  (and
    (instance ?valley Valley)
    (instance ?area LandArea)
    (instance ?U UnitOfLength)
    (earthAltitude ?valley
      (MeasureFn ?height1 ?U))
    (earthAltitude ?area
      (MeasureFn ?height2 ?U))
    (orientation ?valley ?area Adjacent))
  (greaterThan ?height2 ?height1))

(subclass Basin LandForm)
(documentation Basin EnglishLanguage "A &%Basin is an area of land enclosed or partially
enclosed by higher land.")

(=>
  (and
    (instance ?basin Basin)
    (instance ?area LandArea)
    (instance ?U UnitOfLength)
    (earthAltitude ?basin
      (MeasureFn ?height1 ?U))
    (earthAltitude ?area
      (MeasureFn ?height2 ?U))
    (orientation ?basin ?area Adjacent))
  (greaterThan ?height2 ?height1))

(subclass Canyon LandForm)
(documentation Canyon EnglishLanguage "A &%Canyon is a narrow valley with steep sides, usually
created by erosion.")

(=>
  (instance ?Canyon Canyon)
  (exists (?Hole)
    (hole ?Hole ?Canyon)))
(=>
  (instance ?Canyon Canyon)
  (exists (?Erosion)
            (and
          (instance ?Erosion Erosion)
          (result ?Erosion ?Canyon))))

;; Ground and soil types:
(instance groundSurfaceType BinaryPredicate)
(documentation groundSurfaceType EnglishLanguage "(%&groundSurfaceType ?area ?substance) means
that the predominant &%Substance at the ground surface of the &%LandArea ?area is ?substance.")
(domain groundSurfaceType 1 LandArea)
(domainSubclass groundSurfaceType 2 Substance)

(instance groundSubsurfaceType BinaryPredicate)
(documentation groundSubsurfaceType EnglishLanguage "(&%groundSubsurfaceType ?area ?subsoil)
means that the predominant &%Substance in the subsoil or subsurface layer of
the &%LandArea ?area is of type ?subsoil.")
(domain groundSubsurfaceType 1 LandArea)
(domainSubclass groundSubsurfaceType 2 Substance)

(=>
  (and
    (instance ?area LandArea)
    (groundSurfaceType ?area ?type1)
    (groundSubsurfaceType ?area ?type2))
  (exists (?above ?below)
    (and
      (instance ?above ?type1)
      (instance ?below ?type2)
      (orientation ?above ?below Above))))

;; KJN: Moving this to MILO to remove multiple dependencies
;;(subclass Soil Mixture)
;;(documentation Soil EnglishLanguage "&%Soil is a substance composed of fine rock material
;;disintegrated by geological processes, mixed with humus, the organic remains of decomposed vegetation.")

;;(=>
;;  (instance ?Soil Soil)
;;  (exists (?Humus ?Mineral)
;;    (and
;;      (instance ?Humus Humus)
;;      (instance ?Mineral Mineral)
;;      (part ?Humus ?Soil)
;;      (part ?Mineral ?Soil))))

;;(subclass Humus Mixture)
;;(documentation Humus EnglishLanguage "&%Humus is decaying organic matter found in &%Soil
;;and derived from dead animal and plant material.")

;;(=>
;;  (instance ?Humus Humus)
;;  (exists (?Soil)
;;    (and
;;      (instance ?Soil Soil)
;;      (part ?Humus ?Soil))))

(subclass SoilSolution LiquidMixture)
(documentation SoilSolution EnglishLanguage "&%SoilSolution is the liquid component of soils,
which is largely water containing a number of mineral substances in solution,
as well as comparatively large amounts of dissolved oxygen and carbon dioxide.")

(=>
  (instance ?Solution SoilSolution)
  (exists (?Soil)
    (and
      (instance ?Soil Soil)
      (part ?Solution ?Soil))))

(subclass Loam Soil)
(documentation Loam EnglishLanguage "Type of soil intermediate in texture between clay and sand,
consisting of a mixture of clay, sand, gravel, silt, and organic matter.")

(=>
  (instance ?Loam Loam)
  (exists (?Clay ?Sand ?Gravel ?Silt ?Organic)
    (and
      (instance ?Clay Clay)
      (piece ?Clay ?Loam)
      (instance ?Gravel Gravel)
      (piece ?Gravel ?Loam)
      (instance ?Sand Sand)
      (piece ?Sand ?Loam)
      (instance ?Silt Silt)
      (piece ?Silt ?Loam)
      (instance ?Organic BodySubstance)
      (piece ?Organic ?Loam))))

(=>
  (and
    (instance ?area GeographicArea)
    (groundSurfaceType ?area Loam))
  (attribute ?area FertileTerrain))

(subclass Sand Soil)

(documentation Sand EnglishLanguage "&%Sand is a &%Mixture of loose fragments of
&%Minerals or &%Rocks. Smaller than pebbles and larger than &%Silt and &%Clay (in the
Wentworth Scale), sand particles range from 0.062 &%Millimeter to 2 &%Millimeter in
&%approximateDiameter. &%Sand is formed by %Erosion of rocks through the action of &%ater,
&%Ice or &%Air.")

(documentation Sand ChineseLanguage "&%Sand 是由松散零碎的 &%Mineral 或 &%Rock 组成的
&%Mixture。(溫特瓦分級)沙粒比卵石小而比 &%Silt 和 &%Clay 大，它的 &%approximateDiameter 在
0.062 &%Millimeter 到 2 &%Millimeter 之间。&%Sand 是石头受水、冰或空气的 %Erosion 所产生的。")

(termFormat ChineseLanguage Sand "沙")

(=>
  (and
    (part ?Particle ?Soil)
    (instance ?Soil Sand)
    (approximateDiameter ?Particle
      (MeasureFn ?Size Millimeter)))
  (and
    (greaterThan 2.0 ?Size)
    (greaterThan ?Size 0.062)))

(subclass Dune Hill)

(documentation Dune EnglishLanguage "A dune is a landform composed of &%Wind - or
&%Water -driven &%Sand. It typically takes the form of a mound, ridge, or &%Hill.
Dunes are most common in &%Desert environments, where the lack of moisture hinders the growth of
vegetation that would otherwise interfere with the development of dunes. However,
sand deposits are not restricted to deserts, and dunes are also found along sea
&%ShoreAreas, along &%SteamWaterAreas in semiarid climates, in areas of glacial outwash, and in
other areas where poorly cemented sandstone bedrock disintegrates to produce an
ample supply of loose sand. Dunes are generally defined as greater than 7 cm tall [from Wikipedia].")
(termFormat EnglishLanguage Dune "dune")

(=>
  (instance ?D Dune)
  (material Sand ?D))

(=>
  (and
    (instance ?D Dune)
    (height ?D (MeasureFn ?N Centimeter)))
  (greaterThan ?N 7.0))

(documentation Silt EnglishLanguage "&%Silt is sediment carried along by flowing
water and deposited at a bend in a river or at a river's opening. Smaller than
&%Sand and larger than &%Clay (on the Wentworth Scale), silt particles range
from 0.004 &%Millimeters to 0.062 Millimeters.")

(documentation Silt ChineseLanguage "&%Silt 是由水流冲积于河口或河流弯曲处的沉积物。(溫特瓦分級)
淤泥比砂小而比 &%Clay 大，它的 &%approximateDiameter 在0.004 &%Millimeter 到 0.062 &%Millimeter
之间。")

(termFormat ChineseLanguage Silt "淤泥")

(subclass Silt Soil)

(=>
  (and
    (part ?Particle ?Soil)
    (instance ?Soil Silt)
    (approximateDiameter ?Particle
      (MeasureFn ?Size Millimeter)))
  (and
    (greaterThan 0.062 ?Size)
    (greaterThan ?Size 0.004)))

(documentation Gravel EnglishLanguage "&%Gravel is a mixture of &Pebbles which
are between 2 &%Millimeter to 64 &%Millimeter in &%approximateDiameter in the
Wentworth Scale.")

(documentation Gravel ChineseLanguage "&%Gravel 是由 &Pebble 组成的混合物，按照溫特瓦分級，它的
&%approximateDiameter 在 2 &%Millimeter 和 256 &%Millimeter 之间。")

(termFormat EnglishLanguage Gravel "gravel")
(termFormat ChineseLanguage Gravel "砾石")

(subclass Gravel Mixture)

(=>
  (instance ?GRAVEL Gravel)
  (exists (?P)
    (and
      (part ?P ?GRAVEL)
      (instance ?P Pebble))))

(subclass Marble Rock)
(documentation Marble EnglishLanguage "A metamorphic &%Rock that is used in &%Constructing
and in creating &%Sculptures.")

;; KJN: Moving this to MILO to remove dependencies
;;(subclass Rock Substance)
;;(documentation Rock EnglishLanguage "&%Rock is any naturally formed aggregate of one or more
;;minerals, consolidated or not, with some degree of mineralogic and chemical
;;constancy, in popular use the term is usually restricted to those aggregates
;;that are hard, compact, and coherent.")

;;(=>
;;  (instance ?Rock Rock)
;;  (attribute ?Rock Solid))

;;(=>
;;  (instance ?Rock Rock)
;;  (exists (?Mineral)
;;    (and
;;      (instance ?Mineral Mineral)
;;      (part ?Mineral ?Rock))))

(subclass Stone Rock)
(documentation Stone EnglishLanguage "A &%Stone is any small fragment of rock or mineral matter.")

;(=>
;  (instance ?Stone Stone)
;  (relativeSize ?Stone Small Rock))

;;-------------------------------------------------------------------------

;; D. Water Areas
;;     1. Oceans & Seas
;;     2. Tides & Currents
;;     3. Water Subregions
;;     4. Fresh Water Areas

;; Defined in Merge.txt:
;;  WaterArea, StreamWaterArea, SaltWaterArea, and FreshWaterArea

;;  1. Oceans & Seas

(subclass BodyOfWater WaterArea)
(subclass BodyOfWater SelfConnectedObject)

(documentation BodyOfWater EnglishLanguage "A &%BodyOfWater is a connected body of
water with established boundaries marked by either geographical features
or conventional borders.")

(instance WorldOcean SaltWaterArea)
(instance WorldOcean BodyOfWater)
(names "world ocean" WorldOcean)
(names "the ocean" WorldOcean)

(documentation WorldOcean EnglishLanguage "The &%WorldOcean is the collective mass of
sea water that covers 70% of the surface of &%PlanetEarth, surrounding
all of its dry land areas.  Earth's individual &%Oceans are parts of
the &%WorldOcean.")

(=>
  (instance ?AREA Continent)
  (meetsSpatially ?AREA WorldOcean))

(subclass Ocean SaltWaterArea)
(subclass Ocean BodyOfWater)

(documentation Ocean EnglishLanguage "&%Ocean is the class containing the oceans
that are the major subdivisions of the &%WorldOcean.  According to
the International Hydrographic Association, there are five oceans:
the &%AtlanticOcean, &%PacificOcean, &%IndianOcean, &%SouthernOcean,
and &%ArcticOcean.  Note: The largest oceans, the Atlantic and Pacific,
are subdivided into Northern and Southern regions, but those regions
are not separate &%Oceans.")

(=>
  (and
    (instance ?WATER BodyOfWater)
    (not (instance ?WATER Ocean))
    (instance ?OCEAN Ocean))
  (larger ?OCEAN ?WATER))

(=>
  (instance ?OCEAN Ocean)
  (properPart ?OCEAN WorldOcean))

(instance AtlanticOcean Ocean)
(names "Atlantic Ocean" AtlanticOcean)
(documentation AtlanticOcean EnglishLanguage "&%AtlanticOcean represents the Atlantic
Ocean.")

(instance NorthAtlanticOcean SaltWaterArea)
(instance NorthAtlanticOcean BodyOfWater)
(names "North Atlantic Ocean" NorthAtlanticOcean)
(names "North Atlantic" NorthAtlanticOcean)

(geographicSubregion NorthAtlanticOcean AtlanticOcean)
(geographicSubregion NorthAtlanticOcean NorthernHemisphere)
(geographicSubregion NorthAtlanticOcean WesternHemisphere)

(documentation NorthAtlanticOcean EnglishLanguage
"&%NorthAtlanticOcean denotes the northern &%geographicSubregion
of the &%AtlanticOcean.")

(instance SouthAtlanticOcean SaltWaterArea)
(instance SouthAtlanticOcean BodyOfWater)
(names "South Atlantic Ocean" SouthAtlanticOcean)
(names "South Atlantic" SouthAtlanticOcean)

(geographicSubregion SouthAtlanticOcean AtlanticOcean)
(geographicSubregion SouthAtlanticOcean SouthernHemisphere)
(partlyLocated SouthAtlanticOcean WesternHemisphere)
(partlyLocated SouthAtlanticOcean EasternHemisphere)

(documentation SouthAtlanticOcean EnglishLanguage
"&%SouthAtlanticOcean denotes the southern &%geographicSubregion
of the &%AtlanticOcean.")

(instance PacificOcean Ocean)
(names "Pacific Ocean" PacificOcean)
(documentation PacificOcean EnglishLanguage "&%PacificOcean represents the Pacific
Ocean.")

(instance NorthPacificOcean SaltWaterArea)
(instance NorthPacificOcean BodyOfWater)
(names "North Pacific Ocean" NorthPacificOcean)
(names "North Pacific" NorthPacificOcean)

(geographicSubregion NorthPacificOcean PacificOcean)
(geographicSubregion NorthPacificOcean NorthernHemisphere)
(partlyLocated NorthPacificOcean EasternHemisphere)
(partlyLocated NorthPacificOcean WesternHemisphere)

(documentation NorthPacificOcean EnglishLanguage
"&%NorthPacificOcean denotes the northern &%geographicSubregion
of the &%PacificOcean.")

(instance NortheasternPacificOcean SaltWaterArea)
(instance NortheasternPacificOcean BodyOfWater)
(names "Northeastern Pacific Ocean" NortheasternPacificOcean)
(names "Northeastern Pacific" NortheasternPacificOcean)

(geographicSubregion NortheasternPacificOcean NorthPacificOcean)
(partlyLocated NortheasternPacificOcean NorthernHemisphere)
(partlyLocated NortheasternPacificOcean EasternHemisphere)

(documentation NortheasternPacificOcean EnglishLanguage
"&%NortheasternPacificOcean denotes the northeastern &%geographicSubregion
of the &%NorthPacificOcean.")

(instance NorthwesternPacificOcean SaltWaterArea)
(instance NorthwesternPacificOcean BodyOfWater)
(names "Northwestern Pacific Ocean" NorthwesternPacificOcean)
(names "Northwestern Pacific" NorthwesternPacificOcean)

(geographicSubregion NorthwesternPacificOcean NorthPacificOcean )
(partlyLocated NorthwesternPacificOcean NorthernHemisphere)
(partlyLocated NorthwesternPacificOcean WesternHemisphere)

(documentation NorthwesternPacificOcean EnglishLanguage
"&%NorthwesternPacificOcean denotes the northwestern &%geographicSubregion
of the &%NorthPacificOcean.")

(instance SouthPacificOcean SaltWaterArea)
(instance SouthPacificOcean BodyOfWater)
(names "South Pacific Ocean" SouthPacificOcean)
(names "South Pacific" SouthPacificOcean)

(geographicSubregion SouthPacificOcean PacificOcean)
(geographicSubregion SouthPacificOcean SouthernHemisphere)
(partlyLocated SouthPacificOcean EasternHemisphere)
(partlyLocated SouthPacificOcean WesternHemisphere)

(documentation SouthPacificOcean EnglishLanguage
"&%SouthPacificOcean denotes the southern &%geographicSubregion
of the &%PacificOcean.")

(instance IndianOcean Ocean)
(names "Indian Ocean" IndianOcean)

(documentation IndianOcean EnglishLanguage "&%IndianOcean represents the Indian Ocean.")

(connected IndianOcean SouthPacificOcean)
(connected IndianOcean PersianGulf)
(connected IndianOcean SouthernOcean)

(instance ArcticOcean Ocean)
(names "Arctic Ocean" ArcticOcean)

(documentation ArcticOcean EnglishLanguage "&%ArcticOcean represents the Arctic Ocean.")

(geographicSubregion ArcticOcean NorthernHemisphere)
(partlyLocated ArcticOcean EasternHemisphere)
(partlyLocated ArcticOcean WesternHemisphere)

(=>
  (and
    (instance ?SEA Ocean)
    (not (equal ?SEA ArcticOcean)))
  (smaller ArcticOcean ?SEA))

(instance SouthernOcean Ocean)
(names "Southern Ocean" SouthernOcean)

(documentation SouthernOcean EnglishLanguage "&%SouthernOcean represents the Southern
Ocean.")

(geographicSubregion SouthernOcean SouthernHemisphere)
(partlyLocated SouthernOcean EasternHemisphere)
(partlyLocated SouthernOcean WesternHemisphere)

(totalArea SouthernOcean
  (MeasureFn 20327000.0 SquareKilometer))

(objectGeographicCoordinates
  (GeographicCenterFn SouthernOcean)
  (LatitudeFn South
    (MeasureFn 65.0 AngularDegree)
    (MeasureFn 0.0 ArcMinute))
  (LongitudeFn East
    (MeasureFn 0.0 AngularDegree)
    (MeasureFn 0.0 ArcMinute)))

(length
  (ShorelineFn SouthernOcean Antarctica)
  (MeasureFn 17968.0
    (KiloFn Meter)))

(comparativeArea SouthernOcean greaterThan 2.0 UnitedStates)
(naturalHazardTypeInArea SouthernOcean CyclonicStorm)
(naturalHazardTypeInArea SouthernOcean Iceberg)
(naturalHazardTypeInArea SouthernOcean SeaIce)

(attribute SouthernOcean PolarClimateZone)

(elevation
  (ElevationHighPointFn SouthernOcean)
  (MeasureFn 0.0 Meter))

(elevation
  (ElevationLowPointFn SouthernOcean)
  (MeasureFn -7235.0 Meter))

(instance SouthSandwichTrench HoleRegion)
(geographicSubregion SouthSandwichTrench SouthernOcean)

(instance AntarcticCircumpolarCurrent WaterMotion)
(located AntarcticCircumpolarCurrent SouthernOcean)

(=>
  (and
    (origin AntarcticCircumpolarCurrent ?START)
    (destination AntarcticCircumpolarCurrent ?FINISH))
  (distance ?START ?FINISH
    (MeasureFn 21000.0
      (KiloFn Meter))))

(instance OpenSea Attribute)

(documentation OpenSea EnglishLanguage "&%OpenSea is the &%Attribute of a &%WaterArea
that is open ocean beyond five &%NauticalMiles of land, also known as
'blue water'.")

(<=>
  (attribute ?WATER OpenSea)
  (exists (?LAND ?DIST)
    (and
      (instance ?WATER SaltWaterArea)
      (not
        (instance ?WATER LandlockedWater))
      (distance ?LAND ?WATER
        (MeasureFn ?DIST NauticalMile))
      (greaterThan ?DIST 5.0))))

(subclass Iceberg Ice)
(subclass Iceberg SelfConnectedObject)
;(subclass Iceberg (ExtensionFn Solid))

(documentation Iceberg EnglishLanguage "An &%Iceberg is a large chunk of &%Ice that has
broken off from a &%Glacier and fallen into a &%BodyOfWater. The larger &%part of
an &%Iceberg floats underwater.")
(documentation Iceberg ChineseLanguage "&%Iceberg 是一大块 &%Ice，它从&%Glacier
脱落丢进 &%BodyOfWater。大部分 (&%part)的 &%Iceberg 悬浮在水中。")

(=>
  (instance ?BERG Iceberg)
  (exists (?SHEET)
    (and
      (subclass ?SHEET Glacier)
      (holdsDuring
        (ImmediatePastFn
          (WhenFn ?BERG))
        (material ?SHEET ?BERG)))))

(=>
  (instance ?BERG Iceberg)
  (exists (?PART ?WATER)
    (and
      (instance ?PART Ice)
      (most ?PART ?BERG)
      (instance ?WATER BodyOfWater)
      (covers ?WATER ?PART))))

(=>
  (and
    (instance ?BERG Iceberg)
    (instance ?SHIP WaterVehicle))
  (maritimeHazard ?BERG ?SHIP))

(subclass Sea SaltWaterArea)
(subclass Sea BodyOfWater)

(documentation Sea EnglishLanguage "&%Sea is the class of smaller subdivisions of
the &%WorldOcean, typically partially surrounded by land.  However,
for inland salt water bodies that are sometimes called 'Sea', see
&%SaltLake.")

(=>
  (and
    (instance ?SEA Sea)
    (top ?TOP ?SEA))
  (altitude ?TOP SeaLevel
    (MeasureFn 0.0 Meter)))

(=>
  (and
    (instance ?SEA Sea)
    (top ?TOP ?SEA))
  (altitude ?TOP SeaLevel
    (MeasureFn 0.0 FootLength)))

(=>
  (instance ?SEA Sea)
  (properPart ?SEA WorldOcean))

(=>
  (instance ?SEA Sea)
  (exists (?OCEAN ?PATH)
    (and
      (instance ?OCEAN Ocean)
      (instance ?PATH WaterArea)
      (connects ?PATH ?OCEAN ?SEA))))

(=>
  (instance ?SEA Sea)
  (exists (?LAND)
    (and
      (instance ?LAND LandArea)
      (meetsSpatially ?LAND ?SEA))))

(instance MediterraneanSea Sea)
(documentation MediterraneanSea EnglishLanguage "&%MediterraneanSea is a sea connected to
the Atlantic Ocean, surrounded by the Mediterranean basin and almost completely enclosed
by land: on the east by the Levant in West Asia, on the north by Anatolia in West Asia
and Southern Europe, on the south by North Africa, and on the west almost by the
Morocco–Spain border. The Mediterranean Sea covers an area of about 2,500,000 km2 (970,000 sq mi),
representing 0.7% of the global ocean surface, but its connection to the Atlantic via the
Strait of Gibraltar—the narrow strait that connects the Atlantic Ocean to the Mediterranean Sea
and separates the Iberian Peninsula in Europe from Morocco in Africa—is only 14 km (9 mi) wide.
[Wikipedia]")

(totalArea MediterraneanSea (MeasureFn 2500000 SquareKilometer))
(orientation MediterraneanSea MediterraneanRegion Surrounded)
(climateTypeInArea MediterraneanSea MediterraneanClimateZone)

(instance CaribbeanSea Sea)
(instance BlackSea Sea)
(instance RedSea Sea)
(meetsSpatially Egypt RedSea)
(meetsSpatially Sudan RedSea)
(meetsSpatially Eritrea RedSea)
(meetsSpatially SaudiArabia RedSea)

(instance BeringSea Sea)
(instance BalticSea Sea)
(instance ArabianSea Sea)
(instance NorthSea Sea)

;; Sea Level

(instance SeaLevel GeographicArea)
(documentation SeaLevel EnglishLanguage "&%SeaLevel designates the global Mean Sea Surface
(MSS).  Note that &%SeaLevel represents a &%GeographicArea that comprises all
the local Mean Sea Level (MSL) areas used as references for measuring altitude.
In fact, there are local variations in MSL, due to tides and long- and short-
term weather conditions.")

;; Since there are local variations in MSL, &%SeaLevel exhibits local variations
;; from an imaginary level reference ellipsoid called the geoid.
;; the level of the surface of the sea relative to the land, halfway between high
;; and low tide, used as a standard in calculating elevation. The reference is not
;; precise, as sea level varies by region as well as by tide. assoc. with "sea level"

(elevation SeaLevel
  (MeasureFn 0.0 Meter))

(=>
  (and
    (instance ?ocean Ocean)
    (surface ?top ?ocean))
  (elevation ?top
    (MeasureFn 0.0 Meter)))

(=>
  (and
    (instance ?ocean Ocean)
    (surface ?top ?ocean))
  (elevation ?top
    (MeasureFn 0.0 FootLength)))


(instance meanSeaLevel BinaryPredicate)
(documentation meanSeaLevel EnglishLanguage "(&%meanSeaLevel ?place ?measure) means that the Mean Sea
Level (MSL) at ?place is ?measure.  ?measure is a &%distance representing the sea level
at ?place measured against a benchmark (and averaged over a long time).  MSL is measured
by tide gauges for maritime purposes, while geodesists use the difference between the
local sea surface and an ideal level ocean surface (the geoid).")
(domain meanSeaLevel 1 GeographicArea)
(domain meanSeaLevel 2 LengthMeasure)

; See http://www.pol.ac.uk/psmsl/puscience/index.html#2.

(instance DatumFn UnaryFunction)
(domain DatumFn 1 GeographicArea)
(range DatumFn LengthMeasure)
(documentation DatumFn EnglishLanguage "(&%DatumFn ?place) denotes the point of MLLW (Mean Lower Low
Water) used as the initial data point for a coastal or &%WaterArea given on a nautical
chart.  High and low tides are calculated with reference to this point.")

;;----------------------------------

;;  2. Tides & Currents

;; Moving this to MILO to remove dependencies
;;(subclass FlowRegion Region)

;;(documentation FlowRegion EnglishLanguage "&%FlowRegion is a class of things whose
;;boundaries are relatively stable but whose constitutive &%material is continuously moving
;;through the region itself and being replaced by
;;other, similar material.  Each &%FlowRegion is constituted by a stream
;;of matter moving as a whole.  A &%FlowRegion may be liquid or gaseous.
;;A wind may be considered as a &%Process or as a &%FlowRegion, similarly
;;an OceanCurrent or a &%WaterWave.  The motion process associated with a
;;&%FlowRegion F is denoted by (&%FlowFn F).  Note that certain
;;properties belong to the &%FlowRegion itself (e.g., mass, length, volume,
;;temperature, and speed or velocity of the region moving as a whole),
;;while other properties of interest belong to the &%Motion of its
;;constitutive stuff (e.g., velocity, direction).  The motion of
;;a &%FlowRegion as a whole (e.g., &%JetStream moves within the atmosphere)
;;is distinguished from the motion of the &%pieces of stuff constituting the
;;&%FlowRegion.  See &%FlowFn and &%FlowRegionFn.")

;;(=>
;;  (and
;;    (instance ?FR FlowRegion)
;;    (located ?FLUID ?FR))
;;  (attribute ?FLUID Fluid))

(instance FlowFn UnaryFunction)
(domain FlowFn 1 FlowRegion)
(range FlowFn Motion)

(documentation FlowFn EnglishLanguage "(&%FlowFn ?FLUID) denotes the &%Motion process
associated with the constitutive &%pieces of the &%FlowRegion ?FLUID.")

(=>
  (and
    (measure (FlowFn ?FLUID) (SpeedFn ?LENGTH ?TIME))
    (piece ?PART ?FLUID))
  (piece ?PART (SpeedFn ?LENGTH ?TIME)))

(=>
  (and
    (measure (FlowFn ?FLUID) (MeasureFn ?NUM KnotUnitOfSpeed))
    (piece ?PART ?FLUID))
  (piece ?PART (MeasureFn ?NUM KnotUnitOfSpeed)))

(=>
  (and
    (measure (FlowFn ?FLUID) (VelocityFn ?LENGTH ?TIME ?REGION ?DIRECTION))
    (piece ?PART ?FLUID))
  (measure ?PART (VelocityFn ?LENGTH ?TIME ?REGION ?DIRECTION)))

;; if a flow at place x is moving downstream at y mph, then in 1 hr, the constitutive
;; stuff that was at x will be located at a distance y miles downstream from x.
;; Ditto for things moving in, on, or with the flow.

;; a thing moving place x upstream against a flow of y mph, with its own speed of z
;; mph, will be able to travel no more than z_y miles upstream from x in 1 hour

(instance FlowRegionFn UnaryFunction)
(domain FlowRegionFn 1 LiquidMotion)
(range FlowRegionFn Region)

(documentation FlowRegionFn EnglishLanguage "(&%FlowRegionFn ?FLOW) denotes the region
in which the coherent &%LiquidMotion process ?FLOW is occurring.")

(subclass WaterCurrent FlowRegion)
(subclass WaterCurrent WaterArea)

(documentation WaterCurrent EnglishLanguage "&%WaterCurrent is a subclass of &%FlowRegions
consisting of moving water, especially those currents found within a
well_identified area and having an established pattern of movement, such
as an &%OceanCurrent, a &%RipTide, or a &%River.  &%WaterCurrent is
&%disjoint with &%StaticWaterArea.  As a consequence, &%Lakes for example
cannot be &%FlowRegions, however, currents may be present in a lake.")

(disjoint WaterCurrent StaticWaterArea)

(subclass Tide FlowRegion)
(subclass Tide SaltWaterArea)

(documentation Tide EnglishLanguage "A &%Tide is a vertical movement of the water level
in a &%BodyOfWater due to the gravitational attraction between Earth and
the moon.  Tides are diurnally recurrent events.  In most regions with
tides, every day there are two high tides and two low tides.  &%MixedTide
represents a process in which the tides at either extreme are unequal.")

(subclass TidalProcess InternalChange)

(documentation TidalProcess EnglishLanguage "&%TidalProcess is the class of daily
recurring events in which the water level in a &%BodyOfWater rises
and falls with the changing position between Earth and the Moon.")

(=>
  (and
    (instance ?LOW LowTide)
    (instance ?HIGH HighTide)
    (eventLocated ?LOW ?PLACE)
    (eventLocated ?HIGH ?PLACE)
    (instance ?DAY Day)
    (overlapsTemporally ?LOW ?DAY)
    (overlapsTemporally ?HIGH ?DAY))
  (exists (?AMOUNT1 ?AMOUNT2 ?U)
    (and
      (instance ?U UnitOfLength)
      (holdsDuring ?LOW
        (waterDepth ?PLACE
          (MeasureFn ?AMOUNT1 ?U)))
      (holdsDuring ?HIGH
        (waterDepth ?PLACE
          (MeasureFn ?AMOUNT2 ?U)))
      (greaterThan ?AMOUNT2 ?AMOUNT1))))

(subclass LowTide TidalProcess)

(documentation LowTide EnglishLanguage "&%LowTide is the class of &%TidalProcesses that
occur twice a day in marine waters, in which the water level falls below
the mean sea level.")

(subclass LowerLowTide LowTide)

(documentation LowerLowTide EnglishLanguage "&%LowerLowTide is the subclass of &%LowTide
processes that occur in marine waters with a &%MixedTideProcess.")

(subclass HighTide TidalProcess)

(documentation HighTide EnglishLanguage "&%HighTide is the class of &%TidalProcesses that
occur twice a day in marine waters, in which the water level rises above
the mean sea level.")

(subclass HigherHighTide HighTide)

(documentation HigherHighTide EnglishLanguage "&%HigherHighTide is the subclass of
&%HighTide processes that occur in marine waters with a
&%MixedTideProcess.")

(subclass MixedTideProcess TidalProcess)

(documentation MixedTideProcess EnglishLanguage "&%MixedTideProcess is the subclass of
&%TidalProcesses consisting of daily cycles in which the two low and
two high tides are of unequal height.  In areas with this kind of
pattern, the chart (based on average low water) is determined by the
Mean Lower Low Water.")

(instance highTide TernaryPredicate)
(domain highTide 1 Region)
(domain highTide 2 TimeInterval)
(domain highTide 3 LengthMeasure)
(documentation highTide EnglishLanguage "(&%highTide ?PLACE ?TIME ?AMOUNT) means that
there is a &%HighTide at the &$Region ?PLACE at the &%TimeInterval ?TIME
with the relative height ?AMOUNT.  The height is given in relation to
the datum on a standard chart.")

(instance lowTide TernaryPredicate)
(domain lowTide 1 Region)
(domain lowTide 2 TimeInterval)
(domain lowTide 3 LengthMeasure)
(documentation lowTide EnglishLanguage "(&%lowTide ?PLACE ?TIME ?AMOUNT) means that
there is a &%LowTide at the &$Region ?PLACE at the &%TimeInterval ?TIME
with the relative height ?AMOUNT.  The height is given in relation to
the datum on a standard chart.")

(=>
  (and
    (lowTide ?PLACE ?TIME1
      (MeasureFn ?AMOUNT1 ?U))
    (highTide ?PLACE ?TIME2
      (MeasureFn ?AMOUNT2 ?U))
    (instance ?U UnitOfLength)
    (instance ?DAY Day)
    (overlapsTemporally ?TIME1 ?DAY)
    (overlapsTemporally ?TIME2 ?DAY))
  (greaterThan ?AMOUNT1 ?AMOUNT2))

(subclass LittoralCurrent WaterCurrent)

(documentation LittoralCurrent EnglishLanguage "&%LittoralCurrent is the subclass of
&%WaterCurrents that occur near a &%ShoreArea.  Examples include
&%TidalEbb, &%TidalFlow, and &%RipCurrents.")

(subclass TidalFlow LittoralCurrent)

(documentation TidalFlow EnglishLanguage "&%TidalFlow is the subclass of &%WaterCurrents
that are temporary and variable, but regular, currents of ocean water
flowing landward up to and during the time of high tide.  Alternating
with &%TidalEbb.")

(subclass TidalEbb LittoralCurrent)

(documentation TidalEbb EnglishLanguage "&%TidalEbb is the subclass of &%WaterCurrents
that are temporary and variable, but regular, currents of ocean water
flowing seaward up to and during the time of low tide.  Alternating with
&%TidalFlow.")

(subclass TidalBore LittoralCurrent)

(documentation TidalBore EnglishLanguage "A &%TidalBore is an unusally high, fast tidal
inflow from the sea, resulting from a high volume of water rushing from
a wide_mouthed bay or &%Estuary into narrower landward areas.")

(subclass RipCurrent LittoralCurrent)

(documentation RipCurrent EnglishLanguage "A &%RipCurrent is a fast narrow surface current
that flows seaward from a &%ShoreArea.")

; upstream for a RipCurrent is away from the ShoreArea
; found near ShoreArea
; dangerous to people in the water (not in boats)


;;-----------------------------------
;;     3. Water Subregions

(subclass LandlockedWater BodyOfWater)

(documentation LandlockedWater EnglishLanguage "&%LandlockedWater includes water
areas that are surrounded by land, including salt lakes, fresh
water lakes, ponds, reservoirs, and (more or less) wetlands.")

; need a way to say that the body of water is surrounded by land (e.g., perimeter)

(subclass SaltLake SaltWaterArea)
(subclass SaltLake LandlockedWater)
(documentation SaltLake EnglishLanguage "&%SaltLake is the class of landlocked
bodies of salt water, including those referred to as 'Seas', e.g.,
the &%CaspianSea.  But note that the &%MediterraneanSea is a &%Sea.")

(instance CaspianSea SaltLake)
(names "Caspian Sea" CaspianSea)
(instance AralSea SaltLake)
(names "Aral Sea" AralSea)
(instance GreatSaltLake SaltLake)
(names "Great Salt Lake" GreatSaltLake)
(geographicSubregion GreatSaltLake Utah)
(instance DeadSea SaltLake)
(names "Dead Sea" DeadSea)
(instance SaltonSea SaltLake)
(names "Salton Sea" SaltonSea)
(geographicSubregion SaltonSea California)
(instance LakeEyre SaltLake)
(names "Lake Eyre" LakeEyre)
(geographicSubregion LakeEyre Australia)
(instance KokoNor SaltLake)
(names "Koko Nor" KokoNor)
(names "Qinghai Hu" KokoNor)
(instance LakeBakhtegan SaltLake)
(names "Lake Bakhtegan" LakeBakhtegan)
(instance PoopoLake SaltLake)
(names "Poopo Lake" PoopoLake)
(instance LakeMareotis SaltLake)
(names "Lake Mareotis" LakeMareotis)
(names "Lake Maryut" LakeMareotis)
(instance LakeWalker SaltLake)
(names "Lake Walker" LakeWalker)
(instance LakeTorrens SaltLake)
(names "Lake Torrens" LakeTorrens)

(subclass Gulf BodyOfWater)
(subclass Gulf SaltWaterArea)

(documentation Gulf EnglishLanguage "&%Gulf is the class of extensions of a &%Sea or
&%Ocean that reach into a land mass or are partially enclosed by a &%LandArea. A &%Gulf
is typically larger than a &%Bay.")

(=>
  (instance ?GULF Gulf)
  (exists (?SEA)
    (and
         (or
      (instance ?SEA Sea)
      (instance ?SEA Ocean))
         (connected ?GULF ?SEA))))

(=>
  (instance ?GULF Gulf)
  (exists (?WATER)
    (and
      (instance ?WATER SaltWaterArea)
      (properPart ?GULF ?WATER))))

(=>
  (and
    (instance ?GULF Gulf)
    (instance ?BAY Bay))
  (larger ?GULF ?BAY))

(instance PersianGulf Gulf)
(instance PersianGulf SaltWaterArea)
(names "Persian Gulf" PersianGulf)
(connected PersianGulf StraitOfHormuz)
(meetsSpatially Iran PersianGulf)

(instance GulfOfOman Gulf)
(instance GulfOfOman SaltWaterArea)
(names "Gulf of Oman" GulfOfOman)
(connected StraitOfHormuz GulfOfOman)
(connected GulfOfOman ArabianSea)
(meetsSpatially Iran GulfOfOman)
(meetsSpatially Oman GulfOfOman)

(instance GulfOfAden Gulf)
(instance GulfOfAden SaltWaterArea)
(names "Gulf of Aden" GulfOfAden)
(connected RedSea GulfOfAden)
(connected GulfOfAden ArabianSea)
(meetsSpatially Yemen GulfOfAden)
(meetsSpatially Somalia GulfOfAden)

(instance GulfOfMexico Gulf)
(instance GulfOfMexico SaltWaterArea)
(names "Gulf of Mexico" GulfOfMexico)
(connected GulfOfMexico NorthAtlanticOcean)
(connected GulfOfMexico CaribbeanSea)

(instance GulfOfGuinea Gulf)
(documentation GulfOfGuinea EnglishLanguage "The &%GulfOfGuinea is the  northeasternmost
part of the tropical Atlantic Ocean from Cape Lopez in Gabon, north and west to Cape
Palmas in Liberia.[Wikipedia]")
(instance GulfOfGuinea SaltWaterArea)
(termFormat EnglishLanguage GulfOfGuinea "Gulf Of Guinea")
(connected GulfOfGuinea AtlanticOcean)

(instance GulfOfLion Gulf)
(documentation GulfOfLion EnglishLanguage "The &%GulfOfLion is a wide embayment of the Mediterranean
coastline of Catalonia in Spain with Languedoc-Roussillon and Provence in France, extending from
Begur in the west to Toulon in the east.[Wikipedia]")
(instance GulfOfLion SaltWaterArea)
(termFormat EnglishLanguage GulfOfLion "Gulf Of Lion")
(connected GulfOfLion MediterraneanSea)
(orientation MediterraneanSea GulfOfLion South)
(meetsSpatially Spain GulfOfLion)
(meetsSpatially France GulfOfLion)
(partlyLocated MarseillesFrance GulfOfLion)

(instance NullIsland GeographicArea)
(documentation NullIsland EnglishLanguage "&%NullIsland Null Island is the location at
zero degrees latitude and zero degrees longitude (0°N 0°E), i.e., where the prime meridian
and the equator intersect. Since there is no landmass located at these coordinates, it is not
an actual island. The name is often used in mapping software as a placeholder to help find
and correct database entries that have erroneously been assigned the coordinates 0,0.
The point on the Earth's surface defined as Null Island is located in international waters
in the Atlantic Ocean, roughly 600 kilometres (320 nmi) off the coast of West Africa, in
the Gulf of Guinea. A weather buoy, named the Soul buoy after the soul music genre, was
moored at the location.[Wikipedia]")
(instance NullIsland SaltWaterArea)
(termFormat EnglishLanguage NullIsland "null island")
(located NullIsland GulfOfGuinea)
(objectGeographicCoordinates NullIsland
  (MeasureFn 0 AngularDegree)
  (MeasureFn 0 AngularDegree))

(subclass Estuary BodyOfWater)
(subclass Estuary SaltWaterArea)
(subclass Estuary Inlet)

(documentation Estuary EnglishLanguage "&%Estuary is the subclass of &%BodyOfWater that
represents &%WaterAreas where a sea or ocean &%Tide meets a &%River
current.")

(=>
  (instance ?WATER Estuary)
  (exists (?SEA)
    (and
      (instance ?SEA SaltWaterArea)
      (part ?SEA WorldOcean)
      (connected ?SEA ?WATER))))

(=>
  (instance ?WATER Estuary)
  (exists (?RIVER)
    (and
      (instance ?RIVER River)
      (flows ?RIVER ?WATER))))

(=>
  (instance ?WATER Estuary)
  (exists (?MOUTH)
    (and
      (instance ?MOUTH RiverMouth)
      (overlapsSpatially ?MOUTH ?WATER))))

(=>
  (instance ?WATER Estuary)
  (exists (?TIDES)
    (and
      (instance ?TIDES TidalProcess)
      (eventLocated ?TIDES ?WATER))))

;----------------------------------

(subclass Inlet BodyOfWater)

(documentation Inlet EnglishLanguage "&%Inlet is the class of bays or other recesses
into the shore of a lake, sea, or river, includes &%InletPassages, which
are passages leading from open water through some barrier to a bay or
lagoon.")

(=>
  (instance ?INLET Inlet)
  (exists (?LAND)
    (and
      (instance ?LAND LandArea)
      (penetrates ?INLET ?LAND))))

(=>
  (instance ?INLET Inlet)
  (exists (?WATER)
    (and
      (instance ?WATER WaterArea)
      (connected ?INLET ?WATER))))

(=>
  (and
    (instance ?INLET Inlet)
    (meetsSpatially ?INLET ?WATER)
    (instance ?WATER SaltWaterArea))
  (instance ?INLET SaltWaterArea))

(=>
  (and
    (instance ?INLET Inlet)
    (meetsSpatially ?INLET ?WATER)
    (instance ?WATER FreshWaterArea))
  (instance ?INLET FreshWaterArea))

(subclass InletPassage Inlet)

(documentation InletPassage EnglishLanguage "&%InletPassage is the class of water
passages connecting an area of open water to a bay or lagoon, through
some land barrier(s) close on either side.")

(subclass Bay Inlet)  ;; note may occur in Oceans or in Lakes

(documentation Bay EnglishLanguage "&%Bay is the class of extensions of a body of water
(salt or fresh) that reach into the land, usually smaller than a
&%Gulf.")

(subclass Cove Inlet)

(documentation Cove EnglishLanguage "A &%Cove is a small part of a body of water that
reaches into a coast.")

(=>
  (and
    (instance ?COVE Cove)
    (instance ?BAY Bay))
  (larger ?BAY ?COVE))

;;----------------------------------

(subclass Sound BodyOfWater)

(documentation Sound EnglishLanguage "A &%Sound is a long, relatively narrow waterway
lying along the shore of a land mass and protected from open water by
another land area, an island or group of islands.  Some sounds open at
both ends into the same body of water (e.g., Long Island Sound), while
others connect two different bodies of water (e.g., Melville Sound).
A &%Sound may occur in salt or fresh water bodies.  Generally wider than
a &%Strait.")

(=>
  (connects ?BETWEEN ?END1 ?END2)
  (not (equal ?END1 ?END2)))

(subclass Strait BodyOfWater)
(documentation Strait EnglishLanguage "Strait is the subclass of &%BodyOfWater that
consists of narrow water areas connecting two larger bodies of water.
Straits are naturally occurring bodies of water.")

(=>
  (instance ?strait Strait)
  (exists (?body1 ?body2)
    (and
      (instance ?body1 BodyOfWater)
      (instance ?body2 BodyOfWater)
      (connects ?strait ?body1 ?body2))))

(=>
  (and
    (instance ?body1 BodyOfWater)
    (instance ?body2 BodyOfWater)
    (connects ?strait ?body1 ?body2))
  (larger ?body1 ?strait))

(instance StraitOfHormuz Strait)
(instance StraitOfHormuz SaltWaterArea)
(names "Strait of Hormuz" StraitOfHormuz)

(instance StraitOfGibraltar Strait)
(documentation StraitOfGibraltar EnglishLanguage "&%StraitOfGibraltar
is a narrow &%Strait that &%connects the &%AtlanticOcean to the &%MediterraneanSea
and separates &%Europe from &%Africa. The two continents are separated by 7.7
nautical miles (14.2 kilometers, 8.9 miles) at its narrowest point.The strait lies
in the territorial waters of Morocco, Spain, and the British overseas territory of
Gibraltar.[Wikipedia]")
(termFormat EnglishLanguage StraitOfGibraltar "strait of Gibraltar")
(instance StraitOfGibraltar SaltWaterArea)
(names "Strait of Gibraltar" StraitOfGibraltar)
(connects StraitOfGibraltar MediterraneanSea AtlanticOcean)
(connects StraitOfGibraltar Europe Africa)

(subclass Channel BodyOfWater)
(documentation Channel EnglishLanguage "A &%Channel is a narrow deep waterway connecting
two larger bodies of water.  May be natural or dredged, salt or fresh
water.")

(=>
  (instance ?CHANNEL Channel)
  (exists (?WATER1 ?WATER2)
    (and
      (instance ?WATER1 WaterArea)
      (instance ?WATER2 WaterArea)
      (connects ?CHANNEL ?WATER1 ?WATER2))))

(=>
  (and
    (instance ?CHANNEL Channel)
    (instance ?BODY BodyOfWater)
    (connected ?CHANNEL ?BODY))
  (larger ?BODY ?CHANNEL))

;;-----------------------------------

;; 4. Fresh Water Areas

(subclass InlandWaterSystem WaterArea)
(subclass InlandWaterSystem Collection)

(documentation InlandWaterSystem EnglishLanguage "An instance of &%InlandWaterSystem
comprises two or more lakes or rivers, canals, or other waterways that
are interconnected.")

(=>
  (instance ?SYSTEM InlandWaterSystem)
  (exists (?BODY1 ?BODY2)
    (and
      (instance ?BODY1 BodyOfWater)
      (instance ?BODY2 BodyOfWater)
      (not (equal ?BODY1 ?BODY2))
      (geographicSubregion ?BODY1 ?SYSTEM)
      (geographicSubregion ?BODY2 ?SYSTEM))))

;(=>
;  (and
;    (instance ?SYSTEM InlandWaterSystem)
;    (instance ?BODY1 BodyOfWater)
;    (instance ?BODY2 BodyOfWater)
;    (not (equal ?BODY1 ?BODY2))
;    (not (connected ?BODY1 ?BODY2))
;    (part ?BODY1 ?SYSTEM)
;    (part ?BODY2 ?SYSTEM))
;  (exists (?WATER)
;    (and
;      (instance ?WATER WaterArea)
;      (between ?BODY1 ?WATER ?BODY2))))

; really want something more precise (Links correspond to waterways)
;(=>
;  (and
;    (instance ?SYSTEM InlandWaterSystem)
;    (geographicSubregion ?lake1 ?SYSTEM)
;    (geographicSubregion ?lake2 ?SYSTEM))
;  ("generically connected" ?lake1 ?lake2 "by water"))

(=>
  (and
    (instance ?SYSTEM InlandWaterSystem)
    (instance ?WATER1 WaterArea)
    (instance ?WATER2 WaterArea)
    (not (equal ?WATER1 ?WATER2))
    (not (connected ?WATER1 ?WATER2))
    (geographicSubregion ?WATER1 ?SYSTEM)
    (geographicSubregion ?WATER2 ?SYSTEM))
  (exists (?WATER3)
    (and
      (instance ?WATER3 WaterArea)
      (not (equal ?WATER3 ?WATER1))
       (not (equal ?WATER3 ?WATER1))
      (part ?WATER3 ?SYSTEM)
      (connects ?WATER3 ?WATER1 ?WATER2))))

;;------------------------------------

(subclass LakeRegion GeographicArea)

(documentation LakeRegion EnglishLanguage "A &%LakeRegion is a &%GeographicArea
including land surrounding one or more &%Lakes.")

(=>
  (instance ?REGION LakeRegion)
  (exists (?LAKE)
    (and
      (instance ?LAKE Lake)
      (located ?LAKE ?REGION))))

;;-----------------------------------

; StreamWaterArea is defined in Merge.kif (see def.)

(=>
  (instance ?STREAM StreamWaterArea)
  (exists (?WATER)
    (and
      (instance ?WATER BodyOfWater)
      (not (equal ?WATER ?STREAM))
      (flows ?STREAM ?WATER))))

(instance flows BinaryPredicate)
(instance flows AsymmetricRelation)
(instance flows TransitiveRelation)
(domain flows 1 Physical)
(domain flows 2 Physical)

(instance tributary BinaryPredicate)
(documentation tributary EnglishLanguage "A &%Relation between two areas of &%Fluid where one &%flows
into the other and the first argument is an area of smaller width or flow rate.")
(domain tributary 1 Physical)
(domain tributary 2 Physical)
(subrelation tributary flows)


(documentation flows EnglishLanguage "(&%flows ?FLUID1 ?FLUID2) means that the
&%Physical ?FLUID1 moves towards the &%Physical ?FLUID2, to which
it is &%connected.")

; (subrelation flows connected) connected is symmetric so this doesn't work, but rule below does
(=>
  (flows ?FLUID1 ?FLUID2)
  (connected ?FLUID1 ?FLUID2))

(=>
  (flows ?FLUID1 ?FLUID2)
  (orientation ?FLUID1 ?FLUID2 Upstream))

; (=>
;  (and
;    (flows ?STREAM ?WATER)
;    (origin ?STREAM ?SOURCE) ;; bogus - domain 1 violation
;      (orientation ?WATER ?SOURCE ?DIRECTION))
;  (downstream ?STREAM ?DIRECTION))

(instance Upstream PositionalAttribute)

(instance Downstream PositionalAttribute)
(oppositeDirection Upstream Downstream)

(instance connectedDownstream BinaryPredicate)
(domain connectedDownstream 1 Object)
(domain connectedDownstream 2 Object)

(documentation connectedDownstream EnglishLanguage "(&%connectedDownstream ?OBJ1 ?OBJ2)
means that ?OBJ1 is connected, remotely or immediately, with ?OBJ2 along
a directed system such as a &%RiverSystem.")

;;-----------------------------------

(subclass River StreamWaterArea)
(subclass River FreshWaterArea)
(subclass River BodyOfWater)

(documentation River EnglishLanguage "&%River is the class of large streams of fresh
water flowing through land into a lake, ocean, or other body of water.")

; current, direction, upstream and downstream
; rivers flow from higher elevation to lower elevation areas.

;;----------------------------------

(subclass StreamWaterConfluence WaterArea)

(documentation StreamWaterConfluence EnglishLanguage "A &%StreamWaterConfluence is the
place where a stream or other tributary joins a river.")

(=>
  (instance ?FORK StreamWaterConfluence)
  (exists (?STREAM1 ?STREAM2)
    (and
      (instance ?STREAM1 StreamWaterArea)
      (connects ?FORK ?STREAM1 ?STREAM2)
      (instance ?STREAM2 StreamWaterArea)
      (flows ?STREAM1 ?STREAM2))))

(=>
  (instance ?FORK StreamWaterConfluence)
  (exists (?STREAM ?BRANCH)
    (and
      (instance ?STREAM River)
      (instance ?BRANCH StreamWaterArea)
      (flows ?BRANCH ?STREAM)
      (connects ?FORK ?STREAM ?BRANCH))))

;;----------------------------------

(subclass RiverSystem WaterArea)
(subclass RiverSystem Collection)

(documentation RiverSystem EnglishLanguage "A &%RiverSystem comprises all the tributary
streams and rivers (&%StreamWaterAreas) that drain along converging
paths into the main river of the system, which discharges into a
&%StaticWaterArea.")  ;; or a larger river ?

(=>
  (instance ?SYSTEM RiverSystem)
  (exists (?RIVER)
    (and
      (instance ?RIVER River)
      (part ?RIVER ?SYSTEM))))

(=>
  (instance ?SYSTEM RiverSystem)
  (exists (?RIVER ?STATIC)
    (and
      (instance ?RIVER River)
      (part ?RIVER ?SYSTEM)
      (instance ?STATIC StaticWaterArea)
      (connected ?RIVER ?STATIC))))

(=>
  (and
    (instance ?SYSTEM RiverSystem)
    (instance ?STREAM StreamWaterArea)
    (part ?STREAM ?SYSTEM))
  (exists (?RIVER)
    (and
      (instance ?RIVER River)
      (part ?RIVER ?SYSTEM)
      (flows ?STREAM ?RIVER))))  ;; fix this.

;;-----------------------------------------------------------------------

(subclass Waterfall StreamWaterArea)

(documentation Waterfall EnglishLanguage
"&%Waterfall is the subclass of &%StreamWaterAreas where running water
falls steeply downhill.")

(instance flowCurrent BinaryPredicate)
(documentation flowCurrent EnglishLanguage "The relation between &%WaterMotion &%Processes
and the region in which they occur continuously over at least hours, but
typically months or years.  A water balloon bursting and flowing downhill
would not use this relation, because there would be no &%StreamWaterArea
to relate to.")
(domain flowCurrent 1 WaterMotion)
(domain flowCurrent 2 WaterArea)

(=>
  (and
    (instance ?FALL Waterfall)
    (instance ?CURRENT WaterMotion)
    (flowCurrent ?CURRENT ?FALL)
    (origin ?CURRENT ?TOP)
    (destination ?CURRENT ?BOTTOM)
    (instance ?TOP WaterArea)
    (instance ?BOTTOM WaterArea))
  (orientation ?TOP ?BOTTOM Above))

;;-----------------------------------------------------------------------

; water_related artifacts:

(subclass Waterway Transitway)
(subclass Waterway WaterArea)
(documentation Waterway EnglishLanguage "&%Waterway is the class of navigable waters,
including &%Oceans, &%SeaLanes, &%Rivers, &%Canals, &%Lakes, and inland
bodies of water.")

(=>
  (instance ?P Waterway)
  (hasPurpose ?P
    (exists (?T)
      (and
        (instance ?T WaterTransportation)
        (path ?T ?P)))))

;; See definition in Geography.kif.
;; Note: (instance <river> Waterway) asserted only for navigable rivers.

(subclass Canal Waterway)
(subclass Canal StationaryArtifact)
(documentation Canal EnglishLanguage "&%Canal is the subclass of navigable &%Waterways
flowing through an artificial course.  Typically, a canal is a
&%Transitway connecting two bodies of water.")

(=>
  (instance ?CHANNEL Canal)
  (exists (?WATER1 ?WATER2)
    (and
      (instance ?WATER1 WaterArea)
      (instance ?WATER2 WaterArea)
      (connects ?CHANNEL ?WATER1 ?WATER2))))

(subclass CanalStructure StationaryArtifact)

(documentation CanalStructure EnglishLanguage "A &%CanalStructure is the constructed
framework, including &%Locks, that contains the waters of a &%Canal.")

(subclass Dam StationaryArtifact)

(documentation Dam EnglishLanguage "&%Dam is the subclass of &%StationaryArtifacts that
are walls built across a stream or river to hold back water.")

(=>
  (instance ?DAM Dam)
  (exists (?RIVER)
    (and
      (instance ?RIVER WaterArea)
      (traverses ?DAM ?RIVER))))

(subclass Reservoir FreshWaterArea)
(subclass Reservoir Artifact)

(documentation Reservoir EnglishLanguage "A &%Reservoir is an artifically made (or
artifically enlarged) holding area where water is collected and stored
for future use.")

;;--------------------------------

(subclass Rapids StreamWaterArea)

(documentation Rapids EnglishLanguage "&%Rapids is the class of &%WaterAreas that are
parts of a &%StreamWaterArea where the currents move swiftly over
rocks.")

(=>
  (instance ?RAPIDS Rapids)
  (exists (?RIVER)
    (and
      (instance ?RIVER River)
      (part ?RAPIDS ?RIVER))))

(=>
  (and
    (instance ?RAPIDS Rapids)
    (bottom ?BOTTOM ?RAPIDS))
  (exists (?ROCK)
    (and
      (instance ?ROCK Rock)
      (part ?ROCK ?BOTTOM))))

(subclass RiverMouth WaterArea)
(subclass RiverMouth StreamWaterArea)

(documentation RiverMouth EnglishLanguage "&%RiverMouth is the subclass of &%WaterAreas
that are the outfalls of a river or stream into another body of water.")

(=>
  (and
    (instance ?RIVER River)
    (instance ?WATER BodyOfWater)
    (flows ?RIVER ?WATER))
  (exists (?MOUTH)
    (and
      (instance ?MOUTH RiverMouth)
      (streamOutfall ?MOUTH ?RIVER)
      (connected ?MOUTH ?WATER))))

(subclass Creek BodyOfWater)
(subclass Creek StreamWaterArea)
(subclass Creek FreshWaterArea)
(disjoint Creek River)
(documentation Creek EnglishLanguage "Creek is the class of small streams of fresh
water flowing through land, usually into a &%River.")
(=>
  (and
    (instance ?C Creek)
    (instance ?R River))
  (smaller ?C ?R))


(subrelation streamOutfall properPart)
(instance streamOutfall BinaryPredicate)
(domain streamOutfall 1 WaterArea)
(domain streamOutfall 2 River)

(documentation streamOutfall EnglishLanguage "(&%streamOutfall ?WATER ?RIVER) means that
the &%WaterArea ?MOUTH is the outfall of the &%River ?RIVER.")

(=>
  (streamOutfall ?MOUTH ?RIVER)
  (exists (?WATER)
    (and
      (instance ?WATER WaterArea)
      (flows ?RIVER ?WATER)
      (connected ?MOUTH ?WATER))))

(subclass IrrigationChannel Artifact)
(documentation IrrigationChannel EnglishLanguage "&%IrrigationChannel is the class of
artificially created channels used for transporting water to agricultural
fields for &%Irrigating crops.")

(subclass Irrigating Motion)  ;; Translocation (in Transportation)

(documentation Irrigating EnglishLanguage "&%Irrigating is the process of transporting
and applying water to crops by artificial means.")

(=>
  (and
    (instance ?IRRIGATION Irrigating)
    (patient ?IRRIGATION ?AREA)
    (instance ?AREA LandArea))
  (and
    (holdsDuring (ImmediatePastFn (WhenFn ?IRRIGATION)) (attribute ?AREA Dry))
    (holdsDuring (ImmediateFutureFn (WhenFn ?IRRIGATION)) (attribute ?AREA Damp))))

(=>
  (and
    (instance ?IRRIGATION Irrigating)
    (patient ?IRRIGATION ?AREA)
    (instance ?AREA LandArea))
  (hasPurpose ?IRRIGATION (attribute ?AREA FertileTerrain)))

(subclass Lake StaticWaterArea)

(documentation Lake EnglishLanguage "&%Lake is the subclass of &%BodyOfWater whose
instances are naturally occurring static bodies of water surrounded
by land.")

(=>
  (and
    (instance ?LAKE Lake)
    (instance ?OCEAN Ocean))
  (smaller ?LAKE ?OCEAN))

;;-----------------------------------------------------------------------

;;   E. Coastal and Shoreline Areas

(subclass CoastalPlain Plain)

(documentation CoastalPlain EnglishLanguage "&%CoastalPlain is the class of broad plains
areas adjacent to a &%Sea or &%Ocean.  A coastal plain includes a narrower
&%ShoreArea adjacent to a body of water.")

(=>
  (instance ?plain CoastalPlain)
  (exists (?shore)
    (and
      (instance ?shore ShoreArea)
      (part ?shore ?plain))))

(subclass RiverBank ShoreArea)
(subclass RiverBank LandForm)

(documentation RiverBank EnglishLanguage "A &%RiverBank is the &%ShoreArea adjacent to a &%River.")

(=>
  (instance ?bank RiverBank)
  (exists (?river)
    (and
      (instance ?river River)
      (meetsSpatially ?river ?bank))))

(subclass LittoralZone GeographicArea)

(documentation LittoralZone EnglishLanguage "A &%LittoralZone is an area along the shore
of a large body of water, especially an &%Ocean or &%Sea, including the area
extending from the high tide mark out to a depth of 200 meters.  The littoral
zone is of interest for its land features, e.g., slope gradient and soil
composition, including features of its &%SubmergedLandArea.")

(subclass Delta LandForm)

(documentation Delta EnglishLanguage "A &%Delta is a &%LandForm composed of silt or other
alluvium,  deposited at or near the mouth of a river or stream as it enters
a body of relatively static water.  Typically a delta is flat and fan_shaped.")

(=>
  (instance ?Delta Delta)
  (exists (?Mouth)
    (and
      (instance ?Mouth RiverMouth)
      (meetsSpatially ?Mouth ?Delta))))

(=>
  (instance ?Delta Delta)
  (attribute ?Delta FlatTerrain))

(subclass Peninsula LandForm)

(documentation Peninsula EnglishLanguage "A &%Peninsula is a piece of land that extends
into a body of water and is surrounded on three sides by water.  Typically
connected by an &%Isthmus or neck of land that is narrower than its main
portion (contrast with &%Cape).")

(=>
  (instance ?Peninsula Peninsula)
  (exists (?Water)
    (and
      (instance ?Water WaterArea)
      (penetrates ?Peninsula ?Water))))

(subclass Cape LandForm)

(documentation Cape EnglishLanguage "A &%Cape is a piece of land projecting into a body of water.")

(=>
  (instance ?Cape Cape)
  (exists (?WaterRegion)
    (and
      (instance ?WaterRegion WaterArea)
      (penetrates ?Cape ?WaterRegion))))

(subclass Isthmus LandArea)

(documentation Isthmus EnglishLanguage "An &%Isthmus is a narrow strip of land that
connects two larger land masses and is bordered on two sides by water.")

(=>
  (and
    (instance ?Isthmus Isthmus)
    (side ?Leftside ?Isthmus)
    (side ?Rightside ?Isthmus)
            (not (equal ?Leftside ?Rightside)))
  (exists (?Region1 ?Region2 ?Water1 ?Water2)
    (and
      (instance ?Region1 LandArea)
      (instance ?Region2 LandArea)
      (between  ?Region1 ?Isthmus ?Region2)
      (instance ?Water1 WaterArea)
      (instance ?Water2 WaterArea)
      (meetsSpatially ?Leftside ?Water1)
      (meetsSpatially ?Rightside ?Water2))))

(disjoint Island LandlockedArea)

(subclass Archipelago LandForm)
(subclass Archipelago Collection)
(documentation Archipelago EnglishLanguage "An &%Archipelago is a group of islands.")

(=>
  (and
    (instance ?Archipelago Archipelago)
    (component ?Part ?Archipelago))
  (instance ?Part Island))

(subclass ArchipelagicArea GeographicArea)

(documentation ArchipelagicArea EnglishLanguage "An &%ArchipelagicArea is a &%GeographicArea
including an &%Archipelago and the surrounding &%WaterArea.")

(=>
  (instance ?area ArchipelagicArea)
  (exists (?islands)
    (and
      (instance ?islands Archipelago)
      (located ?islands ?area))))

(documentation hazardous EnglishLanguage "&%hazardous is a &%BinaryPredicate which
associates a &%Physical (an &%Object or a &%Process) to the &%Damaging it may bring to
another &%Object. (hazardous ?THING ?OBJECT) means that ?THING may cause damage to ?OBJECT.")
(documentation hazardous ChineseLanguage "&%hazardous 是一种 &%BinaryRelation，它把
一个 &%Physical (&%Object 或 &%Process) 和这个物理可能造成 &%Damaging 的另外一个
&%Object 连系在一起。(hazardous ?THING ?OBJECT) 的月意思是 ?THING 可能为造成 ?OBJECT 损害。")
(domain hazardous 1 Physical)
(domain hazardous 2 Object)
(instance hazardous BinaryPredicate)
(format EnglishLanguage hazardous "%1 is %n &%hazardous to %2")
(termFormat EnglishLanguage hazardous "hazardous")
(termFormat ChineseLanguage hazardous "危险")

(=>
  (hazardous ?A ?B)
  (modalAttribute
    (exists (?D)
      (and
        (instance ?D Damaging)
        (patient ?D ?B)
        (or
          (involvedInEvent ?D ?A)
          (causes ?A ?D)))) Possibility))

(documentation maritimeHazard EnglishLanguage "&%maritimeHazard is a &%subrelation
of the &%BinaryRelation &%Hazardous which posts threat to &%WaterTransportation.")
(documentation maritimeHazard ChineseLanguage "&%maritimeHazard 是 &%Hazardous 的
&%subrelation，是一种 &%BinaryRelation， 专门处理对 &%WaterTransportation 造成的危险情况。")
(domain maritimeHazard 1 Physical)
(domain maritimeHazard 2 Object)
(subrelation maritimeHazard hazardous)
(instance maritimeHazard BinaryPredicate)
(format EnglishLanguage maritimeHazard "%1 is %n &%hazardous to %2.")

(=>
  (maritimeHazard ?A ?B)
  (exists (?SAIL ?WATER)
    (and
      (instance ?SAIL WaterTransportation)
      (instrument ?SAIL ?B)
      (eventLocated ?SAIL ?WATER)
      (located ?A ?WATER)
      (hazardous ?A ?B))))

(documentation Shoal EnglishLanguage "A &%Shoal is a &%LandForm near a &%WaterArea
with most of it is covered by a %BodyOfWater.")
(documentation Shoal ChineseLanguage "&%Shoal 是一处 &%LandForm，它靠近 &%WaterArea，并且
大部分的范围被 %BodyOfWater 淹没。")
(subclass Shoal LandForm)

(=>
  (instance ?LAND Shoal)
  (exists (?WATER)
    (and
      (instance ?WATER WaterArea)
      (meetsSpatially ?LAND ?WATER))))

(=>
  (instance ?LAND Shoal)
  (exists (?PART ?WATER)
    (and
      (instance ?PART LandForm)
      (most ?PART ?LAND)
      (instance ?WATER BodyOfWater)
      (covers ?WATER ?PART))))

(=>
  (and
    (instance ?S Shoal)
    (instance ?B WaterVehicle))
  (maritimeHazard ?S ?B))

(subclass SeaIce Ice) ;; incl glaciers in sea, floes, bergs

(subclass Reef LandForm)

(documentation Reef EnglishLanguage "A &%Reef is a ridge of rock, coral, or sand at or near
the surface of a &%WaterArea.")

(=>
  (instance ?Reef Reef)
  (exists (?Water)
    (and
      (instance ?Water WaterArea)
      (orientation ?Reef ?Water Near))))

(=>
  (instance ?Reef Reef)
  (exists (?Stuff)
    (and
      (or
        (instance ?Stuff Sand)
        (instance ?Stuff Rock)
        (instance ?Stuff Coral))
      (part ?Stuff ?Reef))))

(=>
  (and
    (instance ?R Reef)
    (instance ?B WaterVehicle))
  (maritimeHazard ?R ?B))

(subclass Coral Invertebrate)

(documentation Coral EnglishLanguage "Corals are gastrovascular marine cnidarians (phylum
Cnidaria, class Anthozoa) existing as small anemone_like polyps, typically
forming colonies of many individuals.  The group includes the important
reef builders known as hermatypic corals, found in tropical oceans, and
belonging to the subclass Zoantharia of order Scleractinia (formerly
Madreporaria).  The hermatypic corals obtain much of their nutrient
requirement from symbiotic unicellular algae called zooxanthellae, and so
are dependent upon growing in sunlight.  As a result, these corals are
usually found not far beneath the surface, although in clear waters corals
can grow at depths of 60 m (200 ft).  Corals breed by spawning, with all
corals of the same species in a region releasing gametes simultaneously
over a period of one to several nights around a full moon. (from Wikipedia)")

(subclass CoralReef Reef)

(documentation CoralReef EnglishLanguage "&%CoralReef is the subclass of &%Reefs that are
formed from living organisms that produce the limestone formations of the
reef.  Coral reefs include fringing reefs, barrier reefs, and &%Atolls.")

(=>
  (instance ?REEF CoralReef)
  (exists (?CORAL)
    (and
      (instance ?CORAL Coral)
      (part ?CORAL ?REEF))))

(subclass Atoll CoralReef)

(documentation Atoll EnglishLanguage "&%Atoll is the class of &%CoralReefs which surround
a lagoon.")

(subclass Glacier LandForm)

(documentation Glacier EnglishLanguage "A &%Glacier is a large body of slow_moving ice.
Glaciers displace soil and rock while moving over land surfaces and break
apart, forming &%Icebergs, when they reach the sea.")

(=>
  (instance ?Glacier Glacier)
  (exists (?Water)
    (and
      (instance ?Water Water)
      (attribute ?Water Solid)
      (part ?Water ?Glacier))))

(subclass Cave HoleRegion)
(subclass Cave GeographicArea)

(documentation Cave EnglishLanguage "A &%Cave is a naturally formed opening beneath
the surface of the Earth, generally formed by dissolution of carbonate
bedrock. Caves may also form by erosion of coastal bedrock, partial
melting of glaciers, or solidification of lava into hollow tubes.")

(=>
  (instance ?Cave Cave)
  (exists (?Erosion)
    (and
      (instance ?Erosion Erosion)
      (result ?Erosion ?Cave))))

(subclass Burrow HoleRegion)
(documentation Burrow EnglishLanguage "A small &%HoleRegion created by an &%Animal for the purpose
of inhabiting it.")

(=>
  (instance ?B Burrow)
  (exists (?P ?A)
    (and
      (instance ?P Process)
      (agent ?P ?A)
      (instance ?A Animal)
      (not
        (instance ?A Human))
      (result ?P ?B))))

(=>
  (instance ?B Burrow)
  (exists (?A)
    (hasPurpose ?B
      (inhabits ?A ?B))))

(subclass CaveMatrix LandForm)

(documentation CaveMatrix EnglishLanguage "A &%CaveMatrix is the framework of earth or
rock in which a &%Cave is embedded.")

(=>
  (instance ?cave Cave)
  (exists (?land)
    (and
      (instance ?land CaveMatrix)
      (hole ?cave ?land))))

(=>
  (and
    (instance ?land CaveMatrix)
    (instance ?cave Cave)
    (hole ?cave ?land))
  (equal ?land (HoleSkinFn ?cave)))

(subclass SubmergedLandArea GeographicArea)

(documentation SubmergedLandArea EnglishLanguage "&%SubmergedLandArea is the class of land
regions that are located beneath bodies of water.")

(=>
  (instance ?LAND SubmergedLandArea)
  (exists (?WATER)
    (and
      (instance ?WATER WaterArea)
      (orientation ?LAND ?WATER Below)
      (orientation ?WATER ?LAND On)
      (meetsSpatially ?LAND ?WATER))))

(subclass ContinentalShelf SubmergedLandArea)

(documentation ContinentalShelf EnglishLanguage "A &%ContinentalShelf is a natural
undersea extension of land around a &%Continent.  The shelf is a
gently sloped (average less than one percent) plain that is an extension
of the &%CoastalPlain found off the coast of most continents.")

(subclass ContinentalMargin SubmergedLandArea)

(documentation ContinentalMargin EnglishLanguage "&%ContinentalMargin is class of
&%SubmergedLandAreas that are the extension of land underwater at
the edge of a continent, before a drop to the sea floor.  A
&%ContinentalMargin includes the &%ContinentalShelf and the
continental break, slope, or rise at the outer edge.")

(=>
  (instance ?SHELF ContinentalShelf)
  (exists (?MARGIN)
    (and
      (instance ?MARGIN ContinentalMargin)
      (properPart ?SHELF ?MARGIN)
      (overlapsSpatially ?SHELF ?MARGIN))))

(subclass SeabedArea SubmergedLandArea)

(documentation SeabedArea EnglishLanguage
"&%SeabedArea is the class of &%SubmergedLandAreas that are part of
the sea floor.")

(instance SeacoastFn BinaryFunction)
(domain SeacoastFn 1 GeographicArea)
(domain SeacoastFn 2 SaltWaterArea)
(range SeacoastFn Seacoast)

(documentation SeacoastFn EnglishLanguage "(&%SeacoastFn ?LAND ?SEA) denotes the
&%Seacoast where the &%GeographicArea ?LAND borders the &%SaltWaterArea
?SEA.")

(subclass Seacoast ShoreArea)
(subclass Seacoast LandForm)

(documentation Seacoast EnglishLanguage "A &%Seacoast is the &%ShoreArea along the
margin of an ocean, extending inland approximately 1-3 km from the low
water mark.")

(=>
  (and
    (instance ?COAST Seacoast)
    (instance ?WATER WaterArea)
    (orientation ?WATER ?COAST Adjacent))
  (instance ?WATER SaltWaterArea))

(subclass Shoreline LandArea)

(documentation Shoreline EnglishLanguage "&%Shoreline is the class of &%LandAreas that
are the edge of a larger land mass abutting a bordering &%WaterArea.")

(=>
  (instance ?LINE Shoreline)
  (exists (?WATER)
    (and
      (instance ?WATER WaterArea)
      (meetsSpatially ?WATER ?LINE))))

(=>
  (instance ?SHORE ShoreArea)
  (exists (?LINE)
    (and
      (instance ?LINE Shoreline)
      (part ?LINE ?SHORE))))

(=>
  (and
    (instance ?SHORE ShoreArea)
    (instance ?WATER WaterArea)
    (orientation ?SHORE ?WATER Adjacent))
  (exists (?LINE)
    (and
      (instance ?LINE Shoreline)
      (part ?LINE ?SHORE)
      (meetsSpatially ?LINE ?WATER))))

(=>
  (meetsSpatially ?X ?Y)
  (orientation ?X ?Y Adjacent))

(instance ShorelineFn BinaryFunction)
(domain ShorelineFn 1 GeographicArea)
(domain ShorelineFn 2 WaterArea)
(range ShorelineFn LandArea)

(documentation ShorelineFn EnglishLanguage "(&%ShorelineFn ?LAND ?WATER) denotes
the &%Shoreline where the &%GeographicArea ?LAND borders the
&%WaterArea ?WATER.")

(subrelation ShorelineFn BorderFn)

;---------------------------------------------------------------------------

;;   H. Vegetation and biomes

(instance vegetationType BinaryPredicate)

(documentation vegetationType EnglishLanguage "(&%vegetationType ?area ?type) associates
a particular &%GeographicArea with a type of &%Plant that is found there.")

(domain vegetationType 1 GeographicArea)
(domainSubclass vegetationType 2 Plant)

(=>
  (and
    (vegetationType ?REGION ?TYPE)
    (subclass ?TYPE Plant)
    (instance ?REGION GeographicArea))
  (exists (?PLANT)
    (and
      (instance ?PLANT ?TYPE)
      (located ?PLANT ?REGION))))

(=>
  (and
    (not (vegetationType ?REGION ?TYPE))
    (subclass ?TYPE Plant)
    (instance ?REGION GeographicArea))
  (not   (exists (?PLANT)
      (and
        (instance ?PLANT ?TYPE)
        (located ?PLANT ?REGION)))))

(instance vegetationTypePattern TernaryPredicate)

(documentation vegetationTypePattern EnglishLanguage
"(&%vegetationTypePattern ?AREA ?TYPE ?DENSITY) means that in the
&%GeographicArea ?AREA the &%Plant ?TYPE is found with ?DENSITY.")

(domain vegetationTypePattern 1 GeographicArea)
(domainSubclass vegetationTypePattern 2 Plant)
(domain vegetationTypePattern 3 Attribute)

(=>
  (vegetationTypePattern ?AREA ?TYPE ?DENSITY)
  (vegetationType ?AREA ?TYPE))

(subclass LandVegetationAttribute  RelationalAttribute)
(documentation LandVegetationAttribute EnglishLanguage "&%LandVegetationAttribute
is a &%suclass of &%RelationalAttribute which describe the relation between density
of vegetation and the &%Land on which is grown.")

(instance CanopiedVegetation LandVegetationAttribute)

(documentation CanopiedVegetation EnglishLanguage "&%CanopiedVegetation describes the
density of vegetation in a &%RainForest or &%Jungle.")

(instance DenseVegetation LandVegetationAttribute)

(documentation DenseVegetation EnglishLanguage "&%DenseVegetation describes the
density of vegetation in a &%Forest.")

(instance GroundCoverVegetation LandVegetationAttribute)

(documentation GroundCoverVegetation EnglishLanguage "&%GroundCoverVegetation
describes the density of uniform low vegetation in a field or meadow.")

(instance ScatteredVegetation LandVegetationAttribute)

(documentation ScatteredVegetation EnglishLanguage "&%ScatteredVegetation describes
the pattern of vegetation in which plants appear at intervals.")

(instance SparseVegetation LandVegetationAttribute)

(documentation SparseVegetation EnglishLanguage "&%SparseVegetation describes the
pattern of vegetation in an area where there is very little vegetation.")

(subclass Shrub WoodyPlant)
(documentation Shrub EnglishLanguage "A &%Shrub is the class of low, perennial,
typically multi_stemmed woody plants, called shrubs or bushes.[Wikipedia]")


(=>
  (and
    (instance ?TREE BotanicalTree)
    (instance ?BUSH Shrub)
    (instance ?U UnitOfLength)
    (height ?TREE
      (MeasureFn ?TALL ?U))
    (height ?BUSH
      (MeasureFn ?SHORT ?U)))
  (greaterThan ?TALL ?SHORT))

(subclass Lilac Shrub)
(documentation Lilac EnglishLanguage "A &%Lilac, Syringa, is &%WoodyPlant that has large clusters of
aromatic flowers.It is in the olive family or Oleaceae.[Wikipedia]")

(subclass Vine FloweringPlant)
(documentation Vine EnglishLanguage "A &%Vine is the class of &%FloweringPlant which can be
woody or herbaceous. They grow by climbing on another object with their Scandent stems,Lianas
or runners.[Wikipedia]")

;; added definitons for Succulent and Cactus

(subclass Succulent FloweringPlant)
(documentation Succulent EnglishLanguage "&%Succulents are &%Drought resistant
&%Plants in which their &%Plantleaf and PlantStem &%contain water_storing &%Tissue.")

;; drought resistant
(=>
  (instance ?P Succulent)
  (modalAttribute
    (exists (?D)
      (and
         (instance ?D Drought)
         (experiencer ?D ?P)
         (attribute ?P Living))) Likely))

(subclass Cactus FloweringPlant)
(documentation Cactus EnglishLanguage "The word &%Cactus derives, through Latin,
from Greek Kaktos, a name for spiny plant. Most Cacti live in habitats subject
to at least some drought. Almost all cacti are succulents. Areoles are structures
unique to cacti. (Reference wikipedia)")

;; Most Cacti live in habitats subject to at least some drought
(=>
  (instance ?CAC Cactus)
  (modalAttribute
    (exists (?AREA)
      (and
        (instance ?AREA GeographicArea)
        (located ?CAC ?AREA)
        (exists (?D)
          (and
            (instance ?D Drought)
            (eventLocated ?D ?AREA))))) Likely))

;; Areoles are parts of a Cactus
(=>
  (instance ?A PlantAreole)
  (exists (?C)
    (and
      (instance ?C Cactus)
      (part ?A ?C))))

(subclass Lichen NonFloweringPlant)

(documentation Lichen EnglishLanguage "&%Lichen is the class of complex thallyphytic
plants made up of &%Alga and &%Fungus growing symbiotically.[Wikipedia]")

(=>
  (instance ?LICH Lichen)
  (exists (?ALGA)
    (and
      (instance ?ALGA Alga)
      (part ?ALGA ?LICH))))

(=>
  (instance ?LICH Lichen)
  (exists (?FUNG)
    (and
      (instance ?FUNG Fungus)
      (part ?FUNG ?LICH))))

;;--------------------------------

; Biome types:

(subclass Biome Ecosystem)

(documentation Biome EnglishLanguage "&%Biome is the class of &%GeographicAreas
representing major kinds of ecological communities, that is, areas in
which certain kinds of plants, animals, weather, and terrain interact
to produce and support a distinctive ecosystem.")

(subclass Forest LandArea)
(disjoint Forest Field)
(documentation Forest EnglishLanguage "&%Forest is the class of large &%LandAreas that
are covered by trees and associated undergrowth, either growing wild or
managed for the purpose of timber production.")

(=>
  (instance ?FOREST Forest)
  (vegetationTypePattern ?FOREST BotanicalTree DenseVegetation))

(subclass RainForest Biome)
(subclass RainForest Forest)

(documentation RainForest EnglishLanguage "&%RainForest is the subclass of &%LandAreas
that are densely planted with trees.")

(=>
  (instance ?FOREST RainForest)
  (vegetationTypePattern ?FOREST BotanicalTree CanopiedVegetation))

(subclass TropicalRainForest RainForest)
(subclass TemperateRainForest TemperateHardwoodForest)
(subclass TemperateRainForest RainForest)

(subclass TemperateHardwoodForest Biome)
(subclass TemperateHardwoodForest Forest)

(subclass BorealForest Biome)
(subclass BorealForest Forest)

;;
;; Jennie Pease
;; 14th March 2023
;; Adding terms to BotanicalTree family and link them to WoodProducts in Economy.kif
;;

(subclass DeciduousTree BotanicalTree)
(disjoint DeciduousTree EvergreenTree)
(documentation DeciduousTree EnglishLanguage "&%DeciduousTrees shed
their &%PlantLeaf(ves) annually.")

(=>
  (and
    (sheddingLeaves ?T ?L)
    (instance ?T DeciduousTree)
    (instance ?L PlantLeaf))
  (exists (?SHED ?S)
    (and
      (subclass ?SHED Removing)
      (frequency ?SHED YearDuration)
      (instance ?S ?SHED)
      (origin ?S ?T)
      (patient ?S ?L))))

(subclass SheddingPart BinaryPredicate)
(relatedInternalConcept SheddingPart DeadFn)
(documentation SheddingPart EnglishLanguage "The &%Class of &%BinaryPredicates relating
an &%Organism and the &%Removing of one of its &%Part as part of the normal process of life.
Unlike &%Birth, the &%paitent dies upon its removal from the its origin.")

(=>
  (and
    (instance ?REL SheddingPart)
    (?REL ?O ?P)
    (instance ?O Organism)
    (instance ?P AnatomicalStructure)
    (part ?P ?O))
  (exists (?R)
    (and
      (instance ?R Removing)
      (origin ?R ?O)
      (patient ?R ?P))))

(=>
  (and
    (instance ?REL SheddingPart)
    (?REL ?O ?P)
    (instance ?O ?ORG)
    (subclass ?ORG Organism))
  (exists (?PART)
    (and
      (instance ?P (DeadFn ?PART))
      (initialPart ?PART ?ORG))))

(instance sheddingLeaves SheddingPart)
(domain sheddingLeaves 1 Plant)
(domain sheddingLeaves 2 PlantLeaf)
(documentation sheddingLeaves EnglishLanguage "A relation between a &%Plant and its
leaves which is given off as part of the normal process of life.")

(subclass Moulting Removing)
(documentation Moulting EnglishLanguage "&%Moulting is the &%Process whereby %Organism casts
off a part of its body (often, but not always, an outer layer or covering) as a normal part
of its life.")

(instance sheddingBodyPart SheddingPart)
(domain sheddingBodyPart 1 Organism)
(domain sheddingBodyPart 2 BodyPart)
(documentation sheddingBodyPart EnglishLanguage "A relation between an &%Organisim and its
&%BodyPart which is given off as part of the normal process of life.")

(=>
  (sheddingBodyPart ?O ?P)
  (exists (?M)
    (and
      (instance ?M Moulting)
      (origin ?M ?O)
      (patient ?M ?P))))

(=>
  (and
    (instance ?C DomesticCat)
    (sheddingBodyPart ?C ?H))
  (instance ?H Hair))

(=>
  (and
    (instance ?R Reptile)
    (sheddingBodyPart ?R ?S))
  (instance ?S Skin))

(=>
  (and
    (instance ?C Crustacean)
    (sheddingBodyPart ?C ?S))
  (instance ?S AnimalShell))

(subclass BalsaTree DeciduousTree)
(relatedInternalConcept BalsaTree BalsaWood)
(documentation BalsaTree EnglishLanguage "&%BalsaTree is also known as Ochroma pyramidale.
It is a large, fast growing tree native to &%America.[Wikipedia]")

(subclass BeechTree DeciduousTree)
(relatedInternalConcept BeechTree BeechWood)
(documentation BeechTree EnglishLanguage "&%BeechTree is in the family of Fagaceae. It is native to temperate
&%Europe, &%Asia and &%NorthAmerica.[Wikipedia]")

(subclass BirchTree DeciduousTree)
(relatedInternalConcept BirchTree BirchWood)
(documentation BirchTree EnglishLanguage "&%BirchTree is a thin_leaved &%Hardwood &%BotanicalTree in
the family of Betulaceae.[Wikipedia]")

(subclass BlackCherryTree DeciduousTree)
(relatedInternalConcept BlackCherryTree CherryWood)
(documentation BlackCherryTree EnglishLanguage "&%BlackCherryTree grows in eastern &%UnitedStates. It is
the source of &%CherryWood, a &%Hardwood material in construction and funiture making.[Wikipedia]")

(subclass SweetCherryTree DeciduousTree)
(documentation SweetCherryTree EnglishLanguage "&%SweetCherryTree grows in &%Europe. Its technical name is Prunus avium.
It is farmed mostly as a fruit_bearing tree. It's timber is used for wood working.[Wikipedia]")

(subclass MapleTree DeciduousTree)
(relatedInternalConcept MapleTree MapleWood)
(documentation MapleTree EnglishLanguage "&%MapleTrees are rancked as Acer. It is in the family of Sapindaceae.[Wikipedia]")

(subclass OkoumeTree DeciduousTree)
(relatedInternalConcept OkoumeTree OkoumeWood)
(documentation OkoumeTree EnglishLanguage "&%OkoumeTree is one of the species known as Aucoumea Kalineana. It is
in the family of Burseraceae.[Wikipedia]")

(subclass PoplarTree DeciduousTree)
(relatedInternalConcept PoplarTree PoplarWood)
(documentation PoplarTree EnglishLanguage "&%PoplarTree is a genus of 25-30 species in the family of Salicaceae.[Wikipedia]")

(subclass CottonwoodTree DeciduousTree)
(documentation CottonwoodTree EnglishLanguage "&%CottonwoodTree is a genus of 25-30 species in the family of Salicaceae.[Wikipedia]")

(subclass AspenTree DeciduousTree)
(documentation AspenTree EnglishLanguage "&%AspenTree is a genus of 25-30 species in the family of Salicaceae.[Wikipedia]")

(subclass RedOakTree DeciduousTree)
(relatedInternalConcept RedOakTree RedOakWood)
(documentation RedOakTree EnglishLanguage "&%RedOakTree is also known as Quercus rubra. It is a native tree of &%NorthAmerica.[Wikipedia]")

(subclass SapeleTree DeciduousTree)
(relatedInternalConcept SapeleTree SapeleWood)
(documentation SapeleTree EnglishLanguage "&%SapeleTree is a tree native to tropical &%Africa. It's genus is Entandrophragma,
with 11 known species in the family of Meliaceae.[Wikipedia]")

(subclass WalnutTree DeciduousTree)
(relatedInternalConcept WalnutTree WalnutWood)
(documentation WalnutTree EnglishLanguage "&%WalnutTrees are any species of the &%BotanicalTree in the genus Juglans,
in the family of Juglandaceae. Their seeds are referred as &%Walnuts.[Wikipedia]")

(subclass WhiteOakTree DeciduousTree)
(relatedInternalConcept WhiteOakTree WhiteOakWood)
(documentation WhiteOakTree EnglishLanguage "&%WhiteOakTree is known as Quercus alba. It is native to eastern and central
&%NorthAmerica.[Wikipedia]")

(subclass WillowTree DeciduousTree)
(documentation WillowTree EnglishLanguage "&%WillowTree is also know as Sallow. It is of the genus &%Salix.[Wikipedia]")

(subclass EvergreenTree BotanicalTree)
(documentation EvergreenTree EnglishLanguage "&%EvergreenTrees are &%BotanicalTrees that do not shed
their &%PlantLeaf(ves).")

(=>
  (and
    (instance ?T EvergreenTree)
    (instance ?L PlantLeaf))
  (not
    (sheddingLeaves ?T ?L)))

(subclass Conifer EvergreenTree)
(documentation Conifer EnglishLanguage "&%Conifers are a group of cone_bearing seed plants,
a subset of gymnosperms. They make up the division Pinophyta, also known as Coniferophyta or Coniferae.[Wikipedia]")

(subclass PineTree Conifer)
(relatedInternalConcept PineTree PineWood)
(documentation PineTree EnglishLanguage "Pines are coniferous &%BotanicalTrees of the
genus Pinus, in the family Pinaceae.  They are evergreens, with needle-
shaped leaves and propagate by dropping pine cones, which contains
seeds.  (from Wikipedia)")

(=>
  (instance ?FOREST BorealForest)
  (vegetationType ?FOREST PineTree))

(subclass CedarTree Conifer)
(relatedInternalConcept CedarTree CedarWood)
(documentation CedarTree EnglishLanguage "&%CedarTree is also known as Cedrus in the plant family of Pinaceae. They
are native to the mountains of western Himalayas and the Mediterranean region.[Wikipedia]")

(subclass CypressTree Conifer)
(documentation CypressTree EnglishLanguage "&%CypressTree is also in the family of Cupressaceae.[Wikipedia]")

(subclass RedwoodTree CypressTree)
(documentation RedwoodTree EnglishLanguage "&%RedwoodTree is also known as Sequoioideae. It is in the family of Cupressaceae.
The redwood species contains the largest and tallest trees in the world.[Wikipedia]")

(subclass FirTree Conifer)
(relatedInternalConcept FirTree FirWood)
(documentation FirTree EnglishLanguage "&%FirTree is in the family of Pinaceae. It belongs to the the genus
of Abies. There are 48-56 extant species. The genus is closely related to cedar.[Wikipedia]")

(subclass LarchTree Conifer)
(relatedInternalConcept LarchTree LarchWood)
(documentation LarchTree EnglishLanguage "&%LarchTree is in the family of Pinaceae. It is of the genus Larix. It
is dominant &%BotanicalTrees in the &%BorealForest of &%Canada and Siberia.[Wikipedia]")

(subclass SpruceTree Conifer)
(relatedInternalConcept SpruceTree SpruceWood)
(documentation SpruceTree EnglishLanguage "A &%SpruceTree is a tree of the genus Picea with 35 species in the family
of Pinaceae. They are found in the northern temperate and boreal regions.[Wikipedia]")

(subclass Jungle Biome)
(subclass Jungle LandArea)

(documentation Jungle EnglishLanguage "&%Jungle is a subclass of fertile &%LandAreas
that are overgrown with tropical vegetation.")

(=>
  (instance ?FOREST Jungle)
  (vegetationTypePattern ?FOREST Plant DenseVegetation))

(subclass Swamp Biome)
(documentation Swamp EnglishLanguage "&%Swamp is a subclass of &%Biome
that is &%Damp and overgrown with vegetation.")

(=>
  (instance ?SWAMP Swamp)
  (attribute ?SWAMP Damp))

(=>
  (instance ?SWAMP Swamp)
  (vegetationTypePattern ?SWAMP Grass DenseVegetation))

(subclass Desert LandArea)
(documentation Desert EnglishLanguage "&%Desert is a subclass of &%LandAreas that are
arid regions having sparse or no vegetation.")

(=>
  (instance ?DESERT Desert)
  (attribute ?DESERT Dry))

(=>
  (instance ?DESERT Desert)
  (or
    (groundSurfaceType ?DESERT Rock)
    (groundSurfaceType ?DESERT Sand)))

(=>
  (instance ?DESERT Desert)
  (not (vegetationType ?DESERT BotanicalTree)))

(instance SaharaDesert Desert)
(documentation SaharaDesert EnglishLanguage "&%SaharaDesert spans across North Africa.
With an area of 9,200,000 square kilometres (3,600,000 sq mi). It stretches from the
Red Sea in the east and the Mediterranean in the north to the Atlantic Ocean in the west,
where the landscape gradually changes from desert to coastal plains. To the south it is
bounded by the Sahel, a belt of semi-arid tropical savanna around the Niger River valley
and the Sudan region of sub-Saharan Africa. [Wikipedia]")
(termFormat EnglishLanguage SaharaDesert "Sahara Desert")
(geographicSubregion SaharaDesert NorthernAfrica)
(totalArea SaharaDesert (MeasureFn 3600000 SquareKilometer))
(climateTypeInArea SaharaDesert SubtropicalDesertClimateZone)
(orientation MediterraneanSea SaharaDesert North)
(orientation RedSea SaharaDesert East)
(orientation AtlanticOcean SaharaDesert West)

(instance Sahel LandArea)
(documentation Sahel EnglishLanguage "&%Sahel is a biogeographical region in &%Africa. It is
the transition zone between the more humid Sudanian savannas to its south and the drier Sahara
to the north. The Sahel has a hot semi-arid climate and stretches across the southernmost
latitudes of North Africa between the Atlantic Ocean and the Red Sea. Although geographically
located in the tropics, the Sahel does not have a tropical climate. The Sahel spans 5,900 km
(3,670 mi) from the Atlantic Ocean in the west to the Red Sea in the east, in a belt several
hundred to a thousand kilometers (c. 600 miles) wide. It covers an area of 3,053,200 square
kilometers (1,178,850 sq mi).[Wikipedia]")
(termFormat EnglishLanguage Sahel "Sahel")
(geographicSubregion Sahel NorthernAfrica)
(totalArea Sahel (MeasureFn 3053200 SquareKilometer))
(climateTypeInArea Sahel SteppeClimateZone)
(connects Sahel AtlanticOcean RedSea)
(orientation SaharaDesert Sahel North)
(orientation Sahel SaharaDesert South)
(orientation RedSea Sahel East)
(orientation AtlanticOcean Sahel West)

(subclass Oasis Biome)
(subclass Oasis LandArea)

(documentation Oasis EnglishLanguage "&%Oasis is a subclass of &%LandAreas that are
fertile places within a desert, which have water and some vegetation.")

(=>
  (instance ?OASIS Oasis)
  (exists (?DESERT)
    (and
      (instance ?DESERT Desert)
      (located ?OASIS ?DESERT))))

(=>
  (instance ?OASIS Oasis)
  (exists (?WATER)
    (and
      (instance ?WATER FreshWaterArea)
      (located ?WATER ?OASIS))))

(=>
  (instance ?OASIS Oasis)
  (attribute ?OASIS FertileTerrain))

(subclass Grassland Biome)
(subclass Grassland LandArea)

(documentation Grassland EnglishLanguage "&%Grassland is the class of &%LandAreas
where the predominant vegetation is some kind of grass.")

(=>
  (instance ?PLAIN Grassland)
  (vegetationTypePattern ?PLAIN Grass GroundCoverVegetation))

(subclass Prairie Grassland)
(subclass Prairie Plain)

(documentation Prairie EnglishLanguage "&%Prairie is a class of large plains
&%LandAreas with tall grass vegetation.")

(subclass Pampa Grassland)
(subclass Pampa Plain)

(documentation Pampa EnglishLanguage "&%Pampa is a class of vast, level, typically grassy
&%Plains of land in South America.")

(=>
  (instance ?PLAIN Pampa)
  (located ?PLAIN SouthAmerica))

(subclass Savanna Grassland)
(subclass Savanna Plain)

(documentation Savanna EnglishLanguage "&%Savanna is a class of tropical or subtropical
&%Grasslands, typically treeless.")

(=>
  (instance ?PLAIN Savanna)
  (not
    (vegetationType ?PLAIN BotanicalTree)))

(subclass Steppe Plain)

(documentation Steppe EnglishLanguage "&%Steppe is a subclass of &%Plain, representing
vast, level, treeless areas of land in Asia or SE Europe.")

(=>
  (instance ?PLAIN Steppe)
  (not (vegetationType ?PLAIN BotanicalTree)))

(=>
  (instance ?PLAIN Steppe)
  (or
    (located ?PLAIN Europe)
    (located ?PLAIN Asia)))

(subclass Veldt Grassland)
(subclass Veldt Plain)

(documentation Veldt EnglishLanguage "&%Veldt is a subclass of &%Grasslands which have
scattered shrubs or trees.")

(=>
  (instance ?PLAIN Veldt)
  (located ?PLAIN Africa))

(=>
  (instance ?PLAIN Veldt)
  (vegetationTypePattern ?PLAIN Shrub ScatteredVegetation))

(subclass Tundra Biome)
(subclass Tundra Plain)
; (subclass Tundra ArcticRegion)

(documentation Tundra EnglishLanguage "&%Tundra is a subclass of flat, treeless &%Plains
areas lying within the &%ArcticRegion.  Subsoil in &%Tundra is permanently
frozen.")

(=>
  (instance ?PLAIN Tundra)
  (vegetationType ?PLAIN Lichen))

(=>
  (instance ?PLAIN Tundra)
  (vegetationType ?PLAIN Moss))

;; KJN: Moving this to Mid_level_ontology.kif to remove dependencies
;;(subclass Agriculture Maintaining)
;;(documentation Agriculture EnglishLanguage "&%Agriculture is a class of &%Processes
;;in which land, plants, or animals are cultivated in order to produce
;;food or other organic products.")

;;(=>
;;  (instance ?AGRO Agriculture)
;;  (exists (?GROWTH)
;;    (and
;;      (instance ?GROWTH Growth)
;;      (subProcess ?GROWTH ?AGRO))))

; ; ----------------------------------------------
;
; Natural Disasters that are not related to Weather
;  (covered in CIA World Fact Book)
;
; ; ----------------------------------------------

(subclass Landslide GeologicalProcess)
(subclass Mudslide Landslide)
(subclass Mudflow Landslide)
(subclass Rockslide Landslide)
; TurbidityCurrents are underwater mudslides w/salt water
(subclass VolcanicGasRelease VolcanicEruption)
(subclass LandSubsidence GeologicalProcess)  ;; &%elevation decreases

(=>
  (instance ?SINKING LandSubsidence)
  (exists (?LAND)
    (and
      (instance ?LAND LandArea)
      (eventLocated ?SINKING ?LAND)
      (patient ?SINKING ?LAND))))

(=>
  (and
    (instance ?SINKING LandSubsidence)
    (patient ?SINKING ?LAND)
    (instance ?LAND LandArea)
    (instance ?U UnitOfLength)
    (elevation ?LAND
      (MeasureFn ?HEIGHT1 ?U)))
  (holdsDuring (ImmediateFutureFn (WhenFn ?SINKING))
    (exists (?HEIGHT2)
      (and
        (elevation ?LAND
          (MeasureFn ?HEIGHT2 ?U))
        (lessThan ?HEIGHT2 ?HEIGHT1)))))

(subclass Locust Grasshopper)
(subclass Swarming Motion)
(subclass Famine SocialInteraction)

;;------------------------------------------------------------------------

;; J. Environmental Areas of Concern

(subclass AreaOfConcern RelationalAttribute)

(documentation AreaOfConcern EnglishLanguage "&%AreaOfConcern is a subclass of
&%Attributes that represent and classify the kinds of interests that
agents have.")

(instance regionalIssue BinaryPredicate)
(domain regionalIssue 1 GeographicArea)
(domain regionalIssue 2 AreaOfConcern)

(documentation regionalIssue EnglishLanguage "(&%regionalIssue ?AREA ?ISSUE) means
that the &%AreaOfConcern ?ISSUE is relevant to a problem in the
&%GeographicArea ?AREA.")

; rework this AreaOfConcern hierarchy, and associate the relevant processes:

(subclass EnvironmentalIssue AreaOfConcern)
(documentation EnvironmentalIssue EnglishLanguage "The class of Attributes that
stand for issues of environmental concern.")

(instance AcidificationIssue EnvironmentalIssue)
(instance AcidRainIssue EnvironmentalIssue)
(instance AerosolParticulateIssue EnvironmentalIssue)
(instance DeforestationIssue EnvironmentalIssue)
(subclass SoilQualityIssue EnvironmentalIssue)
(instance SoilErosionIssue SoilQualityIssue)
(instance SoilDegradationIssue SoilQualityIssue)
(instance SoilExhaustionIssue SoilQualityIssue)
(subclass EnvironmentalWaterIssue EnvironmentalIssue)
(instance LimitedFreshWaterIssue EnvironmentalWaterIssue)  ;; WaterResource or FreshWaterResource
(instance InadequateDrinkingWaterIssue EnvironmentalWaterIssue)
(instance OvergrazingIssue EnvironmentalIssue)
(instance DesertificationIssue EnvironmentalIssue)
(instance AirPollutionIssue PollutionIssue)
(instance SoilPollutionIssue PollutionIssue)
(instance SoilPollutionIssue SoilQualityIssue)

(instance WaterPollutionIssue EnvironmentalWaterIssue)
(subAttribute GroundWaterPollutionIssue WaterPollutionIssue)

(subclass PollutionIssue EnvironmentalIssue)
(instance IndustrialPollutionIssue PollutionIssue)
(instance VehicleEmissionsIssue PollutionIssue)
(instance PowerPlantEmissionsIssue PollutionIssue)
(instance ToxicWasteDisposalIssue PollutionIssue)
(instance RawSewagePollutionIssue PollutionIssue)

(instance WaterInfrastructureIssue EnvironmentalIssue)
(subAttribute WastewaterIssue WaterInfrastructureIssue)
(instance SolidWasteDisposalIssue PollutionIssue)
(instance OverpopulationIssue EnvironmentalIssue)
(subclass BiodiversityIssue EnvironmentalIssue)
(instance SiltationIssue EnvironmentalIssue)
(instance OzoneDepletionIssue EnvironmentalIssue)
(instance UVLevelsIssue EnvironmentalIssue)
(instance ClimateChangeIssue EnvironmentalIssue)
(subAttribute ClimateWarmingIssue ClimateChangeIssue)
(instance WaterManagementIssue EnvironmentalIssue) ;; see WaterInfrastructureIssue
(instance EndangeredSpeciesIssue BiodiversityIssue)
(subAttribute EndangeredMarineWildlifeIssue EndangeredSpeciesIssue)
(instance FragileEcosystemIssue BiodiversityIssue)
(subAttribute ThinningPolarIcepackIssue ClimateChangeIssue)
(instance ToxicChemicalPollutionIssue PollutionIssue)
(instance NuclearEnvironmentalIssue PollutionIssue)
(instance OilPollutionIssue PollutionIssue)
(instance FishStockDepletionIssue EnvironmentalIssue)
(instance UrbanizationIssue EnvironmentalIssue) ;; urban waste disposal
(subAttribute SoilSalinityIssue SoilDegradationIssue)
(instance FarmingPracticesIssue EnvironmentalIssue)
(instance AgriculturalChemicalsIssue EnvironmentalIssue) ;; nitrogen, phosphorus - agricultural runoff
(instance PesticideIssue EnvironmentalIssue)
(instance LandClearingIssue BiodiversityIssue) ;; ?
(instance ForestDegradation EnvironmentalIssue)
(subAttribute HydrocarbonEmissionsIssue AirPollutionIssue)
(instance CoastalDegradation   EnvironmentalIssue) ;; damage to coastline, coral reefs, and sea vegetation
(instance CoralReefDecayIssue EnvironmentalIssue)
(instance CoastalMarinePollutionIssue EnvironmentalIssue)

(instance PopulationMigrationIssue EnvironmentalIssue)

(subclass DiseaseConditionsIssue EnvironmentalIssue)
(instance WaterBorneDiseaseIssue EnvironmentalIssue)
(instance InadequateSanitationIssue EnvironmentalIssue)

(instance PollutedFishStocksIssue EnvironmentalIssue)
(instance NuclearPollutionIssue EnvironmentalIssue)
(instance WildlifePoachingIssue EnvironmentalIssue)
(instance AsbestosDisposalIssue EnvironmentalIssue)
(instance IllegalWildlifeTradeIssue EnvironmentalIssue)
(instance WetlandDegradationIssue EnvironmentalIssue)
(instance LossOfHabitatIssue EnvironmentalIssue)
(instance AgriculturalExpansionIssue EnvironmentalIssue)
(instance MiningPollutionIssue EnvironmentalIssue)
(instance LandminesIssue EnvironmentalIssue)
(instance DroughtIssue EnvironmentalIssue)
(instance IndigenousPeoplesPreservationIssue EnvironmentalIssue)
(instance InvasiveSpeciesIssue EnvironmentalIssue)
(instance DriftNetFishingIssue EnvironmentalIssue)
(instance UnexplodedOrdinanceIssue EnvironmentalIssue)
(instance FamineIssue EnvironmentalIssue)
(instance OverhuntingIssue EnvironmentalIssue)

; ResourceDepletion - air, water, wildlife, farmland, minerals
; Pollution - air, water, soil: from organics, oil, hydrocar, metallurg
; Sanitation - water, food supply, toxicity

;;--------------------------------

;; Misc

(instance GeographicPartTypeFn BinaryFunction)
(domainSubclass GeographicPartTypeFn 1 GeographicArea)
(domain GeographicPartTypeFn 2 GeographicArea)
(rangeSubclass GeographicPartTypeFn GeographicArea)
(documentation GeographicPartTypeFn
EnglishLanguage "(&%GeographicPartTypeFn ?TYPE ?AREA) evaluates
to the &%Class of all &%instances of ?TYPE that are &%located in
the &%GeographicArea ?AREA.")

(=>
  (equal ?CLASS (GeographicPartTypeFn ?TYPE ?AREA))
  (subclass ?CLASS ?TYPE))

;; NS: Depending on the bindings/substitutions for ?TYPE and ?AREA,
;; the consequent could be much more specific than &%part.
(=>
  (instance ?GEO (GeographicPartTypeFn ?TYPE ?AREA))
  (part ?GEO ?AREA))

(=>
  (and
    (instance ?GEO (GeographicPartTypeFn ?TYPE ?AREA))
    (subclass ?TYPE GeopoliticalArea)
    (instance ?AREA GeopoliticalArea))
  (geopoliticalSubdivision ?GEO ?AREA))

(=>
  (and
    (instance ?GEO (GeographicPartTypeFn StateOrProvince ?AREA))
    (instance ?AREA Nation))
  (primaryGeopoliticalSubdivision ?GEO ?AREA))

;; NS: TO DO: Much more should be said about England and Wales
;; beyond what is represented here.
(instance England GeographicArea)
(documentation England EnglishLanguage "&%England, with &%Wales,
&%Scotland, and &%NorthernIreland, is one of the four constituent
&%Nations of the &%UnitedKingdom.")

(orientation Scotland England North)
(meetsSpatially Scotland England)
(orientation NorthernIreland Scotland Southwest)
(orientation NorthernIreland England Northwest)

(instance Wales GeographicArea)
(documentation Wales EnglishLanguage "&%Wales, with &%England,
&%Scotland, and &%NorthernIreland, is one of the four constituent
&%Nations of the &%UnitedKingdom.")

(orientation Wales England West)
(meetsSpatially Wales England)
(orientation NorthernIreland Wales Northwest)
(orientation Scotland Wales North)
(orientation IsleOfMan NorthernIreland East)
(orientation Ireland Wales West)

(primaryGeopoliticalSubdivision Scotland UnitedKingdom)
(primaryGeopoliticalSubdivision NorthernIreland UnitedKingdom)
(primaryGeopoliticalSubdivision England UnitedKingdom)
(primaryGeopoliticalSubdivision Wales UnitedKingdom)

;; NS: Note that in 2007, Denmark's 13 "counties" were replaced
;; by five administrative "regions".

(equal AmericanState (GeographicPartTypeFn StateOrProvince UnitedStates))

;;;;;;;;;;;;;;;;;;
;New definitions by Jennie July 2017
;;;;;;;;;;;;;;;;;

; 1 Deposition

(documentation Deposition EnglishLanguage "The &%Class of &%Processes where an &%Object is
cooled and converted directly from a &%Gas to a &%Solid, without going through the state
of being a &%Liquid.")

(documentation Deposition ChineseLanguage "这个 &%Process &%Class 发生在当 &%Object 遇冷，而
无需经过 &%Liquid 状态直接从 &%Gas 转成 &%Solid。")

(subclass Deposition StateChange)

(termFormat EnglishLanguage Deposition "deposition")

(termFormat ChineseLanguage Deposition "凝华")

(=>
  (and
    (instance ?DEPOSITION Deposition)
    (patient ?DEPOSITION ?OBJ))
  (exists (?PART)
    (and
      (part ?PART ?OBJ)
      (holdsDuring
        (BeginFn
          (WhenFn ?DEPOSITION))
        (attribute ?PART Gas))
      (holdsDuring
        (EndFn
          (WhenFn ?DEPOSITION))
        (attribute ?PART Solid))
      (not
        (holdsDuring (WhenFn ?DEPOSITION)
          (attribute ?PART Liquid))))))

(=>
  (and
    (instance ?DEPOSITION Deposition)
    (patient ?DEPOSITION ?OBJ)
    (holdsDuring
      (ImmediatePastFn
        (WhenFn ?DEPOSITION))
      (measure ?OBJ
        (MeasureFn ?X Joule)))
    (holdsDuring
      (ImmediateFutureFn
        (WhenFn ?DEPOSITION))
      (measure ?OBJ
        (MeasureFn ?Y Joule))))
  (lessThan ?Y ?X))

; 2 Sublimation

(documentation Sublimation EnglishLanguage "The &%Class of &%Processes where an &%Object
gains energy from its surrounding and is converted directly from a &%Solid to a &%Gas,
without going through the state of being a &%Liquid.")

(documentation Sublimation ChineseLanguage "这个 &%Process &%Class 发生在当 &%Object 从周围
环境获得能量，而无需经过 &%Liquid 状态直接从 &%Solid 转成 &%Gas。")

(subclass Sublimation StateChange)

(relatedInternalConcept Deposition Sublimation)

(termFormat EnglishLanguage Sublimation "sublimation")

(termFormat ChineseLanguage Sublimation "升华")

(=>
  (and
    (instance ?SUBLIMATION Sublimation)
    (patient ?SUBLIMATION ?OBJ))
  (exists (?PART)
    (and
      (part ?PART ?OBJ)
      (holdsDuring
        (BeginFn
          (WhenFn ?SUBLIMATION))
        (attribute ?PART Solid))
      (holdsDuring
        (EndFn
          (WhenFn ?SUBLIMATION))
        (attribute ?PART Gas))
      (not
        (holdsDuring (WhenFn ?SUBLIMATION)
          (attribute ?PART Liquid))))))

(=>
  (and
    (instance ?SUBLIMATION Sublimation)
    (patient ?SUBLIMATION ?OBJ)
    (holdsDuring
      (ImmediatePastFn
        (WhenFn ?SUBLIMATION))
      (measure ?OBJ
        (MeasureFn ?X Joule)))
    (holdsDuring
      (ImmediateFutureFn
        (WhenFn ?SUBLIMATION))
      (measure ?OBJ
        (MeasureFn ?Y Joule))))
  (greaterThan ?Y ?X))

; 4 Micrometer (New)

(documentation Micrometer EnglishLanguage "Submultiple of Metre. Symbol:µm. It is a unit
of length in the metric system, equal to one million of a metre.")

(documentation Micrometer ChineseLanguage "这是米的约数，符号：µm。1微米是1000000分之一米。")

(instance Micrometer UnitOfLength)

(termFormat EnglishLanguage Micrometer "micrometer")

(termFormat ChineseLanguage Micrometer "微米")

(equal
  (MeasureFn ?NUMBER Micrometer)
  (MeasureFn
    (MultiplicationFn ?NUMBER 0.0000001) Meter))

; 5 Crystal (Georgraphy.kif) (New)

(documentation Crystal EnglishLanguage "A &%Crystal &%Solid is a &%Substance with a
&%CrystallineStructure.")

(documentation Crystal ChineseLanguage "&%Crystal &%Solid 是一种拥有 &%CrystallineStructure
的 &%Substance。")

(subclass Crystal Substance)

(termFormat EnglishLanguage Crystal "crystal")

(termFormat ChineseLanguage Crystal "水晶")

; 6 Crystalline Structure (New)

(documentation CrystallineStructure EnglishLanguage "&%CrystallineStructure is a
&%subclass of &%StructureAttribute which belongs to a &%Crystal &%Solid.")

(documentation CrystallineStructure ChineseLanguage "&%CrystallineStructure 是
&%StructureAttribute 的 &%Subclass，它属于&%Crystal &%Solid 的结构。")

(termFormat EnglishLanguage CrystallineStructure "crystalline structure")

(termFormat ChineseLanguage CrystallineStructure "晶体结构")

(subclass CrystallineStructure StructureAttribute)

(subclass MonoCrystalline CrystallineStructure)

(termFormat EnglishLanguage MonoCrystalline "mono crystalline")

(termFormat ChineseLanguage MonoCrystalline "单晶结构")

(subclass PolyCrystalline CrystallineStructure)

(termFormat EnglishLanguage PolyCrystalline "poly crystalline")

(termFormat ChineseLanguage PolyCrystalline "多晶结构")

(=>
  (and
    (instance ?C Crystal)
    (attribute ?C MonoCrystalline)
    (instance ?SUB Substance)
    (attribute ?SUB Solid)
    (attribute ?SUB PolyCrystalline)
    (instance ?S Substance)
    (surface ?S ?SUB)
    (not
      (part ?C ?S)))
  (exists (?CLNT ?X ?MBR)
    (and
      (instance ?CLNT Collection)
      (memberCount ?CLNT ?X)
      (greaterThanOrEqualTo ?X 4)
      (=>
        (member ?MBR ?CLNT)
        (and
          (part ?MBR ?SUB)
          (meetsSpatially ?C ?MBR))))))

; 7 SulphurDioxide (new) Geography.kif

(subclass SulphurDioxide CompoundSubstance)

(documentation SulphurDioxide EnglishLanguage "&%SulfurDioxide is a toxic &%Gas with a
chemical formula of SO2.")

(documentation SulphurDioxide ChineseLanguage "&%SulfurDioxide 是一种有毒的 &%Gas，它的化学
公式为SO2。")

(termFormat EnglishLanguage SulphurDioxide "sulphur dioxide")

(termFormat ChineseLanguage SulphurDioxide "二氧化硫")

(molecularRatio Sulphur 1 SulphurDioxide)
(molecularRatio Oxygen 2 SulphurDioxide)

(=>
  (and
    (instance ?Y SulphurDioxide)
    (holdsDuring ?T
      (property ?Y StandardAmbientTemperaturePressure)))
  (holdsDuring ?T
    (attribute ?Y Gas)))

(=>
  (instance ?Y SulphurDioxide)
  (modalAttribute
    (exists (?D ?P)
      (and
        (instance ?D Damaging)
        (instance ?P Human)
        (instrument ?D ?Y)
        (patient ?D ?P))) Possibility))

; 8 NitricOxide (new)

(subclass NitricOxide CompoundSubstance)

(documentation NitricOxide EnglishLanguage " &%NitricOxide is a chemical compound in the
state of &%Gas, with a chemical formula of NO.")

(documentation NitricOxide ChineseLanguage " &%NitricOxide 是一种状态为 &%Gas 的化学复合物，
它的化学公式是 NO。")

(termFormat EnglishLanguage NitricOxide "nitric oxide")

(termFormat ChineseLanguage NitricOxide "一氧化氮")

(molecularRatio Nitrogen 1 NitricOxide)
(molecularRatio Oxygen 1 NitricOxide)

(=>
  (and
    (instance ?Y NitricOxide)
    (holdsDuring ?T
      (property ?Y StandardAmbientTemperaturePressure)))
  (holdsDuring ?T
    (attribute ?Y Gas)))

; 9 NitrogenDioxide (new)

(subclass NitrogenDioxide CompoundSubstance)

(documentation NitrogenDioxide EnglishLanguage "&%NitrogenDioxide is a chemical compound
in the state of &%Gas, with a chemical formula of NO2.")

(documentation NitrogenDioxide ChineseLanguage "&%NitrogenDioxide 是一种状态为 &%Gas 的化学
复合物，它的化学公式是 NO2。")

(termFormat EnglishLanguage NitrogenDioxide "nitrogen dioxide")

(termFormat ChineseLanguage NitrogenDioxide "二氧化氮")

(molecularRatio Nitrogen 1 NitrogenDioxide)
(molecularRatio Oxygen 2 NitrogenDioxide)

(=>
  (and
    (instance ?Y NitrogenDioxide)
    (holdsDuring ?T
      (property ?Y StandardAmbientTemperaturePressure)))
  (holdsDuring ?T
    (attribute ?Y Gas)))

; 10 Pebble (New)

(documentation Pebble EnglishLanguage "&%Pebbles are &%Rock fragments between 2
&%Millimeters to 64 &%Millimeters in &%approximateDiameter on the Wentworth Scale.")

(documentation Pebble ChineseLanguage "&%Pebble 是零散的 &%Rock， 按照溫特瓦分級，它的
&%approximateDiameter 在 2 &%Millimeter 到 64 &%Millimeter 之间。")

(partition Rock Boulder Cobble Pebble)

(termFormat EnglishLanguage Pebble "pebble")

(termFormat ChineseLanguage Pebble "卵石")

(subclass Pebble Rock)

(=>
  (and
    (instance ?Particle Pebble)
    (approximateDiameter ?Particle
      (MeasureFn ?Size Millimeter)))
  (and
    (greaterThan 64.0 ?Size)
    (greaterThan ?Size 2.0)))

; 11 Cobble  (New)

(documentation Cobble EnglishLanguage "&%Cobbles are &%Rock fragments between 64
&%Millimeters to 256 &%Millimeters in &%approximateDiameter on the Wentworth Scale.")

(documentation Cobble ChineseLanguage "&%Cobble 是零散的 &%Rock，按照溫特瓦分級，它的
&%approximateDiameter 在 64 &%Millimeter 到 256 &%Millimeter 之间。")

(termFormat EnglishLanguage Cobble "cobble")

(termFormat ChineseLanguage Cobble "大卵石")

(subclass Cobble Rock)

 (=>
  (and
    (instance ?Particle Cobble)
    (approximateDiameter ?Particle
      (MeasureFn ?Size Millimeter)))
  (and
    (greaterThan 256.0 ?Size)
    (greaterThan ?Size 64.0)))

; 12 Boulder (New)

(documentation Boulder EnglishLanguage "&%Boulders are &%Rock fragments larger than
256 &%Millimeters in &%approximateDiameter on the Wentworth Scale.")

(documentation Boulder ChineseLanguage "&%Boulder 是零散的 &%Rock，按照溫特瓦分級，它的
&%approximateDiameter 大于 256 &%Millimeter。")

(termFormat EnglishLanguage Boulder "boulder")

(termFormat ChineseLanguage Boulder "巨石")

(subclass Boulder Rock)

 (=>
  (and
    (instance ?Particle Boulder)
    (approximateDiameter ?Particle
      (MeasureFn ?Size Millimeter)))
  (greaterThan ?Size 256.0))

; 13 SolubilityAttribute (New)

(documentation SolubilityAttribute EnglishLanguage "&%SolubilityAttribute is a &subclass
of &%InternalAttributes. It describes the property of a &%Solid, &%Liquid or &%Gas
&%Substance to say whether it can dissolve in another &%Substance of either &%Solid,
&%Liquid or &%Gas
state.")

(documentation SolubilityAttribute ChineseLanguage "&%SolubilityAttribute 是
&%InternalAttribute 的 &subclass。 它表示一种 &%Solid、 &%Liquid 或 &%Gas &%Substance 是否能够
溶解于另外一种 &%Solid、 &%Liquid 或 &%Gas 态 &%Substance 中。")

(termFormat EnglishLanguage SolubilityAttribute "solubility attribute")

(termFormat ChineseLanguage SolubilityAttribute "可溶性")

(subclass SolubilityAttribute InternalAttribute)

; 14 Soluble (New)

(documentation Soluble EnglishLanguage "If a &Substance has the &%SolubilityAttribute of
being &%Soluble, then it can be combined with another &%Substance to become a %Solution.")

(documentation Soluble ChineseLanguage "如果一种 &Substance 有 &%Soluble
&%SolubilityAttribute 的话，那么它就能和另外一个 &%Substance 结合成为一种均匀混合物。")

(termFormat EnglishLanguage Soluble "soluble")

(termFormat ChineseLanguage Soluble "可溶的")

(subclass Soluble SolubilityAttribute)

(contraryAttribute Soluble Insoluble)

(=>
  (and
    (instance ?X Substance)
    (attribute ?X Soluble))
  (modalAttribute
    (exists (?Y ?Z)
      (and
        (instance ?Y Substance)
        (instance ?Z Solution)
        (piece ?X ?Z)
        (piece ?Y ?Z)))
    Possibility))

; 15 Insoluble (New)

(documentation Insoluble EnglishLanguage "If a &Substance has the &%SolubilityAttribute of
being &%Insoluble, then it cannot be combined with another &%Substance to become a
solution mixture.")

(documentation Insoluble ChineseLanguage "如果一种 &Substance 有 &%Insoluble
&%SolubilityAttribute 的话，那么它就不能和另外一个 &%Substance 结合成为一种均匀混合物。")

(termFormat EnglishLanguage Insoluble "insoluble")

(termFormat ChineseLanguage Insoluble "不可溶的")

(subclass Insoluble SolubilityAttribute)

(=>
  (and
    (instance ?X Substance)
    (attribute ?X Insoluble))
  (not
    (modalAttribute
      (exists (?Y ?Z)
        (and
          (instance ?Y Substance)
          (instance ?Z Solution)
          (piece ?X ?Z)
          (piece ?Y ?Z)))
       Possibility)))

; 16 ParticulateMatter (New)

(documentation ParticulateMatter EnglishLanguage "&%ParticulateMatter is a mixture of very
small solid or liquid matter suspended in Earth's atmosphere with an &%approximateDiameter
of less than 1,000 &%Micrometers (0.001 mm).")

(documentation ParticulateMatter ChineseLanguage "&%ParticulateMatter 悬浮在大气中的微小固体或
液体颗粒, 它的 &%approximateDiameter 小于 1,000 &%Micrometer (0.001mm)。")

(subclass ParticulateMatter Mixture)

(partition ParticulateMatter CoarseParticulateMatter FineParticulateMatter)

(termFormat EnglishLanguage ParticulateMatter "particulate matter")

(termFormat EnglishLanguage ParticulateMatter "PM")

(termFormat ChineseLanguage ParticulateMatter "悬浮颗粒")

(=>
  (instance ?PM ParticulateMatter)
  (exists (?P ?ATMOSPHERE ?S)
    (and
      (instance ?ATMOSPHERE AtmosphericRegion)
      (located ?PM ?ATMOSPHERE)
      (part ?P ?PM)
      (approximateDiameter ?P
        (MeasureFn ?S Micrometer))
      (greaterThan 1000.0 ?S))))

; 17 CoarseParticulateMatter (New)

(documentation CoarseParticulateMatter EnglishLanguage "&%CoarseParticulateMatter are
&%ParticulateMatter with an &%approximateDiameter between 2.5 and 10 &%Micrometers.")

(documentation CoarseParticulateMatter ChineseLanguage "&%CoarseParticulateMatter 是
&%approximateDiameter 介于 2.5 和 10 &%Micrometer 之间的 &%ParticulateMatter。")

(subclass CoarseParticulateMatter ParticulateMatter)

(termFormat EnglishLanguage CoarseParticulateMatter "coarse particulate matter")

(termFormat EnglishLanguage CoarseParticulateMatter "PM10")

(termFormat ChineseLanguage CoarseParticulateMatter "粗悬浮颗粒")

(<=>
  (and
    (instance ?PM ParticulateMatter)
    (part ?P ?PM)
    (approximateDiameter ?P
      (MeasureFn ?S Micrometer))
    (greaterThan 10.0 ?S)
    (greaterThan ?S 2.5))
  (exists (?PM10)
    (and
      (instance ?PM10 CoarseParticulateMatter)
      (part ?PM10 ?PM))))

; 18 FineParticulateMatter (New)

(documentation FineParticulateMatter EnglishLanguage "&%FineParticles are
&%ParticulateMatter with an &%approximateDiameter less than 2.5 &%Micrometers.")

(documentation CoarseParticulateMatter ChineseLanguage "&%FineParticle 是
&%approximateDiameter 小于 2.5 &%Micrometer 的 &%ParticulateMatter。")

(subclass FineParticulateMatter ParticulateMatter)

(termFormat EnglishLanguage FineParticulateMatter "fine particulate matter")

(termFormat EnglishLanguage FineParticulateMatter "PM2.5")

(termFormat ChineseLanguage FineParticulateMatter "细悬浮颗粒")

 (<=>
  (and
    (instance ?PM ParticulateMatter)
    (part ?P ?PM)
    (approximateDiameter ?P
      (MeasureFn ?S Micrometer))
    (greaterThanOrEqualTo ?S 2.5))
  (exists (?PM25)
    (and
      (instance ?PM25 FineParticulateMatter)
      (part ?PM25 ?PM))))

; 19 LiquidDrop (New)

(documentation LiquidDrop EnglishLanguage "A &%LiquidDrop is a small column of liquid,
bounded almost completely by free surfaces.")

(documentation LiquidDrop ChineseLanguage "&%LiquidDrop 是一滴液体， 它几乎完全由自由表面包围。")

(subclass LiquidDrop Substance)

(termFormat EnglishLanguage LiquidDrop "liquid drop")
(termFormat ChineseLanguage LiquidDrop "液体滴")

(=>
  (instance ?LD LiquidDrop)
  (attribute ?LD Liquid))

(=>
  (and
    (instance ?LD LiquidDrop)
    (surface ?S ?LD))
  (modalAttribute
    (exists (?SS ?LL)
      (and
        (instance ?LL Substance)
        (attribute ?LL Liquid)
        (not
          (equal ?LD ?LL))
        (surface ?SS ?LL)
        (meetsSpatially ?S ?SS))) Unlikely))

; 20  Droplet (New)

(documentation Droplet EnglishLanguage "A &%Droplet is a &%LiquidDrop of less than 500
&%Micrometer (µm) in &%approximateDiameter.")

(documentation Droplet ChineseLanguage "&%Droplet 是 &%approximateDiameter 小于 500
&%Micrometer (µm) 的 &%LiquidDrop。")

(subclass Droplet LiquidDrop)

(termFormat EnglishLanguage Droplet "droplet")
(termFormat ChineseLanguage Droplet "小滴液体")

(<=>
  (and
    (instance ?LD LiquidDrop)
    (approximateDiameter ?LD
      (MeasureFn ?S Micrometer))
    (lessThan 500.0 ?S))
  (instance ?LD Droplet))

; 21 Aerosal (New)

(documentation Aerosal EnglishLanguage "An &%Aerosal is a &%Colloid of fine &%Solid
particles or &%Liquid &%Droplets in air.")

(documentation Aerosal EnglishLanguage "&%Aerosal 是一种悬浮在空中，由微小的 &%Solid 颗粒或
&%Liquid &%Droplet 组成 的 &%Colloid。")

(termFormat EnglishLanguage Aerosal "aerosal")
(termFormat ChineseLanguage Aerosal "气溶胶")
(subclass Aerosal GasMixture)
(subclass Aerosal Colloid)

(=>
  (and
    (instance ?C Colloid)
    (part ?P ?C)
    (or
      (attribute ?P Solid)
      (attribute ?P Liquid))
    (instance ?AIR Air)
    (part ?AIR ?C))
  (instance ?C Aerosal))


